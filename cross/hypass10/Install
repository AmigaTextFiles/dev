;
; $VER: HypAss 1.0 Installer Shit (19.7.96) ©1998 RST Ralf Steines Trier, ALL RIGHTS RESERVED
;
; This was the most f**king nerving boaring bulls**t program I have
; ever written, and it is f**king *NOT* true that the installer script
; is made in the same time as a batch file (as stated in installer docs)
; I would have written an OOP object doing the job in the same time!
; I have never seen such a BAD language in my life!!!!!!!!!!!!!!!!!!
; This is the worst area of the complete Amiga project AT ALL!!!!!!!
; I hate it. Amiga is such a comfortable system having a GUI, why do we
; need to write 100 parentheses and 1000 chars to say 'copy' and 'assign'
; I wonder if the writer of installer ever used it.
;

    (set #askbool-prompt
      (cat
        "HypAss 1.0 and HyperSource© are\n"
        "(c) 1998 RST Ralf Steines Trier,\n"
        "ALL RIGHTS RESERVED!\n"
        "\n"
        "HypAss is SHAREWARE and *NOT* public domain!\n"
        "\n"
        "Read the file \"License.guide\"\n"
        "*BEFORE* you install this package!\n"
        "Installing this software\n"
        "means accepting the license!\n"
        "\n"
        "Have fun with HypAss!\n"
      )
    )
    (set #askbool-agree     "I read it and agree")
    (set #askbool-disagree  "No, I don't agree")
    (set #forbid-prompt     "\nYou are not allowed to use or store\nthis archive in any way!")
    (set #bad-luck          "Bad luck")
    (set #makedir-prompt    "I will now create the directory Work:HypAss")
    (set #askdir-prompt     "Where should I copy the HypAss10 distribution?\nCreate a new directory!")
    (set #copyfiles-prompt  "Copying the complete package...")
    (set #startup-prompt    "Adding assigns to your lovely startup...")
    (set #complete-message1 "\nInstallation complete!\n\n")
    (set #complete-message2 " can be found in your\n\"")
    (set #complete-message3 "\" drawer or partition \nand is waiting for its first invocation.\n")

    ; Dear HypAss friends of other countries!
    ; please modify this part, so it contains your language,
    ; and keep the information UNCHANGED, just translated.
    ; Test it some times, and send it to me, so other
    ; people of your language will have it easier to install!

    (if (= @language "deutsch") (
      (set #askbool-prompt
        (cat
          "HypAss 1.0 und HyperSource© sind\n"
          "(c) 1998 RST Ralf Steines Trier,\n"
          "ALLE RECHTE VORBEHALTEN!\n"
          "\n"
          "HypAss ist SHAREWARE und *NICHT* public domain!\n"
          "\n"
          "Lesen Sie \"License.guide\"\n"
          "*BEVOR* Sie dieses Programmpaket installieren!\n"
          "Mit der Installation dieses Programmpakets\n"
          "akzeptieren sie alle Punkte der Lizenzbestimmung!\n"
          "\n"
          "Viel Spaß mit HypAss!\n"
        )
      )
      (set #askbool-agree     "Akzeptiere Lizenz")
      (set #askbool-disagree  "Akzeptiere Lizenz nicht")
      (set #forbid-prompt     "\nSie sind nicht berechtigt dieses Archiv\nin irgendeiner Form zu speichern oder zu nutzen!")
      (set #bad-luck          "Pech gehabt")
      (set #makedir-prompt    "Ich werde nun das Verzeichnis Work:HypAss erzeugen")
      (set #askdir-prompt     "Wohin soll ich das HypAss10 Paket installieren?\nErzeuge ein neues Verzeichnis!")
      (set #copyfiles-prompt  "Kopiere das ganze Paket...")
      (set #startup-prompt    "Füge die Assigns deiner geliebten startup hinzu...")
      (set #complete-message1 "\nInstallation komplett!\n\n")
      (set #complete-message2 " kann befindet sich nun in\n\"")
      (set #complete-message3 "\"\und erwartet seinen ersten Gebrauch.\n")
    ))

    ; end of localized part to be modified!

    (set #agree-license
      (askbool
          (prompt #askbool-prompt)
          (help @askbool-help)
          (choices #askbool-agree #askbool-disagree)
      )
    )
    (if (= #agree-license 0)
      (
        (askbool
            (prompt #forbid-prompt)
            (help @askbool-help)
            (choices #bad-luck #bad-luck)
        )
        (exit (quiet))
      )
    )

    (set @default-dest "Work:")

    (if (= @user-level 0)
      (
        (makedir "Work:HypAss"
            (prompt #makedir-prompt)
            (help @makedir-help)
            (infos)
            (confirm)
        )
        (set @default-dest "Work:HypAss")
      )
    )


    (set #dest-path
        (askdir
            (prompt #askdir-prompt)
            (help @askdir-help)
            (default @default-dest)
        )
    )


    (copyfiles
        (prompt #copyfiles-prompt)
        (help @copyfiles-help)
        (source "")
        (all)
        (dest #dest-path)
        (confirm)
    )

    (makeassign "HAss" #dest-path)
    (makeassign "HSrc" (tackon #dest-path "hsrc"))
    (makeassign "HInc" (tackon #dest-path "hinc"))
    (startup    "HypAss"
                (command "    Assign HAss: " "\""#dest-path"\"\n")
                (command "    Assign HSrc: " "\""(tackon #dest-path "hsrc")"\"\n")
                (command "    Assign HInc: " "\""(tackon #dest-path "hinc")"\"")
                (prompt #startup-prompt)
                (help @startup-help)
    )


    (message #complete-message1
                   @app-name
             #complete-message2
                  #dest-path
             #complete-message3
    )

    (exit (quiet))



