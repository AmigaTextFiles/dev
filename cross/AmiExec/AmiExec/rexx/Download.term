/**************************************************************************** 

$Source: MASTER:Rexx/Download.term,v $
$Revision: 2.0 $
$Date: 1996/11/02 15:32:39 $

Copyright © 1994-1996 by W. John Malone.  All rights reserved.

****************************************************************************/
/**************************************************************************** 
*                                                                           *
*    *NAME                                                                  *
*        Download.term - download S-Records via terminal program Term.      *
*                                                                           *
*     SYNOPSIS                                                              *
*        Download.term ([FILES] {<file>} | MODFILE <modfile>) [BAUD <baud>] *
                       [FBUG]                                               *  
*        Download.term FILES/M,MODFILE/K,BAUD/K/N,FBUG/S                    *
*                                                                           *
*     FUNCTION                                                              *
*        Downloads files of (presumably) S-Records using the terminal       *
*        program Term. Term must be running, sitting at the command line    *
*        prompt of the ROM monitor.  If no switch indicating otherwise      *
*        is provided, the ROM monitor is assumed to be debug.library.       *
*                                                                           *
*     INPUTS                                                                *
*        FILES    - names of the files to download                          *
*        MODFILE  - name of a file whose contents are a list of files to    *
*                   download, one file name per line.  Mutually exclusive   *
*                   with FILES.                                             *
*        BAUD     - specifies the baud rate to use for downloading. This    *
*                   keyword allows the use of debug.library's ability to    *
*                   accept S-Records at a higher baud rate than the         *
*                   terminal port is usually run at.                        *
*        FBUG     - target system is running FBUG.                          *
*                                                                           *
****************************************************************************/

/* Initialization - argument parsing, host selection. */

parse arg args

template = 'FILES/M,MODFILE/K,BAUD/K/N,FBUG/S'

modfile = ''; baud = ''; fbug = 0

if args = '' | args = '?' then do
   say template
   exit 5
   end

if ReadArgs(args, template) == 0 then do
   say template
   exit 20
   end

if files.count == 0 & modfile = '' then do
   say template
   exit 20
   end

if modfile ~= '' then do
   'type' modfile '| execio STEM files.'
   firstdex = 1
   lastdex = files.0
   end
else do
   firstdex = 0
   lastdex = files.count - 1
   end

address 'TERM'
options results


/* Issue the appropriate download command.  Delay for a second to allow
   target to prepare to receive. */

if fbug == 1 then
   send 'lo -t 0\r\n'
else
   send 'lo 0' baud '\r\n'

delay 1           


/* Change the baud rate, first saving the current rate for later restoration. */

if baud ~= '' then do
   getattr serialprefs.baudrate
   oldbaud = result
   setattr serialprefs baudrate baud
   end


/* Send each file a line at a time, displaying a dot for each line sent. */

do i = firstdex to lastdex 

   if open('file', files.i, 'Read') == 0 then do
      say 'Download.term: Could not open' file.i
      exit 20
      end

   say 'Downloading' files.i':'
   dots = 0;
   do while eof('file') = 0
      str = readln('file')
      if str ~= '' then do
         send str
         call writech(STDOUT, '.')
         dots = dots + 1
         if dots > 63 then do
            say ''
            dots = 0
            end
         end      
      end
   say ''

   call close('file')
   end


/* Restore the baud rate. */

if baud ~= '' then
   setattr serialprefs baudrate oldbaud

exit 0
