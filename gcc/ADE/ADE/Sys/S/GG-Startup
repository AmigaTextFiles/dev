; This script is executed from your S:User-Startup script in order to
; set up the Geek Gadgets environment.  All assigns are made relative
; to "GG:", which has to be assigned prior to running this script.
; This would normally be done like:
;
;	assign GG: <root of installed Geek Gadgets binary tree>
;	execute GG:Sys/S/GG-Startup

; Check to see if we have already run this script once, and if so, simply
; exit.  We might try to run it more than once, if for example the user
; reruns the install script in order to change some metatool prefs.

if EXISTS ram:GG-datestamp
  skip END
else
  date >ram:GG-datestamp
endif

; Tools should search LOCAL: for files that may have been added to 
; supplement the standard Geek Gadgets environment, to account for
; the case where it is impossible to add these files directly to
; the GG: tree (such as when it is located on a CD-ROM)
;
; If no LOCAL: is already defined, supply a default assignment to T:,
; which we know exists.  This assignment can be overridden in the
; user's User-Startup script, either by defining it before calling
; this script, or redefining it afterwards.

assign LOCAL: exists >NIL:
if WARN
  assign LOCAL: T:
endif

; Make assigns for Geek Gadgets things, like general binaries,
; manual pages, info files, etc.  We try to keep these to an
; absolute minimum!

assign BIN:	GG:bin
assign USR:	GG:
if EXISTS GG:var
  assign VAR:	GG:var
endif
assign ETC:	GG:etc
assign INFO:	GG:info
assign MAN:	GG:man
assign TMP:	T:
path GG:bin  ADD

; Add various directories under GG: that supplement the normal
; system logical defines, like "LIBS:", "L:", "DEV:", etc.
; We have to have C: in here if we want the Workbench to notice
; anything in BIN:, or have them available from any CLIs already
; running.

assign C:	GG:bin		ADD
assign DEVS:	GG:Sys/Devs	ADD
assign LIBS:	GG:Sys/Libs	ADD
assign L:	GG:Sys/L	ADD
assign S:	GG:Sys/S	ADD

; Assign GNU: to GG: for backwards compatibility.  This will be
; removed at some point in the future.

;assign GNU:	GG:	

; Mount the ixpipe: device

mount IXPIPE: from DEVS:MountList.IXPIPE

; Install GNU Emacs version 18.59
;   Assign GNUEmacs: so emacs can find it's files.
;   Cancel any system supplied alias for emacs.
;   Use the emacs specific shell since bin:sh (PD ksh) causes a crash
;     when you try to use it under emacs.

if EXISTS GG:lib/emacs/18.59
  assign GNUemacs: GG:lib/emacs/18.59
  unalias emacs
  setenv ESHELL GNUemacs:etc/sh
endif

; Install Matt Dillon's fifo library, for emacs and others.
; Note that it must be able to find LIBS:fifo.library when run.

run <nil: >nil: GG:Sys/L/fifo-handler

LAB END
