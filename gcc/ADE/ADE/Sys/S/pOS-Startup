; This script is executed from your S:User-Startup script in order to
; set up the ADE environment.  All assigns are made relative to "ADE:",
; which has to be assigned prior to running this script.  This would
; normally be done like:
;
;	assign ADE: ADE-1:ADE-bin
;	execute ADE:Sys/S/ADE-Startup

; Check to see if we have already run this script once, and if so, simply
; exit.  We might try to run it more than once, if for example the user
; reruns the install script in order to change some metatool prefs.

if EXISTS ram:pOS-datestamp
  skip END
else
  date >ram:pOS-datestamp
endif

echo "Checking assignment of T:"
assign T: exists >NIL:
if WARN
  assign T: ram:
endif

; Tools should search LOCAL: for files that may have been added to 
; supplement the standard ADE environment, to account for the case 
; where it is impossible to add these files directly to the ADE:
; tree (such as when it is located on a CD-ROM)
;
; If no LOCAL: is already defined, supply a default assignment to T:,
; which we know exists.  This assignment can be overridden in the
; user's User-Startup script, either by defining it before calling
; this script, or redefining it afterwards.

echo "Checking assignment of LOCAL:"
assign LOCAL: exists >NIL:
if WARN
  assign LOCAL: T:
endif

; Make assigns for ADE things, like general binaries, manual pages,
; info files, etc.  We try to keep these to an absolute minimum!

echo "Making assigns for BIN: USR: ..."
assign BIN:	ADE:bin
assign USR:	ADE:
if EXISTS ADE:var
  assign VAR:	ADE:var
endif
assign ETC:	ADE:etc
assign INFO:	ADE:info
assign MAN:	ADE:man
assign TMP:	T:
path ADE:bin  ADD

; Add various directories under ADE: that supplement the normal
; system logical defines, like "LIBS:", "L:", "DEV:", etc.
; We have to have C: in here if we want the Workbench to notice
; anything in BIN:, or have them available from any CLIs already
; running.

echo "Making assigns for C: DEVS: ..."
assign C:	ADE:bin		ADD
assign DEVS:	ADE:Sys/Devs	ADD
assign LIBS:	ADE:Sys/Libs	ADD
assign L:	ADE:Sys/L	ADD
assign S:	ADE:Sys/S	ADD

; Assign GNU: to ADE: for backwards compatibility.  This will be
; removed at some point in the future.

;assign GNU:	ADE:	

; Mount the ixpipe: device

echo "IXPIPE: not mounted, AmigaOS only..."
;echo "Mounting IXPIPE: ..."
;mount IXPIPE: from DEVS:MountList.IXPIPE

; Install GNU Emacs version 18.59
;   Assign GNUEmacs: so emacs can find it's files.
;   Cancel any system supplied alias for emacs.
;   Use the emacs specific shell since bin:sh (PD ksh) causes a crash
;     when you try to use it under emacs.

if EXISTS ADE:lib/emacs/18.59
  assign GNUemacs: ADE:lib/emacs/18.59
;  unalias emacs
;  setenv ESHELL GNUemacs:etc/sh
endif

; Install Matt Dillon's fifo library, for emacs and others.
; Note that it must be able to find LIBS:fifo.library when run.

echo "fifo-handler not run, AmigaOS only ..."
;run <nil: >nil: ADE:Sys/L/fifo-handler

LAB END
