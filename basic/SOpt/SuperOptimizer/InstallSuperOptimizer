; $VER: InstallSuperOptimizer 2.28 (03.09.97)
;
; Installer script for the SuperOptimizer
; ¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
; The script has been written by Daniel Seifert in 1997
; SuperOptimizer is copyrighted © by Manuel Andre in 1995-1997
;
;
;--------------------------------------------------------
; Var declarations
;--------------------------------------------------------

(set #ACEnotinstalled
      (cat "SuperOptimizer can not be installed if you do not have "
           "ACE installed correctly. Please refer to the "
           "ACE documentation on how to install it."
           "\n\n ------ \n\n"
           "ACE - AmigaBASIC Compiler with Extras\n"
           "Copyright © by David Benn 1991-1997\n\n"
           "Available on AmiNet in dev/basic."
      )
)

(set #SODrawerPrompt
      (cat "Please choose a directory where I shall copy "
           "the new SuperOptimizer version to!"
      )
)

(set #SODrawerHelp
      (cat "Here you have the choice to specify where "
           "the SuperOptimizer (+ RemoveLine) should be copied to. Please choose a "
           "directory (default: ACE:BIN/). \n\n"
           "No sub-directory is created!"
      )
)


(set #InstallBin
      (cat "Installerscript\n"
           "for\n"
           "SuperOptimizer version 2.28"
      )
)


(set #ReallyInstallDoc
      (cat "SuperOptimizer manual\n\n"
           "This package comes with a (nice) AmigaGuide "
           "manual for the SuperOptimizer.\n"
           "Do you wish to install the manual, too?"
      )
)

(set #ReallyInstallDocHelp
      (cat "If you think you are able to understand "
           "every feature of the SuperOptimizer instantly it is not "
           "necessary to install the manual.\n"
           "But think twice: it's always better to "
           "have a copy somewhere if you wish to "
           "look something up!"
      )
)

(set #InstallDoc
      (cat "Where shall I copy the manual to?")
)

(set #InstallDocHelp
      (cat "Choose a directory I will copy the manual to. "
           "No subdirectory will be created.\n"
           "The default for the directory is ACE:docs "
           "where all the other ACE related manuals should "
           "be as well."
      )
)

(set #SODrawer  "ACE:BIN")
(set #DocsDrawer  "ACE:docs")

;--------------------------------------------------------
; Procedures
;--------------------------------------------------------

(procedure P_InstallDoc
 (
  (copyfiles (SOURCE "docs/SuperOptimizer.guide") (DEST #DocsDrawer) (infos))
 )
)

;--------------------------------------------------------
; Main
;--------------------------------------------------------


; If ACE is not installed we have to abort ;-(
(IF (= (GETASSIGN "ace") "")
  (
   (message #ACEnotinstalled)
   (exit (quiet))
  )
)


;---------------------------------------------------------------------
; now do really start (install automatically or ask for confirmation)
;---------------------------------------------------------------------

; install binary (set destination drawer)
(IF (> @user-level 0)
 (
  (MESSAGE #InstallBin)
  (SET #SODrawer
         (ASKDIR (PROMPT  #SODrawerPrompt)
                 (DEFAULT #SODrawer)
                 (HELP    #SODrawerHelp)
         )
  )
 )
)

; install binary (copy files)
(set @default-dest #SODrawer)
(copyfiles (SOURCE "bin") (DEST #SODrawer) (infos) (pattern "#?"))

; install documentation
(IF (> @user-level 0)
 (
  (IF (ASKBOOL (prompt  #ReallyInstallDoc)
               (help    #ReallyInstallDocHelp)
               (default 1)
               (choices "Yes" "No")
      )
   (
    (SET #DocsDrawer
         (ASKDIR (PROMPT  #InstallDoc)
                 (DEFAULT #DocsDrawer)
                 (HELP    #InstallDocHelp)
         )
    )
    (P_InstallDoc)
   )
  )
 )
 (
  (P_InstallDoc)
 )
)

;
(set #ExitText
      (cat "Installing the SuperOptimizer completed.\n\n"
           "It can be found in "
           #SODrawer
           "\n"
           "The manual can be found in "
           #DocsDrawer
           "\n\n --- \n\n"
           "Happy ACEing ;-)"
      )
)

; and done
(exit (#ExitText) (quiet))
