.key arg1,arg2,arg3

; ** compile, assemble and link an ACE program **

; make sure stack is big enough.
STACK 40000

; don't abort script if ACE or APP quits 
; with a return code of 10 (ERROR).
FAILAT 11

IF <arg2> GT ""

 ; at least 2 arguments: <arg1> = compiler options.
 ;                       <arg2> = ACE source file.
 ;                       <arg3> = extra object module/library to link.

 ; preprocess source file.
 acpp <arg2>.b T:<arg2>.beta
 IF NOT ERROR
  ; remove preprocessor-inserted information lines.
  removeline t:<arg2>.beta t:<arg2>.b
  IF NOT ERROR
   delete >NIL: T:<arg2>.beta
   ; compile preprocessed source file.
   ace <arg1> T:<arg2>

   IF NOT ERROR
     ; assemble and link
     delete >NIL: T:<arg2>.b
     SuperOptimizer 11 T:<arg2>.s T:<arg2>.a
     delete >NIL: T:<arg2>.s
     phxass T:<arg2>.a
     delete >NIL: T:<arg2>.a
     phxlnk from T:<arg2>.o <arg3> ACElib:startup.lib ACElib:db.lib ACElib:ami.lib to T:<arg2> SC SD ND

     ; leave us with the executable (and icon?).
     copy T:<arg2> ""

     IF EXISTS T:<arg2>.info
       copy T:<arg2>.info ""
     ENDIF

   ENDIF

  ENDIF
 ENDIF
ELSE 

 ; no compiler options: <arg1> = ACE source file.
 ;                      <arg2> = extra object module/library to link.

 ; preprocess source file.
 acpp <arg1>.b T:<arg1>.beta
 IF NOT ERROR
  ; remove preprocessor-inserted information lines.
  removeline t:<arg1>.beta t:<arg1>.b
  IF NOT ERROR
   delete >NIL: t:<arg1>.beta
   ; compile source file.
   ace -iO T:<arg1>

   IF NOT ERROR
     ; assemble and link.
     delete >NIL: T:<arg1>.b
     SuperOptimizer 11 T:<arg1>.s T:<arg1>.a
     delete >NIL: T:<arg1>.s
     phxass T:<arg1>.a
     delete >NIL: T:<arg1>.a
     phxlnk from T:<arg1>.o <arg2> ACElib:startup.lib ACElib:db.lib ACElib:ami.lib to T:<arg1> SC SD ND

     ; leave us with the executable (and icon?).
     copy T:<arg1> ""
   
     IF EXISTS T:<arg1>.info
       copy T:<arg1>.info ""
     ENDIF

   ENDIF
  ENDIF 
 ENDIF

ENDIF

; kill any remaining temporary files.
FAILAT 21
delete >NIL: T:<arg2>#?
