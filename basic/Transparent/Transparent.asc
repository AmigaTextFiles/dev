;Transparent WB window demo

;shows how to open a "transparent" window on the WB
;works on standard and GFX card systems

;by Curt Esser  camge@amigaonline.net
;last modified Apr 22, 2000

;sizegadget code by David McMinn

;GFX card corrections by Simon Hitchen

;NOTE:
; You will need Blitzlibs:amigalibs.res  in your
; compiler options

; I used the original one for this demo
; If you get an error on a line with a constant when compiling
; you may have to add or remove the "_" from the constant names
; check your "View Newtypes" for the structure in question



WBStartup         ; I always use this, just in case...
NoCli             ; we don't need one for this demo...

WBenchToFront_
FindScreen 0      ; first we grab the WB as our screen

*scr.Screen = Peek.l(Addr Screen(0))  ;find the WB's screen structure

DEFTYPE.Window *ourwin                ;and we'll need a window structure too

;now we set up some default variables for our window:

WinX.w      = 20
WinY.w      = 20
WinWidth.w  = 300
WinHeight.w = 300

; these next are the OS's equivalent of WTopOff, etc...
; only we can get them before opening the window
; and it will work properly with patches that
; change the size of the borders and gadgets...

Tborder.b   = *scr\BarHeight+1   ;size of title bar
Lborder.b   = *scr\WBorLeft      ;size of left border
Rborder.b   = *scr\WBorRight     ;size of right border

;Bborder.b   = *scr\WBorBottom    ;size of bottom border if no size gadget used

;But, we are using a size gadget, so we must do this (thanks to David McMinn):

Bborder.w = 10                   ;fallback value in case this next bit fails

*dri.DrawInfo = GetScreenDrawInfo_(*scr)

If *dri
  If (*scr\Flags & #SCREENHIRES)
    sis.l=#SYSISIZE_MEDRES
  Else
    sis=#SYSISIZE_LOWRES
  EndIf
  Dim tags.TagItem(4)
  tags(0)\ti_Tag = #SYSIA_DrawInfo,*dri
  tags(1)\ti_Tag = #SYSIA_Which,#SIZEIMAGE
  tags(2)\ti_Tag = #SYSIA_Size,sis
  tags(3)\ti_Tag = #TAG_DONE
  *img.Image = NewObjectA_(0,"sysiclass",&tags(0))
  If *img
    Bborder.w= *img\Height
    DisposeObject_ *img
  End If
  FreeScreenDrawInfo_ *scr,*dri
EndIf

;now, we can pre-calculate our Window's inner width and height:

InWidth.w   = WinWidth  - Lborder - Rborder
InHeight.w  = WinHeight - Tborder - Bborder

AddIDCMP #IDCMP_CHANGEWINDOW  ;have Intuition tell us if our window has
                              ;been moved, re-sized, or zoomed so we
                              ;can refresh our window's backdrop

Gosub RefreshWindow           ;OK, we can open the window


;================== Main Loop ====================

Repeat
  ev.l=WaitEvent
  Select ev

    Case #IDCMP_CHANGEWINDOW  ;window's location or size was changed
                              ;by the user or by another program...

      WinX=*ourwin\LeftEdge   ;so, we must get the new location
      WinY=*ourwin\TopEdge    ;and size from our window structure

      WinWidth = *ourwin\Width
      WinHeight= *ourwin\Height

      InWidth = WinWidth  - Lborder - Rborder
      InHeight= WinHeight - Tborder - Bborder

      Gosub RefreshWindow     ;and redraw it

  End Select

Until ev=$200        ;window was closed by user, we're outta here!

End

;-------------------------------------------------

RefreshWindow

Free Window 0        ;first, we close the old window

;now, we attempt to copy the necessary part of the WB's bitmap
;into a temporary bitmap

DEFTYPE.RastPort BMrp
InitRastPort_ &BMrp  ;we'll need a rastport for our temp bitmap

;now, we ask Intuition for a bitmap

;(fixed for GFX card use by Simon Hitchen)

*bmap=AllocBitMap_(InWidth,InHeight,*scr\_RastPort\_BitMap\Depth,#BMF_MINPLANES,*scr\_RastPort\_BitMap)

If *bmap                 ;good!  we got one!
  BMrp\_BitMap = *bmap
  FromX.w = WinX+Lborder
  FromY.w = WinY+Tborder

; ok, attempt to copy part of the WB's bitmap to our temp bitmap...
; result will be 0 if this fails...

  result.l=BltBitMapRastPort_(*scr\_BitMap,FromX,FromY,BMrp,0,0,InWidth,InHeight,$DCA)
Else
  result=0               ;ERROR! - couldn't allocate a bitmap...
EndIf

;ok, we are ready to open the window again

Window 0,WinX,WinY,WinWidth,WinHeight,$1|$2|$4|$8|$20,"TransparentTest",-1,-1

*ourwin = Peek.l(Addr Window(0))  ;find our window's structure
*rp=RastPort(0)                   ;and rastport

If result                         ;now copy the temp BitMap to our window...
  result.l=BltBitMapRastPort_(*bmap,0,0,*rp,Lborder,Tborder,InWidth,InHeight,$DCA)
EndIf

If *bmap
  FreeBitMap_ *bmap               ;we always must free this ourselves!
EndIf

If result=0
  Request "Trans-Test","Error...|Can't copy WB's bitmap","OK"
EndIf

Return

