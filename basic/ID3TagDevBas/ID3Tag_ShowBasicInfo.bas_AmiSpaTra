' *********************************************************************
'                       ID3Tag_ShowBasicInfo
'               by/de Copyright 2011 Dámaso D. Estévez
'                 {correoamidde-aminet000,yahoo,es}
'         AmiSpaTra - http://www.xente.mundo-r.com/amispatra/
'
'            All rights reserved over this derivative work:
'               my Hisoft Basic developpers package.
'      Forbidden to remove ALL legal/copyrights remarks included
'          in this package: if you create a derivate work,
'                 you MUST to include all legal notes.
'
'         This example prints some ID3Tag info from a MPEG file.
'                                 ----
'      Todos los derechos reservados sobre este trabajo derivado:
'   mi paquete para desarrolladores que programen con Hisoft Basic.
'         Está prohibido eliminar cualquier comentario o nota
'    de autoría/legal de este paquete: si crea un trabajo derivado
'       HA DE INCLUIR OBLIGATORIAMENTE TODAS LAS NOTAS LEGALES.
'
'   Este ejemplo muestra cierta información ID3Tag de un fichero MPEG.
' *********************************************************************

OPTION BASE 1

'           Break trap (for to avoid not release resources)
'         Trampa de errores (para evitar no liberar recursos)
' ---------------------------------------------------------------------
ON BREAK GOTO Salida

'        Compiler's metacommands (see the Hisoft Basic Manual)
' Metacomandos para el compilador (consulte el manual del Hisoft Basic)
' ---------------------------------------------------------------------
REM $NOWINDOW
REM $NOLIBRARY

'     Include files declaring functions, subroutines and constants
' Ficheros de inclusión que declaran funciones, subrutinas y contantes
' ---------------------------------------------------------------------
REM $include exec.bc                                      ' NULL&
REM $include dos.bc                                       ' RETURN_XXX&
REM $include utility.bc                                   ' TAG_DONE&
REM $include id3tag.bh

'    From/De:  http://aminet.net/package/dev/basic/AST_HBRoutines
'
'         Obtain a command line interface's argument (Shell)
'            and convert Basic's strings <-> C's strings.
'
'   Obtiene un argumento de la interfaz de línea de comandos (Shell)
'      y convierte cadenas del Basic <-> cadenas al estilo del C.
' ---------------------------------------------------------------------
REM $include BLib_AST/SGetArg.bas
REM $include BLib_AST/StrTo.bas

' *********************************************************************
'          Print the error (text) / Imprime el error (texto)
' *********************************************************************
SUB ShowE(BYVAL code&)

	'             No all errors type are included
	'     (the program don't write over the MPEG file ;)
	'
	'          No he incluido todos los tipos de error
	'     (el programa no escribe sobre el fichero MPEG ;)
	' ---------------------------------------------------------
	SELECT CASE code&

		CASE ID3TagErr_NoMpegaFile&

			PRINT "This isn't a MPEG file / No es un fichero MPEG"

		CASE ID3TagErr_NoMemory&

			PRINT "No memory! / ¡Memoria insuficiente!"

		CASE ID3TagErr_ObjectNotFound&

			PRINT "Object not found! / ¡Objeto no encontrado!"

		CASE ID3TagErr_ObjectInUse&

			PRINT "Object in use! / ¡Objeto en uso!"

		CASE ID3TagErr_ReadWrite&

			PRINT "Read-write / Lectura-escritura"

		CASE ID3TagErr_ReadProtected&

			PRINT "Read protected! / ¡Protegido contra lectura!"

		CASE ID3TagErr_UnknownTagArg&

			PRINT "Unknow tag arg! / ¡Atributo solicitado desconocido!"

		CASE ID3TagErr_UnknownRemType&

			PRINT "UnknownRemType! / ¡Tipo de borrado desconocido!"

		CASE ID3TagErr_UnknownVersion&

			PRINT "Unknown version! / ¡Versión desconocida!"

		CASE ID3TagErr_BufferToSmall&

			PRINT "Buffer too small! / ¡Tampón demasiado pequeño!"

		CASE ID3TagErr_InvalidHeader&

			PRINT "Invalid header! / ¡Cabecera inválida!"

		CASE ELSE

			PRINT "0x";RIGHT$("0000000"+HEX$(code&),8)

	END SELECT

END SUB

' *********************************************************************
'                Main "subroutine" / "Subrutina" principal
' *********************************************************************
FUNCTION Main&(BYVAL vstring$, idt&)
	LOCAL usage$, fichero$
	LOCAL r&, tags&(20), atribs&(9)

	'               Initial hypothesis / Hipótesis inicial
	' ---------------------------------------------------------------
	Main& = RETURN_FAIL&

	'                     Template / Sintaxis
	' ---------------------------------------------------------------
	usage$   = "FILE/A"

	'                Example for to use only from CLI
	' Ejemplo para utilizar sólo de la interfaz de línea de comandos
	' ---------------------------------------------------------------
	IF PEEKL(SYSTAB+8) <> 0 THEN
		BEEP
		EXIT FUNCTION
	END IF

	'  Obtaining the required arg / Obteniendo el argumento exigido
	' ---------------------------------------------------------------
	fichero$ = SGetArg$(1%,usage$)

	IF NOT FEXISTS(fichero$) THEN

		PRINT CHR$(27);"[1m";Str2B$(RIGHT$(vstring$,LEN(vstring$)-6));CHR$(27);"[0m"

		PRINT "  The program requires an MP3 file as argument!"
		PRINT "¡El programa exige un fichero MP3 como argumento!"
		EXIT FUNCTION

	END IF

	'       I create an ID3Tag object / Creo un objeto ID3Tag
	' ---------------------------------------------------------------
	idt& = ID3Tag_Alloc&

	IF idt& = NULL& THEN

		PRINT "I can't create the ID3Tag object!/¡Imposible crear el objeto ID3Tag!"
		EXIT FUNCTION

	END IF

	'  I fill the ID3Tag object with the info obtained from the file
	'      Relleno el objeto con información obtenida del fichero
	' ---------------------------------------------------------------
	r& = ID3Tag_Read&(idt&,SADD(Str2C$(fichero$)))

	IF r& <> NULL& THEN

		PRINT "Read error!/¡Error de lectura!: ";
		ShowE r&
		EXIT FUNCTION

	END IF

	'  Filling the taglist / Rellenado la lista de atributos a pedir
	' ---------------------------------------------------------------
	TAGLIST VARPTR(tags&(1)), _
		ID3Tag_Album&,      VARPTR(atribs&(1)), _
		ID3Tag_Artist&,     VARPTR(atribs&(2)), _
		ID3Tag_Year&,       VARPTR(atribs&(3)), _
		ID3Tag_Title&,      VARPTR(atribs&(4)), _
		ID3Tag_Track&,      VARPTR(atribs&(5)), _
		ID3Tag_Genre&,      VARPTR(atribs&(6)), _
		ID3Tag_Comment&,    VARPTR(atribs&(7)), _
		ID3Tag_TextWriter&, VARPTR(atribs&(8)), _
		ID3Tag_Composer&,   VARPTR(atribs&(9)), _
		TAG_END&

	'              Asking the attribs (and reusing r&)
	'           Pidiendo los atributos (y reutilizando r&)
	' ---------------------------------------------------------------
	r& = ID3Tag_GetAttrsA&(idt&, VARPTR(tags&(1)))

	IF r& <> NULL& THEN

		PRINT "Error obtaining the attribs!/¡Error al obtener los atributos!: ";
		ShowE r&
		EXIT FUNCTION

	END IF

	' Printing the "basic" info / Imprimiendo la información "básica"
	' ---------------------------------------------------------------
	PRINT "Information/Información"
	PRINT "-----------------------"
	PRINT "File       / Fichero    > ";fichero$
	PRINT "Album      / Álbum      > ";PEEK$(atribs&(1))
	PRINT "Artist     / Artista    > ";PEEK$(atribs&(2))
	PRINT "Textwriter / Letrista   > ";PEEK$(atribs&(8))
	PRINT "Composer   / Compositor > ";PEEK$(atribs&(9))
	PRINT "Year       / Año        > ";PEEK$(atribs&(3))
	PRINT "Title      / Título     > ";PEEK$(atribs&(4))
	PRINT "Genre      / Género     > ";PEEK$(atribs&(6))

	'           As you can see, the two last info fields
	'              don't appear if don't exists info.
	'
	'      Como puede ver, los dos últimos campos informativos
	'             no se muestran si no hay información.
	' ----------------------------------------------------------------
	IF atribs&(5) <> 0& THEN
		PRINT "Track #    / Pista nº   > ";atribs&(5)
	END IF

	IF atribs&(7) <> 0& THEN
		PRINT "Comment    / Comentario > ";PEEK$(atribs&(7))
	END IF

	'                         Done / Hecho
	' ----------------------------------------------------------------
	Main& = RETURN_OK&

END FUNCTION

' *********************************************************************
' ****************    Main code / Código principal    *****************
' *********************************************************************

'              C= string version / Cadena de versión de C=
' ---------------------------------------------------------------------
vstring$ = "$VER: ID3Tag_ShowBasicInfo 1.1 (25.08.2011) by Dámaso 'AmiSpaTra' Domínguez "+CHR$(0)

'      Pointer to struct ID3Tag / Puntero a la estructura ID3Tag
' ---------------------------------------------------------------------
idt& = NULL&

'            Opening the libraries / Abriendo las bibliotecas
' ---------------------------------------------------------------------
LIBRARY OPEN "id3tag.library", ID3TAGVERSION&

'         Doing the job (I don't like the shared vars specially
'                  => I pass them as arguments).
'
'            Haciendo el trabajo (no me gustan especialmente
'   las variables compartidas => Paso la información como argumentos).
' ---------------------------------------------------------------------
rt& = Main&(vstring$, idt&)

' <<<<<<<<<<<<<<<<>>>>>>>>>>>>>>>>>>
Salida:
' <<<<<<<<<<<<<<<<>>>>>>>>>>>>>>>>>>

BREAK STOP

'       Disabling the Break trap / Desactivando la trampa Break
' ---------------------------------------------------------------------
ON BREAK GOTO 0

'      Releasing the ID3Tag object / Liberando el objeto ID3Tag
' ---------------------------------------------------------------------
IF idt& THEN
	ID3Tag_Free idt&
END IF

BREAK ON

LIBRARY CLOSE

STOP rt&
