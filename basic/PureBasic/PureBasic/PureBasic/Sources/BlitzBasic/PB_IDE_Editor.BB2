; --------------------------------------------------------------------------------------
;
; This source file is part of PureBasic
; For the latest info, see http://www.purebasic.com/
; 
; Copyright (c) 1998-2006 Fantaisie Software
;
; This program is free software; you can redistribute it and/or
; modify it under the terms of the GNU General Public License
; as published by the Free Software Foundation; either version 2
; of the License, or (at your option) any later version.
;
; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
;
; You should have received a copy of the GNU General Public License
; along with this program; if not, write to the Free Software
; Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.
;
; --------------------------------------------------------------------------------------
;;
; PureBasic Integrated Editor
;
;   (c) 2005 - Fantaisie Software
;


; -- Need to figure out how to change menuitem states for checks (or hungarians ;)) )

; 08/03/05
;       - Remove the Until Joyb(0)=0 in the LocateMouse routine.. Joyb hits the hardware!
;         .. replaced with Until (NWindowEvent=#IDCMP_MOUSEBUTTONS)
;       - Added NewMouse support.. Mousewheel now works on source view.
;       - Fixed cancel/ok buttons in Find/StructureView not working ..DOH!
;       - Fixed keyup/down to update the vertical slider!

; 07/03/05
;       - Changed the BasicKeywords data to just be case correct keyword
;         .. the keyword data reader creates the lowercase keyword automatically.
;       - Moved Find and Structure View window handling into main loop, can now
;         .. continue typing source without having to close them !
;       - Alpha sorted the Structure list, also only done once..no need to get it everytime!


; 06/03/05
;       - Fixed save routine,it now adds a ".pb" when no extension was given.
;       - Fixed another Cut() bug, it was updating the wrong part of the source.
;         ..and giving the wrong variable to RefreshLines{}!
;       - Finally got async AmigaGuide help working :))
;       - Added block tabbing.. tab routine adds it to all selected lines.
;         ..also RAmiga+Tab = remove tabs
;       - Added block comment ,add= RAmiga +"]" ,remove =RAmiga+"["
;       - Fixed stupid OffsetY bug.. displayed the line below when offsety>0
;       - Fixed old cursor being left on screen when doing find/replace.

; 02/03/05
;       - Fixed old cursor being left on screen when a new source is loaded.
;       - Fixed bug of userlib commands not updating in proper case (introduced when I went from org.BB2 to AB2!)
;       - Finally fixed the stupid up/down cursor bug..
;       - Fixed GetCurrentWord(), was adding a leading space when CursorX=1 !
;       - Fixed Cut(), was leaving 2 blank lines where the source was..


;       Currently know bugs ???
;       . Cursor problem hiding text below..prob. not settign old cursor somewhere.
;       . Loading new source sometimes leaves artifact on screen..old cursor again??
;       . When loading source, some compiler settings aren`t reset?


; 27/02/05
;      - Fixed structure viewer.. bug in newtype for structure size (was .l, should be .w)
;        ..It meant you could only see the main list,(didn`t happen with the 2.9 release, but does with the orginal source.)
;        ..Also, fixed the viewer history to not add structure if the last one was the same type..eg List\minnode!
;
;      - Fixed bug with save source, a typo meant 'NoCliOutput' was always 0.
;      - Fixed bug with pressing enter in source, would leave old cursor on screen.
;      - Fixed text selection when starting point was the end of the text.
;      - Fixed text selection not clearing when clicking a new cursor position.
;      - Fixed paste problem, multiple lines would get merged to one line.
;      - Vert slider is now updated when window is scrolled via text selection.
;      - Added check to "Basic keyword matching", to only work if not in the middle of a string or comment.
;        .. just try typing 'a.s="for next if" ' in the old editor to see what I mean.
;
; 21/02/2005 Doobrey: Just some notes on conversion from BB2 To AmiBlitz.
;
;      - All includes have to be .bb2 tokenised !! otherwise they won`t get included :((
;
;  NB. nFile was given #198, but 'officially' this is identify.library wrapper lib
;      I`ve given it #115 as that should be a free lib number(doesn`t matter for ascii saved source though!)
;
;      - Double check your installed NWindow.obj =3748 bytes
;         released ver(size=3704) has a bug, nEventID always returns 0 !!
;
;      - Mostly seems To be lib #198 which is the missing nFile lib.
;      - Got missing lib #165 too..nMemory ! +167 nMisc ! Got both, but no joy :((
;      - Getting it going,added my own peeks for some bits..
;      - Changed var FilePart$ To FilePart.s Cos FilePart$ is a BLITZ command !!
;        ... that goes for PathPart$ too !!
;      - Changed all <var>$ to <var>.s just in case...
;      - Changed all the Gosubs to Functions/Statements..bit easier to read it all now.
;      - Moved most of the vars to structures, cuts down on all the Shared stuff.


;-------------------------------------------------------------------------------

; 02/06/2001
;   Added automatic quickhelp display.
;   Fixed a bit the Syntaxe autocasing :)
;   Fixed 2 bugs in the editor cursor (suppress and delete)
;
; 31/05/2001
;   Changed the preference system for each source
;   Changed a bit the behavior (enter now split the line, suppress and delete can join/concatenate 2 lines..)
;   Fixed 'Cut'
;   Fixed a selection bug
;   Added FindNext
;   Added AutoReLoad source at start
;   Added Online help
;   Added AutoCasing for PureFunctions and basic keywords..
;   Added keyboard Shortcut for some windows (Esc and Enter)
;   Added 'Enable Inline ASM'
;
; 31/01/2001 - Martin
;   Hopefully fixed a copy/paste crashing bug
;
; 30/01/2001 - Martin
;   Removed Delete to EOL because Shift+Del does the same :-)
;   Shift+Enter does now split the line
;
; 29/01/2001 - Martin
;   Fixed Shift+ArrowRight bug when scrolling
;   Added help menu
;   Added Join
;   Added Delete to EOL
;   Added pattern gadget to load/save source requesters
;   Added standard pattern to save source requester
;   Added message when a source file couldn't be loaded
;
; 10/01/2001
;   Fixed the icon copy bug
;
; 31/12/2000
;   Adapted for the new version of the compiler..
;   Improved some minors stuffs
;   Removed the 'PPC' option in the processors...
;
; 30/07/2000
;   Some bugs fixes...
;
; 24/07/2000
;   Fixed a little bug about this method.
;   EasyRequesters are displayed on the custom screen if any...
;
; 23/07/2000
;   Finally changed the wait method for better multitasking !
;
; 08/07/2000
;   Fixed a bug in Structure Viewer
;
; 02/07/2000
;   Added a nice Structure Viewer
;
; 30/06/2000
;   Moved the status window to compiler (so the editor isn't locked when compiling and
;   the compiler is slighty faster because no need for messages).
;   The faulty line is now displayed at the same time than the error message
;   Fixed the bug if the Shift+Arrow keys where pushed a long time..
;   Fixed a bug when pasting text
;
; 04/06/2000
;   Fixed a bug when creating an Executable (Path & FileName where destroyed)
;
; 02/06/2000
;   Added real 'Run' menu item and removed QuickRun which
;   interfer with 'CreateExecutable'...
;   Added ScreenMode selection !
;   Fixed a little 'Copy' bug.
;   Added 'ClearSelection
;   Added 68020+ support
;   Fixed a display bug when the window was resized.
;
; 21/05/2000
;   Finally added the 'Quick Run' feature. Works fine :)
;
; 20/05/2000
;   Added the ReCompile flag for 'Quick Run', but not implemented yet.
;
; 12/03/2000
;   Fixed a little bug in automatic tab feature.
;   Fixed a big bug in COPY/CUT routine..
;   Fixed a little memory bug (all memory wasn't free...)
;
; 08/03/2000
;   The cut feature is working ! Nice.
;   Added automatic indent function
;   Fixed some display bugs..
;   Removed the demo protection as it's useless (Compiler can be used with any editor)
;   Bug when we copy/cut the first line.. :-(.
;
; 07/03/2000
;   The Copy/Paste features are fully working ! Yeah :*)
;   Cut is on the way..
;   New routine for selection. Much slower but easier and safer.
;
; 05/03/2000
;   Changed 'Abord' -> 'Abort'
;   Fixed a big bug (NDisableGadget)
;
; 04/03/2000
;   Started to implement the cut/copy/paste feature...
;
; 06/02/2000
;   Added error support if an init error happen during
;   the PureBasic launch.
;
; 05/02/2000
;   Added confirmation requester for "Save As..."
;
; 12/12/1999
;   Fixed some bugs (reported by Richard)
;   Added 'Load' and 'New' confirmation requests..
;
; 05/12/1999
;   Added 'USE' and 'SAVE' feature working for CompilerOptions
;   Added 'NoCliOutput'
;   Added the SliderBars !!
;
; 25/11/1999
;   Added '.bak' feature
;   Fixed a cursor bug.
;
; 24/11/1999
;   Fixed a bug in Shift+ArrowUp
;   Fixed a bug in DeleteLine + Added 'Modified' flag support
;   Fixed a bug in 'Enter' stuff (window bottom was crunched..)
;   Fixed a big cursor bug.. Optimized a big the cursor stuffs
;   Added menu shortcut for CreateExecutable and CompilerOptions
;
; 20/11/1999
;   Added 'Modified' flags with the quit requester
;   Fixed some bugs in 'CutPath' stuffs
;   Added Help support
;   Removed 'RefreshWindow{}'
;   Finished 'Find' function ! Ho Yeaaaah, Editor is now ready
;   Added 'Real Tab' function !
;   Finished the multiLanguage Language support (with catalog support)
;
; 19/11/1999
;   Polished the work..
;   Added 'Preferences' support
;   Added 'New'
;   Revised 'Compile/Run' by passing all needed stuffs in the message
;   Added 'CreateIcon' stuff
;
; 18/11/1999
;   'Print' function now works.
;
; 17/11/1999
;   'CreateExecutable' works fully.
;
; 15/11/1999
;   Added the 'Compile Error' support !
;   The editor can now stop a compiling process.
;   Continued the 'Language()' support for future localization
;
; 13/11/1999
;   Added Message ports... Communication between PureBasic
;   and the editor is now possible !
;
; 12/11/1999
;   Added 'clipped text output' feature
;   Added 'OffsetX' so text can be scrolled horizontally.
;
; 11/11/1999
;   Added 'Print' Window
;   Added 'NbLines' for quicker access to number of lines..
;   Added UpdateStatus{}
;   Added 'Tab key' support
;   Fixed some bugs...
;
; 10/11/1999
;   Added 'Find' window layout
;   Added 'InsertBlock' fonction
;   Added 'DeleteLine' fonction (fully working !)
;
; 02/11/1999
;   Changed the 'LoadSource' routine (hundred times faster !)
;
; 24/10/1999
;   Added 'CreateExecutable'
;   Added 'Compile/Run'
;   Added Debugger support
;   'Compiler Options' works fully !
;   Fixed OpenWindow() (was a general bug)
;
; 23/10/1999
;   Finished 'Compiler Options'
;   Finished the text realtime scrolling
;   Added shifted arrow UP and DOWN (With special routine :-)
;   We can can write everywhere in the text area now.
;
; 22/10/1999
;   Started 'Compiler Options' window layout
;
; 19/10/1999
;   Added 2 other shifted functions
;   Added constants instead of fixed digit to menu IDs
;   Added Save/SaveAs routine. We have an editor which work :)
;
; 17/10/1999
;   Cleaned the code. Better looking but may be slower.
;   Added 'Shifted' functions
;
; 14/10/1999
;   Much better cursor routine. Very smart ! :-)
;   Added Dynamic lines.
;
; 13/10/1999
;   Good progress with Delete/Suppress fully working
;
; 10/10/1999
;   Start...
;

Jimi 2 ; For Type checking..

#LoadCompiler = 1 ; Set this to 0 for stand alone..

;------------------------------------------------------------

; Doobrey`s magical multilevel debugger ;)
; Prints more debug info depending on what level it`s set to.
; Set to 0 to disable.  ..currently some level 1-5 msgs.

#DebugLevel=1;2

Macro DPrint
 CNIF `1<=#DebugLevel
  NPrint `2
 CEND
End Macro

;------------------------------------------------------------

XINCLUDE "PB_IDE_Globals.bb2"


WBStartup

NInitGadget(#PB_IDE_MaxWindows)
NInitWindow(#PB_IDE_MaxWindows)

NInitScreen 1
NInitMenu 0,50
NInitFile 1
NInitASL
NInitFont 1
Locale.l = NInitLocale


*TagList.List = NInitTagList(20)

Old_Cursor.s=""

; These can be changed for dynamic lists.

Dim List Source.SourceCode(5000)
Dim LineLength.w(500)



Dim ProcessorList.PBProcList(3)
ProcessorList(0)\Text = "68000"
ProcessorList(1)\Text = "68020+"
;ProcessorList(2) = "PowerPc"

Dim Language.s(#LanguageStrings)

Dim ValidCharacters.b(256)

For k.w=48 To 57
  ValidCharacters(k) = 1
Next

For k.w=65 To 90
  ValidCharacters(k) = 1
Next

For k.w=97 To 122
  ValidCharacters(k) = 1
Next

ValidCharacters(95) = 1  ; '_'

Dim BasicKeyword.s(1, #BasicKeywords)


Restore BasicKeywordsDATA
For k.w=0 To #BasicKeywords
  Read BasicKeyword(1, k)
  BasicKeyword(0,k) = LCase$(BasicKeyword(1,k))
Next

;--Boopsi---

Dim *Arrows.Image(3)    ; Array to hold pointer to 4 Image's
Dim *Boopsi.Gadget(5)   ; Array to hold pointer to 6 gadgets (4 buttons + 2 scroll bars)

Dim Labels.s(2)

NbBoopsi.w = -1  ; This will handle all the new created boopsi
NbArrows.w = -1  ; NOTE: Init is -1, because if no gadgets are created,
               ; the For/Next loop will be executed at the end.

; Both of these used in creation of the border gadgets
DEFTYPE.Gadget *gadlist
DEFTYPE.Gadget *gad     : *gad = &*gadlist


DEFTYPE.DrawInfo  *dri              ; The drawing information of that screen
DEFTYPE.Image     *img              ; An image which represents the size gadget
DEFTYPE.l         sis               ; Size of image for either medium res or lo res screens
DEFTYPE.w         sizew,sizeh       ; sizes of the window resize gadget


SigMask.l=0
DefSigMask.l=0

;---------------------------------------------------------------------------

XINCLUDE "NewMouse.bb2"

XINCLUDE "PB_IDE_Common.bb2"
XINCLUDE "PB_IDE_ParseLine.bb2"

XINCLUDE "PB_IDE_Compiler.bb2"
XINCLUDE "PB_IDE_Display.bb2"

XINCLUDE "PB_IDE_SortStructures.bb2"

XINCLUDE "PB_IDE_StructureViewer.bb2"
XINCLUDE "PB_IDE_CopyPaste.bb2"
XINCLUDE "PB_IDE_Find.bb2"
XINCLUDE "PB_IDE_Requesters.bb2"
XINCLUDE "PB_IDE_Prefs.bb2"
XINCLUDE "PB_IDE_LoadSave.bb2"
XINCLUDE "PB_IDE_CompilerOpts.bb2"

XINCLUDE "PB_IDE_Help.bb2"

XINCLUDE "PB_IDE_KeyProcs.bb2"

;----------------------------------------------------------------------


*EditorPort = CreateMsgPort_
If *EditorPort
  EditorMessage\mn_ReplyPort = *EditorPort
Else
  End
EndIf

; Load Configuration

LoadPrefs{}

If PBGlobals\ASLWidth  = 0 Then PBGlobals\ASLWidth  = 200
If PBGlobals\ASLHeight = 0 Then PBGlobals\ASLHeight = 200

If PBPrefs\TabSize = 0 Then PBPrefs\TabSize = 2

;
; Screen setup
;


PublicScreenName.s = "PureBasic"

nag\nag_BaseName=&PublicScreenName

If PBPrefs\UseCustomScreen

  #PUBLICSCREEN=$0002

  NResetTagList #SA_DisplayID, PBPrefs\CustomScreenID
  ;If DefFontScreen=0
  ;  NResetTagList #SA_Font          ,&FAttr
  ;Else
  ;  NResetTagList #SA_Font          ,0
  ;EndIf

        NAddTag #SA_Type         , #PUBLICSCREEN
        NAddTag #SA_PubName      , &PublicScreenName
        NAddTag #SA_PubSig       , 0
        NAddTag #SA_PubTask      , FindTask_(0)
        NAddTag #SA_Pens         , ?_ScreenPens
        NAddTag #SA_Behind       , 1
        NAddTag #SA_LikeWorkbench, 1

  *MyScreen = NScreen(0, PBPrefs\CustomScreenWidth, PBPrefs\CustomScreenHeight, PBPrefs\CustomScreenDepth, *TagList)

  If *MyScreen
    PubScreenStatus_ *MyScreen, 0 ; Make the screen public...
  Else
    a = MessageBox{"Error", "The custom screen can't be opened...", 0,"Ok"}
    *MyScreen = NFindFrontScreen(0)
  EndIf

Else
  *MyScreen = NFindScreen(0,"Workbench")
  ;nag\nag_Screen=*MyScreen
EndIf

nag\nag_Lock=0

NShowScreen
NDrawingOutput NScreenRastPort

PBGlobals\ScreenBar    = NSBarHeight
PBMetrics\SFontHeight  = NSFontHeight
PBMetrics\WTitleHeight = NWBorderTop

PBGlobals\ScreenCenterW = NScreenWidth/2
PBGlobals\ScreenCenterH = NScreenHeight/2

*dri = GetScreenDrawInfo_(*MyScreen)

If *dri
  If (*MyScreen\Flags & #SCREENHIRES)
    sis=#SYSISIZE_MEDRES
  Else
    sis=#SYSISIZE_LOWRES
  EndIf

  NResetTagList #SYSIA_DrawInfo, *dri
        NAddTag #SYSIA_Which   , #SIZEIMAGE
        NAddTag #SYSIA_Size    , sis
  *img = NewObjectA_(0,"sysiclass",*TagList)
  If *img
    sizew = *img\Width
    sizeh = *img\Height
    DisposeObject_ *img
  End If

  ;
  ; Create the 4 arrow gadgets
  ;

  CreateArrowGadget{#BO_ArrowLeft , #LEFTIMAGE}
  CreateArrowGadget{#BO_ArrowRight, #RIGHTIMAGE}

  CreateArrowGadget{#BO_ArrowUp   , #UPIMAGE}
  CreateArrowGadget{#BO_ArrowDown , #DOWNIMAGE}

  ;
  ; Let's create our 2 sliders bars.
  ;

  CreateSlider{#BO_HorizontalSlider, #FREEHORIZ}
  CreateSlider{#BO_VerticalSlider  , #FREEVERT }
End If


;------------------------------------------------------------------
;
; Menu setup
;

Restore LanguageData
For k=0 To #LanguageStrings
  Read Language(k)
Next

If Locale
  If NOpenCatalog("PB_Editor.catalog",0)
    For k=0 To #LanguageStrings
      tmp.s = NGetCatalogString(k)
      If tmp
        Language(k) = tmp
      EndIf
    Next

    NCloseCatalog
  EndIf
EndIf


Labels(0) = Language(90)
Labels(1) = Language(91)


; shifted these to a struct
PBMetrics\CO_ProcessorW  = NTextLength(Language(22))+9
PBMetrics\FI_FindW       = NTextLength(Language(29))+9
PBMetrics\FI_ReplaceW    = NTextLength(Language(30))+9
PBMetrics\PS_PrintToW    = NTextLength(Language(38))+9
PBMetrics\PR_SourcePathW = NTextLength(Language(57))+9
PBMetrics\PR_AslWidthW   = NTextLength(Language(58))+9
PBMetrics\PR_AslHeightW  = NTextLength(Language(59))+9
PBMetrics\PR_TabSizeW    = NTextLength(Language(60))+9


Short_.s = ""

Short_Load.s  = "L"
Short_Save.s  = "S"
Short_Print.s = "P"
Short_Quit.s  = "Q"

Short_Cut.s    = "X"
Short_Copy.s   = "C"
Short_Paste.s  = "V"
Short_ClearSelection.s = "W"
Short_Delete.s = "D"
Short_Find.s   = "F"
Short_FindNext.s = "N"

Short_CompileRun.s = "R"

Short_StructureViewer.s  = "K"
Short_CompilerOption.s   = "O"
Short_CreateExecutable.s = "E"

Short_Help.s        = "H"





NMenuTitle &Language(0) ; Project
  NMenuItem #Menu_New        , &Language(55), 0
  NMenuItem #Menu_Load       , &Language( 1), &Short_Load
  NMenuItem #Menu_Save       , &Language( 2), &Short_Save
  NMenuItem #Menu_SaveAs     , &Language( 3), 0
  NMenuBar
  NMenuItem #Menu_Print      , &Language(21), &Short_Print
  NMenuItem #Menu_Preferences, &Language( 4), 0
;  NMenuItem #Menu_About      , &Language( 5), 0
  NMenuBar
  NMenuItem #Menu_Quit       , &Language( 6), &Short_Quit

NMenuTitle &Language(7)
  NMenuItem #Menu_Cut  , &Language( 8), &Short_Cut
  NMenuItem #Menu_Copy , &Language( 9), &Short_Copy
  NMenuItem #Menu_Paste, &Language(10), &Short_Paste
  NMenuBar
  NMenuItem #Menu_ClearSelection, &Language(88), &Short_ClearSelection
  NMenuItem #Menu_DeleteLine,     &Language( 11), &Short_Delete
  NMenuBar
  NMenuItem #Menu_InsertBlock, &Language(12), 0
;  NMenuItem 12, &Language(13), 0
;  NMenuItem 13, &Language(14), 0
  NMenuBar
  NMenuItem #Menu_Find    , &Language(15) , &Short_Find
  NMenuItem #Menu_FindNext, &Language(104), &Short_FindNext


NMenuTitle &Language(16)
  NMenuItem #Menu_CompileRun, &Language(17), &Short_CompileRun
  NMenuItem #Menu_Run       , &Language(87), 0
  NMenuBar
  NMenuCheckItem #Menu_Debugger, &Language(18), 0, 1
  NMenuBar
  NMenuItem #Menu_StructureViewer , &Language(93), &Short_StructureViewer
  NMenuItem #Menu_CompilerOptions , &Language(20), &Short_CompilerOption
  NMenuItem #Menu_CreateExecutable, &Language(19), &Short_CreateExecutable

NMenuTitle &Language(98)
  NMenuItem #Menu_Help,            &Language(98), &Short_Help
  NMenuItem #Menu_ReferenceManual, &Language(99), 0
  NMenuBar
  NMenuItem #Menu_About,           &Language( 5), 0

*ess.Menu = NCreateMenu(0, *MyScreen)


#IDCMP2 = #IDCMP_NEWSIZE | #IDCMP_CLOSEWINDOW | #IDCMP_IDCMPUPDATE

WindowTitle1.s = ""

NResetTagList #WA_Title       , &WindowTitle1
      NAddTag #WA_Flags       , #FLAGS2 | #WFLG_DRAGBAR | #WFLG_SIZEBBOTTOM | #WFLG_SIZEGADGET
      NAddTag #WA_IDCMP       , #IDCMP2 | #IDCMP_MENUPICK | #IDCMP_RAWKEY | #IDCMP_VANILLAKEY | #IDCMP_MOUSEBUTTONS
      NAddTag #WA_NewLookMenus, 1
      NAddTag #WA_MinWidth    , 100
      NAddTag #WA_MinHeight   , 100
      NAddTag #WA_ScreenTitle , &ScreenTitle
      NAddTag #WA_CustomScreen, *MyScreen
      NAddTag #WA_Gadgets     , *gadlist

*MainWin = NWindow(#Window_Main,0,PBGlobals\ScreenBar+100,NScreenWidth,NScreenHeight-PBGlobals\ScreenBar-1, *TagList)

UpdateStatus{""}

NAttachMenu 0, NWindowID

NDrawingOutput NWindowRastPort

NFrontColour 1

AddItem Source()

CNIF #LoadCompiler
  If LaunchPureBasic{}
    Goto End_Program
  EndIf
  !DPrint{1,"LaunchPB OK"}
CELSE
 !DPrint{1,"Editor in stand-alone mode"
CEND


SelectedZone\EndY = -1   ; No Selected zone...

PBMainWindow\NbLines = 1
PBMainWindow\TopText = NWBorderTop+5
OldBoopsi_OffsetX.w=0
OldBoopsi_OffsetY.w=0

DisplayLine{1}

PBCompiler\Debugger        = 1
PBCompiler\CommentedSource = 0
PBCompiler\Optimizations   = 0
PBCompiler\CreateIcon      = 0

PBMetrics\Proportional    = 1
PBGlobals\ReCompile       = 1

PBGlobals\Saved = 0

Default_Printer.s = "PRT:"

;
; Load the default saved font..
;
If NLoadFont(0, PBPrefs\EditFontName, PBPrefs\EditFontHeight)
  SetFont_ NWindowRastPort, NFontID
EndIf

SetupDisplay{}

If PBPrefs\ReLoadSource
  LoadSourceQuick{PBGlobals\FileName}
  UpdateBoopsi{}
  UpdateStatus{""}
EndIf


; Create the signal mask ! Must be implemented in PureBasic !
;
WindowSig.l=1 LSL *MainWin\UserPort\mp_SigBit
MsgSig.l   =1 LSL *EditorPort\mp_SigBit

SigMask.l = WindowSig|MsgSig ;|GuideSig
DefSigMask.l=SigMask

CloseMainWindow.b=0

!DPrint{1,"Entering Main loop"}

.Main
Repeat
 ; !DPrint{2,"MASK>"+Hex$(SigMask)}

  sigs.l=Wait_(SigMask) ; Really great wait routine.. Wait for both editor and compiler messages

  Repeat

   ; Wait loop now split into 3 parts..1 for each sigbit..

   If sigs& *HelpGuide\guidesignal
    !DPrint{3,"Guide MSG"}
     *agm.AmigaGuideMsg=AmigaGuideSignal_(*HelpGuide\guidecontext)
     If *agm
       GetAmigaGuideMsg_ *agm
     EndIf
   EndIf

    ; Message signal event.

   If sigs&MsgSig
      *Message = GetMsg_ (*EditorPort)

      NoRefresh.w = 0

      ; Here are computed the message send by the external compiler

      If *Message
        !DPrint{3,"Got message"}

        If *Message\mn_Length = #PB_MSG_Error     ; If an syntax error occurs, go to the
          PBCursor\X = 0                          ;
          PBCursor\Y = *Message\mn_Data1-2        ; right line !
          GotoLine{}                              ; UpdateStatus{""} is done here
          NoRefresh = 1
        EndIf

        If (*Message\mn_Length = #PB_MSG_Finished) AND( PBCompiler\CreateIcon = 1) AND (PBCompiler\CreateExecutable = 1)
          tmpval.w=NRun{"Copy PureBasic:Compilers/Default_Icon.info TO "+Chr$(34)+PBCompiler\Executable+".info"+Chr$(34), 4096}
          PBCompiler\CreateExecutable = 0
        EndIf

        ReplyMsg_ *Message   ; Only reply to msgs that aren`t replies !
      EndIf
   EndIf

   ;- Main Window signal event.

   If sigs&WindowSig

    IDCMP.l= NWindowEvent

    Select NWindowEventID

       Case #Window_Main

       If IDCMP

        Select IDCMP

          Case #IDCMP_IDCMPUPDATE

            Select GetTagData_(#GA_ID,0, NEventID)

              Case #BO_ArrowLeft

                If PBCursor\OffsetX > 0
                  PBCursor\OffsetX-1
                  RefreshLines{0, 1}
                EndIf


              Case #BO_ArrowRight

                If PBCursor\OffsetX < (PBMainWindow\MaxNbChars-PBMainWindow\NbWindowChars)
                  PBCursor\OffsetX+1
                  RefreshLines{0, 1}
                EndIf


              Case #BO_ArrowUp

                If PBCursor\OffsetY>0
                  PBCursor\OffsetY-1
                  PrevItem Source()
                  RefreshLines{0, 1}
                EndIf


              Case #BO_ArrowDown

                If PBCursor\OffsetY < (PBMainWindow\NbLines-PBMainWindow\NbWindowLines)
                  PBCursor\OffsetY+1
                  NextItem Source()
                  RefreshLines{0, 1}
                EndIf


              Case #BO_HorizontalSlider

                PBCursor\OffsetX = GetBoopsiTag{#BO_HorizontalSlider, #PGA_Top}

                If OldBoopsi_OffsetX <> PBCursor\OffsetX
                  OldBoopsi_OffsetX = PBCursor\OffsetX
                  RefreshLines{0, 1}
                EndIf

                NoRefresh = 1


              Case #BO_VerticalSlider
                If PBMainWindow\MouseDown=0 ; Stops updates being done twice during mousedown!
                  PBCursor\OffsetY = GetBoopsiTag{#BO_VerticalSlider, #PGA_Top}

                  If OldBoopsi_OffsetY <> PBCursor\OffsetY  ; Check this is Shared..+ setup!

                    OldBoopsi_OffsetY = PBCursor\OffsetY

                    FirstItem Source()

                    For k=1 To PBCursor\OffsetY+PBCursor\Y
                      NextItem Source()
                    Next

                    RefreshLines{0, 1}

                  EndIf

                  NoRefresh = 1
                EndIf

              End Select  ; - End of Select GetTagData_

          ;Case #NM_WHEEL_UP
          ;  !DPrint{2,"MOUSE UP"}


          Case #IDCMP_MOUSEBUTTONS
            LocateMouse{}        ;- Mental note.. This waits until mouse released..
            Repeat
              VWait
            ;  a = NWindowEvent
            ;Until a = 0
            Until (NWindowEvent=0)

          Case #IDCMP_NEWSIZE
            SetupDisplay{}


          Case #IDCMP_MENUPICK

            Select NEventID

              Case #Menu_New

                NoNew.w = 0
                If PBGlobals\Modified = 1
                  a = MessageBox{Language(68), Language(69)+Chr$(10)+Language(70), 0, Language(85)+"|"+Language(86)+"|"+Language(
}

                  Select a
                    Case 0
                      NoNew = 1

                    Case 2
                      If PBGlobals\Saved
                        SaveSource{}
                      Else
                        SaveSourceAs{}
                      EndIf

                  End Select
                EndIf


                If NoNew = 0

                  ClearList Source()
                  AddItem Source()
                  Source()\Text = ""

                  PBCursor\OffsetX = 0
                  PBCursor\X = 0
                  PBCursor\OffsetY = 0
                  PBCursor\Y = 0

                  SelectedZone\EndY = -1  ; No Selected zone...

                  PBGlobals\Modified  = 0
                  PBGlobals\ReCompile = 1

                  PBGlobals\SaveFileName = PBPrefs\SourcePath

                  PBMainWindow\NbLines   = 1
                  PBMainWindow\MaxNbChars = 1

                  RefreshLines{0, 1}

                EndIf


              Case #Menu_Load
                PBGlobals\InsertMode = 0    ; Check if this is shared...
                !DPrint{1,"Calling LoadSource()"}
                LoadSource{}

                ;*ess\FirstItem\Flags = *ess\FirstItem\Flags - #ITEMENABLED


              Case #Menu_Save

                ;*ess\FirstItem\Flags = *ess\FirstItem\Flags + #ITEMENABLED

                If PBGlobals\Saved
                  SaveSource{}
                Else
                  SaveSourceAs{}
                EndIf


              Case #Menu_SaveAs
                  SaveSourceAs{}


              Case #Menu_Print
                PrintSource{}


              Case #Menu_Preferences
                Preferences{}


              Case #Menu_About
                a = MessageBox{Language(67),PeekS(?AboutText),0,"Ok"}


              Case #Menu_Quit
                IDCMP = #IDCMP_CLOSEWINDOW


              Case #Menu_Cut
                Cut{}


              Case #Menu_Copy
                Copy{}


              Case #Menu_Paste
                Paste{}


              Case #Menu_ClearSelection
                SelectedZone\EndY = -1  ; No Selected zone...
                RefreshLines{0, 1}


              Case #Menu_DeleteLine
                DeleteLine{0}


              Case #Menu_InsertBlock
                PBGlobals\InsertMode = 1
                LoadSource{}


              Case #Menu_Find
                OpenFindWindow{}
                ;Was Find{}


              Case #Menu_FindNext
                PBFind\PreviousMode = 0
                StartSearch{}


              Case #Menu_CompileRun
                Old_Executable.s = PBCompiler\Executable  ; Doobrey: Check these!
                PBCompiler\Executable = ""
                CompileRun{}
                PBCompiler\Executable = Old_Executable

              Case #Menu_Run
                SendMessage{ #PB_MSG_RunExecutable }


              Case #Menu_StructureViewer
                OpenStructureViewer{}

              Case #Menu_Debugger
                PBCompiler\Debugger  = 1-PBCompiler\Debugger
                PBGlobals\ReCompile = 1


              Case #Menu_CompilerOptions
                CompilerOptions{}


              Case #Menu_CreateExecutable
                CreateExecutable{}


              Case #Menu_Help
                *HelpGuide=PB_OpenHelp{"PureBasic:PureBasic.guide","MAIN"}

              Case #Menu_ReferenceManual
                *HelpGuide=PB_OpenHelp{"PureBasic:Help/Reference.guide","MAIN"}

            End Select ;- End of menu select



          Case #IDCMP_RAWKEY
            PBMainWindow\PressedKey   = NGadgetCode
            PBMainWindow\QualifierKey = NQualifier+32768

            If (PBMainWindow\QualifierKey = 1) OR (PBMainWindow\QualifierKey = 513) OR (PBMainWindow\QualifierKey = 514)
              PBMainWindow\QualifierKey = 2  ;
            EndIf               ;  To map all the shift keys in one.

            CheckRawKey{}


          Case #IDCMP_VANILLAKEY
            PBMainWindow\PressedKey   = NGadgetCode
            PBMainWindow\QualifierKey = NQualifier+32768

            If PBMainWindow\PressedKey = 27
              IDCMP = #IDCMP_CLOSEWINDOW
            Else
              If (PBMainWindow\QualifierKey = 1) OR (PBMainWindow\QualifierKey = 513) OR (PBMainWindow\QualifierKey = 514)
                PBMainWindow\QualifierKey = 2  ;
              EndIf               ;

              CheckVanillaKey{}
            EndIf

        End Select  ; - End of Select IDCMP for Main Window.

        UpdateStatus{""}

        If IDCMP = #IDCMP_CLOSEWINDOW
          CloseMainWindow=1
          If PBGlobals\Modified = 1
            a = MessageBox{Language(68), Language(69)+Chr$(10)+Language(70), 0, Language(71)+"|"+Language(72)+"|"+Language(27)}

            Select a
              Case 0
                IDCMP = 0
                CloseMainWindow=0
              Case 2
                If PBGlobals\Saved
                  SaveSource{}
                Else
                  SaveSourceAs{}
                EndIf

            End Select
          EndIf
        EndIf


      EndIf  ;- Endif for if IDCMP for main window

      If NoRefresh = 0
        UpdateBoopsi{}
      EndIf

    Case #Window_Find
     HandleFindEvents{IDCMP}

    Case #Window_StructureViewer
     HandleStructureEvents{IDCMP}

    Default
     !DPrint{2,"EEK, unknown window event !! "+Str$(NWindowEventID)}
     !DPrint{2,"..EVENT>"+Str$(NWindowEvent)}
   End Select

  EndIf

 Until (IDCMP = 0 AND *Message = 0) OR CloseMainWindow  ;--( IDCMP = #IDCMP_CLOSEWINDOW)

Until CloseMainWindow

.End_Program

!DPrint{1,"Closing down"}

  If *StructureWindow Then CloseStructureWindow{}
  If *FindWindow Then CloseFindWindow{}

  ; Close down amigaguide !
  If *HelpGuide
    PB_CloseHelp{*HelpGuide}
  EndIf

  NCloseWindow #Window_Main


  ; Free scroll (border) gadgets
  ;
  For k=0 To NbBoopsi            ; No need to check, as only really created gadgets
    DisposeObject_ *Boopsi(k)    ; are referenced in *Boopsi
  Next                           ;

  ; Free images for scroll buttons
  ;
  For k=0 To NbArrows
    DisposeObject_ *Arrows(k)
  Next

  !DPrint{1,"Window+Boopsi closed"}

CNIF #LoadCompiler
  SendMessage{#PB_MSG_Quit}
  ;--Delay_(1000)
  !DPrint{1,"PB goes bye bye"}   ; Compiler locks up before here.

  If *EditorPort
    Forbid_

     Repeat
      edmsg.l= GetMsg_(*EditorPort)    ; Flush all message before the compiler quits..
      If edmsg
        ReplyMsg_(edmsg)
      EndIf
     Until edmsg=0

     DeleteMsgPort_ *EditorPort
    Permit_
  EndIf
CEND

;  !DPrint{1,"Finished with the msgport"}

  ; Doobrey: What is the editor doing ? *Compiler port is not ours to get from !!
  ;
  ;If *CompilerPort <> 0
  ;  While GetMsg_(*CompilerPort)  ; Remove all messages...
  ;  Wend                          ;
                                                                                                                            

  If PBPrefs\ReLoadSource
    SavePreferences{}
  EndIf

End


;--------------- Ta daaaaaaa -------------------

LanguageData:
Data.s "Project", "Load", "Save", "Save as...", "Preferences", "About", "Quit"
Data.s "Edit", "Cut", "Copy", "Paste", "Delete line", "Insert source file", "Indent block", "Forget block", "Search"
Data.s "Compiler", "Compile/Run", "Debugger", "Create executable", "Compilers options"
Data.s "Print..."

Data.s "Output processor:", "Source code optimizations", "Commented ASM output files", "Create an icon", "Save", "Cancel"

Data.s "Find...", "Find:", "Replace by:", "Case sensitive", "Replace all"
Data.s "Next", "Previous", "Replace", ""

Data.s "Print...","Print to:", "Print", ""

Data.s "Starting Compiling...", "Abort", "PureBasic", "Compiling... (Line", "Creating Executable"
Data.s "Line", "Stopping Compiler..."

Data.s "Loading 'PureLibraries'", "Loading 'OS Libraries'", "Loading 'Resident'"
Data.s "Welcome to...", "PureBasic Request...", "'PureBasic:Compilers/PBCompiler' isn't found", "Quit"

Data.s "New"

Data.s "Preferences...", "Source Path:", "ASL Width:", "Height:", "Tab Size:", "Save", "Use", ""

Data.s "New", "Line:", "Column:"

Data.s "About..."
Data.s "PureBasic", "The source file has been modified", "What do you want to do ?", "Quit", "Save & Quit", ""

Data.s "Saving", "Error...", "The file can't be saved", "Ok"
Data.s "Compiler options..."

Data.s "Information","No more entries found", "Ok"
Data.s "Disable CLI output"
Data.s "Load", "Save & Load", "New", "Save & New"
Data.s "Run", "Clear Selection"

Data.s "Editor font", "Public screen", "Custom screen", "Screen mode"
Data.s "Structure Viewer", "Structures Viewer", "Structures", "Parent", "Ok"

Data.s "Help", "Reference manual", "Load last source at start", "Delete to EOL"
Data.s "Loading", "The file can't be loaded", "Find Next", "Enable Inline ASM"

; For blockquote&blocktab..
Data.s "Block Comment","Block Uncomment"
Data.s "Block Tab","Block UnTab"


BasicKeywordsDATA:

Data.s "And"

Data.s "CallDebugger"
Data.s "Case"
Data.s "CompilerCase"
Data.s "CompilerDefault"
Data.s "CompilerElse"
Data.s "CompilerEndIf"
Data.s "CompilerEndSelect"
Data.s "CompilerIf"
Data.s "CompilerSelect"

Data.s "Default"
Data.s "DefType"
Data.s "Dim"
Data.s "DisableDebugger"

Data.s "Else"
Data.s "EnableDebugger"
Data.s "End"
Data.s "EndIf"
Data.s "EndProcedure"
Data.s "EndSelect"
Data.s "EndStructure"

Data.s "For"
Data.s "ForEver"

Data.s "Global"
Data.s "Gosub"
Data.s "Goto"

Data.s "If"
Data.s "IncludeBinary"
Data.s "IncludeFile"
Data.s "IncludePath"

Data.s "NewList"
Data.s "Next"

Data.s "Or"

Data.s "Procedure"
Data.s "ProcedureReturn"

Data.s "Repeat"
Data.s "Return"

Data.s "Select"
Data.s "Shared"
Data.s "Structure"

Data.s "To"

Data.s "Until"

Data.s "Wend"
Data.s "While"

Data.s "XIncludeFile"





_ScreenPens:
Dc.w 0,0
Dc.w 1   ; Text on background
Dc.w 2   ; Bright edge on 3d objects
Dc.w 1   ; Dark edge on 3d objects
Dc.w 3   ; activew window/select gadgetfill
Dc.w 1   ; text over FILLPEN (filltextpen)
Dc.w 0   ; Backgroundpen
Dc.w 2   ; Special colour text
Dc.w 1   ; Text/deail in screen-bar/menus
Dc.w 2   ; Screenbar/menusfill
Dc.w 1   ; Trim under screenbar
Dc.w 12  ; Number of pens
Dc.w 1   ; Do I need it?

