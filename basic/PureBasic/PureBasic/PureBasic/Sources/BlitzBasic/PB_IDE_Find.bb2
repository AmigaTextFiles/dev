; --------------------------------------------------------------------------------------
;
; This source file is part of PureBasic
; For the latest info, see http://www.purebasic.com/
; 
; Copyright (c) 1998-2006 Fantaisie Software
;
; This program is free software; you can redistribute it and/or
; modify it under the terms of the GNU General Public License
; as published by the Free Software Foundation; either version 2
; of the License, or (at your option) any later version.
;
; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
;
; You should have received a copy of the GNU General Public License
; along with this program; if not, write to the Free Software
; Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.
;
; --------------------------------------------------------------------------------------
;
;-- PB Editor Find/Search functions..



Statement StartSearch{}
  SHARED Source(),PBFind,Language()
  SHARED ReplaceWord.s,PBCursor,PBGlobals

  *Old_Source.l = Source()   ;- Should this be &Source() ??

  CaseSense PBFind\CaseSensitive

  NUseWindow #Window_Main

  If PBFind\ReplaceAll

    If PBFind\IDE_FindWord
      PBGlobals\Modified  = 1
      PBGlobals\ReCompile = 1

      Repeat
        Source()\Text = Replace$(Source()\Text, PBFind\IDE_FindWord,PBFind\ReplaceWord)
      Until NextItem(Source()) = 0
    EndIf

    SetItem Source(), *Old_Source

    RefreshLines{0, 1}

  Else

    PBFind\Found = 0

    o_CursorY.w = PBCursor\Y

    If PBFind\PreviousMode

      While PBFind\Found = 0
        If PrevItem(Source())
          PBFind\Found = Instr(Source()\Text,PBFind\IDE_FindWord)
          PBCursor\Y-1
        Else
          PBFind\Found = -1
        EndIf
      Wend

    Else

      If PBFind\FirstSearch = 0
        If NextItem(Source())
          PBCursor\Y+1
        Else
          PBFind\Found = -1
        EndIf
      EndIf

      PBFind\FirstSearch = 0

      While PBFind\Found = 0

        PBFind\Found = Instr(Source()\Text,PBFind\IDE_FindWord)

        If PBFind\Found = 0
          If NextItem(Source())
            PBCursor\Y+1
          Else
            PBFind\Found = -1
          EndIf
        EndIf
      Wend

    EndIf

    If PBFind\Found > 0

      NDisableGadget #FI_Replace, 0

      DeleteOldCursor{}

      PBCursor\X = PBFind\Found
      PBCursor\Y = PBCursor\OffsetY+PBCursor\Y

      GotoLine{}

    Else
      SetItem Source(), *Old_Source
      PBCursor\Y = o_CursorY
      a.w = MessageBox{Language(79), Language(80),0, Language(81)}
    EndIf
  EndIf

End Statement

;--------------------------------------------------------------------------------

Statement CloseFindWindow{}
 SHARED *FindWindow

  NCloseWindow #Window_Find
  NFreeGadgetList #Window_Find
  NUseWindow  #Window_Main
  NActivate   ;#Window_Main
  *FindWindow=0

End Statement

;--------------------------------------------------------------------------------

Statement OpenFindWindow{}
  SHARED *MyScreen,*TagList,Language(),PBMetrics,*MainWin
  SHARED PBGlobals,PBFind,Source(),*FindWindow

  If *FindWindow=0

    WindowW.w = 440-20

    If NCreateGadgetList(#Window_Find, *MyScreen)

      HG.w  = PBMetrics\WTitleHeight+5
      HF.w  = PBMetrics\SFontHeight+5
      HF2.w = HF+4
      XG.w  = 10

      NSetGadgetFlags #PLACETEXT_LEFT

      NResetTagList #GTST_MaxChars, 100
            NAddTag #GTST_String  , &PBFind\IDE_FindWord

      NStringGadget #FI_Find, XG+PBMetrics\FI_FindW, HG, WindowW-PBMetrics\FI_FindW, HF, Language(29), *TagList
      HG+HF2

      NResetTagList #GTST_MaxChars, 100
            NAddTag #GTST_String  , &PBFind\ReplaceWord
      NStringGadget #FI_ReplaceBy, XG+PBMetrics\FI_ReplaceW, HG, WindowW-PBMetrics\FI_ReplaceW, HF, Language(30), *TagList
      HG+HF2

      NSetGadgetFlags #PLACETEXT_RIGHT

      NResetTagList   #GTCB_Scaled , PBMetrics\Proportional
            NAddTag   #GTCB_Checked, PBFind\CaseSensitive
      NCheckBoxGadget #FI_CaseSensitive, XG, HG, HF, HF, Language(31), *TagList
      HG+HF2+8

      Old_HG.w = HG

      NSetGadgetFlags #PLACETEXT_IN

      a.w = (WindowW/5)-8
      NButtonGadget #FI_Next      , XG         , HG, a, HF, Language(33), 0
      NButtonGadget #FI_Previous  , XG+a+10    , HG, a, HF, Language(34), 0
      NResetTagList #GA_Disabled  , 1
      NButtonGadget #FI_Replace   , XG+(a+10)*2, HG, a, HF, Language(35), *TagList
      NButtonGadget #FI_ReplaceAll, XG+(a+10)*3, HG, a, HF, Language(32), 0
      NButtonGadget #FI_Cancel    , XG+(a+10)*4, HG, a, HF, Language(27), 0

      HG+HF2+5

      *FindWindow= OpenStandardWindow{#Window_Find, WindowW+20, HG, 28}

      !DPrint{2,"FW_Sig>"+Str$(*FindWindow\UserPort\mp_SigBit)}
      !DPrint{2,"MW_Sig>"+Str$(*MainWin\UserPort\mp_SigBit)}
      If *FindWindow=0
        NFreeGadgetList #Window_Find
      EndIf
    EndIf
  EndIf


  If *FindWindow
      ; Put Find window back in front...
      WindowToFront_ *FindWindow
      ActivateWindow_ *FindWindow
      NActivateGadget #FI_Find

      Rect{NWBorderLeft-1,Old_HG-6,NWInnerWidth+2,2,1}

      PBFind\FirstSearch = 1
      PBFind\ReplaceAll  = 0
  EndIf
  NUseWindow #Window_Main
End Statement

;--------------------------------------------------------------------------------

Statement HandleFindEvents{IDCMP.l}
 SHARED PBFind,PBGlobals,Source()

  ;--IDCMP.l = NWindowEvent


  PBFind\IDE_FindWord = NGetStringText(#FI_Find)
  PBFind\ReplaceWord  = NGetStringText(#FI_ReplaceBy)

  If IDCMP
    Select IDCMP
      !DPrint{2,"FE>"+Hex$(IDCMP)}

      Case #IDCMP_GADGETUP
        Code.l= NGadgetCode

        Select NEventID

          Case #FI_CaseSensitive
            PBFind\CaseSensitive = Code


          Case #FI_Replace
            PBGlobals\Modified  = 1
            PBGlobals\ReCompile = 1
            Source()\Text = Replace$(Source()\Text, PBFind\IDE_FindWord, PBFind\ReplaceWord)
            StartSearch{}


          Case #FI_ReplaceAll
            PBFind\ReplaceAll = 1
            StartSearch{}


          Case #FI_Cancel
            ;--IDCMP = #IDCMP_CLOSEWINDOW    Gah..not getting into select!
            CloseFindWindow{}

          Case #FI_Next
            PBFind\PreviousMode = 0
            StartSearch{}


          Case #FI_Previous
            PBFind\PreviousMode = 1
            StartSearch{}

        End Select  ; Select NEventID

      Case #IDCMP_VANILLAKEY

        Select Code
          Case 13
            PBFind\PreviousMode = 0
            StartSearch{}

          Case 27
            ;--IDCMP = #IDCMP_CLOSEWINDOW
            CloseFindWindow{}
        End Select  ; Select Code

      Case #IDCMP_CLOSEWINDOW
        CloseFindWindow{}

    End Select      ; Select IDCMP
  EndIf

End Statement

