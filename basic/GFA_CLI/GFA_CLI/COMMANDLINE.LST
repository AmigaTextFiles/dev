DIM arg$(127)
REM *** für mehr Argumente ist ggf. RESERVE nötig ***
i%=@numargs
END
FUNCTION commandline$
  DIM m%(16)
  d$="dos.library"+CHR$(0)
  d%=V:d$
  db%=OpenLibrary(d%,36)
  c$=""
  IF db%<>0
    m%(6)=db%
    RCALL db%-534,m%()
    c%=m%(0)
    c$=CHAR{c%}
    ~CloseLibrary(db%)
  ENDIF
  ERASE m%()
  IF RIGHT$(c$,1)=CHR$(10)
    c$=LEFT$(c$,LEN(c$)-1)
  ENDIF
  c$=TRIM$(c$)
  RETURN c$
ENDFUNC
FUNCTION numargs
  FOR t%=1 TO 127
    arg$(t%)=""
  NEXT t%
  dummy%=0
  quote%=0
  arg$=" "+@commandline$
  FOR t%=2 TO LEN(arg$)
    a$=MID$(arg$,t%,1)
    IF a$=CHR$(34)
      IF (quote%=0)
        quote%=-1
        dummy%=dummy%+1
      ELSE
        quote%=0
      ENDIF
    ELSE
      IF a$=" " AND quote%=0
        a$=""
      ENDIF
      IF ((MID$(arg$,t%-1,1)=" ") AND (quote%=0) AND (a$<>""))
        INC dummy%
      ENDIF
      IF a$<>CHR$(34)
        arg$(dummy%)=arg$(dummy%)+a$
      ENDIF
    ENDIF
  NEXT t%
  RETURN dummy%
ENDFUNC
