WBStartup

DEFTYPE.l Port,rmsg

MenuTitle 0,0,"Project"
MenuItem 0,0,0,0,"About"
MenuItem 0,0,0,1,"Quit"

FindScreen 0
Use Screen 0
Window 0,0,0,WBWidth,WBHeight,$1004,"ARexx Test - [Port: test]",1,2
Use Window 0
SetMenu 0

Port=CreateMsgPort ("test")
If Port
  a$=""
  Repeat
    Wait
    rmsg.l=RexxEvent(Port)
    ev.l=Event
    If IsRexxMsg (rmsg)
      a$=GetRexxCommand (rmsg,1)
      Print "ARexx> "
      NPrint a$
      If a$="QUIT"
        ende.b=True
      EndIf
      ReplyRexxMsg rmsg,0,0,""
    EndIf
    If ev.l=256
      Select MenuHit
      Case 0
        Select ItemHit
        Case 0
          Request "ARexx Test","ARexx Port: test","OKAY"
        Case 1
          ende.b=True
        End Select
      End Select
      SetMenu 0
    EndIf
  Until (ende.b=True)
  DeleteMsgPort Port
EndIf
End

;The following sub program must be called before the marked line
;to split the ARexx command into one command and several arguments.

;Input:   rexx$ contains the current ARexx command (obtained via
          GetRexxMsg (rmsg.l, 1)
;Results: cmd$ contains the command, rexx$() contains the
;         arguments. (The content of t.l is altered.)
;Note:    Add the following line to your program:
;           Dim rexx$(16)

.processrexxmsg:
  For t.l=0 To 16
    rexx$(t.l)=""
  Next t
  cmd$=""
  dummy.l=0
  quote.b=False
  For t.l=1 To Len(rexx$)
    a$=Mid$(rexx$,t,1)
    If a$=Chr$(34)
      If quote=False
        quote=True
        dummy.l=dummy.l+1
      Else
        quote=False
      End If
    Else
      If ((a$=" ") AND (quote=False)) Then a$=""
      If ((Mid$(rexx$,t-1,1)=" ") AND (quote=False)) Then dummy.l=dummy.l+1
      If a$<>Chr$(34) Then rexx$(dummy.l)=rexx$(dummy.l)+a$
    End If
  Next t
  cmd$=rexx$(0)
Return

