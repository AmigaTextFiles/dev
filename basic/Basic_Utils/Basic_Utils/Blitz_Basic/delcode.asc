WBStartup
If FromCLI=False
  End
EndIf

Goto skipver
  Even
  v: Dc.b "$VER: DelCode 1.05 (21.07.97)",0
  Even
skipver:

If NumPars = 0
  PutStr_ Left$(Peek$(?v + 6),12)+" (C) 1995-1997 by FR-SoftWorks."+Chr$(10)
  PutStr_ Chr$(10)
  PutStr_ "Usage: delcode <file> [<string...>] [-l] [-t<n>] [-i]"+Chr$(10)
  PutStr_ Chr$(10)
  PutStr_ "Deletes control characters and duplicate blank lines. '-@' will be"+Chr$(10)
  PutStr_ "added to the file name."+Chr$(10)
  PutStr_ Chr$(10)
  PutStr_ "All lines containing the optional string(s) are also deleted."+Chr$(10)
  PutStr_ Chr$(10)
  PutStr_ "Option '-l': All blank lines will be deleted."+Chr$(10)
  PutStr_ Chr$(10)
  PutStr_ "Option '-t': All Tab (ASCII 9) characters are replaced by <n> blanks."+Chr$(10)
  PutStr_ Chr$(10)
  PutStr_ "Option '-i': Case sensitivity is disabled."+Chr$(10)
  End
EndIf

f1$=Par$(1)
f2$=f1$+".@"

If NumPars > 1
  For i.l = 2 To NumPars
    If UCase$(Left$(Par$(i.l),2))="-T"
      tab.l=Val(Mid$(Par$(i.l),3))
    EndIf
    If UCase$(Left$(Par$(i.l),2))="-I"
      schrift.l = 1
    EndIf
  Next
EndIf

If ReadFile(1,f1$)
  FileInput 1
  If WriteFile(2,f2$)
    FileOutput 2
    b$=Chr$(10)
    Repeat
      c$=Chr$(10)
      a$=Edit$(1024)
      For i.l = 1 To Len(a$)
        d$=Mid$(a$,i.l,1)
        If d$=Chr$(9)
          d$=SPACE$(tab.l)
        EndIf
        If (Asc(d$) AND 127) >31
          c$=c$+d$
        EndIf
      Next
      If c$<>b$
        ja.b = 1
        If NumPars > 1
          For i.l = 2 To NumPars
            If schrift.l = 1
              If Instr(UCase$(c$),UCase$(Par$(i.l)))
                ja.b = 0
              EndIf
            Else
              If Instr(c$,Par$(i.l))
                ja.b = 0
              EndIf
            EndIf
            If ((UCase$(Par$(i.l))="-L") AND (c$=Chr$(10)))
              ja.b = 0
            EndIf
          Next
        EndIf
        If ja.b = 1
          PutStr_ c$
        EndIf
      EndIf
      b$=c$
    Until Eof(1)
    CloseFile 2
  Else
    PutStr_ "Error writing file."+Chr$(10)
  EndIf
  CloseFile 1
Else
  PutStr_ "File not found."+Chr$(10)
EndIf
End

