WBStartup

Goto SkipVer
  Even
  Dc.b "$VER: Memory Difference 36.3 (25.07.97)",0
  Even
SkipVer:

FindScreen 0
Use Screen 0

TextGadget 0,20,65,0,0,"Start"
TextGadget 0,150,65,0,1,"Stop"
TextGadget 0,280,65,0,2,"Quit"

Window 0,0,0,340,135,$100e,"Memory Difference",1,2,0
NPrint "To measure the memory usage of a program,"
NPrint "perform the following steps:"
NPrint ""
NPrint "(1)  Hit the Start button."
NPrint "(2)  Start the program to be examined."
NPrint "(3)  Hit the Stop button."
Delay_ 10
c.l = AvailMem_(3)
f.l = AvailMem_(5)
Repeat
  ev.l=WaitEvent
  If ev.l=$40
    Select GadgetHit
    Case 0
      Gosub clrscr
      Delay_ 10
      c.l = AvailMem_(3)
      f.l = AvailMem_(5)
    Case 1
      Gosub clrscr
      WLocate 20,80
      Print "Chip Memory used: ",c - AvailMem_(3)," bytes"
      WLocate 20,100
      Print "Fast Memory used: ",f - AvailMem_(5)," bytes"
    Case 2
      CloseWindow 0
      End
    End Select
  End If
Until ev.l=$200
CloseWindow 0
End

clrscr:
  WLocate 20,80
  Print SPACE$(30)
  WLocate 20,100
  Print SPACE$(30)
Return

