*locl.l=OpenLocale_ (0)
If (*locl <> 0)
  mpdecpoint$=Peek$(Peek.l(*locl + 96))
  mpdecgroup$=Peek$(Peek.l(*locl + 100))
  mpdecsize.l=Peek.b(Peek.l(*locl + 108))
  CloseLocale_ (*locl)
Else
  mpdecpoint$="."
  mpdecgroup$=""
  mpdecsize.l=3
End If

Function.s RFmt{a.f}
; This function formats a real number according to the current Locale
; settings.
  SHARED mpdecpoint$,mpdecgroup$,mpdecsize.l
  d$=""
  a$=UStr$(a.f)
  d.l=Instr(a$,".")
  If (d.l <> 0)
    b$=Left$(a$,d-1)
    c$=Mid$(a$,d+1)
  Else
    b$=a$
    c$=""
  End If
  If (Len(b$) > mpdecsize.l)
    d.l=1
    For t.l=Len(b$) To 1 Step -1
      d$=Mid$(b$,t,1)+d$
      If ((d MOD mpdecsize.l) = 0) Then d$=mpdecgroup$+d$
      d+1
    Next t
  Else
    d$=b$
  End If
  If c$ <> "" Then d$=d$+mpdecpoint$+c$
  If Left$(d$,1)=mpdecgroup$ Then d$=Mid$(d$,2)
  Function Return d$
End Function

Function.s IFmt{a.l}
; This function formats an integer number according to the current Locale
; settings.
  SHARED mpdecgroup$,mpdecsize.l
  d$=""
  a$=UStr$(a.l)
  b$=a$
  c$=""
  If (Len(b$) > mpdecsize.l)
    d.l=1
    For t.l=Len(b$) To 1 Step -1
      d$=Mid$(b$,t,1)+d$
      If ((d MOD mpdecsize.l) = 0) Then d$=mpdecgroup$+d$
      d+1
    Next t
  Else
    d$=b$
  End If
  If Left$(d$,1)=mpdecgroup$ Then d$=Mid$(d$,2)
  Function Return d$
End Function

