OPENW #0,0,0,400,200,512+8,4108! *** Use "4108 or &H200000" for WB 3.0+ NewLook
DEFFN esc$=CHR$(27)
DEFFN ris$=@esc$+"c"
DEFFN ak1$=@esc$+"[3m"
DEFFN au1$=@esc$+"[4m"
DEFFN an$=@esc$+"[0m"
DEFFN af1$=@esc$+"[1m"
DEFFN rev$=@esc$+"[7m"
DEFFN help$=CHR$(155)+CHR$(63)+CHR$(126)
DEFFN hochc$=CHR$(155)+"A"
DEFFN runter$=CHR$(155)+"B"
DEFFN rechts$=CHR$(155)+"C"
DEFFN links$=CHR$(155)+"D"
DEFFN f1$=CHR$(155)+"0"+CHR$(126)
DEFFN f2$=CHR$(155)+"1"+CHR$(126)
DEFFN f3$=CHR$(155)+"2"+CHR$(126)
DEFFN f4$=CHR$(155)+"3"+CHR$(126)
DEFFN f5$=CHR$(155)+"4"+CHR$(126)
DEFFN f6$=CHR$(155)+"5"+CHR$(126)
DEFFN f7$=CHR$(155)+"6"+CHR$(126)
DEFFN f8$=CHR$(155)+"7"+CHR$(126)
DEFFN f9$=CHR$(155)+"8"+CHR$(126)
DEFFN f10$=CHR$(155)+"9"+CHR$(126)
DIM m$(20)
m$(0)="Project"
m$(1)="About       "
m$(2)="-­­­­­­­­­­­­"
m$(3)="Quit...     "
m$(4)=""
m$(5)="Test"
m$(6)="Sub Items »"
m$(7)="!1"
m$(8)="!2"
m$(9)=""
m$(10)=""
MENU m$()
MENU KEY 1,ASC("A")
MENU KEY 3,ASC("Q")
'
maxgadgets%=100
rp%=LPEEK(WINDOW(0)+50)
fontheight%=DPEEK(rp%+58)!8
fontwidth%=DPEEK(rp%+60)!8
lastgadget%=1
einf!=TRUE
'
DIM x1%(maxgadgets%),x2%(maxgadgets%),y1%(maxgadgets%),y2%(maxgadgets%),g$(maxgadgets%),s$(maxgadgets%),l%(maxgadgets%)
@button(1,10,20,50,16,"OK")
@string(2,10,50,80,16,"Hello!",100)
@checkbox(3,10,70,2*fontwidth%,2*fontheight%,TRUE,"Checkbox test")
@hslide(4,30,90,50,10,0,5,5)
@vslide(5,10,105,10,50,0,5,0)
@invisible(6,10,10,9,9)
@arrowleft(7,19,90,9,9)
@arrowright(8,81,90,9,9)
REPEAT
  msg=WaitPort(LPEEK(WINDOW(0)+86))
  msg=GetMsg(LPEEK(WINDOW(0)+86))
  WHILE (msg)
    class%=LPEEK(msg+20)
    code%=DPEEK(msg+24)
    ia%=LPEEK(msg+28)
    mx%=DPEEK(msg+32)
    my%=DPEEK(msg+34)
    ~ReplyMsg(msg)
    IF HEX$(class%)="8" ! MOUSEBUTTON
      IF code%=232 ! Button released
        g%=@gadgethit(mx%,my%)
        IF g%<>-1
          ALERT 0,"Gadget "+STR$(g%),1,"OK",nk%
        ENDIF
        IF g%=2
        @handlestring(2)
        ENDIF
        IF g%=3
        @checktoggle(3)
        ENDIF
        IF g%=4
        @handlehslider(4)
        ENDIF
        IF g%=5
        @handlevslider(5)
        ENDIF
        IF g%=7
        @sethslider(4,l%(4)-1)
        ENDIF
        IF g%=8
        @sethslider(4,l%(4)+1)
        ENDIF
      ENDIF
    ENDIF
    IF HEX$(class%)="200" ! *** CLOSEWINDOW
      CLOSEW #0
      EDIT
    ENDIF
    IF HEX$(class%)="100" ! *** MENUPICK
      i$=RIGHT$(STRING$(16,"0")+BIN$(code%),16)
      s%=VAL("%"+MID$(i$,1,5)) ! *** sub item
      i%=VAL("%"+MID$(i$,6,6)) ! *** menu item
      m%=VAL("%"+MID$(i$,12,5)) ! menu
      SELECT m%
      CASE 0 ! *** First menu (# 0)
        SELECT i%
        CASE 0 ! *** About
          ALERT 0,"System friendly|event handling!",1,"Continue",nk%
        CASE 1 ! *** Separator
          '
        CASE 2 ! *** Quit...
          ALERT 0,"Quit demo?",0,"Yes|No",nk%
          IF nk%=1
            CLOSEW #0
            EDIT
          ENDIF
        DEFAULT
        ENDSELECT
      CASE 1 ! *** Second menu (# 1)
        SELECT i%
        CASE 0 ! *** Sub Item...
          SELECT s%
          CASE 0
            ALERT 0,"Sub item #1",1,"Continue",nk%
          CASE 1
            ALERT 0,"Sub item #2",1,"Continue",nk%
          DEFAULT
          ENDSELECT
        DEFAULT
        ENDSELECT
      DEFAULT
      ENDSELECT
    ENDIF
    msg=GetMsg(LPEEK(WINDOW(0)+86))
  WEND
UNTIL FALSE
EDIT
> PROCEDURE button(nr%,x%,y%,w%,h%,txt$)
IF nr%<=maxgadgets%
  IF nr%>lastgadget%
    lastgadget%=nr%
  ENDIF
  x1%(nr%)=x%
  x2%(nr%)=x%+w%
  y1%(nr%)=y%
  y2%(nr%)=y%+h%
  g$(nr%)=txt$
  s$(nr%)=""
  l%(nr%)=0
  ~SetAPen(rp%,2)
  ~Move(rp%,x1%(nr%),y2%(nr%))
  ~Draw(rp%,x1%(nr%),y1%(nr%))
  ~Draw(rp%,x2%(nr%),y1%(nr%))
  ~SetAPen(rp%,1)
  ~Draw(rp%,x2%(nr%),y2%(nr%))
  ~Draw(rp%,x1%(nr%),y2%(nr%))
  g$=g$(nr%)
  x%=(x2%(nr%)-x1%(nr%)-LEN(g$)*fontwidth%)/2+x1%(nr%)
  y%=(y2%(nr%)-y1%(nr%)+fontheight%)/2+y1%(nr%)
  ~Move(rp%,x%,y%)
  ~Text(rp%,V:g$,LEN(g$))
ENDIF
RETURN
> PROCEDURE string(nr%,x%,y%,w%,h%,def$,l%)
IF nr%<=maxgadgets%
IF nr%>lastgadget%
  lastgadget%=nr%
ENDIF
x1%(nr%)=x%
x2%(nr%)=x%+w%
y1%(nr%)=y%
y2%(nr%)=y%+h%
g$(nr%)=""
l%(nr%)=l%
@setstring(nr%,def$)
ENDIF
RETURN
> FUNCTION gadgethit(mx%,my%)
LOCAL t%,gh%
gh%=-1
FOR t%=1 TO lastgadget%
IF ((mx%>=x1%(t%)) AND (mx%<=x2%(t%)) AND (my%>=y1%(t%)) AND (my%<=y2%(t%)))
gh%=t%
ENDIF
NEXT t%
RETURN gh%
ENDFUNC
> FUNCTION fixxl$(fil%,a$,fiz%,fis%)
' Ersatz für Form Input: Länge, Vorgabe, Zeile, Spalte
' Globale Variable einf! steuert Einfügemodus
' mit <Help> wird eine Eingabehilfe angezeigt (mbhilfe$)
' Maximal 53 Zeichen werden agezeigt, Cursor ist immer links.
' Eingabe kann jedoch länger sein.
fis$=SPACE$(fil%)
fip%=1
fi$=""
a$=LEFT$(a$,fil%)
WHILE fi$<>CHR$(13)
LOCATE fis%,fiz%
PRINT MID$(a$+fis$,fip%,width%);
LOCATE fis%,fiz%
PRINT @rev$;
PRINT LEFT$(MID$(a$,fip%,1)+" ",1);
PRINT @an$;
fixxl:
fi$=INKEY$
IF fi$=""
GOTO fixxl
ENDIF
IF fi$=CHR$(13)
ENDIF
IF fi$=@rechts$
INC fip%
CLR fi$
ENDIF
IF fi$=@links$
DEC fip%
CLR fi$
ENDIF
IF fi$=@help$
ENDIF
IF fi$=CHR$(22)
einf!=(0-einf!-1)
CLR fi$
ENDIF
IF fi$=CHR$(8)
IF fip%>1
a$=LEFT$(a$,fip%-2)+MID$(a$,fip%)
DEC fip%
ENDIF
CLR fi$
ENDIF
IF fi$=CHR$(127)
IF fip%>0
a$=LEFT$(a$,fip%-1)+MID$(a$,fip%+1)
ENDIF
CLR fi$
ENDIF
IF fi$=@esc$
CLR a$
fip%=1
CLR fi$
ENDIF
IF ((ASC(fi$) AND 127)>31)
IF einf!=TRUE
a$=LEFT$(a$+fis$,fip%-1)+fi$+MID$(a$,fip%)
ELSE
a$=LEFT$(a$+fis$,fip%-1)+fi$+MID$(a$,fip%+1)
ENDIF
INC fip%
ENDIF
IF fip%<1
fip%=1
ENDIF
IF fip%>fil%
fip%=fil%
ENDIF
WEND
a$=LEFT$(a$,fil%)
LOCATE fis%,fiz%
PRINT LEFT$(LEFT$(a$+fis$,fil%),width%);
~FRE(0)
RETURN a$
ENDFUNC
> PROCEDURE handlestring(nr%)
IF nr%<=maxgadgets%
width%=CINT((x2%(nr%)-x1%(nr%)-10)/fontwidth%)
l%=l%(nr%)
IF l%>0
y%=CINT(y1%(nr%)/fontheight%)
x%=CINT((x1%(nr%)+2)/fontwidth%)
x$=s$(nr%)
x$=@fixxl$(l%,x$,y%,x%)
@setstring(nr%,x$)
ENDIF
ENDIF
RETURN
> PROCEDURE removegadget(nr%)
IF nr%<=maxgadgets%
IF lastgadget=nr%
DEC lastgadget%
ENDIF
~SetAPen(rp%,0)
PBOX x1%(nr%),y1%(nr%),x2%(nr%),y2(nr%)
~SetAPen(rp%,1)
x1%(nr%)=0
x2%(nr%)=0
y1%(nr%)=0
y2%(nr%)=0
g$(nr%)=""
s$(nr%)=""
l%(nr%)=0
ENDIF
RETURN
> PROCEDURE setstring(nr%,def$)
IF nr%<=maxgadgets%
s$(nr%)=def$
width%=CINT((x2%(nr%)-x1%(nr%)-10)/fontwidth%)
IF l%(nr%)>0
y%=CINT(y1%(nr%)/fontheight%)
x%=CINT((x1%(nr%)+2)/fontwidth%)
LOCATE x%,y%
PRINT LEFT$(s$(nr%),width%);
ENDIF
~SetAPen(rp%,1)
~Move(rp%,x1%(nr%),y2%(nr%))
~Draw(rp%,x1%(nr%),y1%(nr%))
~Draw(rp%,x2%(nr%),y1%(nr%))
~SetAPen(rp%,2)
~Draw(rp%,x2%(nr%),y2%(nr%))
~Draw(rp%,x1%(nr%),y2%(nr%))
~SetAPen(rp%,1)
ENDIF
RETURN
> FUNCTION getstring$(nr%)
LOCAL x$
x$=""
IF nr%<=maxgadgets%
x$=s$(nr%)
ENDIF
RETURN x$
ENDFUNC
> PROCEDURE checkbox(nr%,x%,y%,w%,h%,state%,txt$)
IF nr%<=maxgadgets%
IF nr%>lastgadget%
lastgadget%=nr%
ENDIF
x1%(nr%)=x%
x2%(nr%)=x%+w%
y1%(nr%)=y%
y2%(nr%)=y%+h%
g$(nr%)=txt$
s$(nr%)=STR$(state%)
~SetAPen(rp%,2)
~Move(rp%,x1%(nr%),y2%(nr%))
~Draw(rp%,x1%(nr%),y1%(nr%))
~Draw(rp%,x2%(nr%),y1%(nr%))
~SetAPen(rp%,1)
~Draw(rp%,x2%(nr%),y2%(nr%))
~Draw(rp%,x1%(nr%),y2%(nr%))
x%=x2%(nr%)+10
y%=(y2%(nr%)-y1%(nr%)+fontheight%)/2+y1%(nr%)
~Move(rp%,x%,y%)
g$=g$(nr%)
~Text(rp%,V:g$,LEN(g$))
IF state%=TRUE
GOSUB checkon(nr%)
ENDIF
ENDIF
RETURN
> PROCEDURE checkon(nr%)
IF nr%<=maxgadgets%
~SetAPen(rp%,1)
GOSUB checkproc
s$(nr%)=STR$(TRUE)
ENDIF
RETURN
> PROCEDURE checkoff(nr%)
IF nr%<=maxgadgets%
~SetAPen(rp%,0)
GOSUB checkproc
~SetAPen(rp%,1)
s$(nr%)=STR$(FALSE)
ENDIF
RETURN
> PROCEDURE checkproc
GOSUB cb_checkmark
' GOSUB cb_cross
RETURN
> PROCEDURE cb_checkmark
w%=x2%(nr%)-x1%(nr%)
h%=y2%(nr%)-y1%(nr%)
x%=x1%(nr%)+0.2*w%
y%=y1%(nr%)+0.55*h%
~Move(rp%,x%,y%)
x%=x1%(nr%)+0.55*w%
y%=y1%(nr%)+0.8*h%
~Draw(rp%,x%,y%)
x%=x1%(nr%)+0.8*w%
y%=y1%(nr%)+0.2*h%
~Draw(rp%,x%,y%)
RETURN
> PROCEDURE cb_cross
~Move(rp%,x1%(nr%)+1,y1%(nr%)+1)
~Draw(rp%,x2%(nr%)-1,y2%(nr%)-1)
~Move(rp%,x1%(nr%)+1,y2%(nr%)-1)
~Draw(rp%,x2%(nr%)-1,y1%(nr%)+1)
RETURN
> PROCEDURE checktoggle(nr%)
IF nr%<=maxgadgets%
IF s$(nr%)=STR$(TRUE)
GOSUB checkoff(nr%)
ELSE
GOSUB checkon(nr%)
ENDIF
ENDIF
RETURN
> FUNCTION ischecked(nr%)
LOCAL x
x=FALSE
IF nr%<=maxgadgets%
x=VAL(s$(nr%))
ENDIF
RETURN x
ENDFUNC
> PROCEDURE hslide(nr%,x%,y%,w%,h%,mi%,ma%,act%)
IF nr%<=maxgadgets%
IF nr%>lastgadget%
lastgadget%=nr%
ENDIF
x1%(nr%)=x%
x2%(nr%)=x%+w%
y1%(nr%)=y%
y2%(nr%)=y%+h%
g$(nr%)=STR$(mi%)
s$(nr%)=STR$(ma%)
l%(nr%)=act%
~SetAPen(rp%,2)
~Move(rp%,x1%(nr%),y2%(nr%))
~Draw(rp%,x1%(nr%),y1%(nr%))
~Draw(rp%,x2%(nr%),y1%(nr%))
~SetAPen(rp%,1)
~Draw(rp%,x2%(nr%),y2%(nr%))
~Draw(rp%,x1%(nr%),y2%(nr%))
z%=(w%-2)/(ma%-mi%+1)
PBOX x1%(nr%)+z%*act%+1,y1%(nr%)+1,x1%(nr%)+z%*(act%+1),y2%(nr%)-1
ENDIF
RETURN
> PROCEDURE vslide(nr%,x%,y%,w%,h%,mi%,ma%,act%)
IF nr%<=maxgadgets%
IF nr%>lastgadget%
lastgadget%=nr%
ENDIF
x1%(nr%)=x%
x2%(nr%)=x%+w%
y1%(nr%)=y%
y2%(nr%)=y%+h%
g$(nr%)=STR$(mi%)
s$(nr%)=STR$(ma%)
l%(nr%)=act%
~SetAPen(rp%,2)
~Move(rp%,x1%(nr%),y2%(nr%))
~Draw(rp%,x1%(nr%),y1%(nr%))
~Draw(rp%,x2%(nr%),y1%(nr%))
~SetAPen(rp%,1)
~Draw(rp%,x2%(nr%),y2%(nr%))
~Draw(rp%,x1%(nr%),y2%(nr%))
z%=(h%-2)/(ma%-mi%+1)
PBOX x1%(nr%)+1,y1%(nr%)+z%*act%+1,x2%(nr%)-1,y1%(nr%)+z%*(act%+1)
ENDIF
RETURN
> PROCEDURE handlehslider(nr%)
IF nr%<=maxgadgets%
mi%=VAL(g$(nr%))
ma%=VAL(s$(nr%))
w%=x2%(nr%)-x1%(nr%)
z%=(w%-2)/(ma%-mi%+1)
nact%=(mx%-x1%(nr%))/z%
@sethslider(nr%,nact%)
ENDIF
RETURN
> PROCEDURE handlevslider(nr%)
IF nr%<=maxgadgets%
h%=y2%(nr%)-y1%(nr%)
mi%=VAL(g$(nr%))
ma%=VAL(s$(nr%))
z%=(h%-2)/(ma%-mi%+1)
nact%=(my%-y1%(nr%))/z%
@setvslider(nr%,nact%)
ENDIF
RETURN
> PROCEDURE sethslider(nr%,nact%)
IF nr%<=maxgadgets%
act%=l%(nr%)
mi%=VAL(g$(nr%))
ma%=VAL(s$(nr%))
w%=x2%(nr%)-x1%(nr%)
z%=(w%-2)/(ma%-mi%+1)
~SetAPen(rp%,0)
PBOX x1%(nr%)+z%*act%+1,y1%(nr%)+1,x1%(nr%)+z%*(act%+1),y2%(nr%)-1
act%=nact%
act%=MIN(act%,ma%)
act%=MAX(act%,mi%)
l%(nr%)=act%
~SetAPen(rp%,1)
PBOX x1%(nr%)+z%*act%+1,y1%(nr%)+1,x1%(nr%)+z%*(act%+1),y2%(nr%)-1
ENDIF
RETURN
> PROCEDURE setvslider(nr%,nact%)
IF nr%<=maxgadgets%
act%=l%(nr%)
mi%=VAL(g$(nr%))
ma%=VAL(s$(nr%))
h%=y2%(nr%)-y1%(nr%)
z%=(h%-2)/(ma%-mi%+1)
~SetAPen(rp%,0)
PBOX x1%(nr%)+1,y1%(nr%)+z%*act%+1,x2%(nr%)-1,y1%(nr%)+z%*(act%+1)
act%=nact%
act%=MIN(act%,ma%)
act%=MAX(act%,mi%)
l%(nr%)=act%
~SetAPen(rp%,1)
PBOX x1%(nr%)+1,y1%(nr%)+z%*act%+1,x2%(nr%)-1,y1%(nr%)+z%*(act%+1)
ENDIF
RETURN
> FUNCTION getslider(nr%)
x%=0
IF nr%<=maxgadgets%
x%=l%(nr%)
ENDIF
RETURN x%
ENDFUNC
> PROCEDURE invisible(nr%,x%,y%,w%,h%)
IF nr%<=maxgadgets%
IF nr%>lastgadget%
lastgadget%=nr%
ENDIF
x1%(nr%)=x%
x2%(nr%)=x%+w%
y1%(nr%)=y%
y2%(nr%)=y%+h%
g$(nr%)=""
s$(nr%)=""
l%(nr%)=0
ENDIF
RETURN
> PROCEDURE arrowleft(nr%,x%,y%,w%,h%)
IF nr%<=maxgadgets%
IF nr%>lastgadget%
lastgadget%=nr%
ENDIF
x1%(nr%)=x%
x2%(nr%)=x%+w%
y1%(nr%)=y%
y2%(nr%)=y%+h%
g$(nr%)=""
s$(nr%)=""
l%(nr%)=0
~SetAPen(rp%,1)
~Move(rp%,x2%(nr%),y1%(nr%))
~Draw(rp%,x1%(nr%),y1%(nr%)+h%/2)
~Draw(rp%,x2%(nr%),y2%(nr%))
~Draw(rp%,x2%(nr%),y1%(nr%))
FILL x1%(nr%)+w%/2,y1%(nr%)+h%/2
ENDIF
RETURN
> PROCEDURE arrowright(nr%,x%,y%,w%,h%)
IF nr%<=maxgadgets%
IF nr%>lastgadget%
lastgadget%=nr%
ENDIF
x1%(nr%)=x%
x2%(nr%)=x%+w%
y1%(nr%)=y%
y2%(nr%)=y%+h%
g$(nr%)=""
s$(nr%)=""
l%(nr%)=0
~SetAPen(rp%,1)
~Move(rp%,x1%(nr%),y1%(nr%))
~Draw(rp%,x2%(nr%),y1%(nr%)+h%/2)
~Draw(rp%,x1%(nr%),y2%(nr%))
~Draw(rp%,x1%(nr%),y1%(nr%))
FILL x1%(nr%)+w%/2,y1%(nr%)+h%/2
ENDIF
RETURN
> PROCEDURE arrowup(nr%,x%,y%,w%,h%)
IF nr%<=maxgadgets%
IF nr%>lastgadget%
lastgadget%=nr%
ENDIF
x1%(nr%)=x%
x2%(nr%)=x%+w%
y1%(nr%)=y%
y2%(nr%)=y%+h%
g$(nr%)=""
s$(nr%)=""
l%(nr%)=0
~SetAPen(rp%,1)
~Move(rp%,x1%(nr%),y2%(nr%))
~Draw(rp%,x1%(nr%)+w%/2,y1%(nr%))
~Draw(rp%,x2%(nr%),y2%(nr%))
~Draw(rp%,x1%(nr%),y2%(nr%))
FILL x1%(nr%)+w%/2,y1%(nr%)+h%/2
ENDIF
RETURN
> PROCEDURE arrowdown(nr%,x%,y%,w%,h%)
IF nr%<=maxgadgets%
IF nr%>lastgadget%
lastgadget%=nr%
ENDIF
x1%(nr%)=x%
x2%(nr%)=x%+w%
y1%(nr%)=y%
y2%(nr%)=y%+h%
g$(nr%)=""
s$(nr%)=""
l%(nr%)=0
~SetAPen(rp%,1)
~Move(rp%,x1%(nr%),y1%(nr%))
~Draw(rp%,x1%(nr%)+w%/2,y2%(nr%))
~Draw(rp%,x2%(nr%),y1%(nr%))
~Draw(rp%,x1%(nr%),y1%(nr%))
FILL x1%(nr%)+w%/2,y1%(nr%)+h%/2
ENDIF
RETURN
