WBStartup

MUIApplicationTitle       "Aminet FTP by Email"
MUIApplicationCopyright   "(c)1998, Rui Carvalho"
MUIApplicationVersion     "$VER: Version 2.01"

NEWTYPE.en
  *entrie.b[4]
End NEWTYPE
DEFTYPE.en *buf

Dim *en.b(30720)
Dim *entr.b(8192)
Dim *entr2.b(8192)
Dim PageName$(2)
Dim mail$(1024)
Dim find$(1024,2)

Statement _arrtostr{*src, l.w}
    SHARED the$
    the$ = ""
    For b.w = 0 To l
        If Peek.b(*src + b) = 0
            b = l
        EndIf
        the$ + Chr$(Peek.b(*src + b))
    Next
End Statement

Statement _getfiles{dir$}
 SHARED the$
 SHARED mail$()
 SHARED barra
 barra=0

 DEFTYPE .AnchorPath  anchor
 scandir.l = Lock_(dir$, #ACCESS_READ)
 olddir.l  = CurrentDir_(scandir.l)
 suc.w = MatchFirst_("#?", anchor)
 barra=0
  While suc.w = 0
  If anchor\ap_Info\fib_DirEntryType < 1
   _arrtostr{&anchor\ap_Info\fib_FileName, 30}
  EndIf

  If Left$(the$,1)<>"." AND Right$(the$,4)<>"info"
   mail$(barra)=dir$+the$
   barra+1
  EndIf
  suc.w = MatchNext_(anchor)
 Wend
MatchEnd_(anchor)
olddir.l = CurrentDir_(olddir.l)
End Statement


*titulo=Null("Files Available")
*titulo1=Null("count Files")
*titulo2=Null("Options")
*titulo3=Null("Actions")

PageName$(0)="Main"
PageName$(1)="DownLoad"

;MAIN****************************
  MUILabel  0,Chr$(10)+Chr$(27)+"cReply To",0
  MUIString 1,"",64
  MUIHSpace 2,0
  MUILabel  3,Chr$(27)+"cFTP - Server",0
  MUIString  4,"ftp-mail@uni-paderborn.de",64

  MUIHSpace 5,0
  MUILabel  6,Chr$(27)+"cYAM Aminet Directory",0
  MUIString 7,"yam:incoming/",64

  MUISimpleButton 12,"Get Directory"

  MUIAddTags 8,#MUIA_Frame,#MUIV_Frame_Group
  MUIAddTags 8,#MUIA_FrameTitle,*titulo2
MUIAddObjsHGroup 8,0,1,2,3,4,5,6,7,12
MUICreateHGroup  8

  MUISimpleButton 10,"Request Recent Uploads"
  MUISimpleButton 11,"Join Mails"
  MUISimpleButton 13,"Bug!"

  MUISimpleButton 14,"Save Options"

  MUIAddTags 18,#MUIA_Frame,#MUIV_Frame_Group
  MUIAddTags 18,#MUIA_FrameTitle,*titulo3
MUIAddObjsHGroup 18,10,11,14
MUICreateHGroup 18

MUIAddObjsHGroup 19,18,8
MUICreateHGroup  19


;DOWNLOAD************************

;  MUILabel       20,"Select Size:",0
;  MUILabel       21,"0"+"K   ",0
  MUISimpleButton 22,"Send _Request"
  MUISimpleButton 23,"_Check Folder"

MUIAddObjsHGroup 24,20,21,22,23
MUICreateHGroup  24
  MUILabel  33,Chr$(27)+"cNew Files On Aminet"+Chr$(27)+"n",0
  MUIListHook On
  MUIAddTags  25,#MUIA_Frame,#MUIV_Frame_InputList
  MUIList     25,"BAR,BAR,BAR,",*entr()
  MUIListView 26,25
  MUIListTitle 25,Chr$(27)+"bDir",Chr$(27)+"c"+Chr$(27)+"bSize",Chr$(27)+"bName",Chr$(27)+"bDescription"

MUIAddObjsVGroup 27,33,26
MUICreateVGroup  27

  MUIBalanceObject 49

  MUILabel   34,Chr$(27)+"cFiles Selected: "+Str$(Int(value))+" Kb...",0
  MUIAddTags 28,#MUIA_Frame,#MUIV_Frame_InputList
  MUIList    28,"BAR,BAR,BAR,",*entr2()
  MUIListView29,28
  MUIListTitle 28,Chr$(27)+"bDir",Chr$(27)+"c"+Chr$(27)+"bSize",Chr$(27)+"bName",Chr$(27)+"bDescription"


MUIAddObjsVGroup 30,34,29,49,27
MUICreateVGroup  30

MUIAddObjsVGroup 31,30,24
MUICreateVGroup  31

; PAGES **************

  MUIAddObjsPage 35,19,31
  MUICreatePage  35,"Main","DownLoad"

MUIAddObjsVGroup 36,35
MUICreateVGroup  36

MUIAddTags      40,#MUIA_Window_Width,400
MUICreateWindow 40,"AFM 2.0","AFM",36
MUIAddSubWindow 40


; Progress *****

Macro InfoGauge ;obj#
  MUIAddTags `1,#MUIA_Frame,#MUIV_Frame_Gauge
  MUIAddTags `1,#MUIA_Background,#MUII_BACKGROUND
  MUIAddTags `1,#MUIA_Gauge_Max,100
  MUIAddTags `1,#MUIA_Gauge_Divide,1
  MUIAddTags `1,#MUIA_Gauge_Horiz,1
  MUINewObject `1,"Gauge.mui",0
End Macro

  MUIAddTags 70,#MUIA_Group_Columns,2
  MUILabel 71,"Checking Directory",0
  !InfoGauge{72}
  MUILabel 73,"Parsing File",0
  !InfoGauge{74}
  MUIAddTags 75,#MUIA_Weight,0
  MUINewObject 75,"Rectangle.mui",0,0
  MUINewObject 76,"Scale.mui",0,0
MUIAddObjsVGroup 70,71,72,73,74,75,76
MUICreateVGroup  70

MUIAddTags      80,#MUIA_Window_Width,300
MUICreateWindow 80,"Information","Info",70
MUIAddSubWindow 80


If MUICreateApplication<>True Then End

If Exists (".options")
 If ReadFile (0,".options")
  FileInput 0
   While NOT Eof(0)
   MUISetString 1,Edit$("",255)
   MUISetString 4,Edit$("",255)
   MUISetString 7,Edit$("",255)

   Wend
 EndIf
EndIf

MUINotifyApp 10,#MUIA_Pressed,0,10
MUINotifyApp 11,#MUIA_Pressed,0,11
MUINotifyApp 12,#MUIA_Pressed,0,12
MUINotifyApp 14,#MUIA_Pressed,0,14


MUINotifyApp 20,#MUIA_Pressed,0,1
MUINotifyApp 21,#MUIA_Pressed,0,2
MUINotifyApp 22,#MUIA_Pressed,0,3
MUINotifyApp 23,#MUIA_Pressed,0,4
MUINotifyApp 27,#MUIA_List_Active,#MUIV_EveryTime,27
MUINotifyApp 27,#MUIA_Listview_DoubleClick,1,1
MUINotifyApp 30,#MUIA_List_Active,#MUIV_EveryTime,30
MUINotifyApp 30,#MUIA_Listview_DoubleClick,1,2
MUINotifyApp 40,#MUIA_Window_CloseRequest,1,#MUIV_Application_ReturnID_Quit

MUIDoMethod 40,#MUIM_Window_SetCycleChain,MUIObjLoc(26),MUIObjLoc(29),0


MUIOpenWindow 40

a$="Aminet FTP by Mail"+Chr$(10)
a$+Chr$(10)
a$+"Version: 2.01"+Chr$(10)
a$+Chr$(10)
a$+Chr$(27)+"bT H I S  S O F T W A R E  I S  H I T W A R E"+Chr$(10)
a$+Chr$(27)+"n"+Chr$(10)
a$+"Dark Dreams Home: Http://ip.pt/"+Chr$(126)+"ip234558"+Chr$(10)
a$+"My E-Mail: Rui Carvalho <grim@ip.pt>"+Chr$(10)
a$+Chr$(10)+"Compilation date: 27.12.1998"+Chr$(10)
a$+Chr$(10)+"* * *"+Chr$(10)
a$+Chr$(10)+"Especial Thanks to:"
a$+Chr$(10)+"Dan Goran"
a$+Chr$(10)+"Metalurg"
a$+Chr$(10)+"GFS"


MUIRequest 40,0,"Dark Dreams Designs","OK",Chr$(27)+"c"+a$

rep:
Repeat

ev.l=MUIWaitEvent
Select ev

  Case 1
   MUIDoMethod 25,#MUIM_List_GetEntry,#MUIV_List_GetEntry_Active,&*buf
   If Peek$(*buf\entrie[0])<>""
   MUIDoMethod 25,#MUIM_List_Remove,#MUIV_List_Remove_Selected
   MUIDoMethod 28,#MUIM_List_InsertSingle,*buf,#MUIV_List_Insert_Sorted
   MUIDoMethod 28,#MUIM_List_Redraw,#MUIV_List_Redraw_Active
   If Right$(Peek$(*buf\entrie[1]),1)="M"
     value=value+(Val(UnLeft$(Peek$(*buf\entrie[1]),1))*1024)
    Else
     value=value+Val(UnLeft$(Peek$(*buf\entrie[1]),1))
    EndIf
   MUISet 34,#MUIA_Text_Contents,Chr$(27)+"cFiles Selected: "+Str$(Int(value))+" Kb...",0
   count+1
   EndIf

  Case 2
   MUIDoMethod 28,#MUIM_List_GetEntry,#MUIV_List_GetEntry_Active,&*buf
   If Peek$(*buf\entrie[0])<>""
   MUIDoMethod 28,#MUIM_List_Remove,#MUIV_List_Remove_Selected
   MUIDoMethod 25,#MUIM_List_InsertSingle,*buf,#MUIV_List_Insert_Sorted
   MUIDoMethod 25,#MUIM_List_Redraw,#MUIV_List_Redraw_Active
   If Right$(Peek$(*buf\entrie[1]),1)="M"
     value=value-(Val(UnLeft$(Peek$(*buf\entrie[1]),1))*1024)
    Else
     value=value-Val(UnLeft$(Peek$(*buf\entrie[1]),1))
    EndIf
   MUISet 34,#MUIA_Text_Contents,Chr$(27)+"cFiles Selected: "+Str$(Int(value))+" Kb...",0
   count-1
   EndIf

  Case 3
   MUIDoMethod 28,#MUIM_List_GetEntry,b,&*buf
   If count>0
   value=0
   If WriteFile (0,"T:body.tmp")
    FileOutput 0
    NPrint "size huge"
    If MUIGetString$(1)<>""
     NPrint "reply-to "+MUIGetString$(1)
    EndIf
    NPrint "cd /"
    For b=0 To count-1
     MUIDoMethod 28,#MUIM_List_GetEntry,b,&*buf
     NPrint "cd pub/aminet/"+Peek$(*buf\entrie[0])
    NPrint "get "+ Peek$(*buf\entrie[2])
    Next
    NPrint "quit"
    CloseFile 0
    For b=count-1 To 0 Step-1
     MUIDoMethod 28,#MUIM_List_Remove,b
    Next
    count=0
    MUISet 34,#MUIA_Text_Contents,Chr$(27)+"cFiles Selected: "+Str$(Int(value))+" Kb...",0
    JSR script
    JSR yam
    Delay_(50)
    Execute_ "rx T:put.rexx",0,0
   EndIf
  Else
  MUIRequest 40,0,"Error!","_OK",Chr$(27)+"cNo Files Selected! "+Chr$(count)
  EndIf

  Case 4
  JSR mail

  Case 10
  JSR yam
  Delay_(50)
  JSR recent

  Case 11
  JSR join

  Case 12
  a$=RTEZPathRequest("Directory for Aminet Files")
  If a$<>""
   MUISetString 7,a$
  EndIf
  Case 14
  JSR options

End Select
Until ev=-1
Execute_ "delete >NIL: T:put.rexx",0,0
MUICloseWindow 40
End

;*****Procedures*****

mail:

For b=c0+1 To 0 Step-1
 MUIDoMethod 25,#MUIM_List_Remove,b
Next
c0=0
c1=0
value=0
MUISet 34,#MUIA_Text_Contents,Chr$(27)+"cFiles Selected: "+Str$(Int(value))+" Kb...",0
For b=count To 0 Step-1
MUIDoMethod 28,#MUIM_List_Remove,b
Next
count=0
_getfiles{MUIGetString$(7)}

MUIOpenWindow 80

For a=0 To barra
If Exists (mail$(a))
  xx=(100/(barra-1))*(a+1)
  MUISet 72,#MUIA_Gauge_Current,xx,0
   If ReadFile (0,(mail$(a)))
     FileInput 0
     MUISet 74,#MUIA_Gauge_Current,0,0:Delay_(1)
     While NOT Eof(0)
      a$=Edit$("",255)
      x=((100/Lof(0))*Loc(0))
      MUISet 74,#MUIA_Gauge_Current,x,0
      If LCase$(Left$(a$,12))="from: aminet"
       a$=""
       pos=(Loc(0))+320
       FileSeek 0,pos
       While NOT LCase$(Left$(a$,12))="|-----------"
        a$=Edit$("",255)
        x=((100/Lof(0))*Loc(0))
         MUISet 74,#MUIA_Gauge_Current,x,0
       Wend

       While (NOT Eof(0) OR c0>8196)
        x=((100/Lof(0))*Loc(0))
        MUISet 74,#MUIA_Gauge_Current,x,0
        a$=Edit$("",255)
        If a$<>"" AND LCase$(Left$(a$,1))<>"|"
         *en(c1+2)=Null(Left$(a$,19))
         *en(c1)=Null(Mid$(a$,20,11))
         *en(c1+1)=Null(Mid$(a$,31,4))
         *en(c1+3)=Null(Mid$(a$,36))
         *entr(c0)=&*en(c1)
         MUIDoMethod 25,#MUIM_List_InsertSingle,*entr(c0),#MUIV_List_Insert_Sorted
         c1+5
         c0+1
        EndIf
       Wend
      EndIf
      If LCase$(Left$(a$,6))="x-uidl"
       FileSeek 0,Lof(0)
      EndIf
     Wend
     x=((100/Lof(0))*Loc(0))
     MUISet 74,#MUIA_Gauge_Current,x,0
   CloseFile 0
   Else
   MUICloseWindow 80
   JSR rep
   EndIf
Else
NPrint "File -> "+mail$(a)
EndIf
Next
MUICloseWindow 80

 MUIDoMethod 25,#MUIM_List_Redraw,#MUIV_List_Redraw_Active
RTS

yam:
If FindPort_("REXX")=0
 a$="REXX is NOT running!"+Chr$(10)
 a$+Chr$(10)
 a$+"Please run it..."+Chr$(10)
 MUIRequest 40,0,"ERROR!","DAMM",Chr$(27)+"c"+a$
 End
EndIf
If FindPort_("YAM")=0
 a$="YAM is NOT running!"+Chr$(10)
 a$+Chr$(10)
 a$+"Going to start it!"+Chr$(10)
 MUIRequest 40,0,"ERROR!","_OK",Chr$(27)+"c"+a$
 Execute_"run YAM:YAM HIDE",0,0
EndIf
While FindPort_("YAM")=0
Delay_(10)
Wend
RTS

script:

suj$="FTP Email Request"

If MUIGetString$(4)<>""
 server$=MUIGetString$(4)
Else
 a$="NO FTP-SERVER SELECTED!!!"+Chr$(10)
 a$+Chr$(10)
 a$+"UNABLE TO CREATE REQUEST!"+Chr$(10)
 MUIRequest 40,0,"ERROR!","_Opps",Chr$(27)+"c"+a$
 JSR rep
EndIf

If WriteFile (0,"T:put.rexx")
 FileOutput 0
  NPrint "/*(C) to Rui Carvalho 1998*/"
  NPrint "options results"
  NPrint "File="+Chr$(34)+"T:body.tmp"+Chr$(34)
  NPrint "address 'YAM' 'mailwrite'"
  NPrint "address 'YAM' 'writeto "+Chr$(34)+server$+Chr$(34)+"'"
  If MUIGetString$(1)<>""
   repto$=MUIGetString$(1)
   NPrint "address 'YAM' 'writereplyto "+Chr$(34)+repto$+Chr$(34)+"'"
  EndIf
  NPrint "address 'YAM' 'writesubject "+Chr$(34)+suj$+Chr$(34)+"'"
  NPrint "address 'YAM' 'writeletter' File"
  NPrint "address 'YAM' 'writequeue'"
  NPrint "address command 'delete >NIL: ' File"
  NPrint "exit"
EndIf
CloseFile 0
RTS

join:

_getfiles{MUIGetString$(7)}

MUISet 72,#MUIA_Gauge_Current,0,0
MUISet 74,#MUIA_Gauge_Current,0,0

MUIOpenWindow 80

f=0

For a=0 To barra-1
 If Exists (mail$(a))
  xx=(100/(barra-1))*(a+1)
  MUISet 72,#MUIA_Gauge_Current,xx,0
   If ReadFile (0,(mail$(a)))
     FileInput 0
     MUISet 74,#MUIA_Gauge_Current,0,0
     FileSeek 0,(Lof(0)-320)
     While NOT Eof(0)
      a$=Edit$("",255)
      x=((100/Lof(0))*Loc(0))
      MUISet 74,#MUIA_Gauge_Current,x,0
      If LCase$(Left$(a$,7))="include"
       find$(f,0)=Mid$(a$,9)
       find$(f,1)=mail$(a)
       f+1
      EndIf
     Wend
   Else
   EndIf
 EndIf
Next
CloseFile 0

For a=0 To barra-1
 If Exists (mail$(a))
  xx=(100/(barra-1))*(a+1)
  MUISet 72,#MUIA_Gauge_Current,xx,0
   If ReadFile (0,(mail$(a)))
     FileInput 0
     MUISet 74,#MUIA_Gauge_Current,0,0
     While NOT Eof(0)
      a$=Edit$("",255)
      x=((100/Lof(0))*Loc(0))
      MUISet 74,#MUIA_Gauge_Current,x,0
      For s=0 To f-1
       If LCase$(Left$(a$,8))="subject:"
        If Instr((LCase$(a$)),(LCase$(find$(s,0))))
         find$(s,0)=mail$(a)
         FileSeek 0,Lof(0)-256
        EndIf
        FileSeek 0,Lof(0)-256
       EndIf
      Next
     Wend
   Else
   EndIf
 EndIf
Next
CloseFile 0

For a=0 To f-1
If ReadFile (0,find$(a,0))
 If OpenFile (1,find$(a,1))
   While NOT Eof(0)
    FileInput 0
    a$=Edit$("",255)
    x=((100/Lof(0))*Loc(0))
    MUISet 74,#MUIA_Gauge_Current,x,0
    If LCase$(Left$(a$,10))="begin part"
     pos0.l=Loc(0)
     FileSeek 0,Lof(0)
    EndIf
   Wend

   FileSeek 1,Lof(1)-640
   While NOT Eof(1)
    FileInput 1
    a$=Edit$("",255)
    x=((100/Lof(1))*Loc(1))
    MUISet 74,#MUIA_Gauge_Current,x,0
    If LCase$(Left$(a$,7))="include"
     pos1.l=Loc(1)-(Len(a$)+1)
     FileSeek 1,Lof(1)
    EndIf
   Wend
 EndIf

 FileSeek 0,pos0
 FileSeek 1,pos1
 FileInput 0
 FileOutput 1
 While NOT Eof(0)
  a$=Edit$("",255)
  x=((100/Lof(0))*Loc(0))
  MUISet 74,#MUIA_Gauge_Current,x,0
  NPrint a$
 Wend
 EndIf
 CloseFile 0
 CloseFile 1
Next

For a=0 To f
a$="delete >NIL: "+find$(a,0)
Execute_ a$,0,0
Next
MUICloseWindow 80

a$="All Mails Joined!!!"+Chr$(10)
a$+Chr$(10)
a$+"Don't forget to Update Index (Amiga)+U"+Chr$(10)
MUIRequest 40,0,"ERROR!","_OK",Chr$(27)+"c"+a$
MUIDoMethod 25,#MUIM_List_Redraw,#MUIV_List_Redraw_Active
RTS

options:

If WriteFile(0,".options")
 FileOutput 0
 NPrint MUIGetString$(1)
 NPrint MUIGetString$(4)
 NPrint MUIGetString$(7)
EndIf
CloseFile 0
RTS

recent:
If WriteFile (0,"T:body.tmp")
 FileOutput 0
  NPrint "RECENT"
EndIf
CloseFile 0

If WriteFile (0,"T:put.rexx")
 FileOutput 0
  NPrint "/*(C) to Rui Carvalho 1998*/"
  NPrint "options results"
  NPrint "File="+Chr$(34)+"T:body.tmp"+Chr$(34)
  NPrint "address 'YAM' 'mailwrite'"
  NPrint "address 'YAM' 'writeto "+Chr$(34)+"aminet-reply@wuarchive.wustl.edu"+Chr$(34)+"'"
  If MUIGetString$(1)<>""
   repto$=MUIGetString$(1)
   NPrint "address 'YAM' 'writereplyto "+Chr$(34)+repto$+Chr$(34)+"'"
  EndIf
  NPrint "address 'YAM' 'writesubject "+Chr$(34)+"Recent Request"+Chr$(34)+"'"
  NPrint "address 'YAM' 'writeletter' File"
  NPrint "address 'YAM' 'writequeue'"
  NPrint "address command 'delete >NIL: ' File"
  NPrint "exit"
EndIf
CloseFile 0

Execute_ "rx t:put.rexx",0,0
Execute_ "delete >NIL: T:put.rexx",0,0
RTS


End
