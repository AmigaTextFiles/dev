' *********************************************************************
'                           AHI_PlayTest.bas
'                by/de Copyright 2011 Dámaso D. Estévez
'                   {correoamidde-aminet000,yahoo,es}
'          AmiSpaTra - http://www.xente.mundo-r.com/amispatra/
'
'            Based over the PlayTest.c code by Martin Blom.
'            All rights reserved over this derivative work:
'                 my Hisoft Basic developpers package.
'       Forbidden to remove ALL legal/copyrights remarks included
'            in this package: if you create a derivate work,
'                 you MUST to include all legal notes.
'
'    Plays a 8-bit signed mono sampled sound from the default input
'                 (use the redirection symbol as here:
'         PlayTest priority < OnlyAmigaAndWeDoItPossible.raw).
'           Of course, the sample has low quality (8000 Hz).
'                                 ----
'           Basado en el código de PlayTest.c de Martin Blom.
'      Todos los derechos reservados sobre este trabajo derivado:
'    mi paquete para desarrolladores que programen con Hisoft Basic.
'          Está prohibido eliminar cualquier comentario o nota
'     de autoría/legal de este paquete: si crea un trabajo derivado
'        HA DE INCLUIR OBLIGATORIAMENTE TODAS LAS NOTAS LEGALES.
'
'               Reproduce una muestra de sonido de 8 bits
'              mono con signo desde la entrada por defecto
'      (o sea, ha de utilizar el operador de redirección, algo así
'   como "AHI_PlayText prioridad < OnlyAmigaAndWeDoItPossible.raw").
'        Naturalmente, la muestra tiene baja calidad (8000 Hz).
' *********************************************************************

OPTION BASE 1

' ---------------------------------------------------------------------
'        Compiler's metacommands (see the Hisoft Basic Manual)
' Metacomandos para el compilador (consulte el manual del Hisoft Basic)
' ---------------------------------------------------------------------
REM $NOWINDOW
REM $NOLIBRARY
REM $NOBREAK

' ---------------------------------------------------------------------
'                Include files / Ficheros de inclusión
' ---------------------------------------------------------------------

'         OS / SO
' ------------------------
REM $include exec.bh
REM $include dos.bh

' AHI system / Sistema AHI
' ------------------------
REM $include ahi.bc

' ---------------------------------------------------------------------
'    From/De:  http://aminet.net/package/dev/basic/AST_HBRoutines
'
'            Obtain a command line interface's argument
'                  and the program name (Shell).
'
'     Obtiene un argumento de la interfaz de línea de comandos
'                 y el nombre del programa (Shell).
' ---------------------------------------------------------------------
REM $include BLib_AST/SGetArg.bas
REM $include BLib_AST/ArgvZero.bas

' ---------------------------------------------------------------------
'              C= string version / Cadena de versión de C=
' ---------------------------------------------------------------------

vstring$ = "$VER: AHI_PlayTest 1.0 (26.09.2011) by Dámaso 'AmiSpaTra' Domínguez based over the C code wrote by Martin Blom"+CHR$(0)

' ---------------------------------------------------------------------
'             (Global) constants / Constantes (globales)
' ---------------------------------------------------------------------

CONST FREQUENCY&    =   8000&
CONST BUFFERSIZE&   =  20000&

' ---------------------------------------------------------------------
'                  Global vars / Variables globales
' ---------------------------------------------------------------------

AHIDevice& = -1&

'     Pointers' table
'    Tabla de punteros
' ------------------------
DIM ptr&(5)

'   IO requests' table
' Tabla de peticiones E/S
 '------------------------
DIM ior&(2)

'  Constants for to identify the elements
' Constantes para identificar los elementos
' -----------------------------------------
CONST AHIPT_MSGPORT% = 1%
CONST AHIPT_IO%      = 2%
CONST AHIPT_IOCOPY%  = 3%
CONST AHIPT_BUFFER1% = 4%
CONST AHIPT_BUFFER2% = 5%

' ---------------------------------------------------------------------
'              Opening libraries / Abriendo bibliotecas
' ---------------------------------------------------------------------
LIBRARY OPEN "exec.library"
LIBRARY OPEN "dos.library", 36&

' *********************************************************************
'               Functions and subroutines's declarations
'                Declaración de funciones y subrutinas
' *********************************************************************

DECLARE FUNCTION init&(AHIDevice&, ptr&())
DECLARE FUNCTION main&(ptr&(), ior&())
DECLARE SUB      clean(AHIDevice&, ptr&())

' ********************************************************************
'                     Main code / Código principal
' ********************************************************************

BREAK STOP

' Allocating resources
' Reservando recursos
' --------------------

IF init&(AHIDevice&, ptr&()) = RETURN_OK& THEN

	'    The real main code
	' El código principal real
	' ------------------------
	rc& = main&(ptr&(), ior&())

END IF

' Point of exit / Punto de salida
' --------------------------------
Salida:

'       Releasing resources
'       Liberando recursos
' --------------------------------
clean AHIDevice&, ptr&()

BREAK ON

LIBRARY CLOSE

STOP rc&

' ***************************************************************************

FUNCTION init&(AHIDevice&, ptr&())

	' I'm a pesimist... the routine will fail
	'  Soy un pesimista... la rutina fallará
	' ---------------------------------------
	init& = RETURN_ERROR&

	' Creating the port / Creación del puerto
	' ---------------------------------------
	ptr&(AHIPT_MSGPORT%) = CreateMsgPort&

	IF ptr&(AHIPT_MSGPORT%) = NULL& THEN
		PRINT "Failed to create the MsgPort!"
		PRINT "¡Se ha fracasado el crear el puerto de mensajes!"
		EXIT FUNCTION
	END IF

	' Creating the IORequest / Creación de la petición E/S
	' ----------------------------------------------------
	ptr&(AHIPT_IO%) = CreateIORequest&(ptr&(AHIPT_MSGPORT%), AHIRequest_sizeof%)

	IF ptr&(AHIPT_IO%) = NULL& THEN
		PRINT "Failed to create the IORequest!"
		PRINT "¡Se ha fracasado el crear la petición E/S!"
		EXIT FUNCTION
	END IF

	POKEW ptr&(AHIPT_IO%)+ahir_Version%, 4%

	' Opening the device / Apertura del dispositivo
	' ---------------------------------------------
	AHIDevice& = OpenDevice&(SADD("ahi.device"+CHR$(0)), 0&, ptr&(AHIPT_IO%), 0&)

	IF AHIDevice& <> 0& THEN
		PRINT "Failed to open 'ahi.device' v4+!"
		PRINT "¡Se ha fracasado al abrir el dispositivo 'ahi.device' v4+!"
		EXIT FUNCTION
	END IF

	' Allocating memory blocks / Reserva de bloques de memoria
	' --------------------------------------------------------
	ptr&(AHIPT_IOCOPY%)  = AllocMem&(AHIRequest_sizeof%, MEMF_ANY&)
	ptr&(AHIPT_BUFFER1%) = AllocMem&(BUFFERSIZE&,        MEMF_ANY&)
	ptr&(AHIPT_BUFFER2%) = AllocMem&(BUFFERSIZE&,        MEMF_ANY&)

	IF ptr&(AHIPT_IOCOPY%) = NULL& OR ptr&(AHIPT_BUFFER1%) = NULL& OR ptr&(AHIPT_BUFFER2%) = NULL& THEN
		PRINT "Failed to allocate free mem!"
		PRINT "¡Se ha fracasado al reservar memoria!"
		EXIT FUNCTION
	END IF

	' All done! / ¡Todo hecho!
	' ------------------------
	init& = RETURN_OK&

END FUNCTION

' *********************************************************************

FUNCTION main&(ptr&(), ior&())
	LOCAL dummy&, signals&, long&
	LOCAL p1&, p2&, link&
	LOCAL priority$, pri%

	' I'm a pesimist... the routine will fail
	'  Soy un pesimista... la rutina fallará
	' ---------------------------------------
	main& = RETURN_ERROR&

	'               Only from CLI
	' Sólo desde la interfaz de línea de comandos
	' -------------------------------------------
	IF PEEKL(SYSTAB+8) <> 0 THEN
		BEEP
		EXIT FUNCTION
	END IF

	'    Obtaining the priority
	'   Obteniendo la prioridad
	' ----------------------------
	priority$ = SGetArg$(1%,"PRI")

	IF priority$ = "" THEN
		PRINT "Error - I need the priority (-128 to 127)! / ¡Necesito la prioridad (-128 a 127)!"
		EXIT FUNCTION
	ELSE
		pri% = VAL(priority$)
	END IF

	'     My var isn't a byte
	'  Mi variable NO es un octeto
	' -----------------------------
	IF pri% < -128 THEN pri% = -128
	IF pri% >  127 THEN pri% =  127

	PRINT "Sound priority / Prioridad del sonido: ";pri%

	' Copying the IO request / Copiando la petición E/S
	' -------------------------------------------------
	CopyMem ptr&(AHIPT_IO%), ptr&(AHIPT_IOCOPY%), AHIRequest_sizeof%
	ior&(1) = ptr&(AHIPT_IO%)
	ior&(2) = ptr&(AHIPT_IOCOPY%)

	dummy& = SetIoErr&(0&)

	' Some vars / Algunas variables
	' -----------------------------
	p1&       = ptr&(AHIPT_BUFFER1%)
	p2&       = ptr&(AHIPT_BUFFER2%)
	link&     = NULL&

	signals&  = NULL&
	long&     = NULL&

	DO

		'       Reading the data (sample) for the standard input (redirection)
		' Leyendo los datos (muestra) desde la fuente de entrada estándar (redirección)
		' -----------------------------------------------------------------------------
		long& = xRead&(xInput&, p1&, BUFFERSIZE&)

		' Filling the struct / Rellenando la estructura
		' ---------------------------------------------
 		POKEB ior&(1)+ahir_Std%+IOStdReqio_Message%+mn_Node%+ln_Pri%, pri%
		POKEW ior&(1)+ahir_Std%+IOStdReqio_Command%,                  CMD_WRITE&
		POKEL ior&(1)+ahir_Std%+IOStdReqio_Data%,                     p1&
		POKEL ior&(1)+ahir_Std%+IOStdReqio_Length%,                   long&
		POKEL ior&(1)+ahir_Std%+IOStdReqio_Offset%,                   0
		POKEL ior&(1)+ahir_Frequency%,                                FREQUENCY&
		POKEL ior&(1)+ahir_Type%,                                     AHIST_M8S&
		POKEL ior&(1)+ahir_Volume%,                                   &H10000&
		POKEL ior&(1)+ahir_Position%,                                 &H8000&
		POKEL ior&(1)+ahir_Link%,                                     link&

		SendIO ior&(1)

		IF link& THEN

			' Wait until the last buffer is finished (=>the new buffer is started)
			'
			'            Aguarda a que el último tampón haya terminado,
			'        lo que implica que se haya comenzado con el nuevo tampón
			' -------------------------------------------------------------------
			signals& = xWait&(SIGBREAKF_CTRL_C& OR (1& << PEEKB(ptr&(AHIPT_MSGPORT%)+mp_SigBit%)))

			'                   Break support via AmigaOS
			' Soporte de la interrupción del usuario a través del AmigaOS
			' -----------------------------------------------------------
			IF (signals& AND SIGBREAKF_CTRL_C&) THEN
				dummy& = SetIoErr&(ERROR_BREAK&)
				EXIT FUNCTION
			END IF

			'        Remove the reply & abort on error
			' Se borra la respuesta y se aborta si hay un error
			' -------------------------------------------------
			IF WaitIO&(link&) THEN
				dummy& = SetIoErr&(ERROR_WRITE_PROTECTED&)
				EXIT LOOP
			END IF

		END IF

		' Check for end-of-sound, and wait until it is finished before aborting
		'
		'       Comprobación si se ha alcanzado el final del sonido y
		'       se aguarda hasta que su finalización antes de abortar
		' ---------------------------------------------------------------------
		IF long& <> BUFFERSIZE& THEN
			dummy& = WaitIO&(ior&(1))
			EXIT LOOP
		END IF

		link& = ior&(1)

		'        Swapping data and request pointers
		' Intercambiando los punteros de datos y peticiones
		' -------------------------------------------------
		SWAP p1&, p2&
		SWAP ior&(1), ior&(2)

	LOOP

	AbortIO ior&(1)
	dummy& = WaitIO&(ior&(1))

	IF link& THEN
		AbortIO ior&(2)
		dummy& = WaitIO&(ior&(2))
	END IF

	IF IoErr& THEN
		dummy& = PrintFault&(IoErr&, SADD(ArgvZero$+CHR$(0)))
	ELSE
		main& = RETURN_OK&
	END IF

END FUNCTION

' *********************************************************************

SUB clean(AHIDevice&, ptr&())

	IF ptr&(AHIPT_BUFFER2%) THEN
		FreeMem ptr&(AHIPT_BUFFER2%), BUFFERSIZE&
		ptr&(AHIPT_BUFFER2%) = NULL&
	END IF

	IF ptr&(AHIPT_BUFFER1%) THEN
		FreeMem ptr&(AHIPT_BUFFER1%), BUFFERSIZE&
		ptr&(AHIPT_BUFFER1%) = NULL&
	END IF

	IF ptr&(AHIPT_IOCOPY%) THEN
		FreeMem ptr&(AHIPT_IOCOPY%), AHIRequest_sizeof%
		ptr&(AHIPT_IOCOPY%)  = NULL&
	END IF

	IF AHIDevice& = 0& THEN
		CloseDevice ptr&(AHIPT_IO%)
		AHIDevice& =   -1&
	END IF

	IF ptr&(AHIPT_IO%) THEN
		DeleteIORequest ptr&(AHIPT_IO%)
		ptr&(AHIPT_IO%)      = NULL&
	END IF

	IF ptr&(AHIPT_MSGPORT%) THEN
		DeleteMsgPort ptr&(AHIPT_MSGPORT%)
		ptr&(AHIPT_MSGPORT%) = NULL&
	END IF

END SUB

' ********************************************************************
