' *********************************************************************
'                          AHI_CheckAHIPrefs.bas
'                 by/de Copyright 2011 Dámaso D. Estévez
'                    {correoamidde-aminet000,yahoo,es}
'           AmiSpaTra - http://www.xente.mundo-r.com/amispatra/
'
'           Based over the CheckAHIPrefs.c code by Martin Blom.
'             All rights reserved over this derivative work:
'                  my Hisoft Basic developpers package.
'        Forbidden to remove ALL legal/copyrights remarks included
'             in this package: if you create a derivate work,
'                  you MUST to include all legal notes.
'
'           A small example how to read the settings file:
'     says if the debugging mode is activated (global settings) and
'      if the unit has < 2 channels (local setting for each unit).
'                                  ----
'        Basado en el código de CheckAHIPrefs.c de Martin Blom.
'       Todos los derechos reservados sobre este trabajo derivado:
'     mi paquete para desarrolladores que programen con Hisoft Basic.
'           Está prohibido eliminar cualquier comentario o nota
'     de autoría/legal de este paquete: si crea un trabajo derivado
'         HA DE INCLUIR OBLIGATORIAMENTE TODAS LAS NOTAS LEGALES.
'
'     Un pequeño ejemplo de cómo leer el fichero de ajustes de AHI:
'       informa si está activada la depuración (ajuste global) y
'         si la unidad tiene menos de 2 canales (ajuste local).
' *********************************************************************

OPTION BASE 1

' ---------------------------------------------------------------------
'        Compiler's metacommands (see the Hisoft Basic Manual)
' Metacomandos para el compilador (consulte el manual del Hisoft Basic)
' ---------------------------------------------------------------------
REM $NOWINDOW
REM $NOLIBRARY
REM $NOBREAK

' ---------------------------------------------------------------------
'                Include files / Ficheros de inclusión
' ---------------------------------------------------------------------

'         OS / SO
' ------------------------
REM $include exec.bh
REM $include dos.bh
REM $include prefs.bc
REM $include iffparse.bh

' AHI system / Sistema AHI
' ------------------------
REM $include ahi.bc

' ---------------------------------------------------------------------
'    From/De:  http://aminet.net/package/dev/basic/AST_HBRoutines
'
'            Obtain a command line interface's argument
'                  and the program name (Shell).
'
'     Obtiene un argumento de la interfaz de línea de comandos
'                 y el nombre del programa (Shell).
' ---------------------------------------------------------------------
REM $include BLib_AST/SGetArg.bas
REM $include BLib_AST/ArgvZero.bas

' ---------------------------------------------------------------------
'              C= string version / Cadena de versión de C=
' ---------------------------------------------------------------------

vstring$ = "$VER: AHI_CheckAHIPrefs 1.0 (25.09.2011) by Dámaso 'AmiSpaTra' Domínguez based over the C code wrote by Martin Blom"+CHR$(0)

' ---------------------------------------------------------------------
'                  Global vars / Variables globales
' ---------------------------------------------------------------------

iff&  = NULL& ' Pointer to/Puntero a: struct IFFHandle
ahig& = NULL& ' Pointer to/Puntero a: struct StoredProperty
ci&   = NULL& ' Pointer to/Puntero a: struct CollectionItem

unit% = 0&
rc&   = RETURN_OK&

file$ = ""

tmp&  = 0&

' ---------------------------------------------------------------------
'              Opening libraries / Abriendo bibliotecas
' ---------------------------------------------------------------------
LIBRARY OPEN "exec.library"
LIBRARY OPEN "dos.library", 36&
LIBRARY OPEN "iffparse.library"

' ********************************************************************
'                     Main code / Código principal
' ********************************************************************

'               Only from CLI
' Sólo desde la interfaz de línea de comandos
' -------------------------------------------
IF PEEKL(SYSTAB+8) <> 0 THEN
	BEEP
	GOTO Salida
END IF

'   Obtaining the arguments
'  Obteniendo los argumentos
' ---------------------------
file$ =     SGetArg$(1%,"FILE,UNIT")
unit% = VAL(SGetArg$(2%,"FILE,UNIT"))

BREAK STOP

'   The program needs the arguments
' El programa necesita ambos argumentos
' -------------------------------------
IF file$ = "" THEN
	PRINT "Usage/Uso: ";ArgvZero$;" FILE UNIT"
	GOTO Salida
END IF

iff& = AllocIFF&

IF iff& THEN

	tmp& = xOpen&(SADD(file$+CHR$(0)), MODE_OLDFILE&)

	IF tmp& THEN

		POKEL (iff& + iff_Stream%), tmp&

		InitIFFasDOS iff&

		IF NOT(OpenIFF&(iff&, IFFF_READ&)) THEN

			IF NOT(PropChunk&(iff&, ID_PREF&, ID_AHIG&) OR CollectionChunk&(iff&, ID_PREF&, ID_AHIU&) OR StopOnExit&(iff&, ID_PREF&, ID_FORM&)) THEN

				IF (ParseIFF&(iff&, IFFPARSE_SCAN&) = IFFERR_EOC&) THEN

					ahig& = FindProp&(iff&, ID_PREF&, ID_AHIG&)

					IF ahig& THEN

						globalprefs& = NULL& ' Pointer to/Puntero a: struct AHIGlobalPrefs
						globalprefs& = PEEKL(ahig& + sp_Data%)

						IF (PEEKW(globalprefs& + ahigp_DebugLevel%) <> AHI_DEBUG_NONE&) THEN
							PRINT "Debugging is turned on / La depuración está activada."
							rc& = RETURN_WARN&
						END IF

					END IF

					ci& = FindCollection&(iff&, ID_PREF&, ID_AHIU&)

					WHILE ci&

						unitprefs& = PEEKL(ci& + ci_Data%)

						IF PEEKB(unitprefs& + ahiup_Unit%) = unit% THEN

							IF PEEKW(unitprefs& + ahiup_Channels%) < 2% THEN
								PRINT "There are less than 2 channels selected for unit";unit%
								PRINT "Hay menos de 2 canales seleccionados para la unidad";unit%
								rc& = RETURN_WARN&
							END IF

						END IF

						ci& = PEEKL(ci& + ci_Next%)

					WEND

				END IF

			END IF

			CloseIFF iff&

		ELSE

			' Not included in the C version r8-?
			' No incluido en la versión en C r8-?
			' -----------------------------------		
			PRINT "I can't open the IFF file!/¡Imposible abrir el fichero IFF!"
			rc& = RETURN_FAIL&

		END IF

		dummy& = xClose&(PEEKL(iff&+iff_Stream%))

	ELSE

		' Not included in the C version r8-?
		' No incluido en la versión en C r8-?
		' -----------------------------------		
		PRINT "Failed to open!/¡Imposible abrir el fichero! (";file$;")"
		rc& = RETURN_FAIL&
	
	END IF

	FreeIFF& iff&

ELSE

	' Not included in the C version r8-?
	' No incluido en la versión en C r8-?
	' -----------------------------------		
	PRINT "AllocIFF& has failed!/¡AllocIFF& ha fallado!"
	rc& = RETURN_FAIL&
	
END IF

' ------------------------------------------------------------------
Salida:
' ******

BREAK ON

LIBRARY CLOSE

STOP rc&

' ------------------------------------------------------------------
