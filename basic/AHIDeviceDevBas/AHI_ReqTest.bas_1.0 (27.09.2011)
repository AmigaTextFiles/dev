' *********************************************************************
'                           AHI_ReqTest.bas
'                 by/de Copyright 2011 Dámaso D. Estévez
'                    {correoamidde-aminet000,yahoo,es}
'           AmiSpaTra - http://www.xente.mundo-r.com/amispatra/
'
'              Based over req-test.c code by Martin Blom.
'             All rights reserved over this derivative work:
'                  my Hisoft Basic developpers package.
'        Forbidden to remove ALL legal/copyrights remarks included
'             in this package: if you create a derivate work,
'                  you MUST to include all legal notes.
'
'                    Opens an AHI audio mode request.
'        WARNING! Codewatcher says what this program don't release
'          all the memory allocated r8-?. I don't find the bug...
'                                  ----
'           Basado en el código de req-test.c de Martin Blom.
'       Todos los derechos reservados sobre este trabajo derivado:
'     mi paquete para desarrolladores que programen con Hisoft Basic.
'           Está prohibido eliminar cualquier comentario o nota
'     de autoría/legal de este paquete: si crea un trabajo derivado
'         HA DE INCLUIR OBLIGATORIAMENTE TODAS LAS NOTAS LEGALES.
'
'               Abre una petición de modos de audio AHI.
'           ¡ATENCIÓN! Codewatcher informa de que el programa
'  no libera toda la memoria reservada r8-?.  No encuentro el error...
' *********************************************************************

OPTION BASE 1

' ---------------------------------------------------------------------
'        Compiler's metacommands (see the Hisoft Basic Manual)
' Metacomandos para el compilador (consulte el manual del Hisoft Basic)
' ---------------------------------------------------------------------
REM $NOWINDOW
REM $NOLIBRARY
REM $NOBREAK

' ---------------------------------------------------------------------
'                Include files / Ficheros de inclusión
' ---------------------------------------------------------------------

'         OS / SO
' ------------------------
REM $include exec.bh
REM $include dos.bh
REM $include utility.bc

' AHI system / Sistema AHI
' ------------------------
REM $include ahi.bh

' ---------------------------------------------------------------------
'              C= string version / Cadena de versión de C=
' ---------------------------------------------------------------------

vstring$ = "$VER: AHI_ReqTest 1.0 (27.09.2011) by Dámaso 'AmiSpaTra' Domínguez based over the C code wrote by Martin Blom"+CHR$(0)

' ---------------------------------------------------------------------
'                  Global vars / Variables globales
' ---------------------------------------------------------------------

AHIDevice% = -1%

res&       = NULL&
rc&        = RETURN_FAIL&

'     Pointers' table
'    Tabla de punteros
' ------------------------
DIM ptr&(4)

' Tags / Etiquetas
' ----------------
DIM tags&(18)
DIM ReqFilterTags&(4)

TAGLIST VARPTR(ReqFilterTags&(1)), _
	AHIDB_Realtime&,	TRUE&, _
	TAG_DONE&

'  Constants for to identify the elements
' Constantes para identificar los elementos
' -----------------------------------------
CONST AHIRQT_AHIBASE% = 1%
CONST AHIRQT_MSGPORT% = 2%
CONST AHIRQT_IO%      = 3%
CONST AHIRQT_AUDIOREQ%= 4%

' ---------------------------------------------------------------------
'              Opening libraries / Abriendo bibliotecas
' ---------------------------------------------------------------------
LIBRARY OPEN "exec.library"
LIBRARY OPEN "dos.library", 36&

' *********************************************************************
'               Functions and subroutines's declarations
'                Declaración de funciones y subrutinas
' *********************************************************************

DECLARE SUB  clean(AHIDevice%, ptr&())

' *********************************************************************

SUB clean(AHIDevice%, ptr&())

	IF ptr&(AHIRQT_AUDIOREQ%) THEN
		AHI_FreeAudioRequest ptr&(AHIRQT_AUDIOREQ%)
		ptr&(AHIRQT_AUDIOREQ%) = NULL&
	END IF

	IF ptr&(AHIRQT_AHIBASE%) THEN
		LIBRARY VARPTR "ahi.device", NULL&
		ptr&(AHIRQT_AHIBASE%) = NULL&
	END IF

	IF AHIDevice% = 0% THEN
		CloseDevice ptr&(AHIRQT_IO%)
		AHIDevice%           =   -1%
	END IF

	IF ptr&(AHIRQT_IO%) THEN
		DeleteIORequest ptr&(AHIRQT_IO%)
		ptr&(AHIRQT_IO%)      = NULL&
	END IF

	IF ptr&(AHIRQT_MSGPORT%) THEN
		DeleteMsgPort ptr&(AHIRQT_MSGPORT%)
		ptr&(AHIRQT_MSGPORT%) = NULL&
	END IF

END SUB

' ********************************************************************
'                     Main code / Código principal
' ********************************************************************

'               Only from CLI
' Sólo desde la interfaz de línea de comandos
' -------------------------------------------
IF PEEKL(SYSTAB+8) <> 0 THEN
	BEEP
	GOTO mi_salida
END IF

BREAK STOP

' Creating the port / Creación del puerto
' ---------------------------------------
ptr&(AHIRQT_MSGPORT%) = CreateMsgPort&

IF ptr&(AHIRQT_MSGPORT%) = NULL& THEN
	PRINT "Failed to create the MsgPort!"
	PRINT "¡Se ha fracasado el crear el puerto de mensajes!"
	GOTO mi_salida
END IF

' Creating the IORequest / Creación de la petición E/S
' ----------------------------------------------------
ptr&(AHIRQT_IO%) = CreateIORequest&(ptr&(AHIRQT_MSGPORT%), AHIRequest_sizeof%)

IF ptr&(AHIRQT_IO%) = NULL& THEN
	PRINT "Failed to create the IORequest!"
	PRINT "¡Se ha fracasado el crear la petición E/S!"
	GOTO mi_salida
END IF

'        Hmmmm... I think what the version would be 4% because
' the original program uses, as minimum, one tag only available from v4+
'
'       Hmmmm... Creo que el programa debería exigir la versión 4%
'           porque el programa original en C hace uso al menos
'      de una etiqueta sólo disponible a partir de dicha versión r8-?
' ----------------------------------------------------------------------
POKEW ptr&(AHIRQT_IO%)+ahir_Version%, 2%

' Opening the device / Apertura del dispositivo
' ---------------------------------------------
AHIDevice% = OpenDevice&(SADD("ahi.device"+CHR$(0)), AHI_NO_UNIT&, ptr&(AHIRQT_IO%), NULL&)

IF AHIDevice% <> 0& THEN
	PRINT "Unable to open 'ahi.device' v2+!"
	PRINT "¡Imposible abrir el dispositivo 'ahi.device' v2+!"
	GOTO mi_salida
END IF

'    I will use a function from the AHI device and
'   I need to inform to Hisoft Basic how access to it
'
'     Usaré una función del dispositivo AHI y
' necesito informar a Hisoft Basic cómo acceder a ésta
' -----------------------------------------------------
ptr&(AHIRQT_AHIBASE%) = PEEKL(ptr&(AHIRQT_IO%) + ahir_Std% + IOStdReqio_Device% + dd_Library%)
LIBRARY VARPTR "ahi.device", ptr&(AHIRQT_AHIBASE%)

' ------------------------------------------------------------------

TAGLIST VARPTR(tags&(1)), _
	AHIR_SleepWindow&,	TRUE&, _
      AHIR_UserData&,		999&, _
      AHIR_PubScreenName&,	NULL&, _
      TAG_DONE&

ptr&(AHIRQT_AUDIOREQ%) = AHI_AllocAudioRequestA&(VARPTR(tags&(1)))
   
TAGLIST VARPTR(tags&(1)), _
      AHIR_TitleText&,		SADD("Select a mode or cancel / Elija un modo o cancele"+CHR$(0)), _
	AHIR_PositiveText&,     SADD("OK / Aceptar"+CHR$(0)), _
      AHIR_NegativeText&,	SADD("Abort / Abortar"+CHR$(0)), _
      AHIR_DoMixFreq&,		TRUE&, _
      AHIR_DoDefaultMode&,	TRUE&, _
      AHIR_InitialAudioID&,	&H20003&,_ 
	AHIR_InitialMixFreq&,	30000&, _
      AHIR_FilterTags&,		VARPTR(ReqFilterTags&(1)), _
      TAG_DONE&

res& = AHI_AudioRequestA&(ptr&(AHIRQT_AUDIOREQ%), VARPTR(tags&(1)))

IF res& THEN

	dummy& = PEEKL(ptr&(AHIRQT_AUDIOREQ%) + ahiam_AudioID%)

	PRINT "Selected AudioMode/Modo de sonido seleccionado: 0x";
	PRINT RIGHT$("00000000"+LTRIM$(HEX$(dummy&)),8);
	PRINT ",";PEEKL(ptr&(AHIRQT_AUDIOREQ%) + ahiam_MixFreq%);"Hz"
	
ELSE

	IF (IoErr& = ERROR_NO_FREE_STORE&) THEN

		PRINT "AHI ran out of memory! / ¡Memoria insuficiente para AHI!"

	ELSE

		IF (IoErr& = ERROR_NO_MORE_ENTRIES&) THEN

			PRINT "No available modes!/¡No hay modos disponibles!"

		ELSE

			PRINT "Requester cancelled!/¡Petición cancelada!"

		END IF

	END IF

END IF

rc& = RETURN_OK&

' ------------------------------------------------------------------
mi_salida:
' --------

clean AHIDevice%, ptr&()

BREAK ON

LIBRARY CLOSE

STOP rc&

' ------------------------------------------------------------------
