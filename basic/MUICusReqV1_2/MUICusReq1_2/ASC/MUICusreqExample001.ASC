;
; Example usage of MUICustomRequest{} V1.1
;
; (C) Robert Hutchinson - Satanic Dreams Software 1999.
;
; Requires EFMUILib, MUI2.RES
; (found at: http://www.satanicdreams.freeserve.co.uk/)
;
; Please Email me with updates/bugs: loki@sds.in2home.co.uk
;
; CHANGES:
;   - Images are now 0-Transparent.
;

  !StackOpen{12000}

  ;# Startup MUICustomRequest ###########################################

  ;--- Custom requester uses palette and bitmap 4.
  ;    #BMP_Height and #BMP_Width MUST always be specififed before the include.
  ;
  #BMP_Height = 34
  #BMP_Width  = 43
  BitMap        4,#BMP_Width,#BMP_Height,4
  DecodeILBM    4,?_Bang_Pic
  DecodePalette 4,?_Bang_Pic

  ;######################################################################

  MUIApplicationTitle       "LokisExample001"
  MUIApplicationVersion     "$VER: Example001 1.1 (17.08.99)"
  MUIApplicationCopyright   "(C) Satanic Dreams Software"
  MUIApplicationAuthor      "Rob Hutchinson"
  MUIApplicationDescription "Some shite put together in five mins. HA!"
  MUIApplicationBase        "LE00"

  ;# The CustomRequester Include ########################################

  XINCLUDE "MUICustomRequest.BB2"

  ;######################################################################

  ;-- Open a win
  ;
  MUISimpleButton 2,"Press me.. Or Else! :)"
  MUISimpleButton 3,"No.. TRY ME!.. Not him!"
  MUIAddObjsVGroup 4,2,3
  MUICreateVGroup  4

  MUICreateWindow 0,"Testing Testing 123","ABCD",4
  MUIAddSubWindow 0

  If MUICreateApplication<>True Then End
  MUIApplicationObj 20

  ; NOTIFIES:

  MUINotifyApp 0,#MUIA_Window_CloseRequest,1,1
  MUINotifyApp 2,#MUIA_Pressed,0,2
  MUINotifyApp 3,#MUIA_Pressed,0,3

  ;# MUICustomRequest Notifies ##########################################

  XINCLUDE "MUICustomReqNotifies.BB2"

  ;######################################################################

  MUIOpenWindow 0

  Repeat
    EV.l=MUIWaitEvent
    Select EV
      Case 2
        DecodeILBM    4,?_Bang_Pic
        DecodePalette 4,?_Bang_Pic
        Body$=Chr$(27)+"cThis is Centered text in a request. With a"
        Body$+Chr$(10)+"nice image next to it :). Text string of"
        Body$+Chr$(10)+"course is subject to all the MUI"
        Body$+Chr$(10)+Chr$(27)+"bText "+Chr$(27)+"uParsing"+Chr$(27)+"n"
        Body$+Chr$(10)+Chr$(27)+"8Select one of the below:"
        Body$+Chr$(10)+Chr$(27)+"nNote that Image is 0-Transparent."

        Entr=MUICustomRequest{"Hello World",Body$,0,"Snap|Crackle|Plop"}

        NPrint "You selected button number: ",Entr," In requester 1"

      Case 3
        DecodeILBM    4,?_Abbey_Pic
        DecodePalette 4,?_Abbey_Pic
        Body$=Chr$(27)+"cThis is left-aligned text in a request. With a"
        Body$+Chr$(10)+"nice image next to it :). This requester"
        Body$+Chr$(10)+"has no parent window attached to close. So the"
        Body$+Chr$(10)+"other window will still react. Cool huh?"
        Body$+Chr$(10)+"Images MUST be the same width and height, however,"
        Body$+Chr$(10)+"as long as they are, you can change the images and"
        Body$+Chr$(10)+"the palette they use statically."

        Entr=MUICustomRequest{"Hello World 2",Body$,-1,"Bing|Bong|Bang|Buff|Muff|Snuff"}

        NPrint "You selected button number: ",Entr," In requester 2"


    End Select
  Until EV=1

  !StackClose
  End


  _Bang_Pic:
    IncBin "Bang.IFF"

  _Abbey_Pic:
    IncBin "Abbey2.IFF"


