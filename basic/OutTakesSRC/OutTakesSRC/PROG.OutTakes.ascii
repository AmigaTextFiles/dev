
NoCli : WBStartup

DEFTYPE .w timetrack,count,count2,count3,temp
DEFTYPE .w brain,liney,sheep,legs,legsmove
NEWTYPE .xy : x.w : y.w : End NEWTYPE
DEFTYPE .xy dot,dotspeed
NEWTYPE .xys
  x.q : y.q
  xs.q : ys.q
  xp.q : yp.q
End NEWTYPE
NEWTYPE .xyz : x.w : y.w : z.w : End NEWTYPE
Dim man.xys(13)
#NUMDROPS=30
#NUMBACK=50
Dim drop.xy(#NUMDROPS,#NUMBACK)
#NUMCIRCLES=5
#MAXCIRCLESIZE=15
#MINCIRCLESIZE=5
#NUMCIRCLESBIG=#NUMCIRCLES+1
#MAXCIRCLESIZESMALL=#MAXCIRCLESIZE/2
Dim circ.xyz(#NUMCIRCLES)

Gosub setup
BLITZ
Gosub tmodtext
Gosub inbetween
Gosub tword
Gosub inbetween
Gosub train
Gosub inbetween
Gosub tbrain
Gosub inbetween
Gosub trender
Gosub inbetween
Gosub ttd
Gosub inbetween
Gosub tdot
Gosub inbetween
Gosub tsheep
Gosub inbetween
Gosub tline
Gosub inbetween
End

.tmodtext
  BitMap 0,320,256,1 : Cls
  InitCopList 0,44,256,$10001,8,2,0
  DisplayBitMap 0,0
  DisplayPalette 0,8
  CreateDisplay 0

  timetrack=0
  Blit 20,160,128
  While (NOT(RawStatus($45))) AND timetrack<75
    VWait 2
    timetrack+1
  Wend
  Free BitMap 0
  Free CopList 0
Return

.tword
  BitMap 0,320,256,3 : Cls
  Blit 12,160,128
  InitCopList 0,44,256,$10003,8,8,0
  DisplayBitMap 0,0
  DisplayPalette 0,7
  CreateDisplay 0
  DecodeModule 0,?trackmod7
  temp=StartTracker(0)

  For temp=0 To #NUMCIRCLES
    Gosub newcircle
  Next

  timetrack=0
  While (NOT(RawStatus($45))) AND timetrack<150
    Blit 19,160+(Rnd(5)-2),240+(Rnd(5)-2)
    VWait 3
    timetrack+1
  Wend
  timetrack=0
  While (NOT(RawStatus($45))) AND timetrack<375
    Blit 19,160+(Rnd(5)-2),240+(Rnd(5)-2)
    For count=0 To #NUMCIRCLES
      Gosub updatecircles
    Next
    temp=Rnd(#NUMCIRCLES*4)
    If NOT(temp>#NUMCIRCLES)
      If circ(temp)\z>#MINCIRCLESIZE
        Gosub newcircle
      EndIf
    EndIf
    VWait
    timetrack+1
  Wend
  StopTracker
  FreeTrackerModule 0
  Free BitMap 0
  Free CopList 0
Return

.updatecircles
  If circ(count)\z>1
    Circlef circ(count)\x,circ(count)\y,circ(count)\z,0
  EndIf
  circ(count)\z+1
  If circ(count)\z>#MAXCIRCLESIZE+(Rnd(#MAXCIRCLESIZE)-(#MAXCIRCLESIZESMALL))
    temp=count
    Gosub newcircle
  EndIf
Return

.newcircle
  circ(temp)\x=Rnd(160)+80
  circ(temp)\y=Rnd(120)+70
  circ(temp)\z=Rnd(11)-5
Return

.tsheep
  BitMap 0,320,256,3
  BitMap 1,320,256,3
  InitCopList 0,44,256,$10003,8,8,0
  DisplayBitMap 0,0
  DisplayPalette 0,6
  CreateDisplay 0
  DecodeModule 0,?trackmod6
  temp=StartTracker(0)

  count2=0
  sheep=30
  legs=0 : legsmove=2
  timetrack=0
  While (NOT(RawStatus($45))) AND timetrack<200
    Use BitMap count2
    count2=1-count2
    DisplayBitMap 0,count2
    VWait 2
    Boxf 0,128,320,190,0
    Boxf 0,190,320,256,3
    Blit 15,260+(Rnd(5)-2),60+(Rnd(5)-2)
    sheep+1 : If sheep=260 Then sheep=30
    count=Rnd(5)-2
    legs+legsmove
    If legs>10 OR legs<-10 Then legsmove-legsmove-legsmove
    Line sheep-5,160+count,sheep+legs-5,220+Abs(legs),2
    Line sheep-10,160+count,sheep-legs-10,220+Abs(legs),2
    Line sheep+10,160+count,sheep+legs+10,220+Abs(legs),2
    Line sheep+5,160+count,sheep-legs+5,220+Abs(legs),2
    Blit 16,sheep,160+count
    If sheep<120 OR sheep>160
      Blit 17,sheep+20,145+count
    Else
      Blit 18,sheep+20,145
    EndIf
    timetrack+1
  Wend
  StopTracker
  FreeTrackerModule 0
  Free BitMap 0
  Free BitMap 1
  Free CopList 0
Return

.trender
  BitMap 0,320,256,5 : Cls
  BitMap 1,320,256,5 : Cls
  InitCopList 0,44,256,$10005,8,32,0
  DisplayBitMap 0,0
  DisplayPalette 0,5
  CreateDisplay 0
  DecodeModule 0,?trackmod5
  temp=StartTracker(0)

  liney=70
  count=0
  timetrack=0
  While (NOT(RawStatus($45))) AND timetrack<195
    Use BitMap count
    count=1-count
    DisplayBitMap 0,count
    Blit 14,160+(Rnd(5)-2),230+(Rnd(5)-2)
    Blit 2,160,128
    Boxf 110,liney,210,190,0
    VWait 2
    liney+1
    If liney>190 Then liney=190
    timetrack+1
  Wend
  StopTracker
  FreeTrackerModule 0
  Free BitMap 0
  Free BitMap 1
  Free CopList 0
Return

.train
  BitMap 0,320,256,3
  BitMap 1,320,256,3
  InitCopList 0,44,256,$10003,8,8,0
  DisplayBitMap 0,0
  DisplayPalette 0,4
  CreateDisplay 0
  DecodeModule 0,?trackmod4
  temp=StartTracker(0)

  For temp=0 To #NUMDROPS
    Gosub newrain
  Next

  count3=0
  timetrack=0
  While (NOT(RawStatus($45))) AND timetrack<500
    Use BitMap count3
    count3=1-count3
    DisplayBitMap 0,count3
    CopyBitMap count3,1-count3
    Blit 13,160+(Rnd(5)-2),40+(Rnd(5)-2)
    For count=0 To #NUMDROPS
      Gosub updaterainback
    Next
    For count=0 To #NUMDROPS
      Gosub updaterainstreak
    Next
    For count=0 To #NUMDROPS
      Gosub updateraindrop
    Next
    temp=Rnd(#NUMDROPS*4)
    If NOT(temp>#NUMDROPS) Then Gosub newrain
    VWait
    timetrack+1
  Wend
  StopTracker
  FreeTrackerModule 0
  Free BitMap 0
  Free BitMap 1
  Free CopList 0
Return

.updaterainback
  If drop(count,0)\x>320 OR drop(count,0)\x<0 OR drop(count,0)\y>260
    temp=count
    Gosub newrain
  Else
    ClipBlit 10,drop(count,#NUMBACK)\x,drop(count,#NUMBACK)\y
  EndIf
Return

.updaterainstreak
  ClipBlit 9,drop(count,0)\x,drop(count,0)\y
Return

.updateraindrop
  For count2=#NUMBACK To 1 Step -1
    drop(count,count2)\x=drop(count,count2-1)\x
    drop(count,count2)\y=drop(count,count2-1)\y
  Next
  temp=drop(count,0)\y
  drop(count,0)\y+Rnd(4)
  If temp<>drop(count,0)\y Then drop(count,0)\x+(Rnd(5)-2)
  ClipBlit 8,drop(count,0)\x,drop(count,0)\y
Return

.newrain
  For count2=0 To #NUMBACK
    ClipBlit 10,drop(temp,count2)\x,drop(temp,count2)\y
  Next
  drop(temp,0)\x=Rnd(300)+10
  drop(temp,0)\y=Rnd(150)+80
  For count2=1 To #NUMBACK
    drop(temp,count2)\x=drop(temp,0)\x
    drop(temp,count2)\y=drop(temp,0)\y
  Next
Return

.tline
  BitMap 0,320,256,2 : Cls
  BitMap 1,320,256,2 : Cls
  InitCopList 0,44,256,$10002,8,4,0
  DisplayBitMap 0,0
  DisplayPalette 0,3
  CreateDisplay 0
  DecodeModule 0,?trackmod3
  temp=StartTracker(0)

  count2=0
  Gosub makepoints
  timetrack=0
  While (NOT(RawStatus($45))) AND timetrack<763
    DisplayBitMap 0,count2
    count2=1-count2
    Use BitMap count2
    Cls : Gosub drawlines
    Blit 11,245+(Rnd(5)-2),45+(Rnd(5)-2)
    Gosub updatepoints
    VWait 2
    timetrack+1
  Wend
  StopTracker
  FreeTrackerModule 0
  Free BitMap 0
  Free BitMap 1
  Free CopList 0
Return

.updatepoints
  For count=0 To 13
    If man(count)\x<man(count)\xp Then man(count)\xs+1
    If man(count)\x>man(count)\xp Then man(count)\xs-1
    man(count)\x+man(count)\xs
    If man(count)\y<man(count)\yp Then man(count)\ys+1
    If man(count)\y>man(count)\yp Then man(count)\ys-1
    man(count)\y+man(count)\ys
  Next
Return

.drawlines
  Circle man(3)\x,man(3)\y-16,15,1
  Plot man(3)\x-5,man(3)\y-20,1
  Plot man(3)\x+5,man(3)\y-20,1
  Line man(3)\x+man(0)\x,man(3)\y+man(0)\y,man(3)\x+man(1)\x,man(3)\y+man(1)\y,1
  Line man(3)\x+man(2)\x,man(3)\y+man(2)\y,1
  Line man(3)\x,man(3)\y,man(4)\x,man(4)\y,1
  Line man(5)\x,man(5)\y,1
  Line man(6)\x,man(6)\y,1
  Line man(4)\x,man(4)\y,man(7)\x,man(7)\y,1
  Line man(8)\x,man(8)\y,1
  Line man(4)\x,man(4)\y,man(9)\x,man(9)\y,1
  Line man(10)\x,man(10)\y,1
  Line man(11)\x,man(11)\y,1
  Line man(9)\x,man(9)\y,man(12)\x,man(12)\y,1
  Line man(13)\x,man(13)\y,1
Return

.makepoints
  man(0)\xp=-5 : man(0)\yp=-9
  man(0)\x=-5 : man(0)\y=-8
  man(1)\xp=0 : man(1)\yp=-7
  man(1)\x=0 : man(1)\y=-7
  man(2)\xp=5 : man(2)\yp=-9
  man(2)\x=5 : man(2)\y=-10
  man(3)\xp=100 : man(3)\yp=100
  man(3)\x=104 : man(3)\y=103
  man(4)\xp=100 : man(4)\yp=120
  man(4)\x=102 : man(4)\y=126
  man(5)\xp=70 : man(5)\yp=115
  man(5)\x=75 : man(5)\y=117
  man(6)\xp=45 : man(6)\yp=120
  man(6)\x=60 : man(6)\y=140
  man(7)\xp=130 : man(7)\yp=115
  man(7)\x=134 : man(7)\y=113
  man(8)\xp=155 : man(8)\yp=120
  man(8)\x=140 : man(8)\y=100
  man(9)\xp=100 : man(9)\yp=160
  man(9)\x=120 : man(9)\y=166
  man(10)\xp=80 : man(10)\yp=190
  man(10)\x=90 : man(10)\y=200
  man(11)\xp=80 : man(11)\yp=230
  man(11)\x=80 : man(11)\y=227
  man(12)\xp=120 : man(12)\yp=190
  man(12)\x=113 : man(12)\y=200
  man(13)\xp=130 : man(13)\yp=230
  man(13)\x=130 : man(13)\y=233
Return

.tdot
  BitMap 0,320,256,3 : Cls
  InitCopList 0,44,256,$10003,8,8,0
  DisplayBitMap 0,0
  DisplayPalette 0,2
  CreateDisplay 0
  DecodeModule 0,?trackmod2
  temp=StartTracker(0)

  dot\x=Rnd(300)+10
  dot\y=Rnd(246)+10
  dotspeed\x=(Rnd(5)-2)*2
  dotspeed\y=(Rnd(5)-2)*2
  timetrack=0
  While (NOT(RawStatus($45))) AND timetrack<500
    Blit 7,160+(Rnd(5)-2),128+(Rnd(5)-2)
    Plot dot\x,dot\y,0
    Gosub movedot
    Plot dot\x,dot\y,1
    VWait
    timetrack+1
  Wend
  StopTracker
  FreeTrackerModule 0
  Free BitMap 0
  Free CopList 0
Return

.movedot
  dotspeed\x+Rnd(3)-1
  dotspeed\y+Rnd(3)-1
  If dotspeed\x>6 Then dotspeed\x=6
  If dotspeed\x<-6 Then dotspeed\x=-6
  If dotspeed\y>6 Then dotspeed\y=6
  If dotspeed\y<-6 Then dotspeed\y=-6
  dot\x+dotspeed\x
  dot\y+dotspeed\y
  If dot\x<10 OR dot\x>310 Then dotspeed\x-dotspeed\x-dotspeed\x
  If dot\y<10 OR dot\y>246 Then dotspeed\y-dotspeed\y-dotspeed\y
Return

.tbrain
  BitMap 0,320,256,5
  BitMap 1,320,256,5
  InitCopList 0,44,256,$10005,8,32,0
  DisplayBitMap 0,0
  DisplayPalette 0,1
  CreateDisplay 0
  DecodeModule 0,?trackmod1
  temp=StartTracker(0)

  count2=0
  brain=100
  timetrack=0
  While (NOT(RawStatus($45))) AND timetrack<117
    Use BitMap count2
    count2=1-count2
    DisplayBitMap 0,count2
    VWait
    CopyBitMap count2,1-count2
    Blit 6,65+(Rnd(5)-2),56+(Rnd(5)-2)
    ClipBlit Rnd(2)+4,220,brain
    brain+Rnd(2)
    ClipBlit 3,220,brain
    timetrack+1
  Wend
  StopTracker
  FreeTrackerModule 0
  Free BitMap 0
  Free BitMap 1
  Free CopList 0
Return

.ttd
  BitMap 0,320,256,3
  InitCopList 0,44,256,$10003,8,8,0
  DisplayBitMap 0,0
  DisplayPalette 0,0
  CreateDisplay 0
  DecodeModule 0,?trackmod0
  temp=StartTracker(0)

  count=0
  count2=120 : count3=200
  ClipBlit 0,count2,count3
  timetrack=0
  While (NOT(RawStatus($45))) AND timetrack<690
    If count>250
      If count<280
        count2-2
        count3-1
        ClipBlit 0,count2,count3
      Else
        If count<350
          count2-5
          count3-1
          ClipBlit 0,count2,count3
        Else
          If count=401
            count2=350 : count3=246
          Else
            If count<430
              count2-4
              count3-1
              ClipBlit 1,count2,count3
            Else
              count=600
            EndIf
          EndIf
        EndIf
      EndIf
    EndIf
    count+1
    VWait
    timetrack+1
  Wend
  StopTracker
  FreeTrackerModule 0
  Free BitMap 0
  Free CopList 0
Return

.inbetween
  temp=0 : While RawStatus($45) AND temp<25 : temp+1 : VWait : Wend
  If RawStatus($45) Then End
Return

.setup
  DecodeShapes 0,20,?shapes
  DecodePalette 0,?pal0
  DecodePalette 1,?pal1
  DecodePalette 2,?pal2
  DecodePalette 3,?pal3
  DecodePalette 4,?pal4
  DecodePalette 5,?pal5
  DecodePalette 6,?pal6
  DecodePalette 7,?pal7
  DecodePalette 8,?pal8
Return

shapes:IncBin "Shapes"
pal0:IncBin "Palette.0"
pal1:IncBin "Palette.1"
pal2:IncBin "Palette.2"
pal3:IncBin "Palette.3"
pal4:IncBin "Palette.4"
pal5:IncBin "Palette.5"
pal6:IncBin "Palette.6"
pal7:IncBin "Palette.7"
pal8:IncBin "Palette.8"
trackmod0:IncBin "Module.0"
trackmod1:IncBin "Module.1"
trackmod2:IncBin "Module.2"
trackmod3:IncBin "Module.3"
trackmod4:IncBin "Module.4"
trackmod5:IncBin "Module.5"
trackmod6:IncBin "Module.6"
trackmod7:IncBin "Module.7"

