3     SCNCLR
5     PRINT STRING$(13,"*");" SPEAK-IT ";STRING$(14,"*")
7     PRINT"*            Version 1.0            *"
10    PRINT"*";SPC(35);"*"
12    PRINT"*  For instructions type ALT{?} and *"
13    PRINT"*  RETURN  after the prompt >       *"
14    PRINT"*";SPC(35);"*"
15    PRINT STRING$(37,"*")
16    PRINT ""
17    DIM ARGS%(9),CHAN$(12),PHR$(10)
19    CHAN$(0)=" CHANNEL 0"
21    CHAN$(1)=" CHANNEL 1"
23    CHAN$(2)=" CHANNEL 2"
25    CHAN$(3)=" CHANNEL 3"
27    CHAn$(4)=" CHANNELS 0 & 1"
29    CHAN$(5)=" CHANNELS 0 & 2"
31    CHAN$(6)=" CHANNELS 3 & 1"
33    CHAN$(7)=" CHANNELS 3 & 2"
35    CHAN$(8)=" EITHER LEFT"
37    CHAN$(9)=" EITHER RIGHT"
39    CHAN$(10)=" EITHER L&R PAIR"
41    CHAN$(11)=" ANY SINGLE"
43    OOR$=" *** OUT OF RANGE ***"
45    PC$="%"
51    PC%=ASC(PC$)
53    Q$="Ñ"
55    Q%=ASC(Q$)
57    QL$="ñ"
59    QL%=ASC(QL$)
61    CR$=CHR$(10)
63    ARGS%(0)=110
65    Args%(2)=150
67    ARGS%(4)=22200
69    ARGS%(5)=64
71    ARGS%(6)=10
73    PK$=TRANSLATE$("WELCOME TO SPEAK IT")
75    X%=NARRATE("WEHLKUM TUW1 (SPIY6K IH8T).")
76    CH$="^"
78    FIL$=" "
80    FOR X%=0 TO 9
82    PHR$(X%)="EH5MQPTIY."
84    NEXT
100   REM MAIN LOOP
110   LINE INPUT ">",IN$
113   IF LEN(IN$)=0 THEN GOTO 275
115   FC%=ASC(IN$):CC%=0
120   IF IN$="Ñ" OR IN$="ñ" THEN SCNCLR:END:REM QUIT
125   IF IN$="Ð" OR IN$="ð" THEN GOSUB 1200:REM PITCH
128   IF IN$="É" OR IN$="é" THEN GOSUB 1300:REM INFLECT
131   IF IN$="Ò" OR IN$="ò" THEN GOSUB 1400:REM RATE
134   IF IN$="Ö" OR IN$="ö" THEN GOSUB 1500:REM VOICE
137   IF IN$="Ô" OR IN$="ô" THEN GOSUB 1600:REM TUNING
140   IF IN$="ÖÕ" OR IN$="öõ" THEN GOSUB 1700:REM VOL
143   IF IN$="ÃÈ" OR IN$="ãè" THEN GOSUB 1800:REM CHANNEL
146   IF IN$="Í" OR IN$="í" THEN GOSUB 1900:REM MODE
149   IF IN$="Ã" OR IN$="ã" THEN GOSUB 2000: REM CONTROL
152   IF IN$="ÅÄ" OR IN$="åä" THEN ECH%=15:IR%=-1:CH$="^":CURS%=1:FIL$=" ":GOSUB 3000:GOSUB 900:GOTO 110:REM ED
155   IF LEN(IN$)=1 AND ASC(IN$)>175 AND ASC(IN$)<185 THEN GOSUB 1150:REM RECALL
156   IF NOT((FC%=237 OR FC%=205 OR FC%=174 OR FC%=255) AND LEN(IN$)=2) THEN GOTO 161
157   IN$=RIGHT$(IN$,1)
158   GOSUB 1100:REM STORE
159   GOTO 100
161   IF IN$="¯"  OR IN$="¿" THEN GOSUB 300:REM INSTR
164   IF IN$="¯¯" OR IN$="¿¿" THEN GOSUB 500:REM ED INSTR
180   IF FC%=PC% THEN TEXT$="******":PHNM$=RIGHT$(IN$,LEN(IN$)-1):TE%=0
183   IF FC%<>PC% AND CC%=0 THEN TEXT$=IN$:PHNM$=TRANSLATE$(IN$):TE%=0
275   GOSUB 4000:REM SPEAK
277   IF CH$="¤" THEN GOSUB 3000
285   GOSUB 900: REM DISPLAY
290   GOTO 110
300   REM INSTRUCTION SCREENS
302   CC%=1
303   SCNCLR
304   PRINT"******* SPEAK-IT INSTRUCTIONS *******";CR$
307   PRINT"Speak-It alows you to manipulate";CR$;"all the variables involved in";CR$;"generating speech with your Amiga."
310   PRINT"It includes ten storage memories to";CR$;"store phoneme strings in so you can";CR$;"compare alternate implementations.";CR$
313   Print"The system prompt is a carret >";CR$;CR$;"Type a line of text followed by a";CR$;"carriage return (CR) to have it"
316   PRINT"translated and spoken.";CR$
319   PRINT"Type[ % phoneme string CR ] to speak";CR$;"the string exactly as input.";CR$
322   PRINT"Type ALT{Q} CR  to exit to ABasiC.";CR$
325   PRINT"Press any key to continue."
328   GETKEY X$
331   SCNCLR
334   PRINT"To alter the values in the array that";CR$;"controls the manner of speaking, type";CR$;"the commands below, followed by CR."
337   PRINT"{XY} means type the characters XY";CR$;"with the ALT key depressed.";CR$
340   PRINT"  {P}  To adjust the Pitch";CR$;"  {I}  To alter the Inflection"
343   PRINT"  {R}  To adjust the rate";CR$;"  {V}  To alter the Voice"
346   PRINT"  {T}  To adjust the Tuning";CR$;"  {VU} To adjust the VolUme"
349   PRINT"  {CH} To select the CHannel";CR$;"  {M}  To select the Mode"
352   PRINT"  {C}  To select the Control mode";CR$;CR$;"  {ED} To activate the phoneme editor"
353   PRINT"  {??} To get editor instructions";CR$;CR$;"  {?}  To get these instructions";CR$
356   PRINT"Press any key to continue."
359   GETKEY X$
362   SCNCLR
365   PRINT"To store the current phoneme string";CR$;"in one of the ten memories, type"
368   PRINT"{Mn},{.n},or{-n} followed by CR or";CR$;"ENTER, where n is a digit from 0 to 9";CR$
371   PRINT"To recall and speak one of the";CR$;"phoneme memories, type {n} followed"
374   PRINT"by CR or ENTER, where n is a digit";CR$;"from 0 to 9.  The current phoneme";CR$;"string is lost.";CR$
377   PRINT"If the phoneme string has been edited";CR$;"after it was translated from the text"
380   PRINT"string, the text string is shown in";CR$;INVERSE(1);"inverse video";CR$
383   PRINT"The speaking manner argument array";CR$;"values are shown in ";INVERSE(1);"inverse video"
386   PRINT"if they are not the default values";CR$;CR$;"Press any key to continue."
400   getkey X$
410   return
500   REM EDITOR INSTRUCTIONS
501   SCNCLR
502   PRINT"The Phoneme Editor alows you to edit";CR$;"the current phoneme string in order";CR$;"to improve the pronunciation.";CR$
504   PRINT"The string is displayed along with a";CR$;"cursor (^) that indicates the present";CR$;"editing point.";CR$
506   PRINT"The editing mode that is in effect is";CR$;"shown at the bottom of the screen,";CR$;"along with any characters to be";CR$;"inserted or deleted.";CR$
508   PRINT"In the editor, the commands take";CR$;"effect immediately, without having to";CR$;"press CR. RETURN is a command that"
510   PRINT"causes the editing action you have";CR$;"specified to modify the string.";CR$
512   PRINT"Press any key to continue"
514   GETKEY X$
519   SCNCLR
520   PRINT"{XY} means type the characters XY";CR$;"with the ALT key depressed";CR$
521   PRINT"{Q} To exit to the main menu"
522   PRINT"Use the left and right cursor control";CR$;"    keys <- -> to move the cursor"
525   PRINT"BACKSPACE to back-up when editing"
527   PRINT"DEL To enter the Delete mode. Then";CR$;"    use DEL or -> to lengthen the";CR$;"    string to be deleted by RETURN"
530   PRINT"{I} To enter the Insert mode. The";CR$;"    string shown at the bottom of";CR$;"    the screen will be inserted";CR$;"    before the character indicated"
532   PRINT"    by the cursor by pressing RETURN"
535   PRINT"{R} To enter the Replace mode. The";CR$;"    string shown at the bottom of the";CR$;"    screen will replace the same"
537   PRINT"    number of characters, starting";CR$;"    at the cursor, by pressing RETURN";CR$
539   PRINT"Press any key to continue"
540   GETKEY X$
541   SCNCLR
543   PRINT"ESC If you make an error in editing";CR$;"    to get back the previous version"
546   PRINT"{n} Where n is a digit from 0 to 9";CR$;"    to recall and speak the string";CR$;"    stored in phoneme memory #n. The"
549   PRINT"    current string is lost. If you";CR$;"    press multiple {n}s, they will";CR$;"    stack up and be spoken in"
551   PRINT"    sequence, as determined by the";CR$;"    designated synchronization mode"
554   PRINT"{Mn},{.n},or{-n} Where n is a digit";CR$;"    from 0 to 9 to store the current";CR$;"    phoneme string in memory #n"
555   PRINT"{?} To get these instructions";CR$
557   PRINT"You cannot alter the speaking manner";CR$;"argument array from the editor";CR$
560   PRINT"Press any key to continue"
561   GETKEY X$
564   RETURN
900   REM A SUBROUTINE TO DISPLAY THE SCREEN
902   SCNCLR
903   GOSUB 3200:PRINT"":REM SHOW MEMS
904   PRINT "TEXT=";INVERSE(TE%);"""";TEXT$;"""";CR$
906   PRINT "PHONEMES=""";PHNM$;"""";CR$
908   IF ARGS%(0)=110 THEN IV%=0 ELSE IV%=1
910   IF ARGS%(0)<65 OR ARGS%(0)>320 THEN TAG$=OOR$ ELSE TAG$=""
912   ARG$=STR$(ARGS%(0))
914   SP%=5-LEN(ARG$)
916   PRINT "     Pitch=";INVERSE(IV%);ARG$;SPC(SP%);INVERSE(0);TAG$
918   IV%=1
920   IF ARGS%(1)=0 THEN TAG$=" INFLECTED":IV%=0 ELSE IF ARGS%(1)=1 THEN TAG$=" MONOTONE" ELSE TAG$=OOR$
922   ARG$=STR$(ARGS%(1))
924   SP%=5-LEN(ARG$)
926   PRINT ;"Inflection=";INVERSE(IV%);ARG$;SPC(SP%);INVERSE(0);TAG$
928   IF ARGS%(2)=150 THEN IV%=0 ELSE IV%=1
930   IF ARGS%(2)<40 OR ARGS%(2)>400 THEN TAG$=OOR$ ELSE TAG$=""
932   ARG$=STR$(ARGS%(2))
934   SP%=5-LEN(ARG$)
936   PRINT "      Rate=";INVERSE(IV%);ARG$;SPC(SP%);INVERSE(0);TAG$
938   IV%=1
940   IF ARGS%(3)=0 THEN TAG$=" MALE":IV%=0 ELSE IF ARGS%(3)=1 THEN TAG$=" FEMALE" ELSE TAG$=OOR$
942   ARG$=STR$(ARGS%(3))
944   SP%=5-LEN(ARG$)
946   PRINT "     Voice=";INVERSE(IV%);ARG$;SPC(SP%);INVERSE(0);TAG$
948   IF ARGS%(4)=22200 THEN IV%=0 ELSE IV%=1
950   IF ARGS%(4)<5000 OR ARGS%(4)>28000 THEN TAG$=OOR$ ELSE TAG$=""
952   ARG$=STR$(ARGS%(4))
954   SP%=5-LEN(ARG$)
956   PRINT "    Tuning=";INVERSE(IV%);ARG$;SPC(SP%);INVERSE(0);TAG$
958   IF ARGS%(5)=64 THEN IV%=0 ELSE IV%=1
960   IF ARGS%(5)<0 OR ARGS%(5)>64 THEN TAG$=OOR$ ELSE TAG$=""
962   ARG$=STR$(ARGS%(5))
964   SP%=5-LEN(ARG$)
966   PRINT "    VolUme=";INVERSE(IV%);ARG$;SPC(SP%);INVERSE(0);TAG$
968   IF ARGS%(6)=10 THEN IV%=0 ELSE IV%=1
970   IF ARGS%(6)<0 OR ARGS%(6)>11 THEN TAG$=OOR$ ELSE TAG$=CHAN$(ARGS%(6))
972   ARG$=STR$(ARGS%(6))
974   SP%=5-LEN(ARG$)
976   PRINT "   CHannel=";INVERSE(IV%);ARG$;SPC(SP%);INVERSE(0);TAG$
978   IV%=1
980   IF ARGS%(7)=0 THEN TAG$=" SYNCHRONOUS":IV%=0 ELSE IF ARGS%(7)=1 THEN TAG$=" ASYNCHRONOUS" ELSE TAG$=OOR$
982   ARG$=STR$(ARGS%(7))
984   SP%=5-LEN(ARG$)
986   PRINT "      Mode=";INVERSE(IV%);ARG$;SPC(SP%);INVERSE(0);TAG$
988   IV%=1
990   IF ARGS%(8)=0 THEN TAG$=" NORMAL":IV%=0 ELSE IF ARGS%(8)=1 THEN TAG$=" STOP"ELSE IF ARGS%(8)=2 THEN TAG$= " OVERRIDE"ELSE TAG$=OOR$
992   ARG$=STR$(ARGS%(8))
994   SP%=5-LEN(ARG$)
996   PRINT"   Control=";INVERSE(IV%);ARG$;SPC(SP%);INVERSE(0);TAG$
998   RETURN
1100  REM SAVE STRING
1105  X%=ASC(IN$)-176
1110  PHR$(X%)=PHNM$
1115  CC%=2
1120  PRINT"STORED IN MEMORY #";STR$(X%)
1149  RETURN
1150  REM RECALL STRING
1155  X%=ASC(IN$)-176
1160  PHNM$=PHR$(X%)
1165  TEXT$="PHRASE MEMORY #"+STR$(X%)
1168  CC%=1
1171  TE%=0
1199  RETURN
1200  REM ADJUST PITCH
1205  SCNCLR
1207  CC%=1
1210  PRINT"********* PITCH ADJUSTMENT **********";CR$
1214  PRINT"  Enter a value in the range:";cr$;cr$;"    65 Minimum";cr$;"    110 Default";cr$;"    320 Maximum";cr$
1229  PRINT"  Or:";CR$;CR$;"    ALT(Q) to quit";CR$;"    RETURN alone to say it again";CR$
1250  INPUT "PITCH=>",ARG$
1255  IF LEN(ARG$)=0 THEN GOTO 1275
1257  IF ASC(ARG$)=Q% OR ASC(ARG$)=QL% THEN RETURN
1260  ARG%=VAL(ARG$)
1265  IF ARG%<65 OR ARG%>320 THEN GOSUB 4100:GOTO 1200
1270  ARGS%(0)=ARG%
1275  REM LOOPEND
1280  GOSUB 4000: REM SPEAK
1285  GOTO 1200
1300  REM INFLECTION ADJUSTMENT
1302  SCNCLR
1303  CC%=1
1305  PRINT"******* INFLECTION ADJUSTMENT *******";CR$
1310  PRINT"  Enter one of the following:"CR$;CR$;"    0 for inflection (default)";CR$;"    1 for monotone";CR$
1315  PRINT"  Or:";CR$;CR$;"    ALT(Q) to quit";CR$;"    RETURN alone to say it again";CR$
1320  INPUT "INFLECTION=>",ARG$
1325  IF LEN(ARG$)=0 THEN GOTO 1375
1330  IF ARG$=Q$ OR ARG$=QL$ THEN RETURN
1335  ARG%=VAL(ARG$)
1340  IF ARG%<>0 AND ARG%<>1 THEN GOSUB 4100:GOTO 1300
1345  ARGS%(1)=ARG%
1375  GOSUB 4000: REM SPEAK
1380  GOTO 1300
1390  RETURN
1400  REM RATE ADJUSTMENT
1405  SCNCLR
1407  CC%=1
1410  PRINT"***** SPEAKING RATE ADJUSTMENT ******",CR$
1415  PRINT"  Entera value in the range:";CR$;CR$;"    40  Minimum";CR$;"    150 Default";CR$;"    400 Maximum";CR$
1420  PRINT"  Or:";CR$;"    ALT(Q) to quit";CR$;"    RETURN alone to say it again";CR$
1425  INPUT "RATE=>",ARG$
1430  IF LEN(ARG$)=0 THEN GOTO 1475
1435  IF ARG$=Q$ OR ARG$=QL$ THEN RETURN
1440  ARG%=VAL(ARG$)
1445  IF ARG%<40 OR ARG%>400 THEN GOSUB 4100:GOTO 1400
1450  ARGS%(2)=ARG%
1475  GOSUB 4000: REM SPEAK
1480  GOTO 1400
1500  REM VOICE ADJUSTMENT
1505  SCNCLR
1507  CC%=1
1510  PRINT"********** VOICE SELECTION **********";CR$
1520  PRINT"  Enter one of the following:";CR$;CR$;"    0 Male (default)";CR$;"    1 Female";CR$
1525  PRINT"  Or:";CR$;CR$;"    ALT(Q) to quit";CR$;"    RETURN alone to say it again";CR$
1527  INPUT "VOICE=>",ARG$
1530  IF LEN(ARG$)=0 THEN GOTO 1575
1535  IF ARG$=Q$ OR ARG$=QL$ THEN RETURN
1540  ARG%=VAL(ARG$) 
1545  IF ARG%<>0 AND ARG%<>1 THEN GOSUB 4100: GOTO 1500
1550  ARGS%(3)=ARG%
1575  GOSUB 4000: REM SPEAK
1580  GOTO 1500
1600  REM TUNING ADJUSTMENT
1603  SCNCLR
1604  CC%=1
1605  PRINT"********* TUNING ADJUSTMENT *********";CR$;"  Sampling frequency in Hertz";CR$
1610  PRINT"  Enter a value in the range:";CR$;CR$;"     5000 Minimum";CR$;"    22200 Default";CR$;"    28000 Maximum";CR$
1615  PRINT"  Or:";CR$;CR$;"    ALT(Q) to quit";CR$;"    RETURN alone to say it again";CR$
1625  INPUT "TUNING=>",ARG$
1630  IF LEN(ARG$)=0 THEN GOTO 1675
1635  IF ARG$=Q$ OR ARG$=QL$ THEN RETURN
1640  ARG%=VAL(ARG$)
1645  IF ARG%<5000 OR ARG%>28000 THEN GOSUB 4100:GOTO 1600
1650  ARGS%(4)=ARG%
1675  GOSUB 4000:REM SPEAK
1680  GOTO 1600
1700  REM VOLUME ADJUSTMENT
1705  SCNCLR
1707  CC%=1
1710  PRINT"********* VOLUME ADJUSTMENT *********";CR$
1715  PRINT"  Enter a value in the range:";CR$;CR$;"     0 No sound";CR$;"    64 Loudest (default)";CR$
1720  PRINT"  Or:";CR$;CR$;"    ALT(Q) to quit";CR$;"    RETURN alone to say it again";CR$
1725  INPUT "VOLUME=>",ARG$
1730  IF LEN(ARG$)=0 THEN GOTO 1775
1735  IF ARG$=Q$ OR ARG$=QL$ THEN RETURN
1740  ARG%=VAL(ARG$)
1745  IF ARG%<0 OR ARG%>64 THEN GOSUB 4100:GOTO 1700
1750  ARGS%(5)=ARG%
1775  GOSUB 4000:REM SPEAK
1780  GOTO 1700
1800  REM CHANNEL
1802  SCNCLR
1803  CC%=1
1805  PRINT"******** CHANNEL ASSIGNMENT *********";CR$
1810  PRINT" Enter one of the following:";CR$;CR$;"    0  Channel 0";CR$;"    1  Channel 1";CR$;"    2  Channel 2"
1812  PRINT"    3  Channel 3";CR$;"    4  Cannels 0 & 1";CR$;"    5  Channels 0 & 2";CR$;"    6  Channels 3 & 1"
1814  PRINT"    7  Channels 3 & 2";CR$;"    8  Either available left channel";CR$;"    9  Either available right channel"
1816  PRINT"    10 Either L/R pair (default)";CR$;"    11 Any available single channel"CR$
1818  PRINT"    ALT(Q) to quit";CR$;"    RETURN alone to say it again";CR$
1825  INPUT "CHANNEL=>",ARG$
1830  IF LEN(ARG$)=0 THEN GOTO 1875
1835  IF ARG$=Q$ OR ARG$=QL$ THEN RETURN
1840  ARG%=VAL(ARG$)
1845  IF ARG%<0 OR ARG%>11 THEN GOSUB 4100:GOTO 1800
1850  ARGS%(6)=ARG%
1875  GOSUB 4000:REM SPEAK
1880  GOTO 1800
1900  REM SYNCH MODE
1905  SCNCLR
1907  CC%=1
1910  PRINT"**** SYNCHRONIZATION MODE SELECT ****";CR$
1915  PRINT"  Enter one of the following:";CR$;CR$;"    0 Synchronous speech (default)";CR$;"    1 Asynchronous speech";CR$
1920  PRINT"  Or:";CR$;CR$;"    ALT(Q) to quit";CR$;"    RETURN alone to say it again";CR$
1925  INPUT "MODE=>",ARG$
1930  IF LEN(ARG$)=0 THEN GOTO 1975
1935  IF ARG$=Q$ OR ARG$=QL$ THEN RETURN
1940  ARG%=VAL(ARG$)
1945  IF ARG%<0 OR ARG%>1 THEN GOSUB 4100:GOTO 1900
1950  ARGS%(7)=ARG%
1975  GOSUB 4000:REM SPEAK
1980  GOTO 1900
2000  REM CONTROL SELECTION
2005  SCNCLR
2007  CC%=1
2010  PRINT"********* CONTROL SELECTION *********";CR$
2015  PRINT"  Enter one of the following:";CR$;CR$;"    0 Process normally (default)";CR$;"    1 Stop speech processing";CR$;"    2 Override processing";CR$
2020  PRINT"  Or:";CR$;CR$;"    ALT(Q) to quit";CR$;"    RETURN alone to say it again";CR$
2025  INPUT "CONTROL=>",ARG$
2030  IF LEN(ARG$)=0 THEN GOTO 1975
2035  IF ARG$=Q$ OR ARG$=QL$ THEN RETURN
2040  ARG%=VAL(ARG$)
2045  IF ARG%<0 OR ARG%>2 THEN GOSUB 4100:GOTO 2000
2050  ARGS%(8)=ARG%
2075  GOSUB 4000:REM SPEAK
2080  GOTO 2000
3000  REM EDITOR
3001  SCNCLR
3002  PRINT"********** PHONEME EDITOR ***********"
3010  OTE%=1:OLD$=""
3029  GOSUB 3200:REM SHOW MEMS
3033  IF CH$="¤" THEN PRINT"      >>PHONEME ERROR AT BUG<<" ELSE PRINT" "
3035  PRINT""
3040  REM DISPLAY PHONEMES AND CURSOR
3043  IF CURS%>LEN(PHNM$)+1 THEN CURS%=LEN(PHNM$)+1
3046  CURS$=STRING$(CURS%-1," ")+CH$+STRING$(LEN(PHNM$)-CURS%," ")
3049  PH$=PHNM$
3052  OUT$=LEFT$(PH$,37)
3055  PRINT OUT$
3058  IF LEN(PH$)-37>0 THEN PH$=RIGHT$(PH$,LEN(PH$)-37) ELSE PH$=""
3061  OUT$=LEFT$(CURS$,37)
3064  PRINT OUT$
3067  IF LEN(CURS$)-37>0 THEN CURS$=RIGHT$(CURS$,LEN(CURS$)-37) ELSE CURS$=""
3068  IF LEN(CURS$)<>0 THEN GOTO 3052
3069  PRINT AT (1,21);SPC(37)
3070  IF IR%=-1 THEN PRINT AT(1,21);"INSERT------->" ELSE PRINT AT(1,21);"REPLACE WITH->"
3072  BEG%=CURS%
3074  ECH%=15:SIZ%=0:ES$=""
3076  CC%=1
3100  REM EDIT LOOP
3110  GETKEY IN$
3115  IF IN$=Q$ OR IN$=QL$ THEN RETURN
3120  IF IN$<>CHR$(155) THEN GOTO 3135
3122  GET IN$
3124  IF IN$="C" THEN GOTO 3300:REM ->
3126  IF IN$="D" THEN GOTO 3350:REM <-
3135  IF IN$=CHR$(127) THEN GOTO 3400:REM DELETE
3137  IF IN$=CHR$(13) THEN GOTO 3500:REM RETURN
3140  IF IN$="É" OR IN$="é" THEN GOTO 3800:REM INSERT
3143  IF IN$="Ò" OR IN$="ò" THEN GOTO 3900:REM REPLACE
3146  IF IN$=CHR$(8) THEN GOTO 3600:REM BS
3147  IF IN$="¯" OR IN$="¿" THEN GOSUB 500:GOTO 3001:REM INST
3148  IF IN$=CHR$(27) THEN GOTO 3650: REM ESC
3149  IF IN$<>CHR$(237) AND IN$<>CHR$(205) AND IN$<>CHR$(174) AND IN$<>CHR$(255) THEN GOTO 3152
3150  GETKEY IN$
3151  IF ASC(IN$)>175 AND ASC(IN$)<186 THEN GOSUB 1100:GOTO 3100:REM STORE
3152  IF ASC(IN$)>175 AND ASC(IN$)<186 THEN GOSUB 1150:GOSUB 4000:GOTO 3000:REM RECALL
3190  ES$=ES$+IN$
3192  SIZ%=SIZ%+1
3194  PRINT AT(ECH%,21);IN$
3196  ECH%=ECH%+1
3199  goto 3100
3200  REM DISPLAY MEMORIES
3205  X$="MEMORIES USED »"
3210  FOR X%=0 TO 9
3215  IF PHR$(X%)="EH5MQPTIY." THEN X$=X$+"  " ELSE X$=X$+" "+STR$(X%)
3220  NEXT
3225  PRINT X$
3230  RETURN
3300  REM ->
3305  IF CURS%=255 OR CURS%=LEN(PHNM$)+1 THEN GOTO 3100
3315  PRINT AT (((CURS%-1) MOD 37)+1,6+2*FIX((CURS%-1) / 37));FIL$
3320  PRINT AT ((CURS% MOD 37)+1,6+2*FIX(CURS%/37));CH$
3325  CURS%=CURS%+1
3328  IF CH$<>"»" THEN BEG%=CURS%:GOTO 3100
3330  TMP$=MID$(PHNM$,CURS%-1,1)
3333  SIZ%=SIZ%+1
3336  PRINT AT (ECH%,21);TMP$
3339  ECH%=ECH%+1
3345  GOTO 3100
3350  REM <-
3353  IF CH$="»" THEN GOTO 3100
3356  IF CURS%<=1 THEN GOTO 3100
3359  CURS%=CURS%-1:BEG%=CURS%
3362  PRINT AT (((CURS%-1) MOD 37)+1,6+2*FIX((CURS%-1) / 37));CH$
3365  PRINT AT ((CURS% MOD 37)+1,6+2*FIX(CURS%/37));" "
3368  GOTO 3100
3400  REM DELETE HANDLER
3402  IF SIZ%<>0 THEN GOTO 3300
3403  CH$="»"
3405  FIL$="*"
3407  BEG%=CURS%
3409  SIZ%=1
3410  PRINT AT(1,21);SPC(37)
3411  PRINT AT(1,21);"DELETE------->"
3413  ECH%=15
3420  GOTO 3300
3500  REM CR HANDLER
3502  IF SIZ%<>0 THEN OTE%=TE%:TE%=1:OLD$=PHNM$
3503  IF CH$<>"»" THEN GOTO 3520
3506  PHNM$=LEFT$(PHNM$,BEG%-1)+RIGHT$(PHNM$,LEN(PHNM$)-CURS%+1)
3508  CURS%=CURS%-SIZ%+1
3510  CH$="^":FIL$=" ":SIZ%=0
3512  GOTO 3590
3520  IF IR%<>-1 THEN GOTO 3530
3523  PHNM$=LEFT$(PHNM$,CURS%-1)+ES$+RIGHT$(PHNM$,LEN(PHNM$)-CURS%+1)
3526  ES$="":SIZ%=0:ECH%=15
3529  GOTO 3590
3530  PHNM$=LEFT$(PHNM$,BEG%-1)+ES$+RIGHT$(PHNM$,LEN(PHNM$)-BEG%-SIZ%+1)
3532  CURS%=CURS%+SIZ%:ES$="":SIZ%=0:BEG%=CURS%:ECH%=15
3590  X%=NARRATE(PHNM$,ARGS%())
3592  IF X%=0 THEN CH$="^" ELSE CURS%=X% :GOSUB 4020
3599  GOTO 3001
3600  REM BS
3606  IF SIZ%<1 THEN GOTO 3100
3608  SIZ%=SIZ%-1:ECH%=ECH%-1
3609  PRINT AT (ECH%,21);" "
3615  ES$=LEFT$(ES$,SIZ%)
3620  IF CH$<>"»" THEN GOTO 3100
3623  CURS%=CURS%-1
3626  PRINT AT (((CURS%-1) MOD 37)+1,6+2*FIX((CURS%-1)/37));CH$
3629  PRINT AT ((CURS% MOD 37)+1,6+2*FIX(CURS%/37));" "
3649  GOTO 3100
3650  REM ESCAPE HANDLER
3653  X%=OTE%
3656  OTE%=TE%
3659  TE%=X%
3662  PK$=OLD$
3665  OLD$=PHNM$
3668  PHNM$=PK$
3671  GOTO 3001
3800  REM INSERT HANDLER
3803  IR%=-1:ES$="":SIZ%=0
3806  ECH%=15
3807  PRINT AT(1,21);SPC(37)
3809  PRINT AT(1,21);"INSERT------->"
3812  CH$="^"
3890  GOTO 3100
3900  REM REPLACE HANDLER
3903  PRINT AT(1,21);SPC(37)
3905  PRINT AT(1,21);"REPLACE WITH->"
3910  ES$=""
3913  ECH%=15
3916  SIZ%=0
3918  CH$="^"
3920  IR%=0
3950  GOTO 3100
4000  REM SPEAKS A PHONEME STRING
4005  CURS%=NARRATE(PHNM$,ARGS%())
4010  IF CURS%=0 THEN CH$="^":RETURN
4020  X%=NARRATE("IHT (DAH3Z NAA4T) MEYK SEH4NS.")
4025  CH$="¤"
4030  PE%=-1
4050  RETURN
4100  REM OUT OF BOUNDS MESSAGE
4105  PK$="DHAET IHZ AW8T (AHV BAW4NDZ). YUW MAHST RIY5QXEH3NTER AH VAE3LYUW3 (IH4N REY3NDJ)."
4110  X%=NARRATE(PK$)
4120  RETURN
