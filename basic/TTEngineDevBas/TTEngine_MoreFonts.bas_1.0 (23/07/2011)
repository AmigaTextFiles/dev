' *********************************************************************
'                         TTEngine_MoreFonts.bas
'       de/by Dámaso D. Estévez {correoamidde-aminet000,yahoo,es}
'          AmiSpaTra - http://www.xente.mundo-r.com/amispatra/
'
'                    based over C version included in
'             the developper package by Grzegorz Kraszewski
'                            ---------------
'                 basado en la versión en C incluida en
'          el paquete de desarrolladores de Grzegorz Kraszewski
'
'          All rights reserved / Todos los derechos reservados
'
'                 TTEngine's test: Using some TT fonts
'   Prueba de TTEngine: Utilización de algunos tipos de impresión TT
' ---------------------------------------------------------------------
'         CodeWatcher by Michael Plitkins says what this program
'          don't free all memory allocated... but the C version
'               has the same behaviour (a TTEngine library
'            fault or feature (deferred resource release)?).
'
'      CodeWatcher de Michael Plitkins informa de que este programa
'     no libera toda la memoria reservada... pero la versión en C se
'      comporta igual (¿es un problema o una prestación (liberación
'             demorada) de la propia biblioteca TTEngine?).
' ---------------------------------------------------------------------
'             All rights reserved over this derivative work:
'                  my Hisoft Basic developpers package.
'       Forbidden to remove ALL legal/copyrights remarks included
'            in this package: if you create a derivate work,
'                  you MUST to include all legal notes.
'
'       Todos los derechos reservados sobre este trabajo derivado:
'    mi paquete para desarrolladores que programen con Hisoft Basic.
'          Está prohibido eliminar cualquier comentario o nota
'     de autoría/legal de este paquete: si crea un trabajo derivado
'        HA DE INCLUIR OBLIGATORIAMENTE TODAS LAS NOTAS LEGALES.
' *********************************************************************

v$ = "$VER: TTEngine_MoreFonts 1.0 (23/07/2011) by Dámaso 'AmiSpaTra' Domínguez based over C version wrote by Grzegorz Kraszewski"

'        Compiler's metacommands (see the Hisoft Basic Manual)
' Metacomandos para el compilador (consulte el manual del Hisoft Basic)
' ---------------------------------------------------------------------
REM $NOWINDOW
REM $NOBREAK
REM $NOEVENT

' Hisoft Basic include files / Ficheros de inclusión del Hisoft Basic
' ---------------------------------------------------------------------
REM $include dos.bh
REM $include exec.bh
REM $include intuition.bh
REM $include graphics.bh
REM $include ttengine.bh
REM $include asl.bh
REM $include utility.bc                                      ' TAG_END&

'    From/De:  http://aminet.net/package/dev/basic/AST_HBRoutines
'             Fix the paths / Corrige las rutas de acceso
' ---------------------------------------------------------------------
REM $include BLib_AST/FixPath.bas

'          Some global vars / Algunas variables globales
' ---------------------------------------------------------------------
DIM MainTags&((10-1)*2)           ' 10 Tags / Etiquetas (OPTION BASE 0)
CONST NFN% = 4%        ' Number of fonts / Número de tipos de impresión
dummy& = NULL&                                ' Misc uses / Uso variado

' =====================================================================
'         Subroutines and functions / Subrutinas y funciones
' =====================================================================

FUNCTION get_font_name$
	LOCAL freq&, localtags&

	DIM localtags&((6-1)*2)        ' 6 Tags / Etiquetas (OPTION BASE 0)

	freq& = AllocAslRequest&(ASL_FileRequest&, NULL&)

	IF freq& THEN

		TAGLIST VARPTR(localtags&(0)), _
			ASLFR_TitleText&,      SADD("Select a TrueType font / Elija un tipo TrueType"+CHR$(0)), _
			ASLFR_InitialDrawer&,  SADD("FONTS:"+CHR$(0)), _
			ASLFR_DoPatterns&,     TRUE&, _
			ASLFR_InitialPattern&, SADD("#?.ttf"+CHR$(0)), _
			ASLFR_RejectIcons&,    TRUE&, _
			TAG_END&

		IF AslRequest&(freq&, VARPTR(localtags&(0))) THEN

				'           As you can see, this version is more
				'                simple and don't require
				'               a free_font_name function
				'                    vs the C version.
				'
				'              Como puede ver, esta versión
				'              es más simple y no requiere
				'              una función free_font_name
				'               frente a la versión en C.
				' -----------------------------------------------------
				get_font_name$ = FixPath$(PEEK$(PEEKL(freq&+fr_Drawer%))) + PEEK$(PEEKL(freq&+fr_File%))

		END IF

		FreeAslRequest freq&

	END IF

	ERASE localtags&
	
END FUNCTION

' =====================================================================
'                      Main code / Código principal
' =====================================================================

'  Opening all using easy method / Abriendo todo por el método rápido
' ---------------------------------------------------------------------

LIBRARY OPEN "exec.library"
LIBRARY OPEN "dos.library",       37&
LIBRARY OPEN "intuition.library", 39&
LIBRARY OPEN "graphics.library",  39&
LIBRARY OPEN "asl.library",       38&
LIBRARY OPEN "ttengine.library",   4&

'  Workbench or CLI? / ¿Workbench o interfaz de la línea de comandos?
' ---------------------------------------------------------------------
IF PEEKL(SYSTAB+8) <> 0 THEN
	wb& = TRUE&
ELSE
	wb& = FALSE&
END IF

'    Fonts pointers' table / Tabla de punteros a los tipos abiertos
' ---------------------------------------------------------------------
DIM fonts&(NFN%-1)

dummyst$ = "TTEngine test -"+STR$(NFN%)+" fonts at once / Prueba de TTEngine:"+STR$(NFN%)+" tipos a la vez"+CHR$(0)

TAGLIST VARPTR(MainTags&(0)),   _
	WA_Top&,           25&, _
	WA_Left&,           0&, _
	WA_Width&,        550&, _
	WA_Height&,       150&, _
	WA_CloseGadget&, TRUE&, _
	WA_DragBar&,     TRUE&, _
	WA_DepthGadget&, TRUE&, _
	WA_IDCMP&,       IDCMP_CLOSEWINDOW&,_
	WA_Title&,       SADD(dummyst$), _
	TAG_END&

win& = OpenWindowTagList&(NULL&, VARPTR(MainTags&(0)))

IF win& THEN

	running& = TRUE&
	rp&      = PEEKL(win&+RPort%)

	FOR i% = 0% TO NFN%-1

		fontname$ = get_font_name$

		TAGLIST VARPTR(MainTags&(0)),   _
			TT_FontFile&, SADD(fontname$+CHR$(0)), _
			TT_FontSize&, 18&, _
			TAG_END&

		fonts&(i%) = TT_OpenFontA&(VARPTR(MainTags&(0)))

		IF fonts&(i%) THEN

			PRINT "Opening font ";i%+1;"@ $";HEX$(fonts&(i%));" - ";
			PRINT "Abriendo el tipo de impresión nº ";i%+1;"en $";HEX$(fonts&(i%))

		ELSE

			dummy& = PutStr&(SADD("TT_OpenFont failed! / ¡TT_OpenFont ha fracasado!"+CHR$(10)+CHR$(0)))

		END IF

	NEXT i%

	SetDrMd rp&, JAM1&
	SetAPen rp&, 2&

	TAGLIST VARPTR(MainTags&(0)), _
		TT_Window&, win&, _
		TAG_END&

	dummy& = TT_SetAttrsA&(rp&, VARPTR(MainTags&(0)))

	' Spanish text: Texto generado con uno de las cuatro tipos TT
	' -----------------------------------------------------------
	dummyst$ = "Text rendered with one of four TrueType fonts."

	FOR i% = 0% TO NFN%-1

		dummy& = TT_SetFont&(rp&, fonts&(i%))

		IF dummy& THEN

			'         Moved this line: the program won't print
			'            the message if the font isn't open.
			'
			'        Esta línea no está en el mismo lugar que en
			'         la versión en C: el programa no imprimirá
			'          el mensaje si el tipo no ha sido abierto.
			' ---------------------------------------------------------
			PRINT "Using font ";i%+1;"at $";HEX$(fonts&(i%)); " - ";"Utilizando el tipo de impresión nº ";i%+1;"en $";HEX$(fonts&(i%))

			Move rp&, 10&, 20&+(i%+1)*18
			TT_Text& rp&, SADD(dummyst$), LEN(dummyst$)

		END IF

	NEXT i%

	' Closing the TT fonts opened / Cerrando los tipos TT abiertos
	' -------------------------------------------------------------
	FOR i% = 0 TO NFN%-1

		IF fonts&(i%) THEN TT_CloseFont fonts&(i%)

	NEXT i%


	' Beware! Use 1& (and not only 1) in the "(1& <<..." or the expression won't work
	' ¡Cuidado! Utilice 1& (y no sólo 1) en la expresión "(1& <<..." o no funcionará
	' -------------------------------------------------------------------------------
	sigmask& = SIGBREAKF_CTRL_C& OR (1& << PEEKB(PEEKL(win&+UserPort%)+mp_SigBit%))

	WHILE running&

		signals& = xWait&(sigmask&)

		'      Break support (only in this loop) via AmigaOS.

		'         Soporte de la interrupción del usuario
		'       (sólo en este bucle) a través del AmigaOS.
		' ---------------------------------------------------------
		IF (signals& AND SIGBREAKF_CTRL_C&) THEN running& = FALSE&

		IF (signals& AND (1& << PEEKB(PEEKL(win&+UserPort%)+mp_SigBit%))) THEN

			'       Get the message / Se obtiene el mensaje
			' -----------------------------------------------------
			imsg& = GetMsg&(PEEKL(win&+UserPort%))

			IF imsg& THEN

				'       The user want to close the window?
				'      ¿El usuario quiere cerrar la ventana?
				' -------------------------------------------------
				IF PEEKL(imsg&+Class%) = IDCMP_CLOSEWINDOW& THEN
					running& = FALSE&
				END IF

				ReplyMsg imsg&

			END IF

		END IF

	WEND

	TT_DoneRastPort PEEKL(win&+RPort%)
	CloseWindow win&

ELSE

	dummy& = PutStr&(SADD("I can't open a window / ¡Imposible abrir una ventana!"+CHR$(10)+CHR$(0)))

END IF

LIBRARY CLOSE

'       Return a value if the user starts the program from Shell.

'         Devuelve un valor si el usuario ha puesto en marcha
'         el programa desde la interfaz de línea de comandos.
' ---------------------------------------------------------------------
IF wb& = FALSE& THEN
	STOP RETURN_OK&
END IF

END
