; so-installer 1.2 (04.05.2009)
; Copy shared objects easily with version/date checking and scheduled copying
; on reboot for failed files.
; http://www.unsatisfactorysoftware.co.uk 
; 
; Source SObjs must be in SObjs directory, and will be copied to SObjs: assign 
; Usage:
; Copy this entire file up to and not including the line "(welcome)" into
; your Installer script.
; Call it with:
; (p_copysobj "libgcc.so") 
; The variable #failedsobjs contains a list of the files that failed to copy.
;
; This script can also be used verbatim to install all files in an SObjs
; directory to SObjs:
;

(procedure p_failedsobjs
	(if #failedsobjs
		(message "The following shared objects failed to copy.  These will be updated on next reboot.\n\n" #failedsobjs)
	)
)

(procedure p_schedulesobj #sobj
	(transcript "Scheduling update of " #sobj " for next reboot")

	(set #failedsobjs (cat #failedsobjs "\n" #sobj))

	(makedir "SObjs:so-installer")

	(copyfiles
		(source (tackon "SObjs" #sobj))
		(dest "SObjs:so-installer")
	)

	(startup "Shared object installer"
		(prompt "Adding commands to user-startup to schedule copy of " #sobj " on next reboot")
		(help @startup-help)
		(command "if EXISTS SObjs:so-installer\n")
		(command "  copy SObjs:so-installer SObjs: CLONE FORCE QUIET\n")
		(command "  delete SObjs:so-installer ALL FORCE QUIET\n")
		(command "endif\n")
	)
)

(procedure p_comparesobj #sobj
	(set #same 0)
	(set #sobj-version (getversion (tackon "SObjs/" #sobj)))
	(if (= #sobj-version 0)
		(
			(if (exists (tackon "SObjs:" #sobj))
				(
					(set #file-newer (earlier (tackon "SObjs:" #sobj) (tackon "SObjs/" #sobj)))
					(set #old-size (getsize (tackon "SObjs:" #sobj)))
					(set #new-size (getsize (tackon "SObjs/" #sobj)))

					(if (AND (= #old-size #new-size) (= #file-newer 0)) (set #same 1))
				)
			)
		)
		; else if version info is available
		(
			(set #sobj-oldversion (getversion (tackon "SObjs:" #sobj)))

			(if (<= #sobj-version #sobj-oldversion) (set #same 1))
		)
	)
)

(procedure p_copysobj #sobj
	(set #sobj-version (getversion (tackon "SObjs/" #sobj)))

	(if (= #sobj-version 0)
		(
			(transcript "Date compare " #sobj)

			(if (exists (tackon "SObjs:" #sobj))
				(
					(set #file-newer (earlier (tackon "SObjs:" #sobj) (tackon "SObjs/" #sobj)))

					(if (= #file-newer 0)
						(
							(set #newer-text "has an older datestamp")
						)
						;else
						(
							(set #newer-text "has a newer datestamp")
						)
					)

					(set #old-size (getsize (tackon "SObjs:" #sobj)))
					(set #new-size (getsize (tackon "SObjs/" #sobj)))

					(if (AND (= #old-size #new-size) (= #file-newer 0))
						(
							(set #copy 0)
						)
						;else
						(
							(if (OR (= @user-level 2) (AND (= @user-level 1) (<> #file-newer 0)))
; Expert users are always prompted
; Average users are prompted if the file trying to be copied is newer
; Novice users are never prompted
; This is roughly equivalent to (copylib (confirm))
; No prompting occurs if the destination does not exist (silent copy)
; or the files are the same size and the one being copied isn't newer (don't copy)
								(
									(set #copy
										(askbool
											(prompt "Copying " #sobj "...\n\n"
												"Version to install: " #new-size " bytes\n"
												"Version currently installed: " #old-size " bytes\n\n"
												"The file to copy " #newer-text)
											(help @askbool-help)
											(default #file-newer)
											(choices "Proceed with copy" "Skip this part")
										)
									)
								)
								;else
								(
									(set #copy #file-newer)
								)
							)
						)
					)
				)
				; else if dest file does not exist
				(
					(set #copy 1)
				)
			)

			(if (<> #copy 0)
				(
					(if (<> #AutoInstall 1)
						(
							(copyfiles
								(prompt "Copying " #sobj "...")
								(help @copyfiles-help)
								(source (tackon "SObjs/" #sobj))
								(dest "SObjs:")
								(optional "nofail" "force")
							)
						)
						;else
						(
							(run "CopyStore SObjs/" #sobj " SObjs:")
						)
					)

					(p_comparesobj #sobj)
					(if (= #same 0)
						(
							(p_schedulesobj #sobj)
						)
					)
				)
			)
		)
		; else if version info is available
		(
			(if (<> #AutoInstall 1)
				(
					(copylib
						(prompt "Copying " #sobj "...")
						(help @copylib-help)
						(source (tackon "SObjs/" #sobj))
						(dest "SObjs:")
						(optional "nofail" "force")
						(confirm "expert")
					)
				)
				;else
				(
					(run "CopyStore SObjs/" #sobj " SObjs:")
				)
			)

			(p_comparesobj #sobj)
			(if (= #same 0)
				(
					(p_schedulesobj #sobj)
				)
			)
		)
	)
)

;;;;;;;
; The below is example code.  It copies everything from SObjs/ to SObjs:
; If that's all you have to copy you are welcome to use this entire script as-is
; Otherwise copy the above procedures and use as per the comments at the top
; of the file.

(welcome)

(set @default-dest "SObjs:")

(working "Copying Shared Objects")

(foreach "SObjs" "#?"
	(p_copysobj @each-name)
)

(p_failedsobjs)
(exit)
