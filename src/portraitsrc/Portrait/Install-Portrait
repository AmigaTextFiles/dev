; $VER: Install-Portrait 1.5 (28.10.98)
; © 1998 Christopher January

(if (= @language "english")

  (
    (set @app-name "Portrait")

    (set #disclaimer-msg
      (cat "Please remember that Portrait is still very much"
           "\nunder development and it is likely to contain a,"
           "\nnumber of bugs and many unimplemented features. "
           "\nPlease read AFD-COPYRIGHT before installing     "
           "\nPortrait. You may not use Portrait unless you   "
           "\nagree to the terms in the file. This version of "
           "\nPortrait has been released for test purposes    "
           "\nonly.                                           ")
    )

    (set #ask-rtg
      (cat "Please specify if you intend to use Portrait with"
           "\na graphics card or not.                          "
           "\nGraphics card compatibility is minimal at the    "
           "\nmoment but will be improved in future versions.  ")
    )

    (set #ask-rtg-help
      (cat "Clicking Yes here will set the RTG tooltype in"
           "\nthe Portrait icon                             ")
    )

    (set #read-docs-now
      (cat "Do you wish to read the AmigaGuide"
           "\ndocument now?")
    )

    (set #install-program "Installing Portrait program")

    (set #install-mode "Installing PortraitMode program")

    (set #install-projects "Installing example projects")

    (set #install-projectmanager "Installing ProjectManager")

    (set #install-prefs "Installing default preferences")

    (set #install-icons "Installing toolbar icons")

    (set #install-misc "Installing supplementary files")

    (set #install-docs "Installing documentation")

    (set #install-dest
      (cat "Where do you wish to install Portrait?"
           "\nA new drawer will be created")
    )

    (set #ask-datatype
      (cat "Do you wish to have the work buffer"
           "\ndatatype installed?")
    )

    (set #ask-datatype-help
      (cat "Clicking YES will install the work.datatype"
           "\nclass and WORK datatype descriptor")
    )

    (set #install-datatype "Installing datatype")

    (set #bad-kick "Portrait requires Kickstart 3.0+")

    (set #copy-tabs "Copying tabs.gadget to SYS:Classes/Gadgets")

    (set #hi-msg
      (cat "Welcome to the Portrait Installation"
           "\n"
           "\nPortrait by Christopher January"
           "\n© 1998")
    )
  )
  (abort "Install script can only be run in English")
)

(if (< (/ (getversion) 65536) 39)
  (abort #bad-kick)
)

(complete 0)

(welcome #hi-msg)

(complete 7)

(message #disclaimer-msg)

(complete 14)

(set @default-dest
  (askdir
    (prompt #install-dest)
    (help @askdir-help)
    (default @default-dest)
  )
)

(complete 21)

(set @default-dest (tackon @default-dest "Portrait"))

(copyfiles
  (prompt #install-program)
  (source "Portrait")
  (infos)
  (dest @default-dest)
  (help @copyfiles-help)
)

(complete 28)

(if
  (askbool
    (prompt #ask-rtg)
    (help #ask-rtg-help)
  )
  (tooltype
    (dest (tackon @default-dest "Portrait"))
    (settooltype "RTG" "YES")
  )
)

(complete 35)

(copyfiles
  (prompt #install-mode)
  (source "PortraitMode")
  (infos)
  (dest @default-dest)
  (help @copyfiles-help)
)

(complete 42)

(copyfiles
  (prompt #install-projectmanager)
  (source "ProjectManager")
  (infos)
  (dest @default-dest)
  (help @copyfiles-help)
)

(complete 49)

(copyfiles
  (prompt #install-misc)
  (source "")
  (choices "Projects.list")
  (infos)
  (confirm)
  (dest @default-dest)
  (help @copyfiles-help)
)

(complete 56)

(copyfiles
  (prompt #install-icons)
  (help @copyfiles-help)
  (source "Icons")
  (all)
  (infos)
  (dest (tackon @default-dest "Icons"))
  (confirm)
)

(complete 63)

(copyfiles
  (prompt #install-docs)
  (source "Docs")
  (help @copyfiles-help)
  (all)
  (infos)
  (dest (tackon @default-dest "Docs"))
  (confirm)
)

(complete 70)

(copyfiles
  (prompt #install-projects)
  (source "Projects")
  (help @copyfiles-help)
  (all)
  (infos)
  (dest (tackon @default-dest "Projects"))
  (confirm)
)

(complete 77)

(copylib
  (prompt #copy-tabs)
  (source "Classes/Gadgets/tabs.gadget")
  (dest "SYS:Classes/Gadgets")
  (help @copylib-help)
  (confirm)
)

(complete 84)

(if
  (askbool
    (prompt #ask-datatype)
    (help #ask-datatype-help)
  )
  (
    (copylib
      (prompt #install-datatype)
      (source "Datatype/work.datatype")
      (dest "SYS:Classes/Datatypes")
      (help @copylib-help)
    )
    (copyfiles
      (prompt #install-datatype)
      (source "Datatype/WORK")
      (dest "DEVS:Datatypes")
      (infos)
    )
  )
)


(complete 90)

(if
  (askbool
    (help @askbool-help)
    (prompt #read-docs-now)
  )
  (run "Multiview" (tackon @default-dest "Docs/Portrait.guide") (safe))
)

(complete 100)
