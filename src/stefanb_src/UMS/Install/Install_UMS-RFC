; $VER: Install_UMS-RFC 1.0 (22.02.99)

( if (= @language "english")
(
  (set #intro
  (cat "Installing UMS RFC (UUCP/NNTP/SMTP/POP) network driver..."
  ))
  (set #requires
  (   cat "This installer script requires at least UMS-server 11.32 and "
       "ums.library 11.19"
  ))
  (set #rexxdossupport
  (cat "Copying rexxdossupport.library..."
  ))
  (set #cannotwrite
  (cat "Unable to write variable: "
  ))
  (set #cannotdel
  (cat "Unable to delete variable: "
  ))
  (set #cannotread
  (cat "Unable to read variable: "
  ))
  (set #info
  (cat "You should have completed the installation of UMS itself before "
       "you can use this script.\n\n"

       "If you are using more than one newsfeed you'll most likely have "
       "to adjust the configuration manually. Please read all "
       "documentation!.\n\n"
  ))
  (set #copying
  (cat "Copying files..."
  ))
  (set #dest
  (cat "In which disk or drawer did you install Universal Message System?\n\n"

       "You can install \"in place\" if you already unpacked the archive "
       "directly to  the correct destination (in that case just select ok)."
  ))
  (set #sysopaccount
  (cat "Please enter the sysop account on your UMS system"
  ))
  (set #sysoppassword
  (cat "Please enter the sysop password"
  ))
  (set #nologin
  (cat "unable to login"
  ))
  (set #alreadyinstalled
  (cat "RFC subsystem already installed"
  ))
  (set #instlibs
  (cat "Installing libraries"
  ))
  (set #protocols
  (cat "Which transfer protocols do you want to use?"
  ))
  (set #noprotocols
  (cat "If you aren't using any protocols there's no need for UMSRFC!"
  ))
  (set #needpop
  (cat "If you do not have a permanent domainname you need at least "
       "a POP account to receive mail (and use UMSRFC)!"
  ))
  (set #encoding
  (cat "When exporting messages which contain non-ASCII characters for "
       "transportation over non-transparent (that is 8 bit clean) links, "
       "UMS RFC can encode them according to the MIME standard."
  ))
  (set #mailquotedprintable
  (cat "encode mail as quoted-printable"
  ))
  (set #newsquotedprintable
  (cat "encode news as quoted-printable"
  ))
  (set #shortusername
  (cat "Please enter a short username (if you are using POP enter the POP "
       "username) for "
  ))
  (set #cannotcreate
  (cat "Cannot create RFC exporter!"
  ))
  (set #useuucpcall
  (cat "In order to transfer news and mail just call the script "
  ))
  (set #hostnamestatus
  (cat "Status of your host's name (temporary changes on every internet "
       "connection and is common for SLIP or PPP)"
  ))
  (set #hntemp
  (cat "temporary"
  ))
  (set #hnperm
  (cat "permanent"
  ))
  (set #domname
  (cat "Please the domainname of your node"
  ))
  (set #nodename
  (cat "Please enter your node's name (without domain)"
  ))
  (set #maps
  (cat "Is your system registered in the world wide maps?"
  ))
  (set #confuucp
  (cat "Now configuring your UUCP subsystem"
  ))
  (set #feedname
  (cat "Please enter the name of your feed (without domain)"
  ))
  (set #dumb
  (cat "Does your newsfeed rely on information in the UUCP mail "
       "envelopes (RFC976 format) instead of parsing to RFC822 "
       "header?\n\n"
       "Usually it does not, but if your newsfeed is running "
       "Dillon UUCP it requires this outdated information."
  ))
  (set #deldupes
  (cat "Do you want bad jobs, which had only dupe errors "
       "to be automatically deleted?\n\n"
       "Normally duplicate messages are flagged as error "
       "and the job is not deleted."
  ))
  (set #olddillon
  (cat "An old UUCP installation was detected. UMS-UUCP will use "
       "the config files (\"UULIB:config\", \"UULIB:L.sys\") "
       "of this installation. Please check if they are correct.\n"
  ))
  (set #telephone
  (cat "Please enter the telephone number of your newsfeed"
  ))
  (set #feedpw
  (cat "Please enter you password for your newsfeed"
  ))
  (set #expmeth
  (cat "Please choose exporting method for mail and news"
  ))
  (set #mailbatch
  (cat "batch mail"
  ))
  (set #newsbatch
  (cat "batch news"
  ))
  (set #mailcompress
  (cat "Please select compression method for mail"
  ))
  (set #maxbytes
  (cat "Please enter maximal number of bytes for one batch"
  ))
  (set #newscompress
  (cat "Please select compression method for news"
  ))
  (set #instassign
  (cat "Installing Assigns for UMS-UUCP"
  ))
  (set #removemanually
  (cat "You have to remove some parts of the RFC installation manually "
       "(e.g. AKAs, aliases)"
  ))
  (set #popserver
  (cat "Please enter the name of your POP3 server"
  ))
  (set #smtpserver
  (cat "Please enter the name of your SMTP server"
  ))
  (set #nntpserver
  (cat "Please enter the name of your NNTP server"
  ))
  (set #poppasswd
  (cat "Please the password (for the POP3 server) for user "
  ))
  (set #useipcall
  (cat "In order to transfer news and mail just call the script "
  ))
;  (set #
;  (cat
;  ))
))

( if (= @language "deutsch")
(
  (set #intro
  (cat "Installiere UMS RFC (UUCP/NNTP/SMTP/POP) Netzwerk Treiber..."
  ))
  (set #requires
  (cat "Dieses Installer-Script benötigt mindesten UMS-Server 11.32 und "
       "ums.library 11.19"
  ))
  (set #rexxdossupport
  (cat "Kopiere rexxdossupport.library..."
  ))
  (set #cannotwrite
  (cat "Kann Variable nicht schreiben: "
  ))
  (set #cannotdel
  (cat "Kann Variable nicht löschen: "
  ))
  (set #cannotread
  (cat "Kann Variable nicht lesen: "
  ))
  (set #info
  (cat "Sie sollten die UMS-Server-Installation schon abgeschlossen haben, "
       "bevor Sie dieses Script benutzen können.\n\n"

       "Fallst Sie mehr als einen Newsfeed benutzen müssen Sie die "
       "Konfiguration von Hand ändern. "
       "Bitte lesen Sie die Dokumentation!.\n\n"
  ))
  (set #copying
  (cat "Kopiere Dateien..."
  ))
  (set #dest
  (cat "In welche Schublade bzw. auf welches Laufwerk haben Sie das "
       "Universal Message System installiert?\n\n"

       "Es ist möglich eine \"in place\" Installation durchzuführen "
       "(In diesem Fall einfach OK auswählen)"
  ))
  (set #sysopaccount
  (cat "Bitte geben Sie Ihren Sysop Account-Namen für UMS ein"
  ))
  (set #sysoppassword
  (cat "Bitte geben Sie das Sysop Passwort ein."
  ))
  (set #nologin
  (cat "Einloggen fehlgeschlagen"
  ))
  (set #alreadyinstalled
  (cat "Die RFC Treiber wurden schon installiert"
  ))
  (set #instlibs
  (cat "Installiere Libraries"
  ))
  (set #protocols
  (cat "Welche Protokolle möchten Sie benutzen?"
  ))
  (set #noprotocols
  (cat "Wenn Sie keines dieser Protokolle benutzen, dann brauchen Sie "
       "UMS-RFC nicht!"
  ))
  (set #needpop
  (cat "Falls sie keinen dauerhaften Domainnamen haben, dann brauchen Sie "
        "zumindest einen POP-Account um Mail empfangen (und UMS-RFC "
        "benutzen) zu können"
  ))
  (set #encoding
  (cat "Wenn Nachrichten, die exportiert werden und nicht-ASCII Zeichen "
       "enthalten, über nicht transparente (d.h. nicht 8-Bit) "
       "Verbindungen übertragen werden, dann kann UMS RFC diese "
       "Nachrichten nach dem MIME-Standard codieren."
  ))
  (set #mailquotedprintable
  (cat "Mail \"quoted-printable\" codieren"
  ))
  (set #newsquotedprintable
  (cat "News \"quoted-printable\" codieren"
  ))
  (set #shortusername
  (cat "Bitte geben sie einen kurzen Benutzername (falls Sie POP benutzen "
       "geben sie bitte den POP-Benutzernamen) ein für: "
  ))
  (set #cannotcreate
  (cat "Kann RFC-Exporter nicht erzeugen!"
  ))
  (set #useuucpcall
  (cat "Um Mail und News zu Übertragen müssen Sie einfach diese Script "
       "starten: "
  ))
  (set #hostnamestatus
  (cat "Status Ihres Hostnamens: (tempörär ändert sich bei jeder Internet-"
       "Verbindung - kommt häufig bei SLIP oder PPP vor)"
  ))
  (set #hntemp
  (cat "temporär"
  ))
  (set #hnperm
  (cat "permanent"
  ))
  (set #domname
  (cat "Bitte geben Sie den Domain-Namen Ihres Computers ein"
  ))
  (set #nodename
  (cat "Bitte geben Sie den Namen Ihres Computers (ohne Domain) ein"
  ))
  (set #maps
  (cat "Ist Ihr Computer in den weltweiten \"maps\" registriert?"
  ))
  (set #confuucp
  (cat "Nun wird Ihr UUCP-Untersystem konfiguriert"
  ))
  (set #feedname
  (cat "Bitte geben Sie den Namen Ihres \"Neewsfeeds\" (ohne Domain) ein"
  ))
  (set #dumb
  (cat "Verläßt sich Ihr Newsfeed auf die Information im UUCP "
       "\"Mail-Envelopes\" (RFC976 Format), anstelle die RFC822 "
       "\"Header\" zu beachten?\n\n"
       "Normalerweise tut das kein Newsfeed, aber das veraltete "
       "Dillon UUCP benötigt diese Information."
  ))
  (set #deldupes
  (cat "Wollen Sie, daß Nachrichtenpakete, welche nur Fehler aufgrund "
       "doppelter Nachrichten enthalten, gelöscht werden?"
  ))
  (set #olddillon
  (cat "Eine alte UUCP-Installation wurde erkannt. UMS-UUCP benutzt nur "
       "die Konfigurationsdateien (\"UULIB:config\", \"UULIB:L.sys\") "
       "dieser Installation. Bitte überprüfen Die die Korrektheit dieser "
       "Dateien.\n"
  ))
  (set #telephone
  (cat "Bitte geben Sie die Telefonnummer Ihres Newsfeeds ein"
  ))
  (set #feedpw
  (cat "Bitte geben Sie das Passwort Ihres Newsfeeds ein"
  ))
  (set #expmeth
  (cat "Bitte wählen sie die Export-Variante für Ihre Nachrichten:"
  ))
  (set #mailbatch
  (cat "Mail \"batchen\" (zusammenfassen)"
  ))
  (set #newsbatch
  (cat "News \"batchen\" (zusammenfassen)"
  ))
  (set #mailcompress
  (cat "Bitte Komprimierungsmethode für Mail auswählen"
  ))
  (set #maxbytes
  (cat "Bitte Maximalzahl von Bytes pro Batch-Datei angeben"
  ))
  (set #newscompress
  (cat "Bitte Komprimierungsmethode für News auswählen"
  ))
  (set #instassign
  (cat "Installiere Assigns für UMS-UUCP"
  ))
  (set #removemanually
  (cat "Einige Teile der RFC-Installation müssen von Hand entfernt werden "
       "(z.B. AKAs, Aliases)"
  ))
  (set #popserver
  (cat "Bitte Namen Ihres POP3-Servers angeben"
  ))
  (set #smtpserver
  (cat "Bitte Namen Ihres SMTP-Servers angeben"
  ))
  (set #nntpserver
  (cat "Bitte Namen Ihres NNTP-Servers angeben"
  ))
  (set #poppasswd
  (cat "Bitte geben Sie das Passwort (für den POP3-Server) für diesen "
       "User ein: "
  ))
  (set #useipcall
  (cat "Um News und Mails zu übertragen einfach diese Script ausfrufen: "
  ))
;  (set #
;  (cat
;  ))
))

(procedure GETCONFIG
  (
    (set cmd (cat "rx //rexx/GetUMSConfig.rexx >ENV:umsvariabledata \""
                     sysopaccount "\" \"" sysoppassword "\" \""
                     variable "\" " options))
    (debug cmd)
    (set res (run cmd))

;     (if (>= res 20)  (abort (cat #cannotread variable)))
    (set data (getenv "umsvariabledata"))
    (set data (substr data 0 (- (strlen data) 1)))
    (debug (cat "Got (res " res ") configvariable: '" variable
                "'\ndata: '" data  "'\noptions: " options))
  )
)

(procedure DELCONFIG
  (
    (set res (run (cat "rx //rexx/SetUMSConfig.rexx \"" sysopaccount "\" \""
                       sysoppassword "\" \"" variable "\"" options)))
    (debug (cat "Delete configvariable: '" variable "'\noptions: " options))
    (if (<> res 0)  (abort (cat #cannotdel variable)))
  )
)

(procedure SETCONFIG
  (
    (set res (run (cat "rx //rexx/SetUMSConfig.rexx \"" sysopaccount "\" \""
                       sysoppassword "\" \"" variable "\" \"" data "\" "
                       options)))
    (debug (cat "Set configvariable: '" variable "'\ndata: '" data
                "'\noptions: " options))
    (if (<> res 0)  (abort (cat #cannotwrite variable)))
  )
)

(transcript #intro)

(set vernum (getversion "L:umsserver"))
(set ver (/ vernum 65536))
(if (< ver                      11) (abort #requires))
(if (< (- vernum (* ver 65536)) 32) (abort #requires))

(set vernum (getversion "LIBS:ums.library"))
(set ver (/ vernum 65536))
(if (< ver                      11) (abort #requires))
(if (< (- vernum (* ver 65536)) 19) (abort #requires))

(welcome #info)

(complete 0)

(run "SYS:System/RexxMast")
(run "Resident SYS:Rexxc/RX PURE")
(copylib
  (help @copylib-help)
  (prompt #rexxdossupport)
  (source "//libs/rexxdossupport.library")
  (dest "LIBS:")
  (optional "fail" "force")
  (confirm)
)

(complete 5)

(set sysopaccount
  (askstring
    (prompt #sysopaccount)
    (help @askstring-help)
    (default "sysop")
  )
)

(set sysoppassword
  (askstring
    (prompt #sysoppassword)
    (help @askstring-help)
    (default "")
  )
)

(set options "USER=\"rfc.default\"") (set variable "rfc.domainname") (GETCONFIG)
(if (= res 20) (abort #nologin))
(if (= res 0)
  (
    (if (askbool
         (prompt #alreadyinstalled)
         (choices "Uninstall" "Quit")
         (help #askbool-help)
       )
      (
        (set options "GLOBAL")
        (set variable "rfc.domainname") (DELCONFIG)
        (set variable "rfc.receivedname") (DELCONFIG)
        (set variable "rfc.pathname") (DELCONFIG)
        (set variable "rfc.mailencoding") (DELCONFIG)
        (set variable "rfc.newsencoding") (DELCONFIG)
        (run (cat "rx //rexx/DelUser.rexx \"" sysopaccount "\" \""
                  sysoppassword "\" \"rfc.default\""))
        (run (cat "rx //rexx/DelUser.rexx \"" sysopaccount "\" \""
                  sysoppassword "\" \"uucp.default\""))

        (set res (run (cat "rx //rexx/UserList.rexx >T:umsusers \"" sysopaccount
                           "\" \"" sysoppassword "\"")))
        (set n 1)
        (set username "start me up")
        (until (= username "")
          (
            (set res (run (cat "rx //rexx/LineAfterLine.rexx "
                               ">ENV:umsuser T:umsusers " n)))
            (set username (getenv "umsuser"))
            (if (= (substr username (- (strlen username) 1) 1) "\n")
              (
                (set username (substr username 0 (- (strlen username) 1)))
              )
            )

            (debug (cat "found user #" n ": \"" username "\""))
            (if (<> username "")
              (
                (set options (cat "USER=\"" username "\""))
                (set variable "rfc.username") (DELCONFIG)
              )
            )
            (set n (+ n 1))
          )
        )

        (message #removemanually)
      )
    )
    (exit)
  )
)

(complete 10)

(set @default-dest
  (expandpath
    (askdir
      (prompt #dest)
      (help @askdir-help)
      (default (expandpath "//"))
    )
  )
)

(set inplace (= (expandpath "//") @default-dest))

(if (<> (substr @default-dest (- (strlen @default-dest) 1) 1) ":")
  (
    (set @default-dest (cat @default-dest "/"))
  )
)

(if (= inplace 0)
  (
    (set n 0)
    (while
      (set filename
        (select n "//Bin/compress"
                  "//Bin/freeze"
                  "//Bin/gzip"
                  "//Bin/setnntpdate"
                  "//Bin/telser"
                  "//Bin/ums2uucp"
                  "//Bin/umsnntp"
                  "//Bin/umsnntpd"
                  "//Bin/umsnntpget"
                  "//Bin/umspop3"
                  "//Bin/umspop3d"
                  "//Bin/umsrfcprint"
                  "//Bin/umsrfcwrite"
                  "//Bin/umssmtp"
                  "//Bin/umssmtpd"
                  "//Bin/uucico"
                  "//Bin/uuxqt"
                  "//rexx/SetUMSConfig.rexx"
                  "//rexx/GetUMSConfig.rexx"
                  "//rexx/MakeUser.rexx"
                  "//rexx/AddAlias.rexx"
                  "//rexx/AddAKAs.rexx"
                  "//rexx/DelUser.rexx"
                  "//rexx/UserList.rexx"
                  ""
        )
      )
      (
        (copylib
          (help @copylib-help)
          (prompt #copying)
          (source filename)
          (dest (tackon @default-dest (pathonly (substr filename 2))))
          (optional "force" "askuser")
          (infos)
        )
        (set n (+ n 1))
      )
    )

    (complete 20)

    (set n 0)
    (makedir (tackon @default-dest "doc"))
    (makedir (tackon @default-dest "networks"))
    (makedir (tackon @default-dest "networks/RFC"))
    (while
      (set filename
        (select n "//Doc/english/umsrfc.guide"
                  "//Doc/english/umsrfc.txt"
                  "//Doc/english/uucico.txt"
                  "//Networks/RFC/convert/ConvAUUCPMail.rexx"
                  "//Networks/RFC/convert/ConvAUUCPNews"
                  "//Networks/RFC/convert/Readme"
                  "//Networks/RFC/db/telser.conf"
                  "//Networks/RFC/db/telser.hosts"
                  "//Networks/RFC/lib/config"
                  "//Networks/RFC/lib/L.Ports"
                  "//Networks/RFC/lib/L.Sys"
                  "//Networks/RFC/lib/security"
                  "//Networks/RFC/lib/seq"
                  "//Networks/RFC/src/History"
                  ""
        )
      )
      (
        (copyfiles
          (help @copyfiles-help)
          (prompt #copying)
          (source filename)
          (dest (tackon @default-dest (pathonly (substr filename 2))))
          (optional "force" "askuser")
          (infos)
        )
        (set n (+ n 1))
      )
    )
  )
)

(complete 30)

(set n 0)
(while
  (set filename
    (select n "//Libs/OwnDevUnit.library"
              "//Libs/rawin.library"
              "//Libs/umsrfc.library"
              ""
    )
  )
  (
    (copylib
      (help @copylib-help)
      (prompt #instlibs)
      (source filename)
      (dest "LIBS:")
      (optional "force" "askuser")
      (infos)
    )
    (set n (+ n 1))
  )
)

(complete 35)

; -------------------------------------------------------------------------

(set amitcp (exists "AmiTCP:AmiTCP"))

(if amitcp
  (
    (copylib
      (help @copylib-help)
      (prompt #copying)
      (source "//devs/telser.device")
      (dest "DEVS:")
      (optional "force" "askuser")
      (infos)
    )
    (set protocoldefaults 15)
  )
  (
    (set protocoldefaults 1)
  )
)

(set protocols
  (askoptions
    (prompt #protocols)
    (help @askoptions-help)
    (choices "UUCP" "SMTP" "NNTP" "POP3")
    (default protocoldefaults)
  )
)

(if (= protocols 0)
  (
    (abort #noprotocols)
  )
)

(if (in protocols 0)
  (
    (set temporarydomainname 0)
  )
  (
    (set choice
      (askchoice
        (prompt #hostnamestatus)
        (choices #hntemp #hnperm)
        (help @askchoice-help)
      )
    )
    (set temporarydomainname (= choice 0))
  )
)

(if temporarydomainname
  (
    (if (not (in protocols 3)) ; POP3
      (
        (abort #needpop)
      )
    )
  )
  (
    (set nodename
      (askstring
        (prompt #nodename)
        (help @askstring-help)
        (default "mysystem")
      )
    )

    (set options "GLOBAL QUOTED")

    (set domainname
      (askstring
        (prompt #domname)
        (help @askstring-help)
        (default (cat nodename ".do.main"))
      )
    )
    (set variable "rfc.domainname") (set data domainname) (SETCONFIG)

    (if (= 1 (askchoice
               (prompt #maps)
               (choices "no" "yes")
               (help @askchoice-help)
             )
        )
      (
        (set variable "rfc.pathname") (set data nodename) (SETCONFIG)
      )
    )

    (set options "GLOBALONLY")
    (set variable "SYSNAME") (GETCONFIG)
    (if (= data "_not_configured_")
      (
        (set options "GLOBAL")
        (set data domainname)
        (SETCONFIG)
      )
    )
  )
)

(complete 40)

(run (cat "rx //rexx/AddAlias.rexx \"" sysopaccount "\" \""
          sysoppassword "\" \"" sysopaccount "\" \"postmaster\""))

(set res (run (cat "rx //rexx/UserList.rexx >T:umsusers \"" sysopaccount
                   "\" \"" sysoppassword "\"")))
(set n 1)
(set username "start me up")
(until (= username "")
  (
    (set res (run (cat "rx //rexx/LineAfterLine.rexx >ENV:umsuser T:umsusers " n)))
    (set username (getenv "umsuser"))
    (if (= (substr username (- (strlen username) 1) 1) "\n")
      (
        (set username (substr username 0 (- (strlen username) 1)))
      )
    )

    (debug (cat "found user #" n ": \"" username "\""))
    (if (<> username "")
      (
        (set options (cat "USER=\"" username "\""))
        (set variable "EXPORT") (GETCONFIG)
        (if (<> res 0)
          (
            (set variable "rfc.username") (GETCONFIG)
            (if (<> res 0)
              (
                (set data
                  (askstring
                    (prompt (cat #shortusername username))
                    (help @askstring-help)
                  )
                )
                (SETCONFIG)
                (run (cat "rx //rexx/AddAlias.rexx \"" sysopaccount "\" \""
                          sysoppassword "\" \"" username "\" \"" data "\""))
              )
            )
          )
        )
      )
    )
    (set n (+ n 1))
  )
)

(complete 50)

(set opts
  (askoptions
    (prompt #encoding)
    (choices #mailquotedprintable #newsquotedprintable)
    (default 0)
    (help @askoptions-help)
  )
)

(complete 60)

(set options "GLOBAL")
(set variable "rfc.mailencoding") (set data (if (in opts 0) 1 0)) (SETCONFIG)
(set variable "rfc.newsencoding") (set data (if (in opts 1) 1 0)) (SETCONFIG)

(if (in protocols 0) ; UUCP
  (
    (message #confuucp)
    (set uucpfeed
      (askstring
        (prompt #feedname)
        (help @askstring-help)
        (default "feed")
      )
    )
    (set dumbhost
      (askbool
        (prompt #dumb)
        (default 1)
        (help @askbool-help)
      )
    )
    (set deletedupes
      (askbool
        (prompt #deldupes)
        (default 0)
        (help @askbool-help)
      )
    )
    (set dillon (exists "UULIB:config" (noreq)))

    (if dillon
      (
        (message #olddillon)
      )
      (
        (set feedphone
          (askstring
            (prompt #telephone)
            (help @askstring-help)
            (default "08154711")
          )
        )
        (set uucppassword
          (askstring
            (prompt #feedpw)
            (help @askstring-help)
            (default "secret")
          )
        )
      )
    )
    (complete 65)
    (set batch
      (askoptions
        (prompt #expmeth)
        (default 2)
        (help @askoptions-help)
        (choices #mailbatch #newsbatch)
      )
    )
    (if (in batch 0)
      (
        (set mailcompress
          (askchoice
            (prompt #mailcompress)
            (default 0)
            (help @askchoices-help)
            (choices "none (\"rbsmtp\" command)"
                     "compress (\"rcbsmtp\" command)"
                     "compress (\"rbcsmtp\" command)"
                     "freeze (\"rfbsmtp\" command)"
                     "freeze (\"rbfsmtp\" command)"
                     "gzip (\"rgbsmtp\" command)"
                     "gzip (\"rbgsmtp\" command)" )
          )
        )
        (
          (set maxmailcompress
            (asknumber
              (prompt #maxbytes)
              (default 65536)
              (help @asknumber-help)
            )
          )
        )
      )
    )

    (complete 70)

    (if (in batch 1)
      (
        (set newscompress
          (askchoice
            (prompt #newscompress)
            (default 1)
            (help @askchoices-help)
            (choices "none"
                     "compress"
                     "freeze"
                     "gzip (\"#! gunbatch\" header)"
                     "gzip (\"#! zunbatch\" header)")
          )
        )
        (
          (set maxnewscompress
            (asknumber
              (prompt #maxbytes)
              (default 65536)
              (help @asknumber-help)
            )
          )
        )
      )
    )
    (if (= dillon 0)
      (
        (makedir (tackon @default-dest "networks/RFC/spool"))
        (makedir (tackon @default-dest "networks/RFC/pub"))
        (startup "UUCP"
          (prompt #instassign)
          (help @startup-help)
          (command (cat "Assign UULIB: \"" (tackon @default-dest "networks/RFC/lib\"\n")))
          (command (cat "Assign UUSPOOL: \"" (tackon @default-dest "networks/RFC/spool\"\n")))
          (command (cat "Assign UUPUB: \"" (tackon @default-dest "networks/RFC/pub\"\n")))
        )
      )
    )
    (if (= dillon 0)
      (
        (textfile
          (dest (tackon @default-dest "networks/RFC/lib/seq"))
          (append "0000")
        )
        (textfile
          (dest (tackon @default-dest "networks/RFC/lib/config"))

          (append "#\n")
          (append "#  UULIB:config & S:UUConfig -- UUCP Configuration\n\n")
          (append "#  Notes: - Copy this file to S:UUConfig\n")
          (append "#         - UULIB:config and S:UUConfig must be the same file!\n")
          (append "#\n\n")
          (append "#  Node name\n")
          (append "#  WARNING: some UUCP systems can't handle names longer than 7 characters!\n")
          (append (cat "NodeName      " nodename "\n\n"))
          (append "#  User name\n")
          (append (cat "UserName      " shortusername "\n\n"))
          (append "#  Log level\n")
          (append "Debug         0\n\n")
          (append "#  Processor for incoming packets (Default: Uuxqt)\n")
          (append "#Uuxqt        uuxqt\n\n")
          (append "#  Spool directory (Default: UUSPOOL:)\n")
          (append "#UUSpool      UUSPOOL:\n\n")
          (append "#  Config directory (Default: UULIB:)\n")
          (append "#UULib        UULIB:\n\n")
          (append "#  Public directory (Default: UUPUB:)\n")
          (append "#UUPub        UUPUB:\n\n")
          (append "#  Modem init string\n")
          (append "#ModemInit    ATZ\n\n")
          (append "#  Modem answer read timeouts in seconds (default 60)\n")
          (append "#TimeOut      60\n\n")
        )

        (textfile
          (dest (tackon @default-dest "networks/RFC/lib/L.sys"))

          (append "#\n")
          (append "#  UULIB:L.sys -- UUCP sites we connect to\n")
          (append "#\n")
          (append "# Node   Time Device Baudrate Number/     Expect-Send strings\n")
          (append "#                             Dialstring\n")
          (append "#\n")
          (append (cat uucpfeed " Any  SER:   38400    ATD" feedphone))
          (append (cat " ogin:-\\r-ogin: " nodename " ssword: " uucppassword "\\r\n"))
        )
      )
    )

    (complete 75)

    (set cmd (cat "rx //rexx/MakeUser.rexx \"" sysopaccount "\" \""
                  sysoppassword "\" \"uucp." uucpfeed "\" \"uucp.default\" "
                  "EXPORTER "
                  "READACCESS=\"~(fidonet.#?|maus.#?|zer.#?|qwk.#?)\" "
                  "WRITEACCESS=\"#?\" IMPORT=\"#?\" "
                  "EXPORT=\"~(#?@fidonet|#?.(un|%)maus|#?.zer|#?@qwk)\""))
    (debug cmd)
    (if (<> (run cmd) 0)  (abort #cannotcreate))

    (set options (cat "USER=\"uucp." uucpfeed "\""))

    (set variable "uucp.nodename") (set data nodename) (SETCONFIG)

    (if dumbhost
      (
        (set variable "uucp.envelope") (set data "1") (SETCONFIG)
      )
      (
        (set variable "uucp.envelope") (set data "2") (SETCONFIG)
      )
    )
    (if deletedupes
      (
        (set variable "uucp.keepdupes") (set data "N") (SETCONFIG)
      )
      (
        (set variable "uucp.keepdupes") (set data "Y") (SETCONFIG)
      )
    )

    (set variable "uucp.mailexport")
    (if (in batch 0)
      (
        (set data (cat
          (select mailcompress "rbsmtp,,Y,,,"
                               "rcbsmtp,,Y,compress,,"
                               "rbcsmtp,,Y,compress,,"
                               "rfbsmtp,,Y,freeze,,"
                               "rbfsmtp,,Y,freeze,,"
                               "rgbsmtp,,Y,gzip,,"
                               "rbgsmtp,,Y,gzip,,"
          )
          maxmailcompress)
        )
      )
      (
        (set data "rmail")
      )
    )
    (SETCONFIG)

    (set variable "uucp.newsexport")
    (if (in batch 1)
      (
        (set data (cat
          (select newscompress "rnews,,Y,,,"
                               "rnews,,Y,compress,cunbatch,"
                               "rnews,,Y,freeze,funbatch,"
                               "rnews,,Y,gzip,gunbatch,"
                               "rnews,,Y,gzip,zunbatch,"
          )
          maxnewscompress)
        )
      )
      (
        (set data "rnews,,N")
      )
    )
    (SETCONFIG)

    (textfile
      (dest (tackon @default-dest "bin/UUCPCall"))

      (append (cat "ums2uucp -h" uucpfeed "\n"))
      (append (cat "uucico -s" uucpfeed "\n"))
    )
    (protect (tackon @default-dest "bin/UUCPCall") 66)
    (message #useuucpcall "\"" (tackon @default-dest "bin/UUCPCall") "\"")
  )
)

(complete 80)

(if (in protocols 1 2 3) ; SMTP/NNTP/POP3
  (
    (set cmd (cat "rx //rexx/MakeUser.rexx \"" sysopaccount "\" \""
                  sysoppassword "\" \"rfc.default\" SMTP POP3 NNTP "))

    (if (not temporarydomainname)
      (
        (set cmd (cat cmd "SMTPD POP3D NNTPD "))
      )
    )

    (set cmd (cat cmd "EXPORT=\"~(#?@fidonet|#?.(un|%)maus|#?.zer|#?@qwk)\""
                  "READACCESS=\"~(fidonet.#?|maus.#?|zer.#?|qwk.#?)\" "
                  "WRITEACCESS=\"#?\" IMPORT=\"#?\" EXPORTER"))
    (debug cmd)
    (if (<> (run cmd) 0)  (abort #cannotcreate))

    (set options "USER=\"rfc.default\"")

    (if (in protocols 1) ; SMTP
      (
        (set smtpserver
          (askstring
            (prompt #smtpserver)
            (help @askstring-help)
            (default "postoffice.do.main")
          )
        )
      )
    )

    (complete 85)

    (if (in protocols 2) ; NNTP
      (
        (set nntpserver
          (askstring
            (prompt #nntpserver)
            (help @askstring-help)
            (default "news.do.main")
          )
        )

        (set options "USER=\"rfc.default\" QUOTED")

        (set data "*\"de.comm.software.ums\\nde.comp.sys.amiga.#?\\nde.newusers.#?*\"")
        (set variable "nntpget.groups" ) (SETCONFIG)
      )
    )

    (complete 90)

    (set poppoll "")

    (if (in protocols 3) ; POP3
      (
        (set popserver
          (askstring
            (prompt #popserver)
            (help @askstring-help)
            (default "postoffice.do.main")
          )
        )

        (set popaka "")

        (set res (run (cat "rx //rexx/UserList.rexx >T:umsusers \""
                           sysopaccount "\" \"" sysoppassword "\"")))
        (set n 1)
        (set username "start me up")
        (until (= username "")
          (
            (set res (run (cat "rx //rexx/LineAfterLine.rexx >ENV:umsuser "
                               "T:umsusers " n)))
            (set username (getenv "umsuser"))
            (if (= (substr username (- (strlen username) 1) 1) "\n")
              (
                (set username (substr username 0 (- (strlen username) 1)))
              )
            )

            (debug (cat "found user #" n ": \"" username "\""))
            (if (<> username "")
              (
                (set options (cat "USER=\"" username "\""))
                (set variable "rfc.username") (GETCONFIG)
                (if (= res 0)
                  (
                    (set popuser data)
                    (set popaka (cat popaka data "|" ))

                    (set poppasswd
                      (askstring
                        (prompt (cat #poppasswd data))
                        (help @askstring-help)
                        (default "secret")
                      )
                    )
                    (set poppoll (cat poppoll "umspop3 -h" popserver " -n"
                                      data " -i" poppasswd "\n"))
                  )
                )
              )
            )
            (set n (+ n 1))
          )
        )
        (if temporarydomainname
          (
            (set options "USER=\"rfc.default\" QUOTED")
            (set variable "rfc.domainname") (set data popserver) (SETCONFIG)
            (set variable "rfc.noownfqdn") (set data "y") (SETCONFIG)
            (set variable "MSGID")
            (set data (cat "$serial *\"%" popuser "@" popserver "*\""))
            (SETCONFIG)
          )
        )
        (if (not (= popaka ""))
          (
            (run (cat "rx //rexx/AddAKAs.rexx \"" sysopaccount "\" \""
                      sysoppassword "\" \"(%|#?'%)("
                      (substr popaka 0 (- (strlen popaka) 1))
                      ")@" popserver "\""))
          )
        )
      )
    )

    (complete 95)

    (textfile
      (dest (tackon @default-dest "bin/IPCall"))

      (if temporarydomainname
        (
          (append (cat "SetUMSConfig.rexx \"rfc.default\" \"\" "
                       "\"rfc.privatehost\" `hostname`\n"))
        )
      )
      (if (in protocols 1)
        (
          (append (cat "umssmtp -h" smtpserver "\n"))
        )
      )
      (if (in protocols 2)
        (
          (append (cat "umsnntp -h" nntpserver "\n"))
          (append (cat "umsnntpget -h" nntpserver "\n"))
        )
      )
      (append poppoll)
    )
    (protect (tackon @default-dest "bin/IPCall") 66)
    (message #useipcall "\"" (tackon @default-dest "bin/IPCall") "\"")
  )
)

(if (not temporarydomainname)
  (
    (run (cat "rx //rexx/AddAKAs.rexx \"" sysopaccount "\" \""
              sysoppassword "\" \"#?@" domainname "\""))
  )
)

(exit)
