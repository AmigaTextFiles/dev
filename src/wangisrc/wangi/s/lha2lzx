.key LHAARC/A,LZXARC/A,OPT1,OPT2,OPT3
.bra {
.ket }

set tempname lha2lzx-$process
set tempdir RAM:

failat 30
makedir $tempdir$tempname
if warn
	echo "Can't create directory $tempdir"
	quit 5
endif
lha -I -F -M -a -P-1 -q x {LHAARC} $tempdir$tempname/
if warn
	echo "Can't unpack {LHAARC}"
	delete $tempdir$tempname all force quiet
	quit 5
endif
failat 20

assign $tempname: ""
cd $tempdir$tempname
; This is a multitasking OS, so I run lzx with priority -1
lzx -r -a -e -F -Y -P-1 -q -X0 {OPT1} {OPT2} {OPT3} a $tempname:{LZXARC} #?
cd $tempname:
assign $tempname: REMOVE
delete $tempdir$tempname all force quiet

unset tempdir
unset tempname

; EOF
