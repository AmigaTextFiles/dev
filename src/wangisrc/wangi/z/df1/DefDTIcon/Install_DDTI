; DefDTIcon install script for CBM Installer
; My first using CBM installer :-!
;$VER: DDTI_install 1.2
;$AUTHOR: Lee Kindness


; make sure OS 2+
(set vernum (getversion))
(set ver (/ vernum 65536))

(if (< vernum 37)
	(abort 'Workbench 2 or higher required')
)

; copy datatype descriptors
(if (>= vernum 39)
	(copyfiles
		(prompt 'Copying DataType descriptors to DEVS:DataTypes')
		(help 'These files are required to determine datatypes.\n\n'@copyfiles-help)
		(source 'Datatypes')
		(dest 'DEVS:Datatypes')
		(pattern '#?')
		(infos)
		(confirm)
	)
)


; copy icons
(set iname
	(askdir
		(prompt 'Where will the default icons be copied to?')
		(help 'This is where you want the default icons to be located.\n\n'@askdir-help)
		(default 'DEVS:Icons')
		(newpath)
	)
)


(copyfiles
	(prompt 'Copying default icons')
	(help 'These are the default icons.\n\n'@copyfiles-help)
	(source 'Icons')
	(dest iname)
	(pattern '#?.info')
	(confirm)
)

;copy system icons
(copyfiles
	(prompt 'Copying system icons')
	(help 'These are new default system icons. They are not required by DDTI.\n\n'@copyfiles-help)
	(source 'SystemIcons')
	(dest 'ENVARC:Sys/')
	(pattern '#?.info')
	(confirm)
)


(set name
	(askdir
		(prompt 'Where will DefDTIcon be copied to?')
		(help 'This is where you want the main program to be located.\n\n'@askdir-help)
		(default 'SYS:Utilities')
	)
)

(set @default-dest name)

(copyfiles
	(help @copyfiles-help)
	(source 'DefDTIcon')
	(dest name)
	(infos)
)

(Tooltype
	(dest (tackon name "DefDTIcon"))
	(settooltype "ICONDIR" iname)
	(noposition)
)

(transcript '\nDefDTIcon installed to '@default-dest'\n')

(exit '** A reboot is neccesary before using DefDTIcon **\n I hope you find DefDTIcon useful.\n    LSK _\\\\//')

	




