/*******************************************************
  $VER:  GuideList-Wizard 1.0 (28.9.98) Mikael Lund
  USAGE: Guidelist Guidefile/K Title/K (both optional)
  TIP:   Reduce this script with CompressRexx !
 *******************************************************/

/****************************
  Constants & Default values
 ****************************/
MAXFILE=20
MAXSIZE=5
MAXDATE=11
NL='0A'X

/*****************
  Main structure
 *****************/
Parse arg args

Call OPENLIBS
Call RDARGS
Call FILEINFO
Call FORMATLIST
Call ADD2GUIDE
EXIT

/****************
  Open libraries
 ****************/
OPENLIBS:
  if ~show('L','rexxsupport.library')	then
    if ~addlib('rexxsupport.library',0,-30,0) then exit(20)
  if ~show('L','rexxdossupport.library')
    then if ~addlib('rexxdossupport.library',0,-30,0) then exit(20)
RETURN

/****************
  Read arguments
 ****************/
RDARGS:
  tmplt="FILE/A,DESC/A,GUIDEFILE/K/A,TITLE/K/A"
  if args="?" then args=""
  if ~readArgs(args,tmplt,"args.") then do
    say "Usage: "tmplt
    exit
  end
RETURN

/***********************
  Read desc., size etc.
 ***********************/
FILEINFO:
  fullfile=Absolutepath(args.file)
  name=Filepart(args.file)

  info=statef(args.file)
  parse var info n size n n days n n n
  if size<1000 then size=1000;size=Trunc((size/1024)+0.5)"k"
  date='('date("E",days,"i")')'

  if ~open(n,args.desc) then exit(20)
  do i=0 by 1 until eof(n)
    l.i = readln(n)
    if l.i=NL then i=i-1
  end
  close(n) 
RETURN

/********************
  Format description
 ********************/
FORMATLIST:
  name=substr(name,1,MAXFILE)
  size=substr(size,1,MAXSIZE)
  date=substr(date,1,MAXDATE)

  if index(fullfile,' ')=0 then
   guidename='@{"'name'" system "viewdiz 'fullfile' execute quiet"}'
  else
   guidename=name

  info=guidename' 'size' 'date' '
  space=copies(" ",length(info)-length(guidename)+length(name))

  l.0 = info || l.0 || nl
  do i=1 by 1
    if l.i="L."i then break
    l.i= space || l.i || nl
  end
  l.i="@endnode" || nl
  MAXLINE = i
RETURN

/*******************
  Add desc to guide
 *******************/
ADD2GUIDE:
  if ~exists( args.guidefile ) then call CREATEGUIDE
  if ~open(guide,args.guidefile) then exit(20)
  seek(guide,-9,"E")
  do i=0 to MAXLINE
    writech(guide,l.i)
  end
  close(guide)
RETURN

/***********************
  Create new guidefile
 ***********************/
CREATEGUIDE:
  if ~open(guide,args.guidefile,"W") then exit(20)
  writeln(guide,"@database amigaguide.guide")
  writeln(guide,'@author "GuideList-Wizard for ViewDiz by Mikael Lund"')
  writeln(guide,"@node MAIN")
  writeln(guide,'@title "'args.title'"'NL)
  writeln(guide,"@endnode")
  close(guide)
RETURN
