;=============================================================================
;
; ProAsm small installation script for AmigaDOS 2.0 or higher.
;
; ProAsm 1.74
;
;=============================================================================



;=============================================================================
;
; german strings
;
(if (= @language "deutsch")
(

(set #intro
(cat "\n\nDieses Programm installiert die ProAsm Software auf der Harddisk")
)

(set #welcometext
(cat "\nWillkommen zur ProAsm Software Installation.\n"
     "Copyright © 1995 by Daniel Weber\n"
     "All Rights Reserved\n\n\n")
)


(set #askdir_prompt
(cat "Wo soll die ProAsm Software installiert werden?")
)

(set #askdir_help
(cat "Waehle ein Zielverzeichnis (oder -Partition) in welches die "
     "ProAsm Software installiert werden soll. "
     "Der Installer generiert dort ein Verzeichnis ProAsm/ "
     "in das dann die benoetigten Dateien kopiert werden.")
)


(set #askopt_prompt
(cat "Welche Teile der ProAsm Software sollen auf der "
     "Harddisk installieren?\n")
)

(set #askopt_help
(cat "Die komplette Software benoetigt etwa 1.8 Megabytes auf "
     "der Harddisk. Nun koennen die Teile der Software ausgewaehlt "
     "werden, die installiert werden sollen.\n"
     "Die vorgeschlagenen Teile sind entsprechend markiert.\n\n"
     "Ein Ueberblick ueber die verschiedenen Teile und deren "
     "Groesse (ca.):\n"
     "ProAsm Assembler:  154 kbytes\n"
     "ASX             :   78 kbytes\n"
     "ProOpts         :   26 kbytes\n"
     "Utilities       :   60 kbytes\n"
     "Documentation   :  640 kbytes\n"
     "BLink Archive   :   24 kbytes\n"
     "Examples        :  170 kbytes\n"
     "Routine Files   :  460 kbytes\n"
     "Include Files   :   45 kbytes\n"
     "\n")
)


(set #askbool_prompt
(cat "Sollen die env-Variablen nach envarc: und env: kopiert werden?")
)

(set #askbool_help
(cat "Es wird nur der inhalt des ProAsm/env/ Verzeichnisses nach "
     "envarc: und env: kopiert.  Das ProAsm/env/ Verzeichnis "
     "enthaelt einige vordefinierte Konfigurationsdateien.\n\n\n")
)



(set #message1
(cat "Pfade und Assigns werden der User-Startup Datei hinzugefuegt")
)

(set #startup_prompt_a
(cat "Die folgenden Pfade und Assigns werden benoetigt. "
     "Sollen sie der User-Startup Datei hinzugefuegt werden?")
)

(set #startup_prompt_b
(cat "Die Aenderungen werden erst beim naechsten Neustart aktiv.")
)


(set #startup_help
(cat "Die Pfade werden benoetigt, damit die ProAsm Software vom "
     "System gefunden wird.\n"
     "Die Assigns sind noetig, damit die benoetigten Bibliotheken "
     "und Dokumentation "
     "von ProAsm und der dazugehoerigen Utilitysoftware gefunden "
     "werden.")
)

))



;=============================================================================
;
; english strings
;
(if (= @language "english")
(

(set #intro
(cat "\n\nThis program lets you install ProAsm on a hard drive, "
     "by copying all needed files.")
)

(set #welcometext
(cat "\nWelcome to the Installation of the ProAsm Package.\n"
        "Copyright © 1995 by Daniel Weber\n"
        "All Rights Reserved\n\n\n")
)


(set #askdir_prompt
(cat "Select location to install the ProAsm directory")
)

(set #askdir_help
(cat "Choose a destination directory where you want "
     "the ProAsm package have to be copied to. "
     "The Installer will create a directory called ProAsm "
     "at the choosen location. All needed files will then "
     "be copied to that new created directory.")
)


(set #askopt_prompt
(cat "Which part(s) of the ProAsm package would "
     "you like to install onto your hard drive?\n")
)

(set #askopt_help
(cat "The complete software package requires about "
     "1.8 megabyte of disk space for the full "
     "installation.  You have the choice now which "
     "part(s) of the distribution should be installed "
     "and which not.  The suggested parts are especially "
     "marked.\n\n"
     "An overview of the approx. sizes of the different parts:\n"
     "ProAsm Assembler:  154 kbytes\n"
     "ASX             :   78 kbytes\n"
     "ProOpts         :   26 kbytes\n"
     "Utilities       :   60 kbytes\n"
     "Documentation   :  640 kbytes\n"
     "BLink Archive   :   24 kbytes\n"
     "Examples        :  170 kbytes\n"
     "Routine Files   :  460 kbytes\n"
     "Include Files   :   45 kbytes\n"
     "\n")
)


(set #askbool_prompt
(cat "Copying the env-Variables to envarc: and env:?")
)

(set #askbool_help
(cat "This will just copy the contents of the ProAsm/env/ "
     "directory to envarc: and env:. The ProAsm/env/ directory "
     "contains some predefined configuration files.\n\n\n")
)



(set #message1
(cat "Pathes and Assigns will be added to the user-startup")
)

(set #startup_prompt_a
(cat "The following pathes and assigns are needed."
     "Do you want them added to your user-startup file?")
)

(set #startup_prompt_b
(cat "The changes won't be effective till you reboot.")
)


(set #startup_help
(cat "The pathes are needed by the ProAsm package "
     "to be found by the system. "
     "The assigns are needed to assure that ProAsm and "
     "its associated utilities find the needed libraries "
     "and documentations.")
)

))



;=============================================================================
;
; some set up stuff...
;
(set sourcepath (expandpath (pathonly @icon)))



(set @defdest "work:")
(set @destx   "work:")

;=============================================================================
;
; Introduction...
;
(user 1)
(message #intro)


;
; Welcome...
;
(welcome #welcometext)


;
; Install Package
;
(complete 0)

(set destx (askdir (prompt #askdir_prompt)
                   (default @defdest)
                   (help   #askdir_help)
           )
)



(complete 10)
(set destdir (tackon destx "ProAsm"))
(set @default-dest destdir)


(set InstallOpts
	(askoptions (prompt #askopt_prompt)
	            (help   #askopt_help)
		    (choices "ProAsm Assembler (suggested)"
		             "ASX (suggested)"
		             "ProOpts (suggested)"
		             "Utilities (suggested)"
		             "Documentation (suggested)"
		             "BLink Archive"
		             "Examples"
		             "Routine Files"
		             "Include Files")
	)
)

(makedir (destx "ProAsm"))
(copyfiles (source "/ProAsm.info") (dest destx))
(copyfiles (source "readme.txt") (dest destdir))
(copyfiles (source "readme.txt.info") (dest destdir))

(copyfiles (source "registration.doc") (dest destdir))
(copyfiles (source "registration.doc.info") (dest destdir))


;
; ProAsm
;
(complete 20)
(if (BITAND InstallOpts 1) (
    (copyfiles (source "proasm") (dest destdir))
    (makedir (destdir "libs"))
    (set dest2 (tackon destdir "libs"))
    (copyfiles (source "libs") (dest dest2) (all))
    (makedir (destdir "Catalogs"))
    (set dest2 (tackon destdir "Catalogs"))
    (copyfiles (source "Catalogs") (dest dest2) (all))
))

;
; ASX
;
(complete 30)
(if (BITAND InstallOpts 2) (
    (copyfiles (source "ASX") (dest destdir))
    (copyfiles (source "ASX.info") (dest destdir))
    (makedir (destdir "rexx"))
    (set dest2 (tackon destdir "rexx"))
    (copyfiles (source "rexx") (dest dest2) (all))
    (makedir (destdir "env"))
    (set dest2 (tackon destdir "env"))
    (copyfiles (source "env") (dest dest2) (all))
))

;
; ProOpts
;
(complete 40)
(if (BITAND InstallOpts 4) (
    (copyfiles (source "ProOpts") (dest destdir))
    (copyfiles (source "ProOpts.info") (dest destdir))
))

;
; Utilities
;
(complete 45)
(if (BITAND InstallOpts 8) (
    (makedir (destdir "c"))
    (set dest2 (tackon destdir "c"))
    (copyfiles (source "c") (dest dest2) (all))
))


;
; Documentation
;
(complete 55)
(if (BITAND InstallOpts 16) (
    (makedir (destdir "Help"))
    (set dest2 (tackon destdir "Help"))
    (copyfiles (source "Help") (dest dest2) (all))
    (copyfiles (source "Help.info") (dest destdir))
))


;
; BLink
;
(complete 65)
(if (BITAND InstallOpts 32) (
    (makedir (destdir "BLink"))
    (set dest2 (tackon destdir "BLink"))
    (copyfiles (source "BLink") (dest dest2) (all))
    (copyfiles (source "BLink.info") (dest destdir))

))


;
; Examples
;
(complete 70)
(if (BITAND InstallOpts 64) (
    (makedir (destdir "Examples"))
    (set dest2 (tackon destdir "Examples"))
    (copyfiles (source "Examples") (dest dest2) (all))
    (copyfiles (source "Examples.info") (dest destdir))
))


;
; Routines
;
(complete 80)
(if (BITAND InstallOpts 128) (
    (makedir (destdir "Routines"))
    (set dest2 (tackon destdir "Routines"))
    (copyfiles (source "routines") (dest dest2) (all))
))

;
; Includes
;
(complete 85)
(if (BITAND InstallOpts 256) (
    (makedir (destdir "include"))
    (set dest2 (tackon destdir "include"))
    (copyfiles (source "include") (dest dest2) (all))
))




;
; copy the env variables
;
(complete 90)
(set ask_env
	(askbool
		(prompt #askbool_prompt)
		(help #askbool_help)
		(default 0)
	)
)

(if ask_env
(
(copyfiles (source "env")
           (dest "envarc:")
           (all)
)


(copyfiles (source "env")
           (dest "env:")
           (all)
)
))




;
; change user-startup
;
(complete 95)
(user 2)
(message #message1)
(set ProAsmCDir        (cat "path " destdir " " destdir "/c add" "\n"))
(set ProAsmLibsAssign  (cat "assign libs: " destdir "/libs add" "\n"))
(set ProAsmHelpAssign  (cat "assign help: " destdir "/Help add" "\n"))

(startup "ProAsm Assembler Package"
        (prompt ("%s\n\n%s\n%s\n%s\n\n%s\n"
                 #startup_prompt_a
                 ProAsmCDir
                 ProAsmLibsAssign
                 ProAsmHelpAssign
                 #startup_prompt_b)
        )
         (command ("") ProAsmCDir
                  ("") ProAsmLibsAssign
                  ("") ProAsmHelpAssign
         )
        (help #startup_help)
)
(user 1)

(complete 100)
(exit)

;=============================================================================

