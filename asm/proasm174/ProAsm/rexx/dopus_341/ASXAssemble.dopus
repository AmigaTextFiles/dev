/* Assemble selected files       */
/* for Directory Opus, using ASX */
/* 16.06.93 by Daniel Weber      */


ADDRESS 'dopus_rexx'
OPTIONS RESULTS

status 3				/* get #of active window     */
win = result
status 13 win				/* get actual selected drive */
path = result
getselectedfiles '+'
IF RC ~= 0 THEN EXIT(0)
files = result

DO WHILE files ~=''
  PARSE VAR files txtfile '+' files
 
  ADDRESS 'dopus_rexx'			/* show file name  */
  toptext 'Assembling 'path''txtfile

  ADDRESS 'asx_rexx'
  BegOfAssembly				/* allocate a job  */
  port = result
  IF port ~='' THEN DO
    ADDRESS (''||port)			/* ARexx weirdness */
    Assemble path''txtfile		/* assemble selected file */
    PARSE VAR result . errors . warnings . optims optimbytes . codesize . workspace

    ADDRESS 'dopus_rexx'
    toptext errors' Errors 'warnings' Warnings, assembled to 'codesize' bytes.'

    ADDRESS (''||port)			/* ARexx weirdness */
    EndOfAssembly			/* free job        */
  END
END

EXIT(0)

