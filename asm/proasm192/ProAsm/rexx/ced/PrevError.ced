/*****************************************************************************
*                                                                            *
* ARexx script:              PrevError.ced       for the ProAsm Assembler    *
*                                                                            *
* This script uses the ASX interface to determine the previous error and     *
* jump to this line.                                                         *
*                                                                            *
* by Daniel Weber                                                            *
* 26.May.93                                                                  *
*                                                                            *
*****************************************************************************/

OPTIONS RESULTS

ADDRESS 'rexx_ced'
status 19
name = result				/* complete path and filename   */

ADDRESS 'asx_rexx'
FindID 'CygnusEd:'name
port = result

IF port ~='' THEN DO
  ADDRESS (''||port)			/* ARexx wierdness              */
  PrevError
  errortxt = result
  ADDRESS 'rexx_ced'
  IF errortxt ='' THEN DO
    Okay1 'no more errors'
    exit(0)
    END

  PARSE VALUE errortxt WITH 1 LineNumber ' : ' ErrorMsg ' in file ' filename
  IF filename ~='' THEN DO
    status 66
    DO numwins= result-1 TO 1 BY -1 UNTIL UPPER(result) = UPPER(filename)
      next view
      status 21
    END
    IF UPPER(result) ~= UPPER(filename) THEN DO
    split view
    open filename
    END
  END

  Jump To Line LineNumber
  Okay1 ErrorMsg
  END

EXIT(0)
