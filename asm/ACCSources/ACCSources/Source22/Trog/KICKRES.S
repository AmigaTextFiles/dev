*******************************************************************************
***********************RESIDENT RESET ROUTINE VERSION 1.2**********************
*************************CODED BY THE TROG ON 22/12/91*************************
*******************************************************************************
* CODE INFO
* ANOTHER COOL ROUTINE WITH FULL DOCUMENTAION BY THE TROG DECEMBER 91
* COMPLETE KICKSTART V2.0 COMPATIBLE CODE AS IT WAS CODED ON ONE (GREAT MACHINE)
* ALL CODE BY THE TROG IN DECEMBER 91
* A MORE PROFESIONAL WAY TO CODE RESET ROUTINES BETTER THAN COLD OR COOL
* CAN BE CALLED AT ANY TIME DURING RESET FOR ADDED FLEXABILITY
* FOR MORE INFOMATION ON STRUCTURES SEE MAPPING THE AMIGA BY COMPUTE
*******************************************************************************
	OPT C-				* ASSEMBLER OPTIONS AND MAGIC INCLUDE
	INCDIR	source:INCLUDE/		* FILES WITH LOTS OF STRUCTURES AND
	INCLUDE LIBOFFSETS.I		* GOODIES
*******************************************************************************
* THE FOLLOWING ROUTINE RESERVES MEMORY FOR RESET
*******************************************************************************
RESIDENT_INSTALL			
	MOVE.L	$4,A6			* THE USUAL RESERVE MEMORY ROUTINE
	MOVE.L	#REND-RSTART,D0		* NOTHING SPECIAL TO REPORT
	MOVE.L  #MEMF_CHIP,D1
	JSR	ALLOCMEM(A6)
	TST.L	D0
	BEQ	ERROR
	MOVE.L	D0,A5			* MOVE ALLOCATED MEMORY TO A5 AS A BASE 
*******************************************************************************
* THE FOLLOWING WILL COPY MEMORY FROM ONE PLACE TO ANOTHER (O/S STYLE)
*******************************************************************************
	LEA	RSTART(PC),A0		* RESET CODE START ADRESS
	MOVE.L	D0,A1			* START OF RESERVED MEMORY 
	MOVE.L	#REND-RSTART,D0		* LENGHT OF MEMORY TO COPY
	JSR	COPYMEM(A6)		* O/S CALL TO COPY MEMORY
*******************************************************************************
* ROUTINE THAT WILL MAKE CODE RESIDENT BETTER THAN COLD OR WARMCAPTURE
*******************************************************************************
* NOTE
* A5 = BASE ADRESS OF ALLOCATED MEMORY FOR RESIDENT STRUCTURE AND RESET CODE
* MEMLIST = AREA OF MEMORY TO BE PROTECTED FROM RESET AND HOW MANY BYTES ECT 
* ROMTAG = INFO FOR EXEC SIZE OF RESET CODE NAME VERSION ECT
*******************************************************************************
	LEA	ROMTAGPTR-RSTART(A5),A0 *COPY ROMTAGPTR TO A0 
	MOVE.L	A0,KICKTAGPTR(A6)	*COPY CONTENTS TO THE EXECBASE KICKTAG
	LEA	ROMTAG-RSTART(A5),A1	*COPY ROMTAG STRUCTURE TO A1
	MOVE.L	A1,(A0)			*MOVE ROMTAG STRUCTURE INTO KICKTAGPTR
	LEA	MEMLIST-RSTART(A5),A1	*MOVE MEMORY LIST TO A1
	MOVE.L  A5,$10(A1)		*START ADRESS OF MEMORY TO BE PROTECTED
	MOVE.L  KICKMEMPTR(A6),D0	*MOVE ADRESS OF KICKMEMPTR INTO D0
	MOVE.L  D0,LN_SUCC(A1)		*COPY ADRESS TO MEMLIST
	MOVE.L  A1,KICKMEMPTR(A6)	*START OF MEMLIST (MEMORY TO BE PROTECTED)
	LEA	ROMTAG-RSTART(A5),A1	*START ADRESS OF ROMTAG STRUCTURE
	MOVE.L	A1,RT_MATCHTAG(A1)	*POINTER TO START OF ROMTAG STRUCTURE
	LEA	RT_SIZE(A1),A2		*SIZE OF ROMTAG STRUCTURE
	MOVE.L  A2,RT_ENDSKIP(A1)	*COPY END OF ROMTAG STRUC INTO STRUC
	LEA	TAGNAME-RSTART(A5),A2	*MOVE NAME STRUC POINTER TO A2
	MOVE.L	A2,RT_NAME(A1)		*COPY ADRESS OF NAME POINTER  INTO STRUC
	MOVE.L  A5,RT_INIT(A1)		*START OF CODE TO BE EXECUTED (RESET)
	JSR	KICKSUMDATA(A6)		*O/S CALL TO CALCULATE CHECKSUM
	MOVE.L  D0,KICKCHECKSUM(A6)	*COPY RESULT TO CHECKSUM POINTER
	
ERROR:	RTS				* END OF RESIDENT INSALLATION OR ERROR
*************************************************************************************
RSTART:				
	BTST	#7,$BFE001		* CODE TO BE EXECUTED ON RESET TILL
	BNE	RSTART			* COMPUTER IS SWITCHED OFF OR EXECBASE
	RTS				* HAS BEEN DESTROYED
*******************************************************************************
ROMTAGPTR:
	DC.L	0,0			* POINTER TO THE ROMTAG STRUCTURE

TAGNAME DC.B	"TROGS RESET CODE",0	* NAME OF THE RESIDENT STRUCTURE
	EVEN
*******************************************************************************
MEMLIST:
	DS.B	14			* NODE LIST STRUCTURE = 14 BYTES
	DC.W	1			* THIS PROTECTS MEMORY FROM DELATION
	DC.L	0			* FOR MORE INFO SEE MAPPING THE AMIGA
	DC.L	MEMSIZE			* SIZE OF MEMORY TO PROTECT ON RESET
********************************************************************************
ROMTAG:
	DC.W	RT_MATCHWORD		* MAGIC NUMBER $4AFC FOR RECOGNIZATION
	DC.L	0			* POINTER TO START OF STRUCTURE 
	DC.L	0			* POINTER TO END OF STRUCTURE
	DC.B	RTF_COLDSTART		* BIT 7 SET = MAKE LIB FUNCTION
	DC.B	2			* VERSION NUMBER 
	DC.B	0			* TYPE = WHICH COMMAND IS PERFORMED
	DC.B	120			* PRIORITY=120 TO -60(-61 NOT ALLOWED)
 	DC.L	0			* POINTER TO NAME OF STRUCTURE
	DC.L	0			* IDSTRING STRUCTURE EXPLANATION
	DC.L	0			* START OF CODE TO BE EXECUTED ON RESET

REND:
MEMSIZE	EQU	*-RSTART
********************************************************************************
