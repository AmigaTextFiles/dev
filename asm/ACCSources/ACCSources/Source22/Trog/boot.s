**************************************************************************:
DOSHEADER:	
	DC.B  	"DOS",0		*DOS IDENTIFICATION HEADER
	DC.L	0		*LONGWORD FOR CHECKSUM
	DC.L	0		*ROOT BLOCK (NORMAL $370)
***************************************************************************
BOOTPRG:
	MOVEM.L	A0-A6/D0-D7,-(A7)	*SAVE REGISTERS
	BSR	RUTIN			*EXECUTE CUSTOM BOOT
	MOVEM.L (A7)+,A0-A6/D0-D7	*RESTORE REGISTERS
	LEA	DOSNAME(PC),A1		*DOS.LIBRARY
	JSR	-96(A6)			*FIND RESIDENT STRUCTURE
	MOVE.L	D0,A0			*STORE ADRESS IN A0
	MOVE.L  $16(A0),A0		*POINTER TO INIIALIZATION
	MOVEQ	#$00,D0			*CLEAR D0
	RTS				*RETURN TO ROM BOOT PROCEDURE
***************************************************************************
DOSNAME:	DC.B	"dos.library",0		*DOS LIBRARY
		EVEN				*PAD BYTE IF ODD ADRESS
GFXNAME:	DC.B	"graphics.library",0	*GRAPHICS LIBRARY
		EVEN				*PAD BYTE IF ODD ADRESS
****************************************************************************
RUTIN:	
	LEA	GFXNAME(PC),A1		*GRAPHICS.LIBRARY	
	JSR	OPENOLDLIB(A6)		*OPENLIBRARY ANY VERSION WILL DO
	MOVE.L	D0,A6			*GRAPHICS LIBRARY BASE IN A6
	MOVE.L	D0,$68004		*STORE GRAPHICS BASE	

	LEA	COPLIST(PC),A1		*START OF CUSTOM COPPER
	LEA	$52000,A2		*ADRESS TO COPY IT TOO
MYRA:	MOVE.L	(A1),(A2)+		*COPY COPPER
	CMP.L	#$FFFFFFFE,(A1)+	*END OF COPPER MARKER
	BNE	MYRA			*IF NOT EQUAL COUNTINUE COPYING

INIT:	
	LEA	$70000,A1		*CLEAR MEMORY FOR BPLANES	
CLEAR:	CLR.L	(A1)+			*CLEAR NEXT LONGWORD
	CMP.L	#$7E000,A1		*COMPARE ADRESS
	BNE	CLEAR			*IF NOT ALL CLEAR THEN CARRY ON CLEARING

	LEA     $41000,A0		*RASTPORT START ADRESS		
	MOVE.L  #1,D0			*NUMBER OF BITPLANES
	MOVE.L  #768,D1			*PIXEL POSTION ACROSS
	MOVE.L  #192,D2			*PIXEL POSTION DOWN
	JSR     INITBITMAP(A6)		*LIBRARY CALL TO INITALIZE BITMAP
	MOVE.L  #$70000,$41008		*START MEMORY OF BITPLANES	
	LEA     $41100,A1	        *RASTPORT START ADRESS
	JSR     INITRASTPORT(A6)	*INITALIZE RASTPORT
	MOVE.L  #$41000,$41104          *STORE RASTPORT POINTER

	LEA	$41100,A1	        *POINTER TO RASTPORT
	MOVE.W	#1,D0			*PEN COLOR
	JSR	SETAPEN(A6)	        *LIBRARY CALL TO SET PEN COLOR
	LEA	SCREENTEXT(PC),A5	*POINTER TO TEXT TO BE OUTPUT

PRINT:	
	LEA	$41100,A1		*POINTER TO RASTPORT SCREEN ADRESS
	CLR.L	D4			*CLEAR REGISTER			
	MOVE.L	#40,D3		        *START POSTION OF TEXT
	MOVE.B	(A5)+,D4		*FIRST BYTE OF A5 = TEXT POSTION (Y)
	BEQ	PR2			*TEST FOR END OF TEXT MARKER
	
	BSR	TEXTOUT			*COPY TEXT TO RASTPORT (SCREEN)
	ADD.L	#384,D3			
	BSR	TEXTOUT			
	ADD.L	#32,A5			*POINTER TO START OF NEXT LINE TEXT
	BRA	PRINT			*CARRY ON TILL ALL TEXT IS OUTPUT
		
PR2:
	MOVE.W	#$A0,$DFF096		*ENABLE COPPER AND SPRITES
	MOVE.L	$32(A6),$31000		*STORE POINTER TO BOOT SCREEN ADRESS
	MOVE.L	#$52000,$32(A6)         *MOVE MY BOOT SCREEN INTO OLD SCREEN
	MOVE.W	#$8380,$DFF096          *DISABLE ALL DMA
	
GETKEY:	
	CLR.B	$BFEC01			*SERIAL PORT REGISTER (KEYBOARD INPUT)
	CLR.L	D0			*CLEAR REGISTER
READ:	BTST	#$6,$BFE001		*TEST WAS LEFT MOUSE BUTTON PRESSED
	BNE	FKEYS			*IF IT WAS NO MENU SELECTED
	MOVE.L	#$F,D0			*AND LOAD NORMAL STARTUP CODE IN D0
	BRA	NMENU			*BRA TO LOADER
FKEYS:	MOVE.B	$BFEC01,D0		*COPY PORT REGISTER CONTENTS
	NOT.B	D0			*REVERSE THE BYTE (1=0)( 0=1)
	LSR.B	#1,D0
	CMP.B	#79,D0			*COMPARE 79 WITH THE CONTENTS OF D0
	BLS	READ			*IF LESS THAN THEN NOT A F KEY
	CMP.B	#89,D0			*COMPARE 85 WITH THE CONTENTS OF D0
	BHI	READ			*IF HIGHER THAN THEN NAT A F KEY
	SUB.W	#80,D0			*SUB 80 FROM D0 FOR WHICH F KEY IT WAS
NMENU:	MOVE.L	D0,$80			*STORE THE VALUE IN TRAP #0 FOR LOADER

LOADER:	
	MOVE.L	$68004,A6	      	*SAVED ADRESS OF BOOT SCREEN
	MOVE.L	$31000,$32(A6)		*MOVE BOOT SCREEN BACK AGAIN
	RTS				*COUNTINUE ROM BOOT ROUTINE
*******************************************************************************
TEXTOUT
	LEA	$41100,A1		* RASTPORT ADRESS
	MOVE.L	D3,D0			* X CORDINATES
	MOVE.L	D4,D1			* Y CORDINATES
	JSR	MOVE(A6)		* LIBRARY CALL TO POSTION PEN
	LEA	$41100,A1		* RASTPORT ADRESSE
	MOVE.L	A5,A0			* ADRESS OF TEXT
	MOVE.L	#31,D0			* HOW MUCH TEXT TO OUTPUT
	JSR	TEXT(A6)		* LIBRARY CALL TO PRINT TEXT
	RTS
*******************************************************************************
COPLIST:
	DC.W	$0100,$2200,$0102,$0001		*BPLCON0,BPLCON1
	DC.W	$0108,$0000,$010A,$0000		*BPL1MOD,BPL2MOD
	DC.W	$0092,$0020,$0094,$00D8		*DDFSTRT,DDFSTOP
	DC.W	$008E,$3000,$0090,$30FF		*DIWSTRT,DIWSTOP
	DC.W	$00E0,$0007,$00E2,$0870		*BPL1PTH,BPL1PTL
	DC.W	$00E4,$0007,$00E6,$08A0		*BPL2PTH,BPL2PTL
	DC.W	$0180,$0001,$0182,$000E		*COL0 AND COL1
	DC.W	$FFFF,$FFFE			*COPPER LIST END
*******************************************************************************
SCREENTEXT:	
	DC.B	$2A/2+8
LINE1:	DC.B	"        TROG OF PIROSFT        ",0   *SCREEN INTRO TEXT
	DC.B	$3A/2+8
LINE2:	DC.B    "PROUDLY PRESENTS MENU BOOT V1.0",0  
  	DC.B	$3E/2+22
LINE3:	DC.B	"[F1] MUST BE CALLED PRGA IN CLI",0 
	DC.B	$4E/2+22
LINE4:	DC.B	"[F2] MUST BE CALLED PRGB IN CLI",0 
	DC.B	$5E/2+22
LINE5:	DC.B	"  THE SAME GOES FOR F3 TO F10  ",0 
	DC.B	$6E/2+22
LINE6:	DC.B	" YOU CAN HAVE UPTO A MAX OF 10 ",0
        DC.B	$7E/2+22
LINE7:	DC.B	"  THIS IS V1.0 SO IT MAY HAVE  ",0
	DC.B	$8E/2+22
LINE8:	DC.B	"  SOME BUGS BUT THEY WILL BE   ",0
	DC.B	$9E/2+22
LINE9:	DC.B	"ELIMINATED FROM FUTURE VERSIONS",0
	DC.B	$AE/2+22
LINE10:	DC.B	"WATCH OUT FOR MORE STUFF BY US ",0
	DC.B    $BE/2+22
LINE11:	DC.B	"IN THE NEAR FUTURE:    TROG    ",0
	DC.B    $CE/2+22
LINE12:	DC.B	"PIROSOFT ON THE MOVE IN 1991   ",0
	DC.B    $FE/2+22
LINE13:	DC.B	"GREETINGS TO ALL OF OUR FRIENDS",0
	DC.B	0					   
	EVEN						  
******************************************************************************
