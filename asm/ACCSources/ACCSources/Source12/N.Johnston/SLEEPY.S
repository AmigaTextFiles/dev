
*	SLEEPY V1.0	©1991 NEIL JOHNSTON

*	THIS SIMPLE PROGRAM WILL SHRINK A WINDOW TO A SMALL, TITLEBAR
*	SIZED WINDOW, AND WAIT FOR THE USER TO ACTIVATE IT AND PRESS
*	THE RMB BEFORE RESTORING THE ORIGINAL WINDOW.


	SECTION	SLEEPY,CODE
	OPT	C-

**********

	INCDIR	SYS:INCLUDE/
	INCLUDE	EXEC/EXEC_LIB.I
	INCLUDE	INTUITION/INTUITION_LIB.I
	INCLUDE	INTUITION/INTUITION.I

**********

OPENLIBS
	LEA	INTNAME,A1
	MOVEQ.L	#0,D0
	CALLEXEC OPENLIBRARY
	MOVE.L	D0,_INTUITIONBASE
	BEQ	QEXIT

**********

OPENWIN	LEA	MAIN_WIN,A0

	CALLINT	OPENWINDOW

	MOVE.L	D0,WINPTR
	BEQ	CLOSELIBS		NO WINDOW? NO PROGRAM

**********

GETPORTS
	MOVE.L	WINPTR,A0
	MOVE.L	WD_RPORT(A0),WINRASTPORT	GET WINDOW RASTPORT
	MOVE.L	WD_USERPORT(A0),WINPORT		GET WINDOW USERPORT

**********

*	THIS IS THE MAIN GADGET DETECT ROUTINE. IT WAITS FOR THE USER
*	TO DO SOMETHING (PRESS A GADGET) AND THEN JUMPS TO THE APPROPRIATE
*	ROUTINE.	(ONLY THE SLEEP ROUTINE IN THIS CASE)

WAITFORMSG
	MOVE.L	WINPORT,A0

	CALLEXEC WAITPORT		WAIT FOR SOMETHING TO HAPPEN!

	MOVE.L	WINPORT,A0		A0 COULD HAVE BEEN CLEARED!

	CALLEXEC GETMSG			OK, WHAT HAPPENED THEN?

	TST.L	D0			FALSE ALARM?!?!
	BEQ.S	WAITFORMSG

	MOVE.L	D0,A1			MESSAGE PTR --> A1

	MOVE.L	IM_CLASS(A1),D2		SAVE MESSAGE DETAILS...
	MOVE.L	IM_IADDRESS(A1),A2

	CALLEXEC REPLYMSG		SEND A NICE REPLY TO INTUTITION

**********

CHECKMSG
	CMP.L	#CLOSEWINDOW,D2		CLOSE GADGET PRESSED?
	BEQ.S	CLOSEWIN

	CMP.L	#GADGETUP,D2		PRESSED A GADGET?
	BEQ.S	GADG_JMP

	BRA	WAITFORMSG		LOOP AROUND AGAIN

**********

GADG_JMP
	MOVE.L	GG_USERDATA(A2),A0	GET USERDATA (ROUTINE POINTER)

	CMPA.L	#0,A0			GADGET POINTER ASSIGNED?
	BEQ.S	WAITFORMSG		FORGET IT THEN!

	JMP	(A0)			JUMP TO GADGET ROUTINE!

**********

CLOSEWIN
	MOVE.L	WINPTR,A0
	CALLINT	CLOSEWINDOW		SHUT DOWN MAIN WINDOW

CLOSELIBS
	MOVE.L	_INTUITIONBASE,A1
	CALLEXEC CLOSELIBRARY

	MOVEQ.L	#0,D0			NO CLI ERROR MESSAGE

QEXIT	RTS				BYE!!!

**********

*	THE MAIN SLEEP ROUTINE FOLLOWS:


SLEEP	MOVE.L	WINPTR,A0
	CALLINT	CLOSEWINDOW		SHUT DOWN MAIN WINDOW

	LEA	SLEEPWINDSTRUCT,A0	ADDRESS OF SLEEP WINDOW

	CALLINT	OPENWINDOW
	MOVE.L	D0,SLEEPWINPTR

	MOVE.L	SLEEPWINPTR,A0
	MOVE.L	WD_USERPORT(A0),SLEEPWINPORT	SAVE SLEEP PORT

**********

SLEEPWAIT
	MOVE.L	SLEEPWINPORT,A0

	CALLEXEC WAITPORT		WAIT FOR SOMETHING TO HAPPEN!

	MOVE.L	SLEEPWINPORT,A0

	CALLEXEC GETMSG

	TST.L	D0
	BEQ.S	SLEEPWAIT

	MOVE.L	D0,A1

	MOVE.L	IM_CLASS(A1),D2		SAVE MESSAGE CLASS

	CALLEXEC REPLYMSG

	CMP.L	#CLOSEWINDOW,D2
	BEQ.S	CLOSESLEEP		QUIT

	CMP.L	#ACTIVEWINDOW,D2	WINDOW ACTIVATED?
	BEQ.S	SLEEPACTIVE

	BRA.S	SLEEPWAIT

**********

CLOSESLEEP
	MOVE.L	SLEEPWINPTR,A0
	CALLINT	CLOSEWINDOW		CLOSE SLEEPWINDOW

	BRA	CLOSELIBS		CLOSE LIBRARIES AND QUIT

**********

SLEEPACTIVE

	BTST	#10,$DFF016		TST RMB (VIA THE HARDWARE!!!)
	BEQ.S	END_SLEEP		RMB=WAKEUP

	MOVE.L	SLEEPWINPORT,A0

	CALLEXEC GETMSG			NY NEW MESSAGES FOR ME?

	TST.L	D0
	BNE.S	READMSG			GO TO READMSG IF ONE IS FOUND

	BRA.S	SLEEPACTIVE		NO MESSAGE = NO REPLY!!

**********

READMSG
	
	MOVE.L	D0,A1			MESSAGE --> A1

	MOVE.L	IM_CLASS(A1),D2		SAVE MESSAGE CLASS

	CALLEXEC REPLYMSG		SEND NICE REPLY TO INTUITION

	CMP.L	#CLOSEWINDOW,D2
	BEQ.S	CLOSESLEEP		QUIT

	CMP.L	#INACTIVEWINDOW,D2	WINDOW DEACTIVATED?
	BEQ	SLEEPWAIT		USE WAIT PORT TO WAIT

	BRA	SLEEPACTIVE

**********

END_SLEEP
	MOVE.L	SLEEPWINPTR,A0
	CALLINT	CLOSEWINDOW		CLOSE SLEEPWINDOW

	BRA	OPENWIN			REOPEN MAIN WINDOW

**********************************************************************

INTNAME	DC.B	'intuition.library',0
	EVEN

DOSNAME	DC.B	'dos.library',0
	EVEN

_INTUITIONBASE	DC.L	0
_DOSBASE	DC.L	0

WINRASTPORT	DC.L	0
WINPORT		DC.L	0
SLEEPWINPORT	DC.L	0
WINPTR		DC.L	0
SLEEPWINPTR	DC.L	0

**********

*	MAIN WINDOW AND SLEEP WINDOW STRUCTURES FOLLOW

MAIN_WIN
	DC.W	100,100		WINDOW X/Y ORIGIN
	DC.W	300,40		WINDOW WIDTH/HEIGHT
	DC.B	0,1		PEN COLOURS
	DC.L	CLOSEWINDOW+GADGETUP	IDCMP FLAGS
	DC.L	WINDOWDRAG+WINDOWDEPTH+WINDOWCLOSE+NOCAREREFRESH+ACTIVATE	OTHER FLAGS
	DC.L	SLEEPGADGET	GADGET POINTER
	DC.L	0		NO CHECKMARK
	DC.L	MAIN_WIN_NAME	WINDOW TITLE
	DC.L	0		NO CUSTOM SCREEN
	DC.L	0		NO CUSTOM BITMAP
	DC.W	5,5		MINIMUM WIDTH AND HEIGHT
	DC.W	-1,-1		MAXIMUM WIDTH AND HEIGHT
	DC.W	WBENCHSCREEN	DESTINATION SCREEN TYPE

**********

SLEEPWINDSTRUCT
	DC.W	400,0		WINDOW X/Y ORIGIN
	DC.W	176,10		WINDOW WIDTH/HEIGHT
	DC.B	0,1		PEN COLOURS
	DC.L	CLOSEWINDOW+ACTIVEWINDOW+INACTIVEWINDOW		IDCMP FLAGS
	DC.L	WINDOWDRAG+WINDOWDEPTH+WINDOWCLOSE+NOCAREREFRESH	OTHER FLAGS
	DC.L	0		NO GADGETS
	DC.L	0		NO CHECKMARK
	DC.L	SLEEPWINDOWNAME	WINDOW TITLE
	DC.L	0		NO CUSTOM SCREEN
	DC.L	0		NO CUSTOM BITMAP
	DC.W	5,5		MINIMUM WIDTH AND HEIGHT
	DC.W	-1,-1		MAXIMUM WIDTH AND HEIGHT
	DC.W	WBENCHSCREEN	DESTINATION SCREEN TYPE

MAIN_WIN_NAME
	DC.B	'SLEEPY: BY NEIL JOHNSTON',0

SLEEPWINDOWNAME
	DC.B	'SLEEPY V1.0',0
	EVEN

**********

SLEEPGADGET
	DC.L	0		LAST GADGET
	DC.W	100,20		GADGET X/Y POS
	DC.W	88,8		GADGET WIDTH/HEIGHT
	DC.W	GADGIMAGE	GADGET FLAGS
	DC.W	RELVERIFY	ACTIVATION FLAGS
	DC.W	BOOLGADGET	GADGET TYPE
	DC.L	0		NO BORDER/IMAGE
	DC.L	0		NO ALTERNATIVE IMAGE
	DC.L	SLEEP_GADTEXT	INTUITEXT STRUCTURE
	DC.L	0,0		SPECIAL INFO
	DC.W	0		USER ID WORD
	DC.L	SLEEP		JUMP ROUTINE (SLEEP)

SLEEP_GADTEXT
	DC.B	1,0		PEN COLOURS
	DC.B	RP_JAM1,0	DRAWMODE
	DC.W	0,0		TEXT POSITION
	DC.L	0		STANDARD FONT
	DC.L	SLEEP_TEXT	TEXT POINTER
	DC.L	0		NO MORE TEXT

SLEEP_TEXT
	DC.B	'GO TO SLEEP',0
	EVEN

**********

