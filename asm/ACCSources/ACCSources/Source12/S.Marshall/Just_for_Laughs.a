;Libraries and files
;assembly source code

 ;External references
 ;the amiga linker will link these to the program


 XREF  _AbsExecBase        ;exec library address
                           ;always stored in the same location
 XREF  _LVOOpenLibrary     ;exec library routines
 XREF  _LVOAllocMem
 XREF  _LVOCloseLibrary
 XREF  _LVOFreeMem

 XREF  _LVODelay           ;dos library routines
 XREF  _LVOOpen
 XREF  _LVORead
 XREF  _LVOClose
 XREF  _LVOWrite
 XREF  _LVOOutput

     CSECT text,code       ;this directive is used by the Lattice Assembler

main:
 jsr      openlibraries    ;open the libraries
 tst.l    d0
 bne      cleanup          ;exit if there is an error
 move.l   dosbase,a6
 move.l   #500,d1
 jsr      _LVODelay(a6)    ;delay a few seconds
 jsr      allocatememory   ;allocate a memory buffer
 tst.l    d0
 bne      cleanup
 jsr      readfile         ;read in a text file
 tst.l    d0
 bne      cleanup          ;exit if the file has not been read
 jsr      filetoconsole    ;copy the file data to the screen
 jsr      opendiskfontlibrary  ;open the diskfont library
 tst.l    d0
 bne      cleanup          ;exit if there is an error
 move.l   dosbase,a6
 move.l   #500,d1
 jsr      _LVODelay(a6)    ;delay again
cleanup:
 jsr      freememory       ;free the memory buffer
 jsr      closelibraries   ;close the libraries
 rts
 
openlibraries:
 move.l   _AbsExecBase,a6  ;exec library address
 lea      dosname,a1       ;pointer to string containing dos library name
 clr.l    d0               ;any version 
 jsr      _LVOOpenLibrary(a6) ;open the dos library
 movem.l  d0,dosbase       ;and store the returned pointer
 tst.l    d0               ;check for success
 beq      seterrorflag     ;branch if unsuccessful
 lea      graphicsname,a1  ;open graphics library and store the pointer
 clr.l    d0
 jsr      _LVOOpenLibrary(a6)
 movem.l  d0,graphicsbase
 tst.l    d0
 beq      seterrorflag
 lea      intuitionname,a1 ;open the intuitionlibrary and store the pointer
 clr.l    d0
 jsr      _LVOOpenLibrary(a6)
 movem.l  d0,intuitionbase
 tst.l d0
 beq      seterrorflag
 clr.l    d0
 rts

opendiskfontlibrary:
 move.l   _AbsExecBase,a6  ;exec library address
 lea      diskfontname,a1  ;open the diskfont library and store the pointer
 clr.l    d0
 jsr      _LVOOpenLibrary(a6)
 movem.l  d0,diskfontbase
 tst.l d0
 beq      seterrorflag
 clr.l    d0
 rts


closelibraries:
 move.l   _AbsExecBase,a6   ;close each library in turn
 move.l   graphicsbase,a1
 beq      nographicslib
 jsr      _LVOCloseLibrary(a6)
nographicslib:
 move.l   dosbase,a1
 beq      nodoslib
 jsr      _LVOCloseLibrary(a6)
nodoslib:
 move.l   diskfontbase,a1
 beq      nodiskfontlib
 jsr      _LVOCloseLibrary(a6)
nodiskfontlib:
 move.l   intuitionbase,a1
 beq      nointuilib
 jsr     _LVOCloseLibrary(a6)
nointuilib:
 rts

seterrorflag:               ;set a flag if there is an error
 move.l   #1,d0
 rts

allocatememory:
 move.l   _AbsExecBase,a6   ;exec library address
 move.l   #1024,d0          ;number of bytes required
 move.l   #65539,d1         ;type of memory required (chip memory or clear)
 jsr      _LVOAllocMem(a6)  ;allocate the memory
 movem.l  d0,buffer         ;store the pointer
 beq      seterrorflag     
 clr.l    d0
 rts

freememory:
 move.l   _AbsExecBase,a6    ;exec library address
 move.l   #1024,d0           ;number of bytes allocated from this buffer by
                             ;AllocMem command
 move.l buffer,a1            ;pointer to buffer
 jsr      _LVOFreeMem(a6)    ;frees the memory
 rts

readfile:
 move.l   dosbase,a6          ;dos library address
 move.l   #filename,d1        ;name of file to be read
 move.l   #1005,d2            ;the accessmode is 1005 for a file already open
                              ;1006 to create a new file
 clr.l    d0
 jsr      _LVOOpen(a6)        ;open the file
 move.l   d0,d1
 beq      seterrorflag        ;branch if unsuccessful
 movem.l  d0,filehandle       ;store the filehandle
 move.l   buffer,d2           ;pointer to memory buffer
 clr.l    d0
 move.l   #1024,d3            ;number of bytes to be read
 jsr      _LVORead(a6)        ;read the data
 cmpi.l   #-1,d0
 beq      seterrorflag        ;branch if unsuccessful
 clr.l   d0
 move.l  filehandle,d1        ;obtain the filehandle
 jsr     _LVOClose(a6)        ;close the file
 clr.l   d0
  rts

filetoconsole:
 move.l  dosbase,a6            ;dos library address
 jsr     _LVOOutput(a6)        ;obtain a filehandle for output to the console
 move.l  d0,d1
 move.l  buffer,d2             ;pointer to memory buffer
 move.l  #1024,d3              ;number of bytes to be read
 jsr     _LVOWrite(a6)         ;write to the console
 rts

       CSECT data              ;SECTION DATA,data used by Metacomco Assembler
dosname:
 dc.b    'dos.library',0       ;name of dos library
 ds.w    0                     ;word alignment
graphicsname:
 dc.b    'graphics.library',0  ;name of graphics library
 ds.w    0
intuitionname:
 dc.b    'intuition.library',0 ;name of intuition library
 ds.w    0
diskfontname:                  ;name of diskfont library
 dc.b    'diskfont.library',0
 ds.w    0                     
filename:
 dc.b    'intro.a',0           ;name of file to be read, any text file of more
                               ;than 1024 bytes will do
 ds.l    0
graphicsbase:                  ;stored library pointers
 dc.l    0
dosbase:
 dc.l    0
intuitionbase:
 dc.l    0
diskfontbase:
 dc.l    0
filehandle:                     ;filehandle for file to be read
 dc.l    0
buffer:                         ;pointer to memory buffer
 dc.l    0
 END 
   
