
		lea	Text,a0
		lea	Buffer,a1
		bsr	Centralise
		rts

Text		dc.b	'Mark Meany',0
		even
Buffer		ds.b	82
		dc.w	$ffff


; Centralise a heading in an 80 byte display

; Entry		a0->text
;		a1->Buffer

; Corrupt	d0,d1,a0,a1,a2 

Centralise	move.l		a1,a2		get a working copy
		moveq.l		#80,d0		counter
		moveq.l		#' ',d1		fill byte
.blank		move.b		d1,(a2)+	fill the buffer
		dbra		d0,.blank	with 81 spaces

		move.l		a0,a2		make a copy
		moveq.l		#-1,d0		init counter
.loop		addq.l		#1,d0		bump counter
		tst.b		(a2)+		end of text yet?
		bne.s		.loop		if not keep checking


		asr.l		#1,d0		div length by 2
		moveq.l		#40,d1		centre of screen
		sub.l		d0,d1		d1=left offset
		add.l		d1,a1		add to buffer pointer

.loop2		move.b		(a0)+,(a1)+	copy next byte
		bne.s		.loop2		until done

		move.b		#' ',-1(a1)	correct last character
		rts				and return

