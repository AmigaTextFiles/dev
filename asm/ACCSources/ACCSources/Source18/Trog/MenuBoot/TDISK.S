******************************************************************************* 
*TRACKDISK COMMANDS 
*******************************************************************************
CMD_RESET:        EQU     1
CMD_READ          EQU     2
CMD_WRITE:        EQU     3
CMD_UPDATE:       EQU     4
CMD_CLEAR:        EQU     5
CMD_STOP:         EQU     6
CMD_START:        EQU     7
CMD_FLUSH:        EQU     8
TD_MOTOR:         EQU     9
TD_SEEK:          EQU     10
TD_FORMAT:        EQU     11
TD_REMOVE:        EQU     12
TD_CHANGENUM:     EQU     13
TD_CHANGESTATE:   EQU     14
TD_PROTSTATUS:    EQU     15
******************************************************************************* 
*EXTIOREQUEST OFFSETS 
*******************************************************************************
IO_COMMAND:       EQU     28
IO_ERROR:         EQU     30
IO_ACTUAL:        EQU     32
IO_LENGTH:        EQU     36
IO_DATA:          EQU     40
IO_OFFSET:        EQU     44
*******************************************************************************
*THIS ROUTINE OPENS AND CLOSES THE TRACK DISK DEVICE
*******************************************************************************
*TRACKDISK VARIBLES I/O STRUCTURES AND PORTS ECT
*******************************************************************************
TRACKDISK:	DC.B "trackdisk.device",0 *NAME OFF DEVICE
		EVEN				
DISKIO:		DS.L	20		  *RESERVE 20 LONGWORDS FOR STRUCTURE
TRACKPORT:	DS.L	8		  *RESERVE 8 LONGWORDS FOR STRUCTURE
*******************************************************************************
OPENTRACK:
	MOVE.L	$4,A6			*EXECBASE STRUCTURE
	MOVE.L	#$0,A1			*NAME OF TASK TO FIND (0)
	JSR	FINDTASK(A6)		*FIND TASK
	MOVE.L	D0,TRACKPORT+$10	*STORE POINTER IN PORT+1 (HEX $10)
	LEA	TRACKPORT,A1		*POINTER TO TRACK PORT
	JSR	ADDPORT(A6)		*ADDPORT TO SYSTEM LIST (REPLY PORT)
	LEA	DISKIO,A1		*POINTER TO DISK I/O AREA
	MOVE.L	#$0,D0			*DRIVE 0
	CLR.L	D1			*NO FLAGS
	LEA	TRACKDISK,A0		*DEVICE TO OPEN *TRACKDISK.DEVICE*
	JSR	OPENDEVICE(A6)		*OPEN DEVICE
	TST.L	D0			*TEST FO ERRORS
	BNE	TERROR	        	*BRANCH IF ERROR OCURED
	RTS				*ELSE TRACKDISK IS OPEN FOR USE
*******************************************************************************
CLOSETRACK:
	MOVE.L	$4,A6			*EXECBASE STRUCTURE
	LEA	TRACKPORT,A1		*POINTER TO PORT (REPLY)
	JSR	REMPORT(A6)		*REMOVE PORT FROM SYSTEM LIST
	LEA	DISKIO,A1		*POINTER TO DISK I/O AREA
	JSR	CLOSEDEVICE(A6)		*CLOSE TRACKDISK
TERROR:	RTS  				*PROGRAME RETURN
*******************************************************************************
*THIS ROUTINE WRITES BOOT BLOCKS AND CALCULATES CHECKSUMS
*******************************************************************************
CHECK: 
	MOVE.L	BOOTDATA,A0			*BOOTBLOCK PRG STORAGE (1K)
	LEA	4(A0),A1			*POINTER TO BOOT CHECKSUM
	CLR.L	(A1)				*CLEAR REGISTER
	MOVE.W	#$FF,D1				*LONGWORDS IN BLOCK -1
	MOVEQ	#$0,D0				*CLEAR REGISTER
ADLONG:	ADD.L	(A0)+,D0			*ADD NEXT LONGWORD TO D0
	BCC	NXTLNG				*CHECK D1 IF CHANGE IN   FLAG
	ADDQ.L	#1,D0				*THEN ADD LAST LONGWORD (256)
NXTLNG:	DBF	D1,ADLONG			*SUB 1 FROM D1 AND BRA
	NOT.L	D0				*REVERSE D0
	MOVE.L	D0,(A1)				*WRITE NEW CHECKSUM TO BOOTPRG
	RTS
WRITE0:	
	MOVE.W	#512,D1				*AMOUNT OF L/WORDS TO COPY
	LEA	DOSHEADER,A0			*ADRESS OF BOOT PROG
	MOVE.L	BOOTDATA,A1			*ADRESS OF CHIP RAM TO COPY TO
MOVEBT:	MOVE.W	(A0)+,(A1)+			*COPY PROG TO CHIP RAM:
	DBRA	D1,MOVEBT			*BRANCH AND TILL DONE
	BSR	CHECK

WRITEBOOT:
	JSR	OPENTRACK                       *OPEN TRACKSIDE USER TASK
	MOVE.L	$4,A6				*EXECBASE STRUCTURE
	LEA	DISKIO,A1			*DISK I/O STRUCTURE
	MOVE.L  #TRACKPORT,14(A1)		*PORT REPLY STRUCTURE
	MOVE.W	#CMD_WRITE,IO_COMMAND(A1)	*TRACK COMMAND WRITE (3)
	MOVE.L  BOOTDATA,IO_DATA(A1)		*START OF DATA TO WRITE
	MOVE.L  #2*512,IO_LENGTH(A1)		*AMOUNT OF DATA TO WRITE 
	MOVE.L  #0*512,IO_OFFSET(A1)		*OFFSET 0 = BOOTBLOCK
	JSR	DOIO(A6)			*WRITE BOOT

	LEA	DISKIO,A1			*SAME ROUTINE AS THE
	MOVE.L	#TRACKPORT,14(A1)		*ONE ABOVE DOES EVERYTHING
	MOVE.W	#CMD_READ,IO_COMMAND(A1)	*THE SAME EXCEPT IT READS
	MOVE.L	BOOTDATA,IO_DATA(A1)		*BLOCK 100 INSTEAD AND READ
	MOVE.L  #2*512,IO_LENGTH(A1)		*FORCES THE
	MOVE.L  #100*512,IO_OFFSET(A1)		*BUFFER TO BE EMPTIED HENCE
	JSR	DOIO(A6)			*WRITES THE BOOTBLOCK 

MOTOROFF:
	LEA	DISKIO,A1			*DISK I/O STRUCTURE
	MOVE.W	#TD_MOTOR,IO_COMMAND(A1)	*COMMAND MOTOR O=OFF 1=ON
	MOVE.L	#0,IO_LENGTH(A1)		*0= TURN MOTOR OFF
	JSR	DOIO(A6)			*TURN MOTOR OFF
	JSR	CLOSETRACK			*CLOSE TRACKDISK USER TASK
	RTS					*RETURN TO PROGRAME
******************************************************************************
