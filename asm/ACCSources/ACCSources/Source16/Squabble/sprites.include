	;sprites.include
	;


Fireballs
	tst.w game_flag
	bne f_2
	bsr Prepare_fireball
	move.w XL7,d2
	move.w #0,XL6
	move.l #SPRITE0,a1
	move.l #Sprite_variables,a0
	move.w #3,d7
f_1
	bsr set_fireball
	bsr run_fireball
	add.l #72,a1	;point to next sprite
	add.l #2,a0	
	dbf d7,f_1
	rts

set_fireball
	cmp.w #1,(a0)
	beq f_2		;already on the go
	cmp.w #1,XL6
	beq f_2		;already one started
	move.w DISTANCE,d0
	cmp.w shut_off,d0
	bgt.s f_2
	and.w d2,d0
	tst.w d0
	bne.s f_2		;not ready to appear
	move.w XL5,d0
	move.w d0,d1
	and.w #$7f,d0
	add.w #56,d0
	;move.w #80,d0
;there is always the chance that d0 is too large/small
	lsl.w #8,d0		;d0=YY00
	or.w #$00d8,d0	;d0=YYXX
	move.w d0,(a1)
	and.w #$ff00,d0	;d0=yy00
	add.w #$0800,d0	;8 high
	move.w d0,2(a1)
	add.w #75,d1
	and.w #127,d1
	move.w d1,XL5
	move.w #1,(a0)
	move.w #1,XL6
	bsr Sound5
	rts

run_fireball
	cmp.w #1,(a0)
	bne.s f_2		;this fireball not working
	sub.w #3,(a1)
	move.w (a1),d0
	and.w #$00ff,d0
	cmp.w #48,d0
	bgt.s f_2		;not at far left
	move.w #$0000,(a1)
	move.w #$0000,2(a1)
	move.w #0,(a0)	;switch off
	rts


f_2
	rts



Prepare_fireball	;ok, for graphics are correct
	move.l #fireball_gfx_1,a0
	move.w DISTANCE,d0
	btst #1,d0
	beq wf1
	move.l #fireball_gfx_2,a0
wf1
	bsr copy_gfx2
	rts



Set_ackack			;called from enemy dump routine
	;d0=x (0-320) d1= y (0-256) normal screen coords
	cmp.w #5,level
	beq f_2
	cmp.w #6,level
	beq ackok2
	cmp.w #8,level
	beq ackok2
	tst.w clocked
	bne ackok2
	rts
ackok2
	add.w #96,d0
	add.w #4,d1
	moveq #0,d2
	moveq #0,d5
	move.w ackack_ct,d2
	add.w #1,d2
	and.w #3,d2			;only four explosions allowed (0,1,2,3)
	move.w d2,ackack_ct
	bsr ackack_on		;now on with new co-ords
	rts


Run_ackack				;called by main play loop
	cmp.w #5,level
	beq f_2
	cmp.w #6,level
	beq ackok
	cmp.w #8,level
	beq ackok
	tst.w clocked
	bne ackok
	rts

ackok
	moveq #0,d4
	move.l #SPRITE0,a0
ack2
	cmp.w #$ffff,(a0)
	beq.s ack3			;not in use, so skip
	sub.w #$0002,(a0)	;move it left 4 pixels
	move.w (a0),d0
	and.w #$00ff,d0
	cmp.w #40,d0
	blt.s ack5	;if sprite is off screen
	sub.w #$0200,(a0)
	sub.w #$0200,2(a0)	;move it up 2 pixels
	move.w (a0),d0
	lsr.w #8,d0
	cmp.w #5,d0
	blt.s ack5

ack3
	add.l #72,a0
	add.w #1,d4
	cmp.w #4,d4
	bne.s ack2
	rts
ack5	;switch it off
	move.l #$ffffffff,(a0)
	bra.s ack3
	




ackack_on
	;switch on sprite d2 with d0=x,d1=y
	move.l #SPRITE0,a0
	mulu.w #72,d2
	add.l d2,a0		;a0 now points to correct sprite
	cmp.w #$ffff,(a0)	;hold on, is this one already on?
	bne ack9
	lsl.w #8,d1		;d1=YY00
	lsr.w #1,d0		;d0=xx/2
	or.w d0,d1		;d1=YYXX
	move.w d1,(a0)
	and.w #$ff00,d1	;d1=yy00
	add.w #$0800,d1	;8 high
	move.w d1,2(a0)
	bsr Sound7
ack9
	rts





Prepare_ackack
	move.l #ackack_gfx,a0
	bsr copy_gfx2
	rts


copy_gfx2
	move.l #SPRITE0+4,a1
	move.l #3,d0
pf_1
	move.l a0,a2
	move.w #15,d1
pf_2
	move.l (a2)+,(a1)+
	dbf d1,pf_2

	add.l #8,a1
	dbf d0,pf_1
	rts

	even

shut_off
	dc.w 0
which_fireball
	dc.w 0
Sprite_variables
	dcb.w 8
ackack_table
	dcb.w 8
ackack_ct
	dc.w 0


	even