;Set variables

; All the code to reset the various variables

MAX_FUEL equ 3760

Reset_level_1	;	every new game
	move.w #0,clocked
	move.w #0,score
	move.w start_lives,d0
	move.w d0,lives
	rts
	
Reset_level_2	; every time the entire landscape is started
	move.w #0,level_complete
	move.w #0,DISTANCE; was 0
	move.w #2,HEIGHT
	move.w #0,COUNT
	move.w #20,MAX_COUNT
	move.w #0,TRANS
	move.w #0,TOP
	rts

Reset_level_3	; every new life
	
	jsr Clear_screens
	jsr blank_fuel
	move.w #0,fuel
	move.w #160,Ship_pos_x
	move.w #40,Ship_pos_y
	move.w #0,hit
	move.w #0,rhit
	move.w #0,miss_inuse
	move.w #0,miss_variables
	move.w #0,miss_variables+6
	move.w #0,bomb_variables
	move.w #0,bomb_variables+6
	move.w #0,XL5
	move.w #0,XL6
	move.w #0,XL7
	move.w #0,game_flag
	move.w #0,flames
	move.w #0,which_fireball
	bsr Print_details
	jsr All_sprites_off
	jsr Reset_spikers
	rts

Clear_screens
	move.l #SCREEN1,a0
	jsr Blit_Zap
	move.l #SCREEN2,a0
	jsr Blit_Zap
	move.l #SCREEN4,a0
	jsr Blit_Zap2
	move.l #SCREEN6,a0
	jsr Blit_Zap2
	jsr waitblit
	rts

InterOn
	move.w #1,intflag
	rts
InterOff
	move.w #0,intflag
	rts

Print_details
	bsr print_score
	bsr print_lives
	bsr print_high
	bsr print_fuel
	rts

print_score
	moveq #0,d1
	move.w score,d1
	move.l #SCREEN3+4,a0
	bsr printnumber
	bsr print_fuel
	rts
print_lives
	moveq #0,d1
	move.w lives,d0
	move.l #SCREEN3+22,a0
	bsr number
	rts
print_high
	moveq #0,d1
	move.w high,d1
	move.l #SCREEN3+34,a0
	jsr printnumber
	rts

blank_fuel
	moveq #0,d0
	moveq #0,d2
	move.l #SCREEN3+9368,a0
	move.l #SCREEN5+9368,a1

	move.w #6,d1
pu_3
	move.l #0,48(a0)
	move.l #0,(a0)+
	move.l #0,48(a1)
	move.l #0,(a1)+
	dbf d1,pu_3
	move.l #$f,48(a0)
	move.l #$f,(a0)
	move.l #$f,48(a1)
	move.l #$f,(a1)
	rts


print_fuel

	move.w #$0f0,d0
	cmp.w #500,fuel
	bgt pf_5
	move.w #$f00,d0
pf_5
	move.w d0,warning_c

	move.l #SCREEN3+9366,a0
	move.l #SCREEN5+9366,a1

	move.w fuel,d0; here fuel is 0-640. Get it into 0-320
	lsr.w #4,d0		;0-320
	move.w d0,d1	;d1=0-320
	lsr.w #4,d1		;d1=0 to 20  (i.e. to word level)
	move.w d1,d2

	
	;d2 is the number of complete words

	;sub.w #1,d2

	;cmp.w #0,d2
	;beq pu_4

pu_5
	move.w #$ffff,48(a0)
	move.w #$ffff,(a0)+
	dbf d2,pu_5
	
pu_4

	

	lsl.w #4,d1		;scale up d1 again (minus left over bits)
	sub.w d1,d0		;d0 is the leftovers
	cmp.w #0,d0		;cant shift by 0 times
	beq pu_1
	move.w #$8000,d1	;starting number
	asr.w d0,d1		;shift d1 by d0 times. Now d1 is correct value
	move.w d1,(a0)
	move.w d1,48(a0)
	move.l #SCREEN3+9398,a0
	or.w #$000f,(a0)
	or.w #$000f,48(a0)
	rts
pu_1
	move.w #$0000,(a0)
	move.w #$0000,48(a0)
	move.l #SCREEN3+9398,a0
	or.w #$000f,(a0)
	or.w #$000f,48(a0)
	rts

Print_getready
	move.l #text1,a0
	bsr print_text
	rts

Print_gameover
	move.l #text2,a0
	bsr print_text
	rts

Print_pressfire
	move.w #$fcf,text_c_pt
	move.l #text3,a0
	move.l #SCREEN3+3610,a1
	move.w #$9f0,d1
	jsr waitblit
	move.w #0,BLTCON1+CUSTOM
	move.w d1,BLTCON0+CUSTOM
	move.l a0,BLTAPTH+CUSTOM
	move.w #0,BLTAMOD+CUSTOM
	move.l a1,BLTDPTH+CUSTOM
	move.w #22,BLTDMOD+CUSTOM
	move.w #$34d,BLTSIZE+CUSTOM
	jsr waitblit
	rts

Erase_pressfire
	move.l #SCREEN3+3610,a1
	move.w #$100,d1
	jsr waitblit
	move.w #0,BLTCON1+CUSTOM
	move.w d1,BLTCON0+CUSTOM
	move.l a1,BLTDPTH+CUSTOM
	move.w #22,BLTDMOD+CUSTOM
	move.w #$34d,BLTSIZE+CUSTOM
	jsr waitblit
 	rts

print_text
	move.l #SCREEN3+3614,a1
	move.w #$9f0,d1
	jsr waitblit
	move.w #0,BLTCON1+CUSTOM
	move.w d1,BLTCON0+CUSTOM
	move.l a0,BLTAPTH+CUSTOM
	move.w #0,BLTAMOD+CUSTOM
	move.l a1,BLTDPTH+CUSTOM
	move.w #30,BLTDMOD+CUSTOM
	move.w #$389,BLTSIZE+CUSTOM
	jsr waitblit
 	rts

Erase_text
	move.l #SCREEN3+3614,a1
	move.w #$100,d1
	jsr waitblit
	move.w #0,BLTCON1+CUSTOM
	move.w d1,BLTCON0+CUSTOM
	move.l a1,BLTDPTH+CUSTOM
	move.w #30,BLTDMOD+CUSTOM
	move.w #$389,BLTSIZE+CUSTOM
	rts	


intflag	
	dc.w 0	;0 interrupts ignored
game_flag 
	dc.w 0
score
	dc.w 0
lives
	dc.w 0
start_lives
	dc.w 3
fuel
	dc.w 0
high
	dc.w 1000
clocked
	dc.w 0
level_complete
	dc.w 0
