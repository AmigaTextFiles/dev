;explode.include

; opps, bang.



Bang
	;d0=x (0-320) d1= y (0-256) normal screen coords
	add.w #100,d0
	add.w #4,d1
	moveq #0,d2
	moveq #0,d5
	move.w bang_ct,d2
	add.w #1,d2
	and.w #3,d2			;only four explosions allowed (0,1,2,3)
	move.w d2,bang_ct
	move.w d2,d5
	bsr bang_on		;now on with new co-ords
	move.l #bang_table,a0
	lsl.w #1,d5
	add.l d5,a0		;a0 is now table()
	move.w #1,(a0)
	rts

Cycle_bang
	moveq #0,d4
	move.l #SPRITE1,a0
	move.l #bang_table,a1
cyb1
	cmp.w #$ffff,(a0)
	beq cyb4
	sub.w #$0001,(a0)
	move.w (a0),d0
	and.w #$00ff,d0
	cmp.w #40,d0
	blt cyb2	;if sprite is off screen
	add.w #1,(a1)
	cmp.w #21,(a1)
	beq cyb2	;if sprite has reached end of cycle
	;here sprite is ok and going strong
	;a1 points to sp number
	;a0 points to sprite
	move.w (a1),d0
	btst #0,d0
	beq cyb4
	move.l a0,a3
	bsr copy_gfx

cyb4
	add.l #2,a1
cyb3
	add.l #72,a0
	add.w #1,d4
	cmp.w #4,d4
	bne cyb1
cyb5
	rts
cyb2
	move.w #0,(a1)+
	move.l #$ffffffff,(a0)
	bra cyb3
	


bang_off
	;switch off explosion sprite number 0,1,2,3 in d2
	move.l #SPRITE1,a2
	mulu.w #72,d2
	add.l d2,a2		;a2 now points to correct sprite
	move.l #$ffffffff,(a2)	
	rts


bang_on
	;switch on sprite d2 with d0=x,d1=y
	move.l #SPRITE1,a0
	mulu.w #72,d2
	add.l d2,a0		;a0 now points to correct sprite
	lsl.w #8,d1		;d1=YY00
	lsr.w #1,d0		;d0=xx/2
	or.w d0,d1		;d1=YYXX
	move.w d1,(a0)
	and.w #$ff00,d1	;d1=yy00
	add.w #$1000,d1		;16 high
	move.w d1,2(a0)
	rts

All_sprites_off
	moveq #0,d2
	bsr bang_off
	move.l #1,d2
	bsr bang_off
	move.l #2,d2
	bsr bang_off
	move.l #3,d2
	bsr bang_off
	move.l #ackack_table,a0
	move.l #0,(a0)
	move.l #0,4(a0)
	move.l #SPRITE0,a0
	move.w #$ffffffff,d0
	move.l d0,(a0)
	move.l d0,72(a0)
	move.l d0,144(a0)
	move.l d0,216(a0)	;switch off 'baddie' sprites the hard way
	move.l d0,288(a0)
	move.l d0,360(a0)
	move.l d0,432(a0)
	move.l d0,504(a0)	;and these too
	rts

copy_gfx
	;copy data from gfx (number d0) to sprite (a3)
	move.l #bang_gfx,a4
	sub.w #1,d0
	and.l #$000000ff,d0	;cant be too careful
	lsl.w #5,d0
	add.l d0,a4
	add.l #4,a3	;skip header of sprite
	move.w #15,d0	;15+1 = 16 lines
cg_1
	move.l (a4)+,(a3)+
	dbf d0,cg_1
	rts

Explode_ship
	cmp.w #0,flames
	beq set_fuse
	move.w flames,d0
	btst #0,d0
	beq ex_1
	add.w #$73f7,flames_pos
	move.w flames_pos,d2
	move.w d2,d3
	and.w #31,d2
	and.w #15,d3

	move.w flames_xy,d0
	move.w flames_xy+2,d1
	add.w d2,d0
	add.w d3,d1
	bsr Bang
ex_1
	rts


set_fuse
	;go bang
	jsr Sound3
	move.w #0,XL7
	jsr All_sprites_off
	move.w #1,flames
	move.w Ship_pos_x,d1
	move.w Ship_pos_y,d0
	add.w #38,d0
	add.w #16,d1
	move.w d1,flames_xy
	move.w d0,flames_xy+2
	move.l #SCREEN2,a0
	jsr Blit_Zap
	move.l #SCREEN4,a0
	jsr Blit_Zap2
	move.l #SCREEN6,a0
	jsr Blit_Zap2
	rts
	


flames
	dc.w 0
flames_xy
	dc.w 0,0
flames_pos
	dc.l 0
bang_ct
	dc.w 0
bang_table
	dcb.w 8,0


