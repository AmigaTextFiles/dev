; Blitter Include

Blit_Zap
	;a0 = screen to clear (small screen only)
	bsr waitblit
	move.l a0,BLTDPTH(a6)
	move.w #0,BLTDMOD(a6)
	move.w #DEST,BLTCON0(a6)
	move.w #0,BLTCON1(a6)
	move.w #$3218,BLTSIZE(a6)
	rts

Blit_Zap2
	;a0 = screen to clear (big screen only)
	bsr waitblit
	move.l a0,BLTDPTH(a6)
	move.w #0,BLTDMOD(a6)
	move.w #DEST,BLTCON0(a6)
	move.w #0,BLTCON1(a6)
	move.w #$3c18,BLTSIZE(a6)
	rts

Scroll_Background
	bsr copy_back
	move.l #SCREEN3+7726,a0
	bsr waitblit
	move.w #$ffff,BLTAFWM(a6)
	move.w #$ffff,BLTALWM(a6)
	move.w #2,BLTCON1(a6)			; Special modes; descend
	move.l a0,BLTAPTH(a6)			; Area to copy FROM
	move.w #0,BLTAMOD(a6)			; No need to skip a word
	move.l a0,BLTDPTH(a6)			; Area to copy TO
	move.w #0,BLTDMOD(a6)			; No need to skip a word
	move.w #$19F0,BLTCON0(a6)		; Copy A -> D, shift 1 pixel
	move.w #$0818,BLTSIZE(a6)		; Size and DO IT!
	bsr waitblit
	bsr put_back
	bsr change_colours				;bit of a swizz putting it here!
	rts

Scroll_Plane1
	move.l #SCREEN1+$23a0,a0
	bsr waitblit
	move.w #2,BLTCON1(a6)			; Special modes; descend
	move.l a0,BLTAPTH(a6)			; Area to copy FROM
	move.w #0,BLTAMOD(a6)			; No need to skip a word
	move.l a0,BLTDPTH(a6)			; Area to copy TO
	move.w #0,BLTDMOD(a6)			; No need to skip a word
	move.w #$29F0,BLTCON0(a6)		; Copy A -> D, shift 2 pixel
	move.w #$2f98,BLTSIZE(a6)		; Size and DO IT!
	rts

Scroll_tit_1
	move.l #SCREEN1+$12c0,a0
	lea CUSTOM,a6
	bsr waitblit
	move.w #2,BLTCON1(a6)			; Special modes; descend
	move.l a0,BLTAPTH(a6)			; Area to copy FROM
	move.w #0,BLTAMOD(a6)			; No need to skip a word
	move.l a0,BLTDPTH(a6)			; Area to copy TO
	move.w #0,BLTDMOD(a6)			; No need to skip a word
	move.w #$89F0,BLTCON0(a6)		; Copy A -> D, shift 2 pixel
	move.w #$a14,BLTSIZE(a6)		; Size and DO IT!
	rts

Scroll_tit_2
	move.l #SCREEN1+11200,a0
	lea CUSTOM,a6
	bsr waitblit
	move.w #0,BLTCON1(a6)			; Special modes; none
	move.l a0,BLTAPTH(a6)			; Area to copy FROM
	move.w #0,BLTAMOD(a6)			; No need to skip a word
	move.l a0,BLTDPTH(a6)			; Area to copy TO
	move.w #0,BLTDMOD(a6)			; No need to skip a word
	move.w #$89F0,BLTCON0(a6)		; Copy A -> D, shift 2 pixel
	move.w #$a14,BLTSIZE(a6)		; Size and DO IT!
	rts

copy_back
	add.w #1,scroll_ct
	cmp.w #16,scroll_ct
	bne c_b3
	move.w #0,scroll_ct
	move.l #SCREEN3+6144,a0
	move.l #buffer,a1
	move.w #31,d1
c_b1
	move.w (a0),(a1)+
	add.w #48,a0
	dbf d1,c_b1
	move.l #SCREEN3+6144,a0
	move.w #0,(a0)
	move.l #SCREEN3+7726,a0
	move.w #$ffff,(a0)
c_b3
	rts

put_back
	cmp.w #0,scroll_ct
	bne c_b3
	move.l #SCREEN3+6190,a0
	move.l #buffer,a1
	move.w #31,d1
c_b2
	move.w (a1)+,(a0)
	add.w #48,a0
	dbf d1,c_b2
	rts

change_colours
	add.w #1,colour_counter
	cmp.w #3,colour_counter
	bne ccl_2
	move.w #0,colour_counter
	move.l #score_start,a0	
	move.w (a0),d2	;store first colour
	move.w #11,d1
ccl_1
	move.w 12(a0),d0
	move.w d0,(a0)
	add.w #4,a0
	move.w d0,(a0)
	add.w #8,a0
	dbf d1,ccl_1
	move.w d2,d0
	move.w d0,(a0)
	add.w #4,a0
	move.w d0,(a0)
ccl_2	
	rts

waitblit
	move.w DMACONR(a6),d7
waitblit2
	move.w DMACONR(a6),d7
	lsr.w #7,d7
	btst #7,d7
	bne.s waitblit2
	rts

scroll_ct
	dc.w 0
buffer
	dcb.w 32,0
colour_counter
	dc.w 0

