;weapons.include

;Handle missiles and bombs dropped from the player's ship

;This version does not use sprites at all, and relies
;on the individual enemy installations to check for
;collisions. That's life.


Weapons
	move.w #1,d0
	btst #7,$bfe001
	beq w_1
	move.w #0,d0
w_1
	move.w d0,button

	move.l #miss_variables,a3
	bsr set_miss
	bsr run_miss
	move.l #bomb_variables,a3
	bsr set_bomb
	bsr run_bomb
	move.l #miss_variables+6,a3
	bsr set_miss
	bsr run_miss
	move.l #bomb_variables+6,a3
	bsr set_bomb
	bsr run_bomb
	move.w button,miss_inuse
	rts
	
set_miss
	cmp.w #1,button
	bne miss_4				;branch if not pressed
	cmp.w #1,miss_inuse		;Button still held down from last time? 
	beq miss_1				;no auto repeat in THIS game :-)
	tst (a3)
	bne miss_1				;This missile slot in use
	;missile fired - set x,y co-ords

	jsr Sound1

	move.w Ship_pos_x,d0
	lsr.w #3,d0
	add.w #4,d0
	and.w #$fe,d0	;play it safe!
	move.w d0,2(a3)
	move.w Ship_pos_y,d0
	add.w #16,d0
	move.w d0,4(a3)
	move.w #1,miss_inuse
	move.w #1,(a3)
	rts
miss_4
	move.w #0,miss_inuse
	rts




run_miss
	tst (a3)
	beq miss_1				;missile not active
	move.w 2(a3),d1			;x
	move.w 4(a3),d0
	bsr erase_miss
	cmp.w #2,(a3)			;2 is a special case to allow
	beq miss_2				;missile to be removed after hitting enemy
	add.w #1,2(a3)
	cmp.w #44,2(a3)
	bgt miss_2				;reached far right


	;-------- collision detection
	moveq #0,d1
	move.w 2(a3),d1
	move.w 4(a3),d0
	mulu.w #48,d0
	move.w d1,d2		;d1 is still 'raw' x
	bclr #0,d2
	
	move.l #SCREEN1,a2
	add.l d0,a2
	add.l d2,a2			;a2 is landscape
	btst #0,d1
	bne peek_1
	move.w #$FF00,d1
	bra peek_2
peek_1
	move.w #$FF,d1
peek_2					;now d1 is the correct mask

	move.w (a2),d0		;check miss->landscape
	and.w d1,d0
	tst d0
	bne peek_3			;yes - hit ground

	;---- end of collision detection

	move.w 2(a3),d1			;x
	move.w 4(a3),d0
	bsr print_miss
miss_1
	rts
miss_2
	move.w #0,(a3)
	rts

peek_3		;hit ground
	move.w #0,(a3);switch off missile; ****!!***
	rts


print_miss
	;d0 = y d1 = x (0-48)
	and.l #$0000ffff,d0
	and.l #$000000ff,d1
	move.w d1,d2
	bclr #0,d2
	mulu.w #48,d0
	move.l #SCREEN4+1920,a2
	add.l d0,a2
	add.l d2,a2
	btst #0,d1
	bne miss_5
	move.w #$ff00,(a2)
	rts
miss_5
	move.w #$ff,(a2)
	rts


erase_miss
	and.l #$0000ffff,d0
	and.l #$000000ff,d1
	move.w d1,d2
	bclr #0,d2
	mulu.w #48,d0
	move.l #SCREEN4+1920,a2
	add.l d0,a2
	add.l d2,a2
	move.w #0,(a2)
	rts



set_bomb
	cmp.w #1,button
	bne bomb_4				;branch if not pressed
	cmp.w #1,bomb_inuse		;Button still held down from last time? 
	beq bomb_1				;no auto repeat in THIS game :-)
	tst (a3)
	bne bomb_1				;This missile slot in use

	;jsr Sound1;falling bomb sound

	move.w Ship_pos_x,d0
	lsr.w #3,d0
	add.w #2,d0
	and.w #$fe,d0	;play it safe!
	move.w d0,2(a3)
	move.w Ship_pos_y,d0
	add.w #17,d0
	move.w d0,4(a3)
	move.w #1,bomb_inuse
	move.w #1,(a3)
	rts
bomb_4
	move.w #0,bomb_inuse
	rts


run_bomb
	tst (a3)
	beq bomb_1	
	move.w 2(a3),d1
	move.w 4(a3),d0
	bsr erase_bomb
	cmp.w #2,(a3)
	beq bomb_2
	add.w #3,4(a3)
	cmp.w #$bb,4(a3)
	bgt bomb_2				;reached bottom?!

	moveq #0,d1
	move.w 2(a3),d1
	move.w 4(a3),d0
	mulu.w #48,d0
	move.w d1,d2		;d1 is still 'raw' x
	bclr #0,d2
	
	move.l #SCREEN1,a2
	add.l d0,a2
	add.l d2,a2	
	btst #0,d1
	;bne peek_4
	move.w #$FF00,d1
	bra peek_5
peek_4
	;move.w #$FF,d1
peek_5
	move.w (a2),d0		;check bomb->landscape
	and.w d1,d0
	tst d0
	bne peek_6			;yes - hit ground


	move.w 2(a3),d1			;x
	move.w 4(a3),d0
	bsr print_bomb

bomb_1
	rts
bomb_2
	move.w #0,(a3)
	rts

peek_6		;hit ground
	move.w #0,(a3)
	rts


print_bomb
	;d0 = y d1 = x (0-48)
	and.l #$0000ffff,d0
	and.l #$000000ff,d1
	move.w d1,d2
	bclr #0,d2
	mulu.w #48,d0
	move.l #SCREEN4+1920,a2
	add.l d0,a2
	add.l d2,a2
	;btst #0,d1
	;bne bomb_5
	move.w #$f000,(a2)
	move.w #$F000,48(a2)
	move.w #$f000,96(a2)
	rts
;bomb_5
;	move.w #$0f,(a2)
;	move.w #$0F,48(a2)
;	move.w #$0f,96(a2)
;	rts


erase_bomb
	and.l #$0000ffff,d0
	and.l #$000000ff,d1
	move.w d1,d2
	bclr #0,d2
	mulu.w #48,d0
	move.l #SCREEN4+1920,a2
	add.l d0,a2
	add.l d2,a2
	move.w #0,(a2)
	move.w #0,48(a2)
	move.w #0,96(a2)
	rts

	even
	include "Source:Squabble/sprites.include"
	even

button
	dc.w 0

miss_inuse
	dc.w 0

miss_variables
	dcb.w 12,0
	;miss_on +0
	;miss_x  +2
	;miss_y  +4

bomb_inuse
	dc.w 0

bomb_variables
	dcb.w 12,0
	;bomb_on +0
	;bomb_x  +2
	;bomb_y  +4


	even

