/*
** $VER: BasmNextError 1.6 (05.04.95)
**       (c) 1995 by Christian Rattei
*/

options results
LF = '0a'x

parse arg CEDport
if CEDport = '' then CEDport = address()

address value CEDport
status 84	/* windownumber */
view = result

jump to file 'RAM:Basm_Errors'
if result ~= 1 then do
	okay1 'Error:'LF'File not loaded'LF'(RAM:Basm_Errors).'
	exit 0
	end

status 69	/* blocky */
if result ~= -1 then mark block

status 17	/* numlines */
lines = result

lastline = 0
error = 0
do while lastline + error = 0
	status 55	/* linebuffer */
	line = result

	if (left(line,6) == 'Error ' | left(line,8) == 'Warning ') then error = 1
	if (left(line,6) == 'Error,' | left(line,8) == 'Warning,') then error = 1

	status 47	/* cursorline */
	if result = lines then lastline = 1

	down
	if error = 1 then do
		status 55	/* linebuffer */
		line2 = result

		if (left(line2,6) == 'Error ' | left(line2,8) == 'Warning ') then up

		down
		end
	end

if lastline = 1 & error = 0 then do
	okay1 'Found no more Errors/Warnings.'
	foundview = 0
	do while foundview = 0
		'previous view'
		status 84	/* windownumber */
		if result = view then foundview = 1
		end
	exit 0
	end

up
status 55	/* linebuffer */
line = result

number = subword(line,find(line,'Line')+1,1)

down
down
'end of line'

search for "^" 0 0 1 1
status 46	/* cursorcolumn */
coloumn = result + 1

up
up
'beg of line'

mark block
down

foundview = 0
do while foundview = 0
	'previous view'
	status 84	/* windownumber */
	if result = view then foundview = 1
	end

jumpto number coloumn
