;
;	$VER: Install-Barfly 1.0 (10.07.94)
;
;	Install script for Barfly
;	© Copyright 1994 by Ralph Schmidt
;	All Rights Reserved
;	


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;;  CheckObsoleteDir procedure
;;;
;;;  Asks the user if he wants an "obsolete" directory and creates it,
;;;  if this is the case. The name will be stored in installObsoleteDir
;;;  in that case, otherwise this variable will receive the value "".
;;;
;;;  Uses: installDir   name of installation directory
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
(set installObsoleteDirChecked 0)
(procedure CheckObsoleteDir

    (if (NOT installObsoleteDirChecked)
    (   (set installObsoleteDir (tackon installDir "obsolete"))
	(if (<> (exists installObsoleteDir) 2)
	(   (makedir installObsoleteDir
		(prompt "Creating a directory " installObsoleteDir " to keep "
			"old config files."
		)
		(help "Old user-editabe files may be prevented from "
		      "deletion by copying them to " installObsoleteDir
		      ". You may skip this part, if you don't want this.\n\n"
		      @makedir-help
		)
		(confirm 2)
		(infos)
	    )
	    (if (<> (exists installObsoleteDir) 2)
	    (   (set installObsoleteDir "")
	    ))
	))
	(set installObsoleteDirChecked 1)
    ))
)


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;;  CheckObsoleteFile procedure
;;;
;;;  Uses: checkObsoleteFileName    name of file to be copyied into the
;;;                                 obsolete directory (relative to
;;;                                 installDir)
;;;        installObsoleteDir       name of the "obsolete" directory
;;;        installDir               name of the installation directory
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
(procedure CheckObsoleteFile

    (if (exists checkObsoleteFileName)
    (   (CheckObsoleteDir)

	(if installObsoleteDir
	(   (copyfiles
		(prompt ("Preserving old file %s ..." checkObsoleteFileName))
		(help "The old file " checkObsoleteName " may be preserved "
		      "by copying it to " installObsoleteDir ". You may skip "
		      "this part, if you don't want this.\n\n"
		      @copyfiles-help
		)
		(source checkObsoleteFileName)
		(dest installObsoleteDir)
		(infos)
		(confirm 2)
		(optional "oknodelete" "force" "askuser")
	    )
	))
    ))
)


 
(set intro
(cat "\n Welcome to the installation of Barfly!\n\n"
     "© Copyright 1994 by Ralph Schmidt\n"
     "All Rights Reserved"
))



; Kickstart version test

(if (< (/ (getversion) 65536) 37)
(
 (abort "\n\n\nYou must be using at least Kickstart 2.04 (Version 37.175) !")
))

; here we go

(welcome intro)

; install Program

(set @default-dest "SYS:")

(set dest2 "SYS:")

(complete 0)

(set dest2 (askdir  (prompt "Select the location to install the Barfly directory")
                    (default @default-dest)
                    (help "Choose a destination partition or directory " 
                          "to contain the Barfly Development System."
                          "The install will create a directory called"
                          "Barfly at the location your choose and copy"
                          "the needed files to that directory"
                    )
            )
)

(set dest1 (tackon dest2 "Barfly"))
(set installDir dest1)


(set @default-dest dest1)
(message "Package will be installed in " 
          dest1
)

(if (= (exists @default-dest) 0)
    (makedir @default-dest)
)

(copyfiles (source "/Barfly.info")
           (dest   dest2)
)


(set n 0)
(while (set file (select n "s/BarFly/BARFLY.DebugConfig"
			   "s/BarFly/BARFLY.DefIcon.info"
			   "s/BarFly/BARFLY.FD"
			   "s/BarFly/BARFLY.FD_V2.04"
			   "s/BarFly/BARFLY.Include"
			   "s/Barfly/Barfly.Include_v37"
			   ""
       ))
(   (set checkObsoleteFileName (tackon dest1 file))
    (CheckObsoleteFile)
    (set n (+ n 1))
))


(copyfiles (source "")
           (dest   @default-dest)
           (all)
)

(copylib (source "libs/cachefile.library")
         (dest   "libs:")
)
(message "cachefile.library is used for the BASM cache functions and is copied to libs:")

(if (<> (exists "libs/barfly.library") 0)
        (copylib (source "libs/barfly.library")
                 (dest   "libs:")
        )
)

(if (<> (exists "libs/barfly.library") 0)
        (message "barfly.library is the key and was copied to libs:")
)


(message "Path will be added to the user-startup")

(set dest3 (cat "path " dest1 " add" "\n"))

(startup "Barfly Development System"
   (prompt ("%s\n%s\n\n%s\n%s\n"
            "The following path is needed."
            "Do you want the path added to your startup?"
            dest3
            "NOTE: The path will not be updated until you reboot."
            )
   )
   (command 
           ("") dest3
   )
   (help "The programs BAsm and BDebug need to be in the path"
         "to be found by the system"
         "If you request it, the installer will add the necessary"
         "path to your user-startup script, which is run when your"
         "machine boots.  If you decide not to allow the installer"
         "to make these changes, you will need to make them yourself."
   )
)

(message "env:BarflyPath will be created for the configuration files")

(textfile (dest "env:BarflyPath")
          (append dest1)
          (append "/s/Barfly")
)

(message "envarc:BarflyPath will be created for the configuration files")

(textfile (dest "envarc:BarflyPath")
          (append dest1)
          (append "/s/Barfly")
)


(message "BDebug-Desktop Screen is shown to demonstrate the Debuggers abilities")

(set dest4 (cat "sys:utilities/Multiview " dest1 "BDebug.iff" ))

(run dest4
   (prompt ("%s\n"
            "Do you wanna see a Desktop-Screen of the Debugger?"
            )
   )
)
