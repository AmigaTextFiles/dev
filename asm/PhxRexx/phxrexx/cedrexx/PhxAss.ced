/***************************************************************************\
**									   **
**  PhxAssemble 1.2 (11.11.96) for CygnusEd 3.5+			   **
**									   **
**  By Kenneth C. Nilsen (11.11.96)					   **
**									   **
**  (Based on some parts of BasmAssemble (v1.5) by Christian Rattei)	   **
**  Added and modified features to fit my own needs. Public Domain.	   **
**									   **
**									   **
\***************************************************************************/

/* Modify here:
----------------------------------------------------------------------------
   You can modify the string below to redirect output from binary output
   to f.ex nil: */

/* outputcon  = 'CON:0/30/640/220/Phx Output/CLOSE/WAIT'    redirect file */

outputcon  = 'NIL:'   /* redirect file */
defaultbin = 'ram:test'            /* default binary name to assemble to */

requesterbox	=	0	/* 1=on, 0=off */

/*--------------------------------------------------------------------------
 Here we go: */

DM "Phx Assembler script by Kenneth C. Nilsen"

Options results

LF = '0A'X

address "rexx_ced"

status 18
if result ~= 0 then save

status 19
filename = result

destfilename = defaultbin

Status LINEBUFFEROFFSET
LinePos=RESULT

'beg of file'
status 55
topfile=RESULT

if substr(topfile,2,1)=">" then destfilename=substr(topfile,3,length(topfile)-3)

'Jump to byte' LinePos

if requestbox = 1 then do
	if destfilename = defaultbin then do
		'GETSTRING "'defaultbin'" "Binary filename:"'
		destfilename = result
	end
end

/* Here we can 'cancel' assembling */

if result = "RESULT" then do
	DM
	exit 0
end

DM 'PhxAss:  Assembling "'destfilename'" ... please wait!'

/* Here we assemble our program: */
address command 'code:phxass >RAM:Phx_Dump from "'filename'" to "'destfilename'" m=68000 case opt ! errors=0 rc=0'

status 84
view = result

if exists('Ram:Phx_Dump') = 0 then do
	okay1 'Error: "Ram:Phx_Dump" not found!'
	exit 0
end

jump to file 'RAM:Phx_Dump'
if result = 0 then open new

open 'RAM:Phx_Dump'

/* We remove our resultfile since we don't need it anymore */

address command "delete ram:Phx_Dump >nil:"

/* Should we start the file ? */

If requesterbox = 1 then do

search for "line" 0 0 1 1	/* find the word "line" in result window */
errors = result

/* If ok we can ask user if he/she wants to start the assembled file.. */

if errors = 0 then do
	'GETSTRING "y" "Execute binary ?"'
	answer = result

	if answer = "y" then do
		address command 'run >nil: "'destfilename'" > "'outputcon'"'
		DM
		Exit 0
	end
end

if errors ~= 0 then okay1 "Assembler error!"LF||LF"Can't start file! Please correct"LF"the error(s) and try again."LF"-"

end

DM
exit 0
