(transcript "Installing Magic User Interface System...")


; **************************************************************************
; **************************************************************************
; **************************************************************************


(set @default-dest "SYS:")


; **************************************************************************
; **************************************************************************
; **************************************************************************


(copylib
	(help @copylib-help)
	(prompt "Installing muimaster.library...")
	(confirm)
	(source "Libs/muimaster.library")
	(dest
		(askdir
			(prompt "Please select a place for \"muimaster.library\".")
			(help @askdir-help)
			(default "LIBS:")
		)
	)
)


(complete 10)


; **************************************************************************
; **************************************************************************
; **************************************************************************


(set classdest
	(askdir
		(prompt "Please select a place for the classes. A sub drawer called \"MUI\" will be created here.")
		(help @askdir-help)
		(default "SYS:Classes")
		(newpath)
	)
)


(makedir classdest)

(makedir (tackon classdest "MUI"))

(if (exists (tackon classdest "MUI/application.mui"))
(
	(if (askbool (help @askbool-help) (prompt "Some old classes are obsolete and should be deleted. Do you want to delete them now?"))
	(
		(foreach (tackon classdest "MUI") "#?.mui"
			(delete
				(tackon (tackon classdest "MUI") @each-name)
				(prompt "Deleting obsolete classes...")
			)
		)
	)
	)
)
)


(foreach "Classes/MUI" "#?.mui"
	(copylib
		(prompt "Installing Classes...")
		(source (tackon "Classes/MUI" @each-name))
		(help @copylib-help)
		(dest (tackon classdest "MUI"))
	)
)


(complete 20)


; **************************************************************************
; **************************************************************************
; **************************************************************************


(copylib
	(prompt "Installing the preferences program...")
	(source "Prefs/MUI")
	(confirm)
	(infos)
	(noposition)
	(help @copylib-help)
	(dest
		(askdir
			(prompt "Please select a place for the preferences program.")
			(help @askdir-help)
			(default "SYS:Prefs")
			(newpath)
		)
	)
)


(complete 30)


; **************************************************************************
; **************************************************************************
; **************************************************************************

(if (exists "envarc:mui/-global.prefs")
(
	(if (askbool (help @askbool-help) (prompt "Your old preferences are obsolete and should be deleted. This will not affect window positions and application's private prefs. Do you want to delete the old config files now (recommended)?"))
	(
		(foreach "envarc:mui" "#?.prefs"
			(delete
				(tackon "envarc:mui" @each-name)
				(prompt "Deleting obsolete preferences files...")
			)
		)
	)
	)
)
)

(complete 40)


; **************************************************************************
; **************************************************************************
; **************************************************************************


(if (askbool (help @askbool-help) (prompt "Do you want to install the german language catalog for the preferences program?"))
(
	(copyfiles
		(prompt "Installing the german language catalog...")
		(source "Locale/Catalogs/Deutsch/MUI-Prefs.catalog")
		(help @copyfiles-help)
		(dest "locale:Catalogs/Deutsch/")
	)
)
)

(complete 50)


; **************************************************************************
; **************************************************************************
; **************************************************************************


(if (askbool (help @askbool-help) (prompt "Do you want to install the default AppIcon for MUI Programs?"))
(
	(copyfiles
		(prompt "Installing the AppIcon...")
		(source "Prefs/Env-Archive/Sys/def_MUI.info")
		(help @copyfiles-help)
		(dest "envarc:Sys/")
	)
)
)


(complete 60)


; **************************************************************************
; **************************************************************************
; **************************************************************************


(if (askbool (help @askbool-help) (prompt "Do you want to install the additional BOOPSI images?"))
(
	(set boopsidest
		(askdir
			(prompt "Please select a place for the BOOPSI images.")
			(help @askdir-help)
			(default (tackon classdest "Images"))
			(newpath)
		)
	)

	(foreach "Classes/Images" "#?.image"
		(copylib
			(prompt "Installing BOOPSI Images...")
			(source (tackon "Classes/Images" @each-name))
			(help @copylib-help)
			(dest boopsidest)
		)
	)
)
)


(complete 70)


; **************************************************************************
; **************************************************************************
; **************************************************************************


(if (askbool (help @askbool-help) (prompt "Installing the additional MUI brushes is recommended if you want to use the preferences builtin presets. If you change the path, you will ran into problems. Do you want to install them now?"))
(
	(copyfiles
		(prompt "Installing MUI brushes...")
		(help @copyfiles-help)
		(source "Prefs/MUI-Images")
		(all)
		(dest
			(askdir
				(prompt "Please select a place for the brushes.")
				(help @askdir-help)
				(default "sys:prefs/MUI-Images")
				(newpath)
			)
		)
	)
)
)


(complete 80)


; **************************************************************************
; **************************************************************************
; **************************************************************************


(if (askbool (help @askbool-help) (prompt "Do you want to install the english online help manual for the preferences program?"))
(
	(if (exists "libs:amigaguide.library")
	(
		(copyfiles
			(prompt "Installing the english online help...")
			(source "Docs/English/MUI.guide")
			(help @copyfiles-help)
			(dest
				(set manpath (askdir
					(prompt "Please select a place for the english online help.")
					(help @askdir-help)
					(default "HELP:english")
					(newpath)
				))
			)
		)
	)
   (
		(message "I couldn't locate \"amigaguide.libary\" on your system, you won't be able to use online help.")
	)
	)
)
)


(complete 90)


; **************************************************************************
; **************************************************************************
; **************************************************************************


(if (askbool (help @askbool-help) (prompt "Do you want to install the german online help manual for the preferences program?"))
(
	(if (exists "libs:amigaguide.library")
	(
		(copyfiles
			(prompt "Installing the german online help...")
			(source "Docs/Deutsch/MUI.guide")
			(help @copyfiles-help)
			(dest
				(askdir
					(prompt "Please select a place for the german online help.")
					(help @askdir-help)
					(default "HELP:deutsch")
					(newpath)
				)
			)
		)
	)
   (
		(message "I couldn't locate \"amigaguide.libary\" on your system, you won't be able to use online help.")
	)
	)
)
)


(complete 95)



; **************************************************************************
; **************************************************************************
; **************************************************************************


(set vernum (getversion "exec.library" (resident)))
(set ver (/ vernum 65536))

(if (< ver 39)
(
	(startup "MUI"
		(prompt "You are not running Kickstart 3.0 or higher, so I have to add some commands to your user-startup.")
		(help @startup-help)
		(command "assign add LIBS: \"" classdest "\"")
	)
)
)


(complete 100)


; **************************************************************************
; **************************************************************************
; **************************************************************************


(exit "Please reboot now!")
