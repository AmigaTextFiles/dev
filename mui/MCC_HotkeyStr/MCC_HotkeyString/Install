; An installer script for HotkeyString class.
;
; Written by Ilkka Lehtoranta in January 14th 2004

(set #def_module_68k	1)
(set #def_module_mos 2)
(set #def_module #def_module_68k)

(if (exists "MOSSYS:" (noreq))
	(set #def_module #def_module_mos)
)

(set module
	(askoptions
		(prompt	"Which versions you wish to install?")
		(choices	"AmigaOS 68k" "MorphOS")
		(default	#def_module)
		(help		@askoptions-help)
	)
)

(if (< module 1)
	(abort	"No files installed.")
)

(if (bitand module #def_module_68k)
	(
		(set dest_dir "MUI:Libs/MUI")

		(if (not (exists dest_dir (noreq)))
			(set dest_dir
				(askdir
					(default	dest_dir)
					(prompt	"Where to install HotkeyString 68k class?")
					(help		@askdir-help)
				)
			)
		)

		(copylib
			(confirm)
			(nogauge)
			(optional fail)
			(prompt	"Copying HotkeyString.mcc (68k)")
			(help		@copylib-help)
			(source	"AmigaOS-68k/Libs/MUI/HotkeyString.mcc")
			(dest		dest_dir)
		)
	)
)

(if (bitand module #def_module_mos)
	(
		(set dest_dir	"MOSSYS:Classes/MUI")

		(if (not (exists dest_dir (noreq)))
			(set dest_dir
				(askdir
					(default	dest_dir)
					(prompt	"Where to install HotkeyString MorphOS class?")
					(help		@askdir-help)
				)
			)
		)

		(copylib
			(confirm)
			(nogauge)
			(optional fail)
			(prompt	"Copying HotkeyString.mcc (MOS)")
			(help		@copylib-help)
			(source	"MorphOS/Classes/MUI/HotkeyString.mcc")
			(dest		dest_dir)
		)
	)
)
