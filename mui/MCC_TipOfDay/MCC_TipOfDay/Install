;******************************************************************************
;                                         
; $VER: 0.40 (23-July-1999)       
; TipOfTheDay ©1999 by Marcin Orlowski
; TipOfTheDay Installer by Robert C. Reiswig ©1999 
;
; If you wish to use any part of this installer you must ask. May not be 
; integrated/placed into any other package or any other MUI classes! 
; You may NOT change this in an way!
; Changes, suggestions or problems: pph@vgr.com or rob@vapor.com
;******************************************************************************

;*** System Stuff ***
(set cpu (database "cpu"))
(if (exists "libs:68060.library") (set #cpu 5)) (if (exists "libs:68060.library" (resident)) (set #cpu 5))
(if (= cpu 68000) (set #cpu 0)) (if (= cpu 68010) (set #cpu 1))
(if (= cpu 68020) (set #cpu 2)) (if (= cpu 68030) (set #cpu 3))
(if (= cpu 68040) (set #cpu 4)) 

(set mui_localdir "") (if (exists "Libs/MUI") (set mui_localdir "Libs/MUI"))
(if (exists "MUI") (set mui_localdir "MUI"))
(foreach mui_localdir "#?.mcc" (set muiclassguess @each-name))
(set howlong (strlen muiclassguess))
(set muiclass (substr muiclassguess 0 (- howlong 4) ))

(if (= muiclass "PowerHelp_nlist") (set muiclass "PowerHelp"))
(if (= muiclass "TipOfTheDaystr") (set muiclass "TipOfTheDay"))
(if (= muiclass "tipboard") (set muiclass "TipOfDay"))
(if (= muiclass "tipwindow") (set muiclass "TipOfDay"))
(set vernum1 (getversion (tackon mui_localdir (cat muiclass ".mcc")))) 
(set ver1 (/ vernum1 65536)) (set rev1 (- vernum1 (* ver1 65536) ) )
(set muiclassver (cat ver1 "." rev1))
(if (= "0.0" muiclassver) (set muiclassver ""))

;********************************************************************
; English Strings
;********************************************************************
(set #about (cat "\n\n\nWelcome to " muiclass " " muiclassver "\nMUI Class\n\nCopyright ©1999 by\n\nMarcin Orlowski\n<carlos@amiga.com.pl>\n") )
(set #whattodo (cat "\n\n\n\n\nThis installer will copy or update the " muiclass " classes in MUI:Libs/MUI if needed."))
(set #nomui (cat "\n\n" muiclass " uses Magic User Interface (MUI).\n\nThe installer has not found MUI: to be assigned, "
                 "therefore " muiclass " will not work correctly.  You will need to install "
                 "MUI before running " muiclass "."
            )
)
(set #updateinstaller "\nJust to let you know ESCOM AG has released a new version of the installer program.\n\n\nVersion 43.3 can be found on Aminet in:\n\n/util/misc/Installer-43_3.lha\n\n\nYou might want to upgrade soon, if you can. Older installers might fail.")
(set #updatingmuic "\nUpdating the MUI Classes if needed.\n\n(#?.mcc & #?.mcp)")
(set #newermui (cat "\n" muiclass " needs version 3.8 or higher of MUI installed to work correctly.\n\nNow would be a good time to Upgrade!\n\nYou can find the new MUI on Aminet in util/libs\n\nIt looks like you have: \n\n"))
(set #nompc (cat "\nThe .MCC files for " muiclass " can not be installed to 'mui:libs/mui'"))
(set #byebye (cat "\n" muiclass " Install Complete!\n\n" ))
(set #cpuver (cat "\nWhat version of " muiclass " would you like to be installed?"))
(set #nlclassY " The NList MUI classes was found in MUI:Libs/mui.")
(set #nlclassN " The NList MUI classes were not found in MUI:Libs/mui.")
(set #nlclass #nlclassY)

;********************************************************************************
;Polish Strings
;********************************************************************************
(if (= @language "polski")
 (
  (set #test "This is only a test")
 )
)
;********************************************************************
; Hello and check formui

;********************************************************************
(message #about)
(welcome) 
(message #whattodo)
(if (< (exists "mui:") 2)  (message #nomui) )

;********************************************************************
; Check on New installer 
;********************************************************************
(set InstallerVer 0) (set current_installer "43.3")
(set vernum1 @installer-version) (set ver1 (/ vernum1 65536)) 
(set rev1 (- vernum1 (* ver1 65536) ) ) (set InstallerVer (cat ver1 "." rev1))
(if (< InstallerVer current_installer) (message #updateinstaller) )
 
;********************************************************************
; Copy over #?.mpc|#?.mcc (Need to be in mui:libs/mui)
;********************************************************************
(if (> #cpu 1) (set guesscpu 1) (set guesscpu 0)) 

(if (<> (getassign "mui") "")
(
  (if (> (exists "mui:libs/mui") 0) 
   (

    (if (> (exists (tackon mui_localdir (cat muiclass ".mcc.020"))) 0)
      (set whatver (askchoice (choices  "68000" "68020")
                      (prompt #cpuver)
	              (help   #cpuver)
                      (default guesscpu)
	           )
      )
    )

    (if (exists "mui:libs/mui/NList.mcc") (set guessnl 1) (set #nlclass #nlclassN) )
    ;(if (> (exists (tackon mui_localdir (cat muiclass "_nlist.mcc"))) 0)
    ;  (set whatn   (askchoice (choices  "Normal version" "NList version")
    ;                  (prompt (cat #cpuver #nlclass "\n"))
    ;	              (help   (cat #cpuver #nlclass "\n"))
    ;                  (default guessnl)
    ;	           )
    ;  )
    ;)
    (if (= whatn 1) (set nlist "_nlist") (set nlist "")) 

    (working #updatingmuic)
    (foreach mui_localdir "#?.mcp"
      (if (AND (= whatver 1) (exists (cat (tackon mui_localdir @each-name) ".020") ) )
        (copylib (source (cat (tackon mui_localdir @each-name) ".020")) (dest "mui:libs/mui") (newname @each-name) (optional "nofail") ) 
        (copylib (source (tackon mui_localdir @each-name)) (dest "mui:libs/mui") (optional "nofail") ) 
      )
    )
    (foreach mui_localdir "#?.mcc"
       (if (AND (= whatver 1) (exists (cat (tackon mui_localdir @each-name) ".020") ) )
        (copylib (source (cat (tackon mui_localdir @each-name) ".020")) (dest "mui:libs/mui") (newname @each-name) (optional "nofail") ) 
        (copylib (source (tackon mui_localdir @each-name)) (dest "mui:libs/mui") (optional "nofail") ) 
       )  
    )
   )
   (message (cat #newermui muiver #nompc)) 
  )
)
)

;********************************************************************
; End Bye 
;********************************************************************
(if (> (exists ("c:multiview")) 0) (set #mpath "c:") )
(if (> (exists ("sys:Utilities/multiview")) 0) (set #mpath "sys:Utilities") )
(run (cat "run " #mpath "/multiview MCC_Popph.readme") )
(set @default-dest mui_localdir)
(exit #byebye (quiet))
