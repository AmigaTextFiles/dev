; $VER: Install_CPRFiX 1.1 (13.05.00) ® 2000 R.E.D.Group by Przemyslaw 'SENSEI' Gruchala

; Inicjalizacja

; Ustawienie wersji CPRFiX'a jaka instalujemy.
(set #cprfixversion  1)
(set #cprfixrevision 1)

; Ustawiamy nazwe wersji CPRFiX'a.
(set #cprfixversionname ("%ld.%ld" #cprfixversion #cprfixrevision))

; Ustawienie wersji installera CPRFiX'a.
(set #cprfixinstallversionname "1.1")
(set #cprfixinstalldate "13.05.00")

; Zapamietanie poziomu uzytkownika.
(set #userlevel @user-level)

; Domyslna wersja angielska.
(set #languagetemp 0)

; Ustawiamy pytanie o wersje jezykowa po angielsku.
(set #selectlanguage "Select your language...")

; Ustawiamy tekst powitania po angielsku.
(set #welcome
	(cat
		"\n\n\n\nWelcome to the CPRFiX v" #cprfixversionname " installation script.\n\n\n"
		"Copyright (c) 2000 by R.E.D.Group.\n\n\n"
		"Install_CPRFiX v" #cprfixinstallversionname " (" #cprfixinstalldate ") by Przemyslaw 'SENSEI' Gruchala.\n"
	)
)

; Ustawiamy wersje angielska.
(if
	(= @language "english")
	(set #languagetemp 0)
)

; Ustawiamy wersje niemiecka.
; German translation by Rainer "No.3" Müller. Thx!
(if
	(= @language "deutsch")
	(
		(set #languagetemp 1)

; Ustawiamy pytanie o wersje jezykowa po niemiecku.
		(set #selectlanguage "Wähle Sprache...")

; Ustawiamy tekst powitania po niemiecku.
		(set #welcome
			(cat
				"\n\n\n\nWillkommen zum CPRFiX v" #cprfixversionname " Installationsskript.\n\n\n"
				"Copyright (c) 2000 by R.E.D.Group.\n\n\n"
				"Install_CPRFiX v" #cprfixinstallversionname " (" #cprfixinstalldate ") by Przemyslaw 'SENSEI' Gruchala.\n"
			)
		)
	)
)

; Swedish translation made by Tommy Lindgren. Thx! ;)
; Ustawiamy wersje szwecka.
(if
	(= @language "svenska")
	(
		(set #languagetemp 2)

; Ustawiamy pytanie o wersje jezykowa po szwecku.
		(set #selectlanguage "Välj ditt språk...")

; Ustawiamy tekst powitania po szwecku.
		(set #welcome
			(cat
				"\n\n\n\nVälkommen till CPRFiX v" #cprfixversionname " installationsskriptet.\n\n\n"
				"Copyright (c) 2000 by R.E.D.Group.\n\n\n"
				"Install_CPRFiX v" #cprfixinstallversionname " (" #cprfixinstalldate ") av Przemyslaw 'SENSEI' Gruchala.\n"
			)
		)
	)
)

; Ustawiamy wersje polska.
(if
	(= @language "polski")
	(
		(set #languagetemp 3)

; Ustawiamy pytanie o wersje jezykowa po polsku.
		(set #selectlanguage "Wybierz swój jëzyk...")

; Ustawiamy tekst powitania do polsku.
		(set #welcome
			(cat
				"\n\n\n\nWitam w skrypcie instalacyjnym CPRFiX'a v" #cprfixversionname ".\n\n\n"
				"Copyright (c) 2000 by R.E.D.Group.\n\n\n"
				"Install_CPRFiX v" #cprfixinstallversionname " (" #cprfixinstalldate ") by Przemysîaw 'SENSEI' Gruchaîa.\n"
			)
		)
	)
)

; Poczatkowe powitanie.
(message #welcome (all))

(if
	(>= #userlevel 2)
	(

; Wybor jezyka w ktorym bedziemy informowani w installer'ze.
		(set #language
			(select
				(askchoice
					(prompt	#selectlanguage)
					(help		@askoptions-help)
					(choices
						"English"
						"Deutsch"
						"Svenska"
						"Polski"
					)
					(default #languagetemp)
				)
				"English"
				"Deutsch"
				"Svenska"
				"Polski"
			)
		)
	)
	(set #language @language)
)

; ???
(set #installermsg
	(cat
		"Install_CPRFiX script requires Installer V43+.\n"
	)
)

(set #cprfilenamemsg
	(cat
		"Select the filename and path of your copy of CPR..."
	)
)

(set #confirmcprfilenamemsg
	(cat
		"Is the filename and path to CPR correct?"
	)
)

(set #cprnotexistmsg
	(cat
		"Sorry! CPR not found...\n\n"
		"Please install SAS/C v6.58 before attempting to install CPRFiX.\n"
	)
)

(set #doyouwanttobackupcprmsg
	(cat
		"Do you want to backup the original CPR?"
	)
)

(set #cprbackupfilenamemsg
	(cat
		"Select a filename and path for the backup copy of CPR..."
	)
)

(set #cprbackupmsg
	(cat
		"Are you sure that you want to backup the original CPR?"
	)
)

(set #cprbackuphelpmsg
	(cat
		"Install_CPRFiX will make a backup of your original CPR, "
		"so if you find any bugs in this patch you will be able to "
		"return to your old CPR without doing a full reinstallation "
		"of SAS/C v6.58..."
	)
)

(set #exitmsg
	(cat
		"\n\n\n\n\nThanks for installing CPRFiX!\n"
	)
)

; Wersja niemiecka.
(if
	(= #language "Deutsch")
	(

		; ???
		(set #installermsg
			(cat
				"Install_CPRFiX Skript benötigt Installer V43+.\n"
			)
		)

		(set #cprfilenamemsg
			(cat
				"Wähle Dateiname und Pfad Deiner Kopie von CPR..."
			)
		)

		(set #confirmcprfilenamemsg
			(cat
				"Ist Dateiname und Pfad zu CPR korrekt?"
			)
		)

		(set #cprnotexistmsg
			(cat
				"Tut mir leid! Konnte CPR nicht finden...\n\n"
				"Bitte zuerst SAS/C v6.58 und danach CPRFiX installieren.\n"
			)
		)

		(set #doyouwanttobackupcprmsg
			(cat
				"Soll ein Backup des original CPR erstellt werden?"
			)
		)

		(set #cprbackupfilenamemsg
			(cat
				"Wähle Dateiname und Pfad für das Backup von CPR..."
			)
		)

		(set #cprbackupmsg
			(cat
				"Bist Du sicher, dass von dem original CPR ein Backup erstellt werden soll?"
			)
		)

		(set #cprbackuphelpmsg
			(cat
				"Install_CPRFiX erstellt ein Backup des original CPR. Wenn "
				"Du einen Fehler in diesem Patch findest, kannst Du leicht "
				"die original Version wiederherstellen, ohne SAS/C v6.58 "
				"komplett neu installieren zu müssen..."
			)
		)

		(set #exitmsg
			(cat
				"\n\n\n\n\nDanke für die Installation von CPRFiX!\n"
			)
		)

	)
)

; Wersja szwecka.
(if
	(= #language "Svenska")
	(

		; ???
		(set #installermsg
			(cat
				"Install_CPRFiX-skriptet kräver Installer V43+.\n"
			)
		)

		(set #cprfilenamemsg
			(cat
				"Välj filnamnet och sökvägen till CPR..."
			)
		)

		(set #confirmcprfilenamemsg
			(cat
				"Är filnamnet och sökvägen till CPR korrekt?"
			)
		)

		(set #cprnotexistmsg
			(cat
				"Ledsen! CPR hittades inte...\n\n"
				"Var god installera SAS/C v6.58 före du försöker installera CPRFiX.\n"
			)
		)

		(set #doyouwanttobackupcprmsg
			(cat
				"Vill göra en säkerthetskopia av original CPR?"
			)
		)

		(set #cprbackupfilenamemsg
			(cat
				"Välj filnamn och sökväg för säkerhetskopian av CPR..."
			)
		)

		(set #cprbackupmsg
			(cat
				"Är du säker på att du vill säkerhetskopiera original CPR?"
			)
		)

		(set #cprbackuphelpmsg
			(cat
				"Install_CPRFiX kommer att säkerhetskopiera din original CPR, "
				"så om du hittar några buggar kan du återvända till din gamla "
				"version av CPR utan att behöva göra en hel ominstallation"
				"av SAS/C v6.58..."
			)
		)

		(set #exitmsg
			(cat
				"\n\n\n\n\nTack för att du installerar CPRFiX!\n"
			)
		)

	)
)

; Wersja Polska.
(if
	(= #language "Polski")
	(

		; ???
		(set #installermsg
			(cat
				"Skrypt Install_CPRFiX wymaga Installer'a w wersji V43+.\n"
			)
		)

		(set #cprfilenamemsg
			(cat
				"Proszë wybraê nazwë i ôcieûkë dostëpu do CPR'a..."
			)
		)

		(set #confirmcprfilenamemsg
			(cat
				"Czy to jest poprawna nazwa i ôcieûka dostëpu do CPR'a?"
			)
		)

		(set #cprnotexistmsg
			(cat
				"Przykro mi! Nie znalazîem CPR'a...\n\n"
				"Proszë zainstalowaê SAS/C v6.58 przed próbâ instalacji CPRFiX'a.\n"
			)
		)

		(set #doyouwanttobackupcprmsg
			(cat
				"Czy mam zrobiê kopië zapasowâ oryginalnego CPR'a?"
			)
		)

		(set #cprbackupfilenamemsg
			(cat
				"Proszë wybraê nazwë i ôcieûkë dostëpu gdzie zostanie umieszczona kopia zapasowa CPR..."
			)
		)

		(set #cprbackupmsg
			(cat
				"Czy jesteô pewien, ûe chcesz kopië zapasowâ oryginalnego CPR'a?"
			)
		)

		(set #cprbackuphelpmsg
			(cat
				"Install_CPRFiX zrobi kopië zapasowâ oryginalnego CPR'a, "
				"dziëki czemu jeôli znajdziesz jakieô bîëdy w tej îatce "
				"bëdziesz mógî powróciê do starej wersji bez potrzeby peînego "
				"reinstalowania SAS/C v6.58..."
			)
		)

		(set #exitmsg
			(cat
				"\n\n\n\n\nDziëkujë za zainstalowanie CPRFiX'a!\n"
			)
		)

	)
)

; Wlasciwy skrypt instalacyjny.
(complete 0)

; ???
; Sprawdzenie wersji Installer'a. Wymagamy V42.9. Nie chcialo mi sie sprawdzac revision ;)
(if (< (/ @installer-version 65536) 43)
	(abort #installermsg)
)

(set #cprfilename "SC:c/cpr")

; Czy CPR znajduje sie w domyslnej sciezce dostepu?
(if
	(not
		(exists #cprfilename (noreq))
	)
	(

; CPR nie zostal odnaleziony...
; Pytamy sie o sciezke dostepu do CPR'a.
		(set #cprfilename
			(askfile
				(prompt	#cprfilenamemsg)
				(help		@askfile-help)
				(default	#cprfilename)
			)
		)

	)
	(

; Czy uzytkownik w trybie experta?
		(if
			(>= #userlevel 2)
			(

; Potwierdzamy sciezke dostepu do CPR'a.
				(set #cprfilename
					(askfile
						(prompt	#confirmcprfilenamemsg)
						(help		@askfile-help)
						(default	#cprfilename)
					)
				)

			)

		)

	)
)

(complete 20)

; Czy uzytkownik wskazal poprawna sciezke dostepu do CPR'a?
(if
	(not
		(exists #cprfilename (noreq))
	)
	(

; Jesli nie to wyswietlamy komunikat o bledzie i konczymy instalacje.
		(abort	#cprnotexistmsg)

	)
	(

; Czy uzytkownik chce zrobic backup'a oryginalnego CPR'a?
		(if
			(askbool
				(prompt	#doyouwanttobackupcprmsg)
				(help		@askbool-help)
			)
			(

; Pytamy sie pod jaka nazwa uzytkownik chce miec backup'a?
				(set #cprbackupfilename
					(askfile
						(prompt	#cprbackupfilenamemsg)
						(help		@askfile-help)
						(default
							(cat	#cprfilename ".original")
						)
					)
				)

				(complete 40)

; Robimy backup'a CPR'a.
				(copyfiles
					(prompt	#cprbackupmsg)
					(help		#cprbackuphelpmsg)
					(source	#cprfilename)
					(dest
						(pathonly	#cprbackupfilename)
					)
					(newname
						(fileonly	#cprbackupfilename)
					)
					(files)
					(infos)
					(noposition)
					(nogauge)
					(confirm		"expert")
					(optional	"askuser")
				)

				(complete 60)

				(protect	#cprbackupfilename "-d")

			)
		)

		(complete 70)

; Zakladamy latke na CPR'a.
		(run
			(cat
				"Bin/spatch -o" #cprfilename " -pPatch/cpr.pch " #cprfilename
			)
		)

		(complete 90)

; Skopiowanie sciezki do CPR'a jako standardowa.
		(set @default-dest #cprfilename)

	)
)

(complete 100)

(exit #exitmsg (quiet))
