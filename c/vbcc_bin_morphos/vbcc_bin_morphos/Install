; vbcc binaries installer
; written by Frank Wille <frank@phoenix.owl.de>

(if (< (/ @installer-version 65536) 43)
  (abort "You are running an old version of Installer.\n\n"
         "Please update to at least version 43, available on Aminet.")
)

(welcome)
(set #srcdir (pathonly @icon))


(if (exists "vbcc:" (noreq))
  (
    (set #vers (getversion "vbcc:vbcc_version"))
    (set #vbccver (/ #vers 65536))
    (set #vbccrev (- #vers (* #vbccver 65536)))
    (set #vbccmin (/ #vbccrev 100))
    (set #chrn (- #vbccrev (* #vbccmin 100)))
    (set #vbccchr (select #chrn "" "a" "b" "c" "d" "e" "f" "g" "h" "i" "j" "k" "l" "m" "n" "o" "p" "q" "r" "s" "t" "u" "v" "w" "x" "y" "z"))

    (if (> #vers 0)
      (
        (if
          (askbool
            (prompt ("vbcc V%ld.%ld%s is already installed.\nDo you want to update?" #vbccver #vbccmin #vbccchr))
            (help "The update will be done by overwriting, "
                  "which means that only the files belonging to the "
                  "vbcc development system will be updated. None of "
                  "your private data in vbcc: will be modified." )
            (choices "Proceed" "Cancel")
          )
          (set @default-dest "vbcc:")
          (exit "Nothing changed." (quiet))
	)
      )

      (
        (if (> @user-level 0)
          (if
            (askbool
              (prompt "An old vbcc version is already installed.\n"
                      "For an update I will need to remove the old "
                      "version first.")
              (help "This version comes with a different directory structure "
                    "and different assignments, which requires to delete the "
                    "old version completely before the new files can be copied.\n"
                    "Choose \"Uninstall now\" to let the installer do this for "
                    "you, otherwise you need to uninstall manually.\n"
                    "The installer will remove all known vbcc assignments, "
                    "delete the files in the former VBCC: directory and then "
                    "remove all vbcc-specific entries from your User-Startup." )
              (choices "Do it manually" "Uninstall now")
            )
            (exit "Please uninstall vbcc manually, and remove all the "
                  "assignments.\nThen restart the installer." (quiet))
          )
        )

        ; uninstall vbcc

        (set #vbccdir (expandpath "vbcc:"))
        (run "assign C: vbcc:bin remove")
        (makeassign "vbcc")
        (makeassign "vbccm68k")
        (makeassign "vbccppc")
        (makeassign "vbccwos")
        (makeassign "vbccmos")
        (makeassign "vbccos4")
        (makeassign "vbccos3")
        (makeassign "vbccos1")
        (makeassign "vbccpup")
        (makeassign "vincludem68k")
        (makeassign "vincludeppc")
        (makeassign "vincludewos")
        (makeassign "vincludemos")
        (makeassign "vincludeos4")
        (makeassign "vincludeos3")
        (makeassign "vincludepup")
        (makeassign "ixinclude")
        (makeassign "vlibm68k")
        (makeassign "vlibppc")
        (makeassign "vlibwos")
        (makeassign "vlibmos")
        (makeassign "vlibos4")
        (makeassign "vlibos3")
        (makeassign "vlibos1")
        (makeassign "vlibpup")

        (delete #vbccdir
          (prompt ("Deleting everything in %s." #vbccdir))
          (help "This will delete everything in your old VBCC: directory.\n"
                "Make sure you don't have any private data left in it.")
          (confirm)
          (optional "force")
          (all)
        )

        (if (= @pretend 0)
          (if (= (run (tackon #srcdir "clean_startup SYS:S/User-Startup T:us.tmp")) 0)
            (copyfiles
              (source "T:us.tmp")
              (dest "SYS:S")
              (newname "User-Startup")
              (nogauge)
              (optional "force" "askuser" )
            )
            (exit "Error while running clean_startup.\n"
                  "You might try to uninstall vbcc manually." (quiet))
          )
        )
      )
    )
  )
)


(if (<> @default-dest "vbcc:")
  (
    (set @default-dest
      (tackon
        (askdir
          (prompt "Where do you want to have vbcc installed?\n"
                  "A directory called \"vbcc\" will be created there.")
          (help "A new directory will be created to hold all files "
                "belonging to the vbcc development system.\n"
                "The required assigns will be added to your User-Startup later.")
          (default @default-dest)
          (disk)
        )
        "vbcc"
      )
    )
    (makedir @default-dest)
  )
)

(set #vbccdir (expandpath @default-dest))

(copyfiles
  (prompt "Copying vbcc binaries.")
  (help @copyfiles-help)
  (source "bin")
  (dest (tackon @default-dest "bin"))
  (optional "force")
  (all)
)

(copyfiles
  (prompt "Copying default configuration.")
  (help @copyfiles-help)
  (source "config")
  (dest (tackon @default-dest "config"))
  (optional "force")
  (all)
)

(copyfiles
  (prompt "Copying documentation.")
  (help @copyfiles-help)
  (source "doc")
  (dest (tackon @default-dest "doc"))
  (optional "force")
  (all)
)

(copyfiles
  (prompt "Copying vbcc_version.")
  (help @copyfiles-help)
  (source "vbcc_version")
  (dest @default-dest)
  (optional "force")
)

(startup @app-name
  (prompt "Performing changes to User-Startup.")
  (help "The following commands will be added to your "
        "SYS:S/User-Startup file:\n"
        "1. An assignment \"vbcc:\".\n"
        "2. \"vbcc:bin\" as an addtional C: assignment.\n"
        "3. The environment variable VBCC is set to \"vbcc:\".")
  (confirm)
  (command (cat "assign >NIL: vbcc: " #vbccdir "\n"))
  (command "assign >NIL: C: vbcc:bin ADD\n")
  (command "setenv VBCC vbcc:")
)

(if (<> @default-dest "vbcc:")
  (
    (makeassign "vbcc" @default-dest)
    (run "assign C: vbcc:bin add")
    (run "setenv VBCC vbcc:")
  )
)
