/*
**  $VER: MakeCHeader 1.002 (17 Sep 1995)  **
**
**  © 1995 Stefan Berendes
**
**  PROGRAMNAME:
**      MakeCHeader
**
**  FUNCTION:
**      Create a header on C programs
**      This style is required by some of my scripts!
**
**  $HISTORY:
**
**  17 Sep 1995 : 001.002 :  generates include and prototypes areas
**  19 Jun 1995 : 001.001 : adapted
**
**  based on 1994 François Helsen
*/

OPTIONS RESULTS                             /* enable return codes     */


if (LEFT(ADDRESS(), 6) ~= "GOLDED") then    /* not started by GoldEd ? */
    address 'GOLDED.DICE'

'LOCK CURRENT'                              /* lock GUI, gain access   */
OPTIONS FAILAT 6                            /* ignore warnings         */
SIGNAL ON SYNTAX                            /* ensure clean exit       */

/* ------------------------- INSERT YOUR CODE HERE: ------------------ */

'QUERY CAT'

if (RESULT = "deutsch") then do

    ASKNAME    = "Ihr Name"
    ASKPRGNAME = "Programmname (mit Suffix):"
end
else do

    ASKNAME    = "Type in your name"
    ASKPRGNAME = "Program name (including suffix):"
end

USERNAME = ""
'QUERY VERSION'

IF (RESULT >= 991) THEN
    'QUERY USER VAR=USERNAME'

IF (USERNAME = "") THEN DO
    IF Open('namefile', "Env:USERNAME", 'READ') THEN
        USERNAME = ReadLn('namefile')
    ELSE DO
        'REQUEST STRING TITLE="' || ASKNAME || '" VAR=USERNAME'
    END
END


'GOTO TOP'
'FIRST'

'REQUEST TITLE "' || ASKPRGNAME || '" STRING VAR PROGNAME'

IF rc ~= 5 THEN DO

'NAME NEW=daten:programs/' || PROGNAME || ''

'INSERT LINE'
'TEXT CR T="//"'
'TEXT CR T="//  $VER: ' || PROGNAME || ' 0.001 (' || Date() || ')  ****"'
'TEXT CR T="//"'
'TEXT CR T="//  © ' || Right(Date(), 4, ' ') || ' ' || USERNAME || '"'
'TEXT CR T="//"'
'TEXT CR T="//  PROGRAMNAME: ' || PROGNAME || '"'
'TEXT CR T="//"'
'TEXT CR T="//  FUNCTION:"'
'TEXT CR T="//"'
'TEXT CR T="//  $HISTORY:"'
'TEXT CR T="//"'
'TEXT CR T="//   ' || Date() || ' : 0.001 : initial release"'
'TEXT CR T="//"'
'CR'

'QUERY FOLDA VAR FOLDSTART'
'QUERY FOLDB VAR FOLDEND'

'TEXT CR T="' || FOLDSTART || ' $INCLUDES:"'
'CR'
'CR'
'TEXT CR T="#define Prototype extern"'
'TEXT CR T="#define Local     static"'
'CR'
'TEXT CR T="' || FOLDEND || ' "'

'TEXT CR T="' || FOLDSTART || ' $PROTOTYPES:"'
'CR'
'CR'
'TEXT CR T="' || FOLDEND || ' "'
'CR'
'DOWN'

'FOLD ALL OPEN=FALSE'

'DOWN'
'DOWN'

END

/* ---------------------------- END OF YOUR CODE ----------------------- */

'UNLOCK' /* VERY important: unlock GUI */
EXIT

SYNTAX:

SAY "Sorry, error line" SIGL ":" ERRORTEXT(RC) ":-("
'UNLOCK'
EXIT


