/*
**  $VER: MakeHeader 1.001 (20 Sep 1995)
**
**  © 1995 Stefan Berendes
**
**  PROGRAMNAME:
**      MakeHeader
**
**  FUNCTION:
**      Create a header on ARexx and C programs
**
**  $HISTORY:
**
**  20 Jun 1995 : 001.001 : adapted
**
**  based on 1994 François Helsen
*/

OPTIONS RESULTS                             /* enable return codes     */

if (LEFT(ADDRESS(), 6) ~= "GOLDED") then    /* not started by GoldEd ? */
    address 'GOLDED.DICE'

'LOCK CURRENT'                              /* lock GUI, gain access   */
OPTIONS FAILAT 6                            /* ignore warnings         */
SIGNAL ON SYNTAX                            /* ensure clean exit       */

/* ------------------------- INSERT YOUR CODE HERE: ------------------ */

'QUERY CAT'

if (RESULT = "deutsch") then do

    ASKNAME    = "Ihr Name"
    ASKPRGNAME = "Programmname (mit Suffix):"
    ASKCOMMENT = "Funktion des Programms:"
end
else do

    ASKNAME    = "Type in your name"
    ASKPRGNAME = "Program name (including suffix):"
    ASKCOMMENT = "Function:"
end

USERNAME = ""
'QUERY VERSION'

IF (RESULT >= 991) THEN
    'QUERY USER VAR=USERNAME'

IF (USERNAME = "") THEN DO
    IF Open('namefile', "Env:USERNAME", 'READ') THEN
        USERNAME = ReadLn('namefile')
    ELSE DO
        'REQUEST STRING TITLE="' || ASKNAME || '" VAR=USERNAME'
    END
END


'GOTO TOP'
'FIRST'

'REQUEST TITLE "' || ASKPRGNAME || '" STRING VAR PROGNAME'

IF rc = 0 THEN DO

    'REQUEST TITLE "' || ASKCOMMENT || '" STRING VAR COMMENT'

    IF rc = 0 THEN DO

        'NAME NEW=GoldED:ARexx/' || PROGNAME || ''

        'INSERT LINE'
        'TEXT CR T="/**"'
        'TEXT CR T="****  $VER: ' || PROGNAME || ' 0.001 (' || Date() || ')"'
        'TEXT CR T="****"'
        'TEXT CR T="****  © ' || Right(Date(), 4, ' ') || ' ' || USERNAME || '"'
        'TEXT CR T="****"'
        'TEXT CR T="****  PROGRAMNAME:  ' || PROGNAME || '"'
        'TEXT CR T="****"'
        'TEXT CR T="****  FUNCTION:     ' || COMMENT || '"'
        'TEXT CR T="****"'
        'TEXT CR T="****  $HISTORY:"'
        'TEXT CR T="****"'
        'TEXT CR T="****   ' || Date() || ' : 000.001 : initial release"'
        'TEXT CR T="**/"'
        'CR'

        'DOWN'
        'DOWN'
    END
END

/* ---------------------------- END OF YOUR CODE ----------------------- */

'UNLOCK' /* VERY important: unlock GUI */
EXIT

SYNTAX:

SAY "Sorry, error line" SIGL ":" ERRORTEXT(RC) ":-("
'UNLOCK'
EXIT


