/*
**  $VER: runexe.ged 0.001 (20 Sep 1995)
**
**  © 1995 Stefan Berendes
**
**  PROGRAMNAME: runexe.ged
**
**  FUNCTION:   starts the program with the same name as the current srcfile
**
**  $HISTORY:
**
**   20 Sep 1995 : 0.001 : initial release, based on my dcc.ged
*/


GOLDEDNAME = "GOLDED.DICE"                  /* Adjust this if you want */

OPTIONS RESULTS                             /* enable return codes     */

if (LEFT(ADDRESS(), 6) ~= "GOLDED") then    /* not started by GoldEd ? */
    address GOLDEDNAME

'LOCK CURRENT'                              /* lock GUI, gain access   */


OPTIONS FAILAT 6                            /* ignore warnings         */
SIGNAL ON SYNTAX                            /* ensure clean exit       */

/* ------------------------- INSERT YOUR CODE HERE: ------------------ */

'QUERY CAT'
IF result = "deutsch" THEN DO
    TXTEXTENS = "Dateiname muﬂ eine Endung besitzen!"
    ASKOPTIONS = "Befehlsoptionen: "
    TXTNOTEXT = "Kein Text vorhanden?"
END
else DO
    TXTEXTENS = "Filename should have an extension"
    ASKOPTIONS = "Commandline options: "
    TXTNOTEXT = "No text?"
END

'QUERY ANYTEXT'

if (result = 'TRUE') then do
    'QUERY DOC VAR OLDNAME'             /* remember current file name */
    'QUERY FILE VAR FILENAME'           /* Let GoldED work for us     */
    'QUERY PATH VAR PATH'

    dotposfile = LASTPOS(".", FILENAME) /* gives some security */

    if (dotposfile = 0) THEN DO

        'REQUEST STATUS ' || TXTEXTENS

        'UNLOCK'
        EXIT 5
    END

    BASENAME = LEFT( FILENAME, dotposfile-1)  /* name without extension */

    ADDRESS COMMAND                 /* address shell */

    PRAGMA('D', PATH)               /* set shell current directory */

    ADDRESS                         /* Address GoldED */

    'REQUEST TITLE=' || ASKOPTIONS || 'BODY="'PATH || ': ' || BASENAME '" BUTTON="_OK|_Cancel" STRING VAR OPTIONS'
    IF options ~= "OPTIONS" THEN DO

        ADDRESS     /* Address shell */
        BASENAME || ' <>"con:0/200//240/'|| BASENAME ||'/CLOSE/WAIT/SCREEN ' || GOLDEDNAME'" ' || OPTIONS
            /* Yeah, we now have output AND input !!! */

        ADDRESS     /* Address GOLDED */
    END
end

else do

    'REQUEST BODY=' || TXTNOTEXT
end

/* ---------------------------- END OF YOUR CODE ----------------------- */

'UNLOCK' /* VERY important: unlock GUI */
EXIT

SYNTAX:

SAY "Sorry, error line" SIGL ":" ERRORTEXT(RC) ":-("
'UNLOCK'
EXIT
