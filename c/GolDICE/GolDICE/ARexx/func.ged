/*
**  $VER: func.ged 1.002 (20 Sep 1995)  **
**
**  © 1993, 1995 Dietmar Eilert & Stefan Berendes
**
**  PROGRAMNAME:
**      func.ged
**
**  FUNCTION:
**      Insert empty function body
**
**  ARGUMENTS:
**      doComment [BOOL] to add a comment before the function declaration - def FALSE
**      doFold [BOOL] to add foldmarks araound the function body - def TRUE
**      doProto [BOOL] to add a function Prototype - def TRUE
**
**  $HISTORY:
**
**  20 Sep 1995 : 001.002 :  added arguments doFold and doProto
**  19 Sep 1995 : 1.001 : first documented version
**
**  based on func.ged ©1993 Dietmar Eilert. Insert empty function body
**
*/


OPTIONS RESULTS                             /* enable return codes     */
arg doComment doFold doProto                /* set to TRUE/FALSE       */

IF doComment = "" THEN                      /* set default values      */
    doComment = FALSE
IF doFold = "" THEN
    doFold = TRUE
IF doProto = "" THEN
    doProto = TRUE

OPTIONS RESULTS                             /* enable return codes     */

if (LEFT(ADDRESS(), 6) ~= "GOLDED") then    /* not started by GoldEd ? */
    address 'GOLDED.DICE'

'LOCK CURRENT'                              /* lock GUI, gain access   */
OPTIONS FAILAT 6                            /* ignore warnings         */
SIGNAL ON SYNTAX                            /* ensure clean exit       */

/* ------------------------- INSERT YOUR CODE HERE: ------------------ */

'QUERY CAT'

if (RESULT = "deutsch") then do

    ASKNAME = "Funktionsname  ?"
    ASKTYPE = "Funktionstyp"
    FTYPES  = "_void|_int|_char **|_UWORD|_anderer"
    COMMENT = "Kommentar  ?"
    REFER   = "Referenzieren"
    RTYPES  = "_Global|_Lokal|_Nicht"
end
else do

    ASKNAME = "function name (e.g. main) ?" 
    ASKTYPE = "function type"
    FTYPES  = "_void|_int|_char **|_UWORD|_other"
    COMMENT = " Comment ?"
    REFER   = "Reference"
    RTYPES  = "_local|_global|_none"
end

'REQUEST TITLE "' || ASKNAME || '" STRING VAR NAME'

if (RC = 0) then do

    'REQUEST BODY="' || ASKTYPE || " " || NAME || '() ?" BUTTON="' || FTYPES || '"'

    if (RC = 0) then do

        if (RESULT = 1) then
            TYPE = "void  "

        else if (RESULT = 2) then
            TYPE = "int   "

        else if (RESULT = 3) then
            TYPE = "char *"

        else if (RESULT = 4) then
            TYPE = "UWORD "

        else do
            'REQUEST TITLE "' || ASKTYPE || ' ?" STRING VAR TYPE'
            TYPE = TYPE || " "
        end

        /* replace single '*' by '**' (required by parser) */

        'FIX VAR=TYPE'

        IF doProto = TRUE THEN DO
            'REQUEST BODY="' || REFER || " " || NAME || '() ?" BUTTON="' || RTYPES || '"'

            if (RC ~= 0) then do
                'UNLOCK'
                EXIT
            end

            if (RESULT = 1) then
                PROTO = "Prototype "
            else if (RESULT = 2) then
                PROTO = "Local     "
            else
                PROTO = ""
        end
        else
            PROTO = ""

        'REQUEST TITLE "' || COMMENT || '" STRING VAR COMM'

        if (RC = 0) then do

            'QUERY FOLDA VAR FOLDSTART'
            'QUERY FOLDB VAR FOLDEND'

            'INSERT LINE'

            if (doComment = 'TRUE') then do

                HEAD  = '// ' || CENTER(' ' || NAME || ' ', 77, '-')

                'CR'
                'FIRST'
                'TEXT CR T="' || LEFT(HEAD, 81) || '"'
            end

            'FIRST'
            'CR'
            'TEXT CR T="' || TYPE || '"'
            'TEXT CR T="' || NAME || '()"'
            'FIRST'

            IF doFold = TRUE THEN DO
                'TEXT T="' || FOLDSTART || ' "'
                'TEXT T="**  ' || COMM || '  **"'
                'CR'
            END
            else IF COMM ~= "" THEN DO
                'TEXT T="/**  ' || COMM || '  **/"'
                'CR'
            end

            'TEXT CR T={'
            'FIRST'                                  
            'TAB'
            'PING SLOT 0'
            insertedline = FALSE
            'FIRST'
            'DOWN'
            'TEXT T=}'

            IF doFold = TRUE THEN DO
                'CR'
                'FIRST'
                'TEXT CR T="' || FOLDEND || ' end of ' || NAME || '()"'
                'CR'
            end
            else do
                'TEXT CR T=" /** end of ' || NAME || '() **/"'
                'CR'
            end

            if ( PROTO ~= "") then do
                'FIND FIRST STRING="$PROTOTYPES:"'

                if (RESULT ~= 0) then do
                    'FOLD OPEN=TRUE'
                    'DOWN'
                    'DOWN'
                    'INSERT LINE'
                    'FIRST'
                    'TEXT T="' || PROTO || TYPE || NAME || ' ( );"'
                    'FOLD OPEN=FALSE'
                    insertedline = TRUE
                end
            end
            'PONG SLOT 0'
            IF insertedline = TRUE THEN
                'DOWN'
        end
    end

end


'UNLOCK' /* VERY important: unlock GUI */
EXIT

SYNTAX:

SAY "Sorry, error line" SIGL ":" ERRORTEXT(RC) ":-("
'UNLOCK'
EXIT

