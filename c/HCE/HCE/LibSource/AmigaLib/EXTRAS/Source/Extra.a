* Extra stuff needed for Amiga.lib - (Modified J.P)

	INCLUDE "exec/types.i"
	INCLUDE	"exec/lists.i"
        INCLUDE "exec/io.i"

	SECTION Extra,CODE

	XDEF	_AbsExecBase
*
* AbsExecBase:	tell where to find it.
*

_AbsExecBase	EQU	4

*
* BeginIO(IORequest)
*

_BeginIO:  move.l 4(sp),a1
           move.l a6,-(a7)
           move.l IO_DEVICE(a1),a6
           jsr    DEV_BEGINIO(a6)
           move.l (a7)+,a6
           rts
*
* NewList(list) - initialize a new list.
*

_NewList:  move.l 4(sp),a0
           move.l a0,d0
           clr.l  LH_TAIL(a0)
           move.l a0,LH_TAILPRED(a0)
           addq.l #LH_TAIL,a0
           move.l a0,-(a0)
	   rts

           XDEF _BeginIO
	   XDEF	_NewList
           END
