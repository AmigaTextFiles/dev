; $VER: TWiMUI Install 2.0, Copyright ©1997 Thomas Wilhelmi

; ------------------------------ English strings -------------------------------

(if (not (= @language "deutsch"))

    (
        (set #twimui_introduction (cat "\n"

            "TWiMUI 2.0 · MUI C++-Class-Library                 \n"
            "©1997 Thomas Wilhelmi                              \n"
            "All Rights Reserved.                               \n"
            "                                                   \n"
            "The demo-version of TWiMUI is freely distributable \n"
            "as long as you don't change the contents of the    \n"
            "distribution-archive.                              \n"
        ))

        (set #twimui_complete "\nInstallation complete.")

        (set #twimui_where_include (cat

            "Select a directory where to install the           \n"
            "Header-Files. The directory 'Classes' and all     \n"
            "subdirectories are created if they do not exist.  "
        ))

        (set #twimui_where_include_help (cat

            " Simply select a directory (a root directory is   \n"
            " fine, too). The directories                      \n"
            "                                                  \n"
            "              'Classes/Datastructures'            \n"
            "              'Classes/Exceptions'                \n"
            "              'Classes/Exec'                      \n"
            "              'Classes/TWiMUI'                    \n"
            "              'Classes/Utility'                   \n"
            "                                                  \n"
            " are created if they do not exist.                "
        ))

        (set #twimui_where_lib (cat

            "Select a directory where to install the           \n"
            "Linker-Libs.                                      \n"
        ))

        (set #twimui_where_lib_help (cat

            " Simply select a directory (a root directory is   \n"
            " fine, too).                                      "
        ))

        (set #twimui_where_other (cat

            "Select a directory where to install TWiMUI. The   \n"
            "directory 'TWiMUI is created if it does not exist."
        ))

        (set #twimui_where_other_help (cat

            " Simply select a directory (a root directory is   \n"
            " fine, too). The directory                        \n"
            "                                                  \n"
            "                  'TWiMUI'                        \n"
            "                                                  \n"
            " is created in the path you specify.              "
        ))

        (set #twimui_comp_prompt (cat

            "Wich compiler are you using?"
        ))

        (set #twimui_comp_prompt_help (cat

            " Because there are more than one C++-Compiler     \n"
            " TWiMUI can be installed for different compiler.  \n"
        ))

        (set #twimui_comp_choice_0 "StormC/C++")
        (set #twimui_comp_choice_1 "Maxon C++")

        (set #twimui_lib_prompt (cat

            "Wich version of Link-Lib should be installed?     "
        ))

        (set #twimui_lib_prompt_help (cat

            " StormC allows merging the Link-Libraries of      \n"
            " different Data-models to one Link-Library. You   \n"
            " may install the merged version of the library    \n"
            " (one big file), the divided version (three small \n"
            " files) or both versions.                         "
        ))

        (set #twimui_lib_choice_0 "One big library")
        (set #twimui_lib_choice_1 "Three small libraries")
        (set #twimui_lib_choice_2 "Both versions")

    )
)

; -------------------------------- German strings ------------------------------

(if (= @language "deutsch")

    (
        (set #twimui_introduction (cat

            "TWiMUI 2.0 · MUI C++-Klassen-Bibliothek            \n"
            "©1997 Thomas Wilhelmi                              \n"
            "Alle Rechte vorbehalten.                           \n"
            "                                                   \n"
            "Die Demo-Version von TWiMUI ist frei vertreibbar   \n"
            "solange der Inhalt des Auslieferungs-Archiv nicht  \n"
            "verändert wird.                                    "
        ))

        (set #twimui_complete (cat "\nInstallation abgeschlossen."))

        (set #twimui_where_include (cat

            "Wählen Sie ein Verzeichnis für die Header-Dateien. \n"
            "In dem angegebenen Verzeichnis wird ein Verzeichnis\n"
            "'Classes' und alle Unterverzeichnisse erzeugt.     "
        ))

        (set #twimui_where_include_help (cat

            " Wählen Sie ein beliebiges Verzeichnis. Hauptver- \n"
            " zeichnisse wie DH0: können auch benutzt werden.  \n"
            " In dem angegebenen Verzeichnis werden die        \n"
            " Verzeichnisse                                    \n"
            "                                                  \n"
            "             'Classes/Datastructures'             \n"
            "             'Classes/Exceptions'                 \n"
            "             'Classes/Exec'                       \n"
            "             'Classes/TWiMUI'                     \n"
            "             'Classes/Utility'                    \n"
            "                                                  \n"
            " erzeugt wenn sie noch nicht existieren.          "
        ))

        (set #twimui_where_lib (cat

            "Wählen Sie ein Verzeichnis für die Linker-         \n"
            "Bibliotheken.                                      "
        ))

        (set #twimui_where_lib_help (cat

            " Wählen Sie ein beliebiges Verzeichnis. Hauptver- \n"
            " zeichnisse wie DH0: können auch benutzt werden.  "
        ))

        (set #twimui_where_other (cat

            "Wählen Sie einen Pfad für die Installation von     \n"
            "TWiMUI. In dem angegebenen Pfad wird der Pfad      \n"
            "'TWiMUI' erzeugt.                                  "
        ))

        (set #twimui_where_other_help (cat

            " Wählen Sie ein beliebiges Verzeichnis. Hauptver- \n"
            " zeichnisse wie DH0: können auch benutzt werden.  \n"
            " In dem angegebenen Verzeichnis wird das Verzeich-\n"
            " nis                                              \n"
            "                                                  \n"
            "                'TWiMUI'                          \n"
            "                                                  \n"
            " erzeugt wenn es noch nicht existiert.            "
        ))

        (set #twimui_comp_prompt (cat

            "Für welchen Compiler wird installiert?"
        ))

        (set #twimui_comp_prompt_help (cat

            " Da es mehrere C++-Compiler für den Amiga gibt    \n"
            " kann TWiMUI auch für verschiedene Compiler       \n"
            " installiert werden.                              \n"
        ))

        (set #twimui_comp_choice_0 "StormC/C++")
        (set #twimui_comp_choice_1 "Maxon C++")

        (set #twimui_lib_prompt (cat

            "Welche Version der Link-Bibliothek soll installiert werden?"
        ))

        (set #twimui_lib_prompt_help (cat

            " StormC erlaubt es Linker-Bibliotheken für ver-   \n"
            " schiedene Data-Modelle zu einer Bibliothek zu-   \n"
            " sammen zu mischen. Sie haben hier die Möglichkeit\n"
            " entweder die zusammengemischte Version (eine     \n"
            " große Bibliothek), die getrennte Version (drei   \n"
            " kleine Bibliotheken) oder beide Versionen zu     \n"
            " installieren.                                    \n"
        ))

        (set #twimui_lib_choice_0 "Eine große Bibliothek")
        (set #twimui_lib_choice_1 "Drei kleine Bibliotheken")
        (set #twimui_lib_choice_2 "Beide Versionen")

    )
)

; ----------------------------- Welcome user -----------------------------------

(message #twimui_introduction)

(welcome)

(set #twimui_path_other
    (askdir

        (prompt #twimui_where_other)
        (help   #twimui_where_other_help)

        (default "Work:")
    )
)

(set #twimui_path_include
    (askdir

        (prompt #twimui_where_include)
        (help   #twimui_where_include_help)

        (default "StormC:Include")
    )
)

(set #twimui_path_lib
    (askdir

        (prompt #twimui_where_lib)
        (help   #twimui_where_lib_help)

        (default "StormC:Lib")
    )
)

(set #twimui_comp_choice
    (askchoice
        (prompt #twimui_comp_prompt)
        (help #twimui_comp_prompt_help)
        (choices #twimui_comp_choice_0 #twimui_comp_choice_1)
    )
)

(set #twimui_lib_choice
    (askchoice
        (prompt #twimui_lib_prompt)
        (help #twimui_lib_prompt_help)
        (choices #twimui_lib_choice_0 #twimui_lib_choice_1 #twimui_lib_choice_2)
    )
)

(set #twimui_path (tackon #twimui_path_other "TWiMUI"))

(if (= (exists #twimui_path) 0)
    (makedir #twimui_path
        (infos)
    )
)

(set #twimui_path_demo (tackon #twimui_path "Demos"))

(if (= (exists #twimui_path_demo) 0)
    (makedir #twimui_path_demo
        (infos)
    )
)

(set #twimui_path_docs (tackon #twimui_path "Docs"))

(if (= (exists #twimui_path_docs) 0)
    (makedir #twimui_path_docs
        (infos)
    )
)

(set #twimui_include1 (tackon #twimui_path_include "TWiClasses"))

(if (= (exists #twimui_include1) 0)
    (makedir #twimui_include1)
)

(if (= #twimui_comp_choice 0)
    (
    (set #twimui_include2 (tackon #twimui_path_include "Classes"))
    (if (= (exists #twimui_include2) 0)
        (makedir #twimui_include2)
    )
    ))

(set #twimui_lib (cat #twimui_path_lib))

(copyfiles
    (source "Demos")
    (dest #twimui_path_demo)
    (all)
)

(copyfiles
    (source "Docs")
    (dest #twimui_path_docs)
    (all)
)

(copyfiles
    (source "History.txt")
    (dest #twimui_path)
)

(copyfiles
    (source "History.txt.info")
    (dest #twimui_path)
)

(copyfiles
    (source "LiesMich")
    (dest #twimui_path)
)

(copyfiles
    (source "LiesMich.info")
    (dest #twimui_path)
)

(copyfiles
    (source "Readme")
    (dest #twimui_path)
)

(copyfiles
    (source "Readme.info")
    (dest #twimui_path)
)

(copyfiles
    (source "Include/TWiClasses")
    (dest #twimui_include1)
    (all)
)

(if (= #twimui_comp_choice 0)
    (copyfiles
        (source "Include/Classes")
        (dest #twimui_include2)
        (all)
    )
    )

(if (not (= #twimui_lib_choice 0))
    (copyfiles
        (source "Lib")
        (dest #twimui_lib)
        (all)
    )
)

(if (not (= #twimui_lib_choice 1))
    (
    (set #twimui_lib_join_dest (tackon #twimui_lib "TWiMUI.lib"))
    (set #twimui_lib_join (cat
        "Join "
        "\"Lib/TWiMUI_far.lib\" \"Lib/TWiMUI_near.lib\" \"Lib/TWiMUI_near_a6.lib\" TO \""
        #twimui_lib_join_dest
        "\""))
    (run #twimui_lib_join)
    )
)

(exit #twimui_complete)
