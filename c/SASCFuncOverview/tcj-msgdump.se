/*
** msgdump.se
**
** Save SCMSG contents, if any, to file RAM:scmsg.txt 
**
** 31-Dec-2010 tcj   [tim@aaack.org]
**
** notes: 
**	- will attempt to open file in APPEND mode, if it exists
**  - success is indicated on SE status bar
**  - yes, I guess this could instead create a file in SE and
**		record the data there, but that is too good of an idea ;)
**
** installation:
**  - see installation instructions in funcoverview.se
**
*/

/* TRACE RESULTS */			/* for debugging */

IF ~SHOW( Ports, "SC_SCMSG" ) THEN DO
	SAY "SCMSG must be running!"
	RETURN
END

ADDRESS "SC_SCMSG"
OPTIONS RESULTS

funcText = ""						/* initialize to empty string */
outputFileName = "ram:scmsg.txt"	/* change if you want */

IF OPEN( 'myOutFile',outputFileName,APPEND ) THEN DO
	fileMode = "appended"
END 
ELSE IF OPEN( 'myOutFile',outputFileName,WRITE ) THEN DO
	fileMode = "written"	
END
ELSE EXIT 30					/* can't open file, exit with error */

'TOP'							/* SCMSG: go to top of list */
DO UNTIL (RC > 0)				/* do until we reach end of file */
	'FILE'						/* filename of current line in SCMSG */
	currentFileName = RESULT	
	IF ~(oldFileName == currentFileName) THEN DO
		fileNameString = currentFileName':'
		CALL WRITELN 'myOutFile', fileNameString
	END
	'LINE'
	lineNo = RESULT
	'TEXT'
	funcText = RESULT
	outLine = RIGHT(lineNo,8) || ': ' || funcText
	CALL WRITELN 'myOutFile', outLine

	oldFileName = currentFileName
	SIGNAL ON ERROR				/* NEXT will error when there are no more lines */
	'NEXT'						/* so that is our signal to exit the DO loop */
END

ERROR:
CALL CLOSE 'myOutFile' 			/* outFile is automatically closed at script exit */
								/* but what the heck */
ADDRESS							/* talk to SE again */
'DM SCMSG output 'fileMode' to ram:scmsg.txt'
