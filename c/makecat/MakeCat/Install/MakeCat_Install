; $VER: Install_MakeCat 38.22 (9.11.92)
; Diese Script-Datei dient zum Installieren von MakeCat.

(complete 0)
(delopts "oknodelete" "force" "askuser")

; Globaler Pfad für alles !
(set #globalpath
  (cat ":MakeCat/")
)

; Pfad+File für das Programm MakeCat
(set #MakeCatFile
  (cat #globalpath "MakeCat")
)

; Pfad+File für das Piktogramm
(set #MakeCatIconFile
  (cat #globalpath "OrigIcons/MakeCat.info")
)

; Pfad+File für die MakeCat-Dokumentation
(set #MakeCatDocFile
  (cat #globalpath "MakeCat.doc")
)

; Pfad+File für das Piktogramm der MakeCat-Dokumentation
(set #MakeCatDocInfoFile
  (cat #globalpath "MakeCat.doc.info")
)

; Pfad für die MakeCat-Kataloge
(set #MakeCatCatPath
  (cat #globalpath "Catalogs/MakeCat")
)

; Pfad+File für die Paladin-Library
(set #PaladinLibFile
  (cat #globalpath "paladin.library")
)

; Pfad für die Paladin-Kataloge
(set #PaladinCatPath
  (cat #globalpath "Catalogs/Paladin")
)

; Pfad+File für die Library-Dokumentation
(set #LibDocFile
  (cat #globalpath "Paladin.doc")
)

; Pfad+File für das Piktogramm der Library-Dokumentation
(set #LibDocInfoFile
  (cat #globalpath "Paladin.doc.info")
)

(set #bad-kick
  (cat "Sie müssen mindestens Kickstart 2.04 zum Installieren von MakeCat benutzen")
)

(set #about-this-thing
  (cat "\nPALADIN SOFTWARE\n"
       "präsentiert\n"
       "MakeCat V38.22\n"
       "\n\nBitte booten Sie von Ihrer Platte\n"
       "oder\n"
       "setzen die Assigns LIBS: und Locale: .")
)

(set #about-this-thing-help
  (cat "\n                     MakeCat\n"
       "dient vor allem zum Erzeugen von Katalogen und ersetzt das "
       "Programm CatComp von der Developers-Disk völlig "
       "und bietet noch viele Extras !!!\n"
       "Lesen Sie außerdem die beigefügte Dokumentation !")
)

(set #which-disk
  (cat "Wo soll MakeCat installiert werden ?")
)

(set #which-disk-help
  (cat "\nHier geben Sie an, in welchem Verzeichnis MakeCat installiert werden soll.\n\n\n"
       @askdir-help)
)

(set #confirm-info
  (cat "\n\n\nAuch ein eigenes Icon kopieren ?")
)

(set #confirm-info-help
  (cat "Um MakeCat auch von der Workbench zu starten, sollte diese "
       "Datei ins gleiche Verzeichnis wie MakeCat kopiert werden.")
)

(set #confirm-doc
  (cat "\n\n\nAuch die Dokumentation kopieren ?")
)

(set #confirm-doc-help
  (cat "Die Dokumentation zu MakeCat wird mit dem Programm in dieses "
       "Verzeichnis kopiert. Es handelt sich dabei um eine ASCII-Datei. "
       "Diese wird normalerweise mit WatchIt angezeigt. Soll sie mit "
       "einem anderen Textanzeigeprogramm (wie More, Multiview o.ä.) "
       "angezeigt werden, so muß noch das Default-Tool geändert werden.")
)

(set #confirm-libdoc
  (cat "\n\n\nAuch die Dokumentation zur Paladin-Library kopieren ?")
)

(set #confirm-libdoc-help
  (cat "Die Dokumentation zur Paladin-Library wird mit zum Programm MakeCat "
       "kopiert. Es handelt sich dabei um eine ASCII-Datei. "
       "Diese wird normalerweise mit WatchIt angezeigt. Soll sie mit "
       "einem anderen Textanzeigeprogramm (wie More, Multiview o.ä.) "
       "angezeigt werden, so muß noch das Default-Tool geändert werden.")
)

(set #which-langs
  (cat "Welche Sprachen sollen unterstützt werden ?")
)

(set #which-langs-help
  (cat "\nBitte geben Sie an, "
       "welche Sprachen unterstützt werden sollen.")
)

(set #what-next
  (cat "Viel Spaß beim Arbeiten\n"
       "wünschen Ihnen\n"
       "die Programmierer von\n"
       "PALADIN SOFTWARE\n"
       "\n\nZu näheren Informationen lesen Sie bitte die Dokumentation !")
)     

(set #confirm-lib
  (cat "Paladin.library V38.109 auch kopieren ?\n"
       "( MakeCat benötigt diese unbedingt ! )")
)

(set #confirm-lib-help
  (cat "MakeCat V38.22 benötigt dringend die paladin.library V38.109.\n"
       "Sollten Sie bereits eine Paladin-Library mit einer höheren "
       "Versionsnummer im LIBS: haben, so sollten Sie sie NICHT installieren !")
)

(procedure DoComplete
  (set anz (+ anz 17)) ; 100/6, da 5 DoCompletes !
  (complete anz)
)
  
; Kickstart V37 oder höher ?

(if
  (< (/ (getversion) 65536) 37)
  (abort #bad-kick)
)

(message #about-this-thing
  (help #about-this-thing-help)
)

; Wohin mit MakeCat ?

(set target "dh0:")
(set target 
  (askdir 
    (prompt #which-disk)
    (help #which-disk-help)
    (default target)
  )
)

(DoComplete)

; Was noch ?

(copyfiles 
  (source (#MakeCatFile))
  (dest target)
)

(Docomplete)

(if 
  (askbool
    (prompt (#confirm-info))
    (help (#confirm-info-help))
  )
  (
    (set icons 1)
    (copyfiles
      (source #MakeCatIconFile)
      (dest target)
    )
  )
)

(Docomplete)

(set whatfiles 
  (askoptions
    (prompt #which-langs)
    (help #which-langs-help)
    (choices "Deutsch"
             "Dansk"
             "Espagñol"
             "Français"
             "Italiano"
             "Norsk"
             "Nederlands"
             "Português"
             "Svenska"
    )
    (default 1)
  )
)

(set n 0)
(set filepat "(%")
(while
  (set file
    (select n "Deutsch"
              "Dansk"
              "Español"
              "Français"
              "Italiano"
              "Norsk"
              "Nederlands"
              "Português"
              "Svenska"
              ""
    )
  )
  (
    (if
      (IN whatfiles n)
      (set filepat (cat filepat "|" file ))
    )
    (set n (+ n 1))
  )
)

(copyfiles
  (source (#MakeCatCatPath))
  (dest "Locale:Catalogs")
  (pattern (cat filepat ")"))
)

(Docomplete)

(if 
  (askbool
    (prompt (#confirm-doc))
    (help (#confirm-doc-help))
  )
  (
    (copyfiles
      (source #MakeCatDocFile)
      (dest target)
    )
    (if (= icons 1)
      (copyfiles
        (source #MakeCatDocInfoFile)
        (dest target)
      )
    )
  )
)

(if 
  (askbool
    (prompt (#confirm-lib))
    (help (#confirm-lib-help))
  )
  (
    (copyfiles
      (source (#PaladinLibFile))
      (dest "LIBS:")
    )

    (Docomplete)
    (copyfiles
      (source (cat #PaladinCatPath)) 
      (dest "Locale:Catalogs/")
      (pattern (cat filepat ")"))
    )
    (if 
      (askbool
        (prompt (#confirm-libdoc))
        (help (#confirm-libdoc-help))
      )
      (
        (copyfiles
          (source #LibDocFile)
          (dest target)
        )
        (if 
          (= icons 1)
          (copyfiles
            (source #LibDocInfoFile)
            (dest target)
          )
        )
      )
    )
  )
)

(complete 100)
(exit (#what-next ))

