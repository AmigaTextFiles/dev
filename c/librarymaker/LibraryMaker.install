; -------------------------------------------------------
; Installer script for LibraryMaker V1.0+
; -------------------------------------------------------

(set #welcome "This is the installer for LibraryMaker!")

(set #wrongversion
(cat "You have an old version of the program 'Installer' "
     "on your Amiga!\n\n"
     "The installation procedure needs at least Installer 42.9."
     "\n\nPlease check your System & obtain a newer version!"
))

(set #general-help 
(cat "The install script will copy all %s files &"
     "directories to the destination that you specify."
))

(set #destdir-prompt "Select a Volume & Path where LibraryMaker will be added:")

(set #destdir-help   
(cat "Select the volume that you wish to place LibraryMaker in."
     "Then select a directory (if any) that will serve as a "
     "parent for LibraryMaker.  Example:  Select DH0: as the volume,"
     "then select a directory (such as Programs) & the installer"
     "will make a directory called DH0:Programs/LibraryMaker, where"
     "the LibraryMaker program will be placed."
))

(set #includedir-prompt 
(cat "Select a Volume & Path where your Compiler"
     "expects to find header files:"
))

(set #inlcudedir-help   
(cat "Select the volume:Path/ that you wish to place AmigaDOSErrs.h in."
     "This is where all of your C-Compiler header files are in."
))

(set #no-diskspace 
(cat "To install LibraryMaker into '%s', you need at"
     "least %ld bytes of free harddrive space!"
))

;------------------------------------------------------------------------
; Cleanup any temporary mess we created
(procedure CLEANUP
    ; Nothing to cleanup
    (delete (tackon @default-dest ".tempinstall") (all) (optional "force"))
)   ; CLEANUP


;------------------------------------------------------------------------
; Obtain an installation path
(procedure GETPATH #gp_default #gp_remdir #gp_msg #gp_help

    (set #gp_res "")
    (while (= "" #gp_res)
        (set #gp_res #gp_default)
        (if (not (exists #gp_res (noreq)))
          (
            ; Surfer 1.1 Hack:
            (if (= #gp_res "Work:")
              (
                (set #gp_res "")
                (set #gp_default @default-dest)
              )
            ) ; if

          )
        ) ; if

        (if (OR (= "" #gp_res) (= @user-level 2))
          (
            ; We had a problem. So we need to ask the user!
            (set #gp_res
                (expandpath
                    (askdir
                        (prompt #gp_msg)
                        (help #gp_help)
                        (default #gp_default)
                    )
                )
            )
          )
        ) ; if

        ; If the directory does not exist, retry!
        (if (<> "" #gp_res)
          (
            (if (not (exists #gp_res (noreq)))
                (set #gp_res "")
            ) ; if
          )
        ) ; if
    ) ; while

    ; return the result!
    (set #gp_res #gp_res)

) ; GETPATH

;------------------------------------------------------------------------
; We can't handle this script without Installer 42.9 or better!
(if (< @installer-version 2752521)
  (
    ; Big problem!
    (message #wrongversion)
    (exit (quiet))
  )
)

; find out how the user wants to install things
(welcome #welcome)

(complete 0)

; First find the Destination base:

(set #LGDirBase (GETPATH "Work:" TRUE #destdir-prompt #destdir-help))

(set #IncludeDir (GETPATH "Work:" TRUE #includedir-prompt #includedir-help))

; We have to create a drawer "LibraryMaker" there!
(set #LGDir (tackon #LGDirBase "LibraryMaker"))
(set #LGDrawer #LGDirBase)

(set #TemplateDir (tackon #LGDir "Templates"))
(set #ResultsDir  (tackon #LGDir "Results"  ))

(set @default-dest #LGDir)

; If this is a new installation, we check the diskspace!

    (if (not (exists @default-dest))
      (
        ; For a new setup, we should leave some space!
        (if (< (getdiskspace #LGDirBase) 500000)
            (abort (#no-diskspace #LGDirBase 500000))
        )

        (makedir @default-dest)

        (set #CatalogsDir "Locale:")

        (complete 9)

        ; Create our Directory Icon...

        (run (cat "Copy LibraryMakerDrawer.info " (cat #LGDirBase "/LibraryMaker.info")))

        (copyfiles (prompt  "Copying Catalogs...")
                   (help    @copyfiles-help)
                   (source  "LibraryMaker.ct")
                   (dest    #CatalogsDir)
                   (pattern "#?")
        )

        (copyfiles (prompt  "Copying Catalogs...")
                   (help    @copyfiles-help)
                   (source  "LibraryMaker.cd")
                   (dest    #CatalogsDir)
                   (pattern "#?")
        )

        (complete 50)

        (copyfiles (prompt  "Copying Templates...")
                   (help    @copyfiles-help)
                   (source  "Templates")
                   (dest    #TemplateDir)
                   (pattern "#?")
        )

        (copyfiles (prompt  "Copying Results...")
                   (help    @copyfiles-help)
                   (source  "Results")
                   (dest    #ResultsDir)
                   (pattern "#?")
        )

        (complete 60)

        (copyfiles (prompt  "Copying Include file(s)...")
                   (help    @copyfiles-help)
                   (source  "AmigaDOSErrs.h")
                   (dest    #IncludeDir)
        )

        (copyfiles (prompt  "Copying Main files...")
                   (help    @copyfiles-help)
                   (source  "LibraryMaker.guide")
                   (dest    #LGDir)
        )

        (copyfiles (prompt  "Copying Main files...")
                   (help    @copyfiles-help)
                   (source  "LibraryMaker.info")
                   (dest    #LGDir)
        )

        (copyfiles (prompt  "Copying Main files...")
                   (help    @copyfiles-help)
                   (source  "LibraryMaker.guide.info")
                   (dest    #LGDir)
        )

        (copyfiles (prompt  "Copying Main files...")
                   (help    @copyfiles-help)
                   (source  "LibraryMaker.readme")
                   (dest    #LGDir)
        )

        (complete 90)

        (copyfiles (prompt  "Copying Main program...")
                   (help    @copyfiles-help)
                   (source  "LibraryMaker")
                   (dest    #LGDir)
                   (pattern "LibraryMaker")
        )

        (complete 98)
      )
    )

;------------------------------------------------------------------------
; That's it!

(complete 100)

(CLEANUP)
(exit)

(abort "Well, I tried!")
