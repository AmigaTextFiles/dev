/*
 * $VER: ged4findent 1.2 (25.9.97)
 *
 * ged4findent - indent current context in a C sourcefile
 *               in GoldEd, using GNUindent
 *
 * Written by Thomas Aglassinger <agi@giga.or.at>
 *
 * This is Public Domain, refer to ReadMe for usage.
 */

OPTIONS RESULTS                             /* enable return codes     */

if (LEFT(ADDRESS(), 6) ~= "GOLDED") then    /* not started by GoldEd ? */
    address 'GOLDED.1'

'LOCK CURRENT RELEASE=4'                    /* lock GUI, gain access   */
OPTIONS FAILAT 6                            /* ignore warnings         */
SIGNAL ON SYNTAX                            /* ensure clean exit       */

/* temporary filenames to store block in */
srcfname = 't:ged4sfindent.' || PRAGMA('ID')
indfname = 't:ged4ifindent.' || PRAGMA('ID')

/* remember current position */
'PING 0'

/*
 * find top of current function
 */
foundFunc = 0                               /* found beginning of func? */
quitLoop = 0                                /* quit loop (because of any reason)? */

/* if not currently above a "{", search for the previous one */
DO WHILE ~quitLoop
    'QUERY CODE VAR ch'
    'QUERY COLUMN VAR col'

    IF ((ch = 123) & (col = 1)) THEN DO
        /* begining of function found */
        foundFunc = 1
        quitLoop = 1
    END
    ELSE DO
        /* search for previous "{" */
        'FIND STRING="{" PREV QUIET'
        IF (RC ~=0 ) THEN DO
            /* error, if none found */
            quitLoop = 1
            'REQUEST TITLE="Indent function" PROBLEM="No `{` at begining of line found"'
        END
    END
END /* while */

/*
 * mark whole function
 */
IF foundFunc THEN DO
   'MARK HIDE'
   'MARK SET'

   'BRACKET MATCH'
    if (RC = 0) THEN DO
        /* mark end of block */
        'MARK SET'                            
    END
    ELSE DO
        /* error, if no matching "}" found */
        foundFunc = 0
        'MARK HIDE'
        'REQUEST TITLE="Indent function" PROBLEM="No corresponding `}` found"'
    END
END

/*
 * save block and indent it
 */
IF foundFunc THEN DO

    /* save block */
    'SAVE BLOCK FORCE NAME="' || srcfname || '"'

    /* determine which options to use for indent:
     * tries to read the environmental variable `ged4indent.prefs';
     * if it can't be found, default options matching the style of
     * GoldEd will be used */
    indentOpts = GetEnv('ged4indent.prefs')
    IF indentOpts = '' THEN DO
        indentOpts = '-br -ce -i4 -sob -sc -npsl -npcs -lp'
    END

    /* invoke indent */
    indentCommand = 'indent' indentOpts '-o "' || indfname || '" "' || srcfname || '"'
    ADDRESS COMMAND indentCommand

    IF (RC = 0) THEN DO
        /* move to begin of function, remember line number */
        'BRACKET MATCH'
        'QUERY ABSLINE VAR beforeDelBlkLine'

        /* delete old block */
        'DELETE BLOCK'

        /* if at a different line now, insert CR; otherwise for
         * functions at end of file, the function declaration
         * would end up after the actual function text */
        'QUERY ABSLINE VAR afterDelBlkLine'
        if (beforeDelBlkLine ~= afterDelBlkLine) THEN DO
            'GOTO EOL'
            'CR'
        END

        /* insert new data from file */
        'OPEN FORCE INSERT NAME="' || indfname || '"'
    END
    ELSE DO
        /* error message */
        SAY ''
        SAY 'error calling indent; file left untouched'
    END
END

'PONG 0'                       /* restore position */

CALL DelFileExists(srcfname)   /* remove temporary file */
CALL DelFileExists(indfname)

'UNLOCK' /* VERY important: unlock GUI */
EXIT

/***************
 * subroutines *
 ***************/

/*
 * handle syntax errors
 */
SYNTAX:

SAY "Sorry, error line" SIGL ":" ERRORTEXT(RC) ":-("
'UNLOCK'
EXIT

/*
 * GetEnv: return the value of an environmental variable
 */
GetEnv: procedure
    IF Open(Env, 'env:' || arg(1), 'R') THEN DO
        EnvVar = ReadLn(Env)
        CALL Close Env
    END
    ELSE DO
        EnvVar = ''
    END
    RETURN EnvVar

/*
 * DelFileExists: delete a file if it exists
 */
DelFileExists: procedure
    IF Exists(arg(1)) THEN DO
        ADDRESS COMMAND 'delete QUIET "' || arg(1) || '"'
    END
    RETURN

