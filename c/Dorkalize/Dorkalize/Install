; $VER: Dorkalize installer script v 0.91
;
; Copyright by F.Borghese & G.Di Giacomo, 2001

(message
   "\n\n\nDorkalize 0.91\n"
   "\n\n© F.Borghese & G.Di Giacomo, 1999-2001\n\n"
)

(set @app-name "Dorkalize")
(set vernum (getversion "MUI:libs/muimaster.library"))
(set ver (/ vernum 65536))
(set rev (- vernum (* ver 65536)))
(set #prompt_copy "\nAbout to copy the following files:")
(set #MUI_libs_dir "MUI:Libs/MUI")

(complete 5)

(if (< ver 19)
    (if (= ver 0)
        (message
            "\n\nWarning\n\n"
            "\nThe installer script can't find MUI\n"
            "\nDorkalize requires MUI 3.0 or higher to work"
        )
        (message
            "\n\nWarning\n"
            ("\nThe installer script detected MUI version %ld.%ld\n" ver rev)
            "\nWe suggest to install the latest version"
        )
    )
)

(complete 10)

(set @default-dest
    (askdir
        (prompt "In which directory do you want to install Dorkalize?\n(A directory called \"Dorkalize\" will be created there)\nIf Dorkalize is already installed, it will be updated.\nNo configuration files will be overwritten.")
        (help @askdir-help)
        (default "")
        (disk)
    )
)

(complete 15)

(copyfiles
    (prompt #prompt_copy)
    (help @copyfiles-help)
    (source "")
    (dest
        (set #dest_dir
            (tackon @default-dest "Dorkalize")
        )
    )
    (pattern "~((#?.guide)|(~(dork#?)))")
    (files)
    (infos)
    (optional "fail" "askuser")
)

(complete 25)

(set #result
    (askoptions
        (prompt "Which languages do you want to install?\n(English is builtin)")
        (help @askoptions-help)
        (choices
            "italiano"
        )
        (default 0)
    )
)

(complete 30)

(if
    (<> #result 0)
    (makedir (tackon #dest_dir "catalogs/")
        (infos)
    )
)

(complete 35)

(if
    (<> (BITAND #result 1) 0)
    (copyfiles
        (source "catalogs/italiano")
        (dest (tackon #dest_dir "catalogs/italiano"))
        (all)
        (infos)
        (optional "fail" "askuser")
    )
)

(complete 40)

(set #guides_to_copy
    (askoptions
        (prompt "What languages do you want to install documentation for?")
        (help @askoptions-help)
        (choices "english" "italiano")
        (default 1)
    )
)

(complete 45)

(if
    (<> (BITAND #guides_to_copy 1) 0)
    (
        (copyfiles
            (source "dorkalize.guide")
            (dest #dest_dir)
            (infos)
            (optional "fail" "askuser")
        )
        (set #english "english")
        (set #guide_number 1)
        (set #first_guide 0)
    )
    (
        (set #english "")
        (set #guide_number 0)
    )
)

(complete 50)

(if
    (<> (BITAND #guides_to_copy 2) 0)
    (
        (copyfiles
            (source "dorkalize_italiano.guide")
            (dest #dest_dir)
            (infos)
            (optional "fail" "askuser")
        )
        (set #italiano "italiano")
        (if
            (= #guide_number 0)
            (set #first_guide 1)
        )
        (set #guide_number (+ #guide_number 1))
    )
    (set #italiano "")
)

(complete 55)

(if
    (> #guide_number 1)
    (
        (set #default_guide
            (askchoice
                (prompt "What language do you want for the default help file?")
                (help @askchoice-help)
                (choices #english #italiano)
                (default #first_guide)
            )
        )
    )
    (set #default_guide #first_guide)
)

(complete 60)

(if
    (AND (> #guide_number 0) (> #default_guide 0))
    (
        (if
            (= #first_guide 0)
            (
                (rename
                    (tackon #dest_dir "dorkalize.guide")
                    (tackon #dest_dir "dorkalize_english.guide")
                )
                (rename
                    (tackon #dest_dir "dorkalize.guide.info")
                    (tackon #dest_dir "dorkalize_english.guide.info")
                )
            )
        )
        (select (- #default_guide 1)
            (
                (rename
                    (tackon #dest_dir "dorkalize_italiano.guide")
                    (tackon #dest_dir "dorkalize.guide")
                )
                (rename
                    (tackon #dest_dir "dorkalize_italiano.guide.info")
                    (tackon #dest_dir "dorkalize.guide.info")
                )
            )
        )
    )
)

(complete 65)

(if
    (=
        (set #result
            (askchoice
                (prompt "Do you want to install the ARexx scripts?")
                (help @askchoice-help)
                (choices
                    "Yes"
                    "No"
                )
                (default 0)
            )
        )
    0)
    (
        (copyfiles
            (source "rexx.info")
            (dest #dest_dir)
            (optional "fail" "askuser")
        )
        (copyfiles
            (prompt #prompt_copy)
            (help @copyfiles-help)
            (source "rexx/")
            (dest (tackon #dest_dir "rexx"))
            (pattern "#?")
            (infos)
            (confirm)
            (optional "fail" "askuser")
        )
    )
)

(complete 70)

(copylib
    (prompt #prompt_copy "\n\nNList.mcc" )
    (help @copylib-help)
    (confirm "average")
    (source "libs/MUI/NList.mcc")
    (dest #MUI_libs_dir)
    (optional "fail" "askuser")
)

(complete 75)

(copylib
    (prompt #prompt_copy "\n\nNListview.mcc" )
    (help @copylib-help)
    (confirm "average")
    (source "libs/mui/NListview.mcc")
    (dest #MUI_libs_dir)
    (optional "fail" "askuser")
)

(complete 80)

(copylib
    (prompt #prompt_copy "\n\nNFloattext.mcc" )
    (help @copylib-help)
    (confirm "average")
    (source "libs/mui/NFloattext.mcc")
    (dest #MUI_libs_dir)
    (optional "fail" "askuser")
)

(complete 85)

(copylib
    (prompt #prompt_copy "\n\nNListviews.mcp" )
    (help @copylib-help)
    (confirm "average")
    (source "libs/mui/NListviews.mcp")
    (dest #MUI_libs_dir)
    (optional "fail" "askuser")
)

(complete 90)

(copylib
    (prompt #prompt_copy "\n\nBetterString.mcc" )
    (help @copylib-help)
    (confirm "average")
    (source "libs/mui/BetterString.mcc")
    (dest #MUI_libs_dir)
    (optional "fail" "askuser")
)

(complete 95)

(copylib
    (prompt #prompt_copy "\n\nBetterString.mcp" )
    (help @copylib-help)
    (confirm "average")
    (source "libs/mui/BetterString.mcp")
    (dest #MUI_libs_dir)
    (optional "fail" "askuser")
)

(complete 100)
