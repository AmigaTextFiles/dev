;
; Script to install:
;
; Tabulator   By: Mark Ritter
;
; A Swiss Army Knife Utility
;


;-------------------------------------------------------------------------------
; Initialization
;-------------------------------------------------------------------------------
(set v39 (>= (/ (getversion "exec.library" (resident)) 65536) 39))

(set cpu (database "cpu"))
(if (= cpu 68000) (set DefaultCPU 0))
(if (= cpu 68030) (set DefaultCPU 1))
(if (= cpu 68040) (set DefaultCPU 2))
(if (= cpu 68060) (set DefaultCPU 3))

(set Program0 "Tabulator")
(set Program1 "Tabulator")
(set Program2 "tabulator")

(set IconName (cat Program0 ".info"))
(set GuideName (cat Program0 ".guide"))
(set GuideNameInfo (cat GuideName ".info"))
(set CatalogName (cat Program2 ".catalog"))
(set BlankName (cat Program0 "_Blank.cd"))

(set vernum1 (getversion Program1)) (set Ver1 (/ vernum1 65536)) (set Rev1 (- vernum1 (* Ver1 65536)))
(set Version1 (cat Ver1 "." Rev1))
(set Released "5.6.97")
(set CopyrightYears "1995-1997")


;-------------------------------------------------------------------------------
; English strings
;-------------------------------------------------------------------------------
(if (= @language "english")
(
    (set #need39 "This program requires OS 3.0 or higher.")
    (set #need43installer "This install script requires Installer V43.\n\nIt is available on Aminet:\nutil/misc/Installer-43_3.lha\n\nPlease upgrade your Installer and try again.")

    (set #installing "Installing ")

    (set #welcome (cat "\n\n\n" Program1 "\nBy: Mark Ritter\n\nVersion: " Version1 "     Released: " Released "\n\nCopyright © " CopyrightYears "   All Rights Reserved\n\nA Swiss Army Knife Utility"))
    (set #welcome2 (cat "\nThe install script will check the versions of all files before copying.  This will prevent an older version being copied over a newer version."))

    (set #iconsystem "What icon system do you have installed?")

    (set #installwhere (cat "Where do you want to install " Program1 "?  A directory will NOT be created.  Only a newer version will be copied."))

    (set #pickcpu "What CPU version do you want to install?  If your choice does not exist, then the 68000 version will be used.")

    (set #copyicon (cat Program1 " is already installed.  Do you want to copy the icon?  If you have set some of the tooltypes and want to keep them, then don't copy the icon."))
    (set #yes "Yes")
    (set #no "No")

    (set #guidewhere "Where do you want to install the AmigaGuide?")

    (set #chooselanguages (cat Program1 " has English as the built-in language.  It is localized with the languages below.  Which catalogs do you want to install?"))

    (set #goodbye (cat "\n" Program1 " Installation Complete\n\n"))
)
)


;-------------------------------------------------------------------------------
; Deutsch strings
;-------------------------------------------------------------------------------
(if (= @language "deutsch")
(
    (set #need39 "Dieses Programm benötigt OS 3.0 oder höher.")
    (set #need43installer "Dieses Install-Skript benötigt Installer V43.\n\nEr ist verfügbar im Aminet:\nutil/misc/Installer-43_3.lha\n\nBitte verbessere deinen Installer und versuche es nochmal.")

    (set #installing "Installiere ")

    (set #welcome (cat "\n\n\n" Program1 "\nVon: Mark Ritter\n\nVersion: " Version1 "     Released: " Released "\n\nCopyright © " CopyrightYears "   Alle Rechte vorbehalten\n\nEin Schweizer Armee-Messer-Hilfsprogramm."))
    (set #welcome2 (cat "\nDas Install-Skript durchsucht vor dem Kopieren alle Versionen von allen Dateien.  Dies beugt dem Kopieren einer älteren Version über eine neuere vor."))

    (set #iconsystem "Welches Piktogrammsystem hast du installiert?")

    (set #installwhere (cat "Wohin möchtest du installieren " Program1 "?  Eine Schublade wird nicht erzeugt.  Nur eine neuere Version wird kopiert."))

    (set #pickcpu "Welche CPU Version möchtest du installieren?  Wenn nichts gewählt ist, wird die 68000 Version benutzt.")

    (set #copyicon (cat Program1 " ist bereits installiert.  Möchtest du das Piktogramm kopieren?  Wenn du ein paar Merkmale gesetzt hast und sie behalten möchtest, dann kopiere das Piktogramm nicht."))
    (set #yes "Ja")
    (set #no "Nein")

    (set #guidewhere"Wohin moechtest du AmigaGuide installieren?")

    (set #chooselanguages (cat Program1 " hat Englisch als eingebaute Sprache.  Es befindet sich unter den anderen Sprachen.  Welche Kataloge möchtest du installieren?"))

    (set #goodbye (cat "\n" Program1 " Installation komplett\n\n"))
)
)


;-------------------------------------------------------------------------------
; Magyar strings
;-------------------------------------------------------------------------------
(if (= @language "magyar")
(
    (set #need39 "Ez a program OS 3.0-át vagy nagy verziót igényel.")
    (set #need43installer "Ez az telepítõ parancsállomány Installer V43-at vagy nagyobb verziót igényel.\n\nBeszerezhetõ az Amineten:\nutil/misc/Installer-43_3.lha\n\nKérlek cseréld le a telepítõd és próbáld újra!")

    (set #installing "Telepítés ")

    (set #welcome (cat "\n\n\n" Program1 "\nKészítette: Mark Ritter\n\nVerzió: " Version1 "     Kiadta: " Released "\n\nCopyright © " CopyrightYears "   Minden jog fentartva.\n\nEgy Swiss Army Knife Utility"))
    (set #welcome2 (cat "\nA telepítõ állomány megvizsgálja minden állomány vezióját másolás elõtt. Ez megakadályozza, hogy egy újabb verziót lecseréljen egy régebbivel."))

    (set #iconsystem "Milyen ikon rendszered van telepítve?")

    (set #installwhere (cat "Hova akarod telepíteni a(z)" Program1 "-ot?  Könyvtár NEM LESZ ott létrehozva. Csak újabb verziók lesznek másolva."))

    (set #pickcpu "Melyik CPU verziót akarod telepíteni?  Ha az általad kívánt változat nem létezik, akkor a 68000-es verzió kerül telepítésre.")

    (set #copyicon (cat Program1 " már telepítve van.  Akarod az ikont másolni?  Ha már beállítottál paramétereket (tooltype), és mega akarod tartani a beállításokat, akkor ne másold át az ikont."))
    (set #yes "Igen")
    (set #no "Nem")

    (set #guidewhere "Hova akarod telepiteni az AmigaGuide allomanyt?")

    (set #chooselanguages (cat "A(z) " Program1 " beépített nyelve az angol.  Az alább felsorolt lokalizációs állományokat tartalmazza.  Melyik katalógusokat akarod telepíteni?"))

    (set #goodbye (cat "\n" Program1 " telepítése kész.\n\n"))
)
)


;-------------------------------------------------------------------------------
; Check versions
;-------------------------------------------------------------------------------
(complete 0)

(if (not v39)
    (abort #need39)
)

(if (< (/ @installer-version 65536) 43)
    (abort #need43installer)
)


;-------------------------------------------------------------------------------
; Show welcome message
;-------------------------------------------------------------------------------
(complete 0)

(message #welcome)

(welcome)

(message #welcome2)


;-------------------------------------------------------------------------------
; Install what icon system
;-------------------------------------------------------------------------------
(complete 0)

(set IconSystem
    (askchoice
        (prompt #iconsystem)
        (choices "NewIcons" "MagicWB")
        (help #iconsystem)
        (default 0)
    )
)


;-------------------------------------------------------------------------------
; Get destination directory
;-------------------------------------------------------------------------------
(complete 8)

(set WhatAssign
    (askdir
        (prompt #installwhere)
        (help #installwhere)
        (default @default-dest)
    )
)

(set @default-dest WhatAssign)


;-------------------------------------------------------------------------------
; Ask for CPU
;-------------------------------------------------------------------------------
(complete 20)

(set PickCPU
    (askchoice
        (prompt #pickcpu)
        (choices "68000" "68020" "68030" "68040" "68060" "PPC")
        (help #pickcpu)
        (default DefaultCPU)
    )
)


;-------------------------------------------------------------------------------
; Check CPU and copy executable
;-------------------------------------------------------------------------------
(complete 32)

(set NameCPU Program0)

(if (= PickCPU 1)
(
    (set NameCPU (cat Program0 "030"))

    (if (not (exists NameCPU))
        (set NameCPU Program0)
    )
)
)

(if (= PickCPU 2)
(
    (set NameCPU (cat Program0 "040"))

    (if (not (exists NameCPU))
        (set NameCPU Program0)
    )
)
)

(if (= PickCPU 3)
(
    (set NameCPU (cat Program0 "060"))

    (if (not (exists NameCPU))
        (set NameCPU Program0)
    )
)
)

(if (= PickCPU 4)
(
    (set NameCPU (cat Program0 "PPC"))

    (if (not (exists NameCPU))
        (set NameCPU Program0)
    )
)
)

(copylib
    (source NameCPU)
    (dest @default-dest)
    (prompt #installing Program1)
)


;-------------------------------------------------------------------------------
; Verify copy icon
;-------------------------------------------------------------------------------
(complete 44)

(set OldIcon (exists (tackon WhatAssign IconName)))

(if (= OldIcon 0)
(
    (if (= IconSystem 0)
        (copyfiles
            (source (tackon "Icons/NewIcons/" IconName))
            (dest @default-dest)
            (optional "force" "nofail")
        )
    )

    (if (= IconSystem 1)
        (copyfiles
            (source (tackon "Icons/MagicWB/" IconName))
            (dest @default-dest)
            (optional "force" "nofail")
        )
    )
)
)

(if (= OldIcon 1)
(
    (set CopyNewIcon
        (askbool
            (prompt #copyicon)
            (choices #yes #no)
            (help #copyicon)
        )
    )

    (if (= CopyNewIcon 1)
    (
        (if (= IconSystem 0)
            (copyfiles
                (source (tackon "Icons/NewIcons/" IconName))
                (dest @default-dest)
                (optional "force" "nofail")
            )
        )

        (if (= IconSystem 1)
            (copyfiles
                (source (tackon "Icons/MagicWB/" IconName))
                (dest @default-dest)
                (optional "force" "nofail")
            )
        )
    )
    )
)
)


;-------------------------------------------------------------------------------
; Install AmigaGuide
;-------------------------------------------------------------------------------
(complete 56)

(set WhatAssign
    (askdir
        (prompt #guidewhere)
        (help #guidewhere)
        (default "HELP:")
    )
)

(set @guide-dest WhatAssign)

(if (= IconSystem 0)
(
    (copyfiles
        (source (tackon "Icons/NewIcons/" GuideNameInfo))
        (dest @guide-dest)
        (optional "force" "nofail")
    )
)
)

(if (= IconSystem 1)
(
    (copyfiles
        (source (tackon "Icons/MagicWB/" GuideNameInfo))
        (dest @guide-dest)
        (optional "force" "nofail")
    )
)
)

(copylib
    (source GuideName)
    (dest @guide-dest)
    (prompt #installing GuideName)
)


;-------------------------------------------------------------------------------
; Ask what language(s)
;-------------------------------------------------------------------------------
(complete 68)

(set Languages
    (askoptions
        (prompt #chooselanguages)
        (choices "Deutsch" "Magyar")
        (help #askoptions-help)
        (default 0)
    )
)

(if (bitand Languages 1)
(
    (copylib
        (source (tackon "Locale/deutsch/" CatalogName))
        (dest "LOCALE:Catalogs/deutsch/")
        (prompt #installing CatalogName)
    )
)
)

(if (bitand Languages 2)
(
    (copylib
        (source (tackon "Locale/magyar/" CatalogName))
        (dest "LOCALE:Catalogs/magyar/")
        (prompt #installing CatalogName)
    )
)
)


;-------------------------------------------------------------------------------
; Install miscellaneous files
;-------------------------------------------------------------------------------
(complete 80)

(copylib
    (source BlankName)
    (dest @default-dest)
    (optional "nofail")
    (prompt #installing BlankName)
)


;-------------------------------------------------------------------------------
; Install libraries
;-------------------------------------------------------------------------------
(complete 92)

(copylib
    (source "libs/gtlayout.library")
    (dest "LIBS:")
    (prompt #installing "gtlayout.library")
)
(copylib
    (source "libs/screennotify.library")
    (dest "LIBS:")
    (prompt #installing "screennotify.library")
)


;-------------------------------------------------------------------------------
; Finished
;-------------------------------------------------------------------------------
(complete 100)

(exit #goodbye)
