;Benoetigt amigalibs.res sowie const.res
;(im Options-Requester eintragen!)

#TOOLMANAGER=1
#TOOLMANAGER_GADGETS=32

;INCLUDE "StormWizard:Examples/ToolManager.bb2"

;Falls "ToolManager.asc" nicht vorhanden sein sollte,
;muss Stormwizard zunaechst auf die Erzeugung von
;BlitzBasic-Headerfiles eingestellt werden. Dies
;wird ueber die ToolTypes von Stormwizard geregelt.

;Dann sollte die Beispieldatei "ToolManager.wizard"
;(oder eine beliebige andere Datei) eingeladen werden
;und wieder gespeichert werden. Dabei werden automatisch
;die Header-Files mit den Konstanten erzeugt.

;Das erzeugte ".asc"-File muss mit dem BB-Editor eingeladen
;werden und als ".bb2"-File wieder abgespeichert werden.
;Dadurch wird das Programm tokenisiert. Jetzt kann es mit
;INCLUDE eingebunden werden.

;Anmerkung: Fuer kleinere Files mit nur ein paar Konstanten
;kann das HeaderFile auch direkt in den Sourcecode mitauf-
;genommen werden (Menu:Project/Include File...)

DEFTYPE.l                               ;Long-Vars fuer Pointer
DEFTYPE.IntuiMessage *msg               ;Intuition-Message

Dim Tags.TagItem (8)                    ;Array fuer Tags
Dim Gads.l       (#TOOLMANAGER_GADGETS) ;Array fuer Gadgets

;-------------------------------------------------------------------------

Surface=WZ_OpenSurfaceA_ (0,?SurfaceData, 0)
If Surface

  WbToScreen 0
  WinHandle=WZ_AllocWindowHandleA_ (ActiveScreen,0,Surface,0)
  If WinHandle

    Tags(0)\ti_Tag=#WWH_GadgetArray,     &Gads(0)
    Tags(1)\ti_Tag=#WWH_GadgetArraySize, #TOOLMANAGER_GADGETS*4
    Tags(2)\ti_Tag=#TAG_END,             0
    NewWindow=WZ_CreateWindowObjA_ (WinHandle,#TOOLMANAGER,&Tags(0))
    If NewWindow

      Tags(0)\ti_Tag=#WA_AutoAdjust, True
      Tags(1)\ti_Tag=#TAG_DONE,      0
      MyWindow=WZ_OpenWindowA_ (WinHandle,NewWindow,&Tags(0))
      If MyWindow

        Port.l=Peek.l(MyWindow+86)  ;Window\UserPort
        Repeat
          WaitPort_ Port
          Repeat
            *msg=GetMsg_ (Port)
            If *msg
              If *msg\Class = $200 Then exit=True
              ReplyMsg_ *msg
            EndIf
          Until *msg=0
        Until exit
        WZ_CloseWindow_ WinHandle

      Else
        NPrint "Fehler bei WZ_CreateWindowObj!"
      EndIf

    Else
      NPrint "Fehler bei WZ_CreateWindowObj!"
    EndIf
    WZ_FreeWindowHandle_ WinHandle

  Else
    NPrint "Fehler bei WZ_AllocWindowHandle!"
  EndIf
  WZ_CloseSurface_ Surface

Else
  NPrint "Fehler bei WZ_OpenSurface!"
EndIf

End

;-------------------------------------------------------------------------

SurfaceData: IncBin "StormWizard:Examples/ToolManager.Wizard"
SurfaceDataEnd:

