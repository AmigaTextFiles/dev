
; This file is part of the MegaConvert distribution. It may not be changed and/
; or (re-)distributed under any means that do not preserve the MegaConvert
; license.

(set #requires-os-two_prompt "\n\n\nSorry, but MegaConvert requires at least\nOS2.04 (V37) to run.")
(set #choose-dest_prompt "Please choose the directory into which\nMegaConvert should be installed !")
(set #choose-dest_help (cat "Here you can choose the directory into which "
    "MegaCon-\nvert should be installed. A sub-directory named 'MegaConvert' "
    "will be created automatically.\n\n\n" @askdir-help))
(set #already-exists_prompt "\n\nThere is already a file/directory named\n'%s'.\nProceeding will automatically delete\nthis file/directory.")
(set #choose-startup-pos_prompt "Please choose the position of the startup commands !")
(set #choose-startup-pos_help (cat "There are several commands which must be "
    "executed on startup. Here you can choose the position of these lines "
    "(should be at the end). The lines will be merged into the file 's:User-"
    "Startup' (or 's:Startup-\nSequence', if necessary).\n\n\n" @startup-help))
(set #perform-reset_prompt "To start MegaConvert correctly,\nyou should perform a reset !")

(if (= @language "deutsch")
  (
    (set #requires-os-two_prompt "\n\n\nMegaConvert benötigt OS2.04 (V37) oder höher.")
    (set #choose-dest_prompt "Bitte wählen Sie das Verzeichnis, in das\nMegaConvert installiert werden soll !")
    (set #choose-dest_help (cat "Hier können Sie das Verzeichnis wählen, in "
        "das Mega-\nConvert installiert werden soll. Ein Unterverzeichnis "
        "namens 'MegaConvert' wird automatisch erstellt.\n\n\n" @askdir-help))
    (set #already-exists_prompt "\n\nEs existiert bereits ein(e) Verzeichnis/Datei namens\n'%s'.\nWenn die Installation fortgesetzt wird, wird diese(s) Verzeichnis/Datei automatisch gelöscht.")
    (set #choose-startup-pos_prompt "Bitte wählen Sie die Position der Startup-Kommandos !")
    (set #choose-startup-pos_help (cat "Einige bestimmte Kommandos müssen "
        "beim Starten des Systems ausgeführt werden. Hier können Sie die Posi-"
        "\ntion dieser Startup-Kommandos wählen. Die Zeilen wer-\nden in die "
        "Datei 's:User-Startup' (oder 's:Startup-\nSequence', falls nötig) ein"
        "gefügt.\n\n\n" @startup-help))
    (set #perform-reset_prompt "Um MegaConvert korrekt zu starten,\nsollten Sie einen Reset ausführen !!")
  )
)

(message "\n\n\n\n\nMegaConvert\n\nCopyright © by Maik Schreiber [SHAREWARE]")

(if (< (/ (getversion "exec.library" (resident)) 65536) 37)
  (abort #requires-os-two_prompt)
)

(set #dest
  (askdir
    (prompt #choose-dest_prompt)
    (default @default-dest)
    (help #choose-dest_help)
  )
)

(set @default-dest #dest)
(set #dest (tackon @default-dest "MegaConvert"))

(if (> (exists #dest) 0)
  (
    (message (#already-exists_prompt #dest))
    (run ("Delete >NIL: \"%s(|.info)\" ALL" #dest) (prompt "") (help ""))
  )
)

(makedir #dest)

(copyfiles
  (prompt "")
  (help "")
  (source "MegaConvert/")
  (dest #dest)
  (all)
)

(copyfiles
  (prompt "")
  (help "")
  (source "MegaConvert.info")
  (dest @default-dest)
)

(run "Copy >NIL: MegaConvert/MaikSchreiber.key s:" (prompt "") (help ""))

(startup "MegaConvert"
  (prompt #choose-startup-pos_prompt)
  (help #choose-startup-pos_help)
  (command (cat "Assign LIBS: \"" (tackon #dest "libs") "\" ADD\n"))
  (command (cat "Assign LOCALE: \"" #dest "\" ADD"))
)

(exit #perform-reset_prompt)

