/*
**                  EAutoGuide.ced, by Mathieu Dhondt,
**                     is a modification for CED of
**            get_EAutoGuide.rexx, written by Sven Steiniger,
**                    which was based on the idea of
**                  Eric BURGHARD's ged_guideref.rexx.
*/

ARG auto_path

OPTIONS RESULTS

IF (LEFT(ADDRESS(), 8) ~= "rexx_ced") THEN DO
  SAY 'Fire me up from Cygnus Ed!'
  EXIT
END 

'make screen public'

'getword' /* Get the word under CED's cursor */

IF (result = 0) THEN DO
  'getstring' /* Ask the user for a keyword */
  IF (result = "") | (result = "RESULT") THEN DO
    'okay1' 'No word to search for !'
  	EXIT
  END
END
word = result

IF ~SHOW('L','amigaguide.library') THEN CALL ADDLIB('amigaguide.library',0,-30)

Open("DirFile",""auto_path"/E_AutoGuide.ref","R")

/* Every line of the .ref file constists of to parts: the keyword and the node.
** The parts are separated with a space.
** The loop scans the .ref file and if he found a match he stores the node.
*/

node=""
DO WHILE ~EOF("DirFile") & node="" 
  line=ReadLn("DirFile")
  PARSE VAR line pword " " pnode
  IF COMPARE(UPPER(pword),UPPER(word))=0 THEN node=pnode
END 
IF node="" THEN DO
  'okay1' word' is not referenced!'
  EXIT 
END
ELSE DO
  'okay1' 'Found 'pword'! Loading relevant node.'
END

'QUERY SCREEN VAR SCREEN'

IF ~show('P','E_AUTODOCS_CED') THEN DO
  /* No Amigaguide process running. Create new one with port name 'E_AUTODOCS_CED' */
  cmd = 'run >NIL: amigaguide database "'auto_path'/e_autoguide.guide" DOCUMENT 'node' PUBSCREEN CygnusEdScreen1 PORTNAME E_AUTODOCS_CED'
  ADDRESS COMMAND cmd
END
ELSE DO
  /* Amigaguide viewer already running. Signal that we wanna show a new node. */
  cmd = 'Link 'node''
  ADDRESS E_AUTODOCS_CED cmd
  ADDRESS 'windowtofront'
END
EXIT
