/*
**                 EAutoGuide.amis, by Mathieu Dhondt,
**                    is a modification for AMIS of
**            get_EAutoGuide.rexx, written by Sven Steiniger,
**                    which was based on the idea of
**                  Eric BURGHARD's ged_guideref.rexx.
*/

ARG auto_path

OPTIONS RESULTS

IF (LEFT(ADDRESS(), 8) ~= "AMISPORT") THEN DO
  SAY 'Fire me up from AMIS!'
  EXIT
END 

/* if ADDRESS()="REXX" THEN
   ADDRESS 'AMISPORT.1' */

'GetWord' /* Get the word under AMIS' cursor */

IF (result = 10) THEN DO
    'Request BODY="No editor window opened/selected!"'
  	EXIT
END
IF (result = "") | (result = "RESULT") THEN DO
    'Request BODY="No word to search for !"'
  	EXIT
END

word = result

IF ~SHOW('L','amigaguide.library') THEN CALL ADDLIB('amigaguide.library',0,-30)

Open("DirFile",""auto_path"/E_AutoGuide.ref","R")


/* Every line of the .ref file constists of to parts: the keyword and the node.
** The parts are separated with a space.
** The loop scans the .ref file and if he found a match he stores the node.
*/

node=""
DO WHILE ~EOF("DirFile") & node="" 
  line=ReadLn("DirFile")
  PARSE VAR line pword " " pnode
  IF COMPARE(UPPER(pword),UPPER(word))=0 THEN node=pnode
END 
IF node="" THEN DO
  'Request BODY="%s is not referenced!" VAR='word
  EXIT 
END 
ELSE DO
  'Request BODY="%s found. Loading relevant node." VAR='word
END

'QUERY SCREEN VAR SCREEN'

IF ~show('P','E_AUTODOCS_AMIS') THEN DO 
/* No Amigaguide process running. Create new one with port name 'E_AUTODOCS_AMIS' */
  cmd = 'run >NIL: amigaguide database "'auto_path'/e_autoguide.guide" DOCUMENT 'node' PUBSCREEN AMIS.1 PORTNAME E_AUTODOCS_AMIS'
  ADDRESS COMMAND cmd 
END 
ELSE DO
  /* Amigaguide viewer already running. Signal that we wanna show a new node. */
  cmd = 'Link 'node''
  ADDRESS E_AUTODOCS_AMIS cmd
  ADDRESS E_AUTODOCS_AMIS 'windowtofront'
END 
EXIT
