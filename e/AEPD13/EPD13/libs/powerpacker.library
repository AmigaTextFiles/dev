det.
 ==============================================================================*/
    Gt_SetGadgetAttrsA(gad,win,NIL,[GTLV_LABELS,-1,TAG_DONE,0])
ENDPROC
PROC p_UnLockListView(gad,win,list) /*"p_UnLockListView(gad,win,list)"*/
/*===============================================================================
 = Para         : Address of gadget (gadget),address of window (winodw),address of list (lh)
 = Return       : NONE.
 = Description  : Unlock the LISTVIEW if the list is not empty.
 ==============================================================================*/
    IF p_EmptyList(list)<>-1
        Gt_SetGadgetAttrsA(gad,win,NIL,[GA_DISABLED,FALSE,GTLV_LABELS,list,TAG_DONE,0])
    ELSE
        Gt_SetGadgetAttrsA(gad,win,NIL,[GA_DISABLED,TRUE,GTLV_LABELS,-1,TAG_DONE,0])
    ENDIF
ENDPROC

PROC p_LookAllMessage() /*"p_LookAllMessage()"*/
/*===============================================================================
 = Para         : NONE.
 = Return       : NONE.
 = Description  : Wait events on (window port and CTRL C/D/E/F ).
 ==============================================================================*/
    DEF sigreturn
    DEF tlport:PTR TO mp
    IF tl_window THEN tlport:=tl_window.userport ELSE tlport:=NIL
    sigreturn:=Wait(Shl(1,tlport.sigbit) OR
                    $F000)
    IF (sigreturn AND Shl(1,tlport.sigbit))
        p_LooktlMessage()
    ENDIF
    IF (sigreturn AND $F000)
        reelquit:=TRUE
    ENDIF
ENDPROC
PROC p_LooktlMessage() /*"p_LooktlMessage()"*/
/*===============================================================================
 = Para         : NONE
 = Return       : NONE
 = Description  : IDCMP Events.
 ==============================================================================*/
   DEF mes:PTR TO intuimessage
   DEF g:PTR TO gadget
   DEF gstr:PTR TO stringinfo
   DEF type=0,infos=NIL
   DEF ndn:PTR TO datanode
   DEF nd:PTR TO ln
   DEF pv[256]:STRING
   DEF countnode
   IF mes:=Gt_GetIMsg(tl_window.userport)
       type:=mes.class
       SELECT type
           CASE IDCMP_MENUPICK
              infos:=mes.code
              SELECT infos
              ENDSELECT
           CASE IDCMP_CLOSEWINDOW
               reelquit:=TRUE
           CASE IDCMP_GADGETUP
              g:=mes.iaddress
              infos:=g.gadgetid
              SELECT infos
                  CASE GA_G_ADD
                      p_LockListView(g_list,tl_window)
                      ndn:=New(SIZEOF datanode)
                      ndn.data:=String(EstrLen('(New)'))
                      StrCopy(ndn.data,'(New)',ALL)
                      currentnode:=p_AjouteNode(mylist,'(New)',ndn)
                      p_UnLockListView(g_list,tl_window,mylist)
                      p_RendertlWindow()
                  CASE GA_G_REM
                      p_LockListView(g_list,tl_window)
                      ndn:=p_GetAdrNode(mylist,currentnode)
                      IF ndn.data THEN DisposeLink(ndn.data)
                      currentnode:=p_EnleveNode(mylist,currentnode,-1,[14,DISL,DISE])
                      p_UnLockListView(g_list,tl_window,mylist)
                      p_RendertlWindow()
                  CASE GA_G_UP
                      p_LockListView(g_list,tl_window)
                      currentnode:=p_DoUpNode(mylist,currentnode)
                      p_UnLockListView(g_list,tl_window,mylist)
                      p_RendertlWindow()
                  CASE GA_G_DOWN
                      p_LockListView(g_list,tl_window)
                      currentnode:=p_DoDownNode(mylist,currentnode)
                      p_UnLockListView(g_list,tl_window,mylist)
                      p_RendertlWindow()
                  CASE GA_G_SORT
                      p_LockListView(g_list,tl_window)
                      p_SortList(mylist)
                      p_UnLockListView(g_list,tl_window,mylist)
                      p_RendertlWindow()
                  CASE GA_G_GETADR
                      nd:=p_GetAdrNode(mylist,currentnode)
                      RtEZRequestA('Address :\h[8]\n'+
                                  'Succ    :\h[8]\n'+
                                  'Pred    :\h[8]\n','Ok',0,[nd,nd.succ,nd.pred],[RT_LOCKWINDOW,TRUE,RT_WINDOW,tl_window,TAG_DONE,0])
                  CASE GA_G_GETNUM
                      RtEZRequestA('Current Node Number:\d','Ok',0,[currentnode],[RT_LOCKWINDOW,TRUE,RT_WINDOW,tl_window,TAG_DONE,0])
                  CASE GA_G_COUNT
                      countnode:=p_CountNodes(mylist)
                      RtEZRequestA('Number Of node(s):\d','Ok',0,[countnode],[RT_LOCKWINDOW,TRUE,RT_WINDOW,tl_window,TAG_DONE,0])
                  CASE GA_G_DATA
                      p_LockListView(g_list,tl_window)
                      gstr:=g.specialinfo
                      ndn:=p_GetAdrNode(mylist,currentnode)
                      IF ndn.data THEN DisposeLink(ndn.data)
                      StringF(pv,'\s',gstr.buffer)
                      ndn.data:=String(EstrLen(pv))
                      StrCopy(ndn.data,pv,ALL)
                      p_UnLockListView(g_list,tl_window,mylist)
                      p_RendertlWindow()
                  CASE GA_G_LIST
                      currentnode:=mes.code
                      p_RendertlWindow()
                  CASE GA_G_NODENAME
                      p_LockListView(g_list,tl_window)
                      gstr:=g.specialinfo
                      nd:=p_GetAdrNode(mylist,currentnode)
                      IF nd.name THEN DisposeLink(ndn.data)
                      StringF(pv,'\s',gstr.buffer)
                      nd.name:=String(EstrLen(pv))
                      StrCopy(nd.name,pv,ALL)
                      p_UnLockListView(g_list,tl_window,mylist)
                      p_RendertlWindow()
              ENDSELECT
       ENDSELECT
       Gt_ReplyIMsg(mes)
   ENDIF
ENDPROC
PROC p_MakeCurrentDirList(dir) /*"p_MakeCurrentDirList()"*/
/*===============================================================================
 = Para         : directory.
 = Return       : NONE.
 = Description  : Just add some node to the list.