.KEY largs/K,path/A/K,src/A/K,cpu/K,lib/S,nostartup/S,nostd/S,symbol/S,noexe/S,delete/S,module/S,trace/S

.BRA {
.KET }

;rE - generatoRe  (051224-080503) (C) /\/\.A.

;derived from: YAEC 1.5a - generate.script  (010224) / Leif 2001

CD "{path}"

IF {nostartup} EQ 0 VAL
  SET ESTART   " "
ELSE
  SET ESTART   "re:lib/{cpu}/startup.o"
ENDIF
IF {lib} EQ 0 VAL
  SET LSTART   "re:lib/{cpu}/libfuncs.o"
ELSE
  SET LSTART   " "
ENDIF
IF {trace} EQ 0 VAL
  SET TRACE   "re:lib/{cpu}/trace.o"
ELSE
  SET TRACE   " "
ENDIF

IF {cpu} EQ 68K
   IF {symbol} EQ 0 VAL
     SET SYMARG DS
   ELSE
     SET SYMARG " "
   ENDIF
   SET COMPILER re:bin/OptiRe_68K "{src}.re"
   SET DEFAARGS NOEXE OPT nrqbtlps $SYMARG
   SET ASSEMBLER PHXASS >t:op $DEFAARGS "{src}.ass" TO "{src}.o"
   SET DEFLARGS SC SD noshortrelocs
   SET LINKER PHXLNK >t:op $DEFLARGS FROM $ESTART $LSTART $TRACE @{largs} "{src}.o" TO "{src}"
ELSE
   IF {symbol} NOT EQ 0 VAL
     SET SYMARG -s
   ELSE
     SET SYMARG " "
   ENDIF
   SET COMPILER re:bin/OptiRe_wup "{src}.re"
   SET DEFAARGS -F2 -mo -E 0 -I re:lib/
   SET ASSEMBLER pasm >t:op $DEFAARGS "{src}.ass" -o "{src}.o"
   SET DEFLARGS -bamigaehf $SYMARG -x -sc -sd
   SET LINKER vlink >t:op $DEFLARGS -o "{src}" $ESTART $LSTART $TRACE "{src}.o" -F {largs}
ENDIF
SET ASMERR   "Assembler error !"
SET LNKERR   "Linker error !"

FAILAT 21 ; script takes care of errors

;IF {mode} EQ 4544581
   ECHO "Compiling/Optimizing... " NOLINE
   $COMPILER
   IF WARN
      SKIP err
   ENDIF
   ECHO "*EM*EEGenerating executable..." NOLINE
   $ASSEMBLER
   IF WARN
      ECHO $ASMERR
      SKIP err
   ENDIF
   ECHO "*EM*EEAssembled OK...         " NOLINE
   IF {noexe} NOT EQ 0 VAL
      $LINKER
      IF $RC NOT EQ 0
         ECHO $LNKERR
         SKIP err
      ENDIF
      ECHO "*EM*EELinked OK.                "
   ENDIF
   SKIP ok
;ENDIF

ECHO "Generate error ! - unknown target." ; we shouldnt get here..

LAB err
TYPE t:op

ECHO "There was errors in generating target.                     "

SKIP end

LAB ok

ECHO "No Errors.                                                 "

DELETE t:op >NIL:

LAB end ; cleanup

UNSET TRACE
UNSET ESTART
UNSET LSTART
UNSET SYMARG
UNSET DEFAARGS
UNSET DEFLARGS
UNSET COMPILER
UNSET ASSEMBLER
UNSET LINKER
UNSET ASMERR
UNSET LNKERR
IF {delete} EQ 0 VAL
  IF {module} NOT EQ 0 VAL
    DELETE "{src}.o"
  ENDIF
  DELETE "{src}.ass"
  DELETE "{src}.re"
ENDIF
FAILAT 10

QUIT

