; d0 - length
;; partially ripped from Wouters van Oortmerssen's EC_733.S source

	xdef	_String

_String
	movem.l	d2-d3/a0,-(a7)
	CMP.L	#$7FF0,D0
	BPL.S	.1
	MOVE.L	D0,D3
	ADD.L	#17,D0		; 8 (LIST) + 4 (LINK) + 4 (LENGTHS) + 1 (ZERO)
	MOVE.L	#$50001,D1	; MEMF_CLEAR | MEMF_REVERSE | MEMF_PUBLIC
	MOVE.L	D0,D2
	MOVE.L	4.W,A6
	tst.l	___epool
	bne.s	.3
	JSR	-198(A6)	; AllocMem
	TST.L	D0
	BEQ.S	.1
	MOVE.L	D0,A0
	MOVE.L	___memlist,(A0)	; put pred in header
	MOVE.L	D2,4(A0)	; store total len
	MOVE.L	D0,___memlist	; this becomes new head of list
.4	MOVE.W	D3,12(A0)
	ADDQ.L	#8,D0
	ADDQ.L	#8,D0
	bra.s	.2
.1	MOVEQ	#0,D0
.2	movem.l	(a7)+,d2-d3/a0
	RTS
; if pool then use allocpooled
.3	move.l	___epool,a0
	jsr	-708(a6)
	tst.l	d0
	beq.s	.1
	move.l	d0,a0
	bra.s	.4

	xref	___epool
	xref	___memlist


