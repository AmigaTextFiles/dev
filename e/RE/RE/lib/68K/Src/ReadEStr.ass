; (8,a7) - fh
; (4,a7) - estr

	xdef	_ReadEStr

_ReadEStr	movem.l	d2-d4/d6-d7/a2/a6,-(a7)
	move.l	(4+28,a7),a2		; estr
	move.l	a2,d4
	move.l	(8+28,a7),d7		; fh
	moveq	#0,d6
	move.w	(-4,a2),d6		; maxlen
	add.l	a2,d6
	move.l	_DOSBase,a6
	moveq	#1,d3
.1:	cmp.l	d6,a2
	beq.s	.4
	move.l	d7,d1
	move.l	a2,d2
	addq.l	#1,a2
	jsr	(-42,a6)			; Read()
	cmp.w	#1,d0
	bmi.s	.5
	cmp.b	#10,(-1,a2)
	bne.s	.1
	moveq	#0,d0
	subq.l	#1,a2
.4:	move.l	a2,d1
	clr.b	(a2)+
	sub.l	d4,d1
	move.l	d4,a0
	move.w	d1,(-2,a0)
	movem.l	(a7)+,d2-d4/d6-d7/a2/a6
	rts
.5:	subq.l	#1,a2
	moveq	#-1,d0
	bra.s	.4

	xref	_DOSBase
