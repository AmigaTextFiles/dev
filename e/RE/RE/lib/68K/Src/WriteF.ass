	xdef	_VWriteF
	xdef	_WriteF
_VWriteF:
_WriteF:
WriteF_a	equ	12
WriteF_b	equ	8
	link	a5,#0
*	movem.l	a0,-(a7)
	move.l	_stdout,d1
	bne	.else
	lea	(.str,pc),a0
	move.l	a0,d1
	move.l	#1006,d2
	movea.l	_DOSBase,a6
	jsr	-30(a6)		; Open()
	tst.l	d0
	bne.s	.write
.exit	moveq	#20,d1
	jsr	(-144,a6)	; Exit()
*	movem.l	(a7)+,a0
	unlk	a5
	rts
.write	move.l	d0,_conout
	move.l	d0,_stdout

.else	move.l	WriteF_a(a5),a0
	move.l	WriteF_b(a5),a1
	lea	(.copych,pc),a2
	lea	(-512,a7),a7	; reserve space on stack
	move.l	a7,a3
	move.l	a3,d2
	movea.l	4.w,a6
	jsr	-522(a6)	; RawDoFmt()
	move.l	_stdout,d1
	move.l	d2,a0
.loop	tst.b	(a0)+
	bne.s	.loop
	sub.l	d2,a0
	move.l	a0,d3
	subq.l	#1,d3
	cmpi.l	#512,d3
	bge.s	.exit
	move.l	_DOSBase,a6
	jsr	(-48,a6)	; Write()
	move.l	d3,d0
	lea	(512,a7),a7
*	movem.l	(a7)+,a0
	unlk	a5
	rts
.copych	move.b	d0,(a3)+
	rts

.str	dc.b	"con:///80/Output/CLOSE",0
	cnop	0,2

	xref	_DOSBase
	xref	_conout
	xref	_stdout
