#rE
#pasm -F 2 -mo -O -1 -I re:lib/ re:lib/PPC/Src/Mem.ass

	.include	ppcmacros.pasm
	.text
	.sdreg	r2
	.align	2

#d0 - size
	.global	_ReNewR
_ReNewR:
	link	a5,0
	push	d0	# store size

#IFN _repool THEN IFN _repool:=CreatePoolPPC($10005,$2000,$1000) THEN Raise("MEM")
	law	a0,__repool
	lwz	d0,0(a0)
	tstw	d0
	bne	ReNewR_else
	liw	r4,65541
	liw	r5,8192
	liw	r6,4096
	law	a6,_PowerPCBase
	lwz	r3,0(a6)
	lwz	a6,-816+2(r3)
	mtlr	a6
	blrl

	law	a0,__repool
	stw	d0,0(a0)
	tstw	d0
	beq	ReNewR_Raise
#IFN mem:=AllocVecPooled(_repool,size) THEN Raise("MEM")
ReNewR_else:
	mr	a0,d0
	pop	d0	# restore size
	bl	_AllocVecPooled
	tstw	d0
	bne	ReNewR_end
ReNewR_Raise:
	liw	d0,5064013
	liw	d1,0
	bl	_Raise
ReNewR_end:
	unlk	a5
	blr

	.type	_ReNewR,2
	.size	_ReNewR,$-_ReNewR




#a1 - mem
	.global	_ReDispose
_ReDispose:
	link	a5,0
#IF mem THEN FreeVecPooled(_repool,mem)
	tstw	a1
	beq	ReDispose_else
#FreeVecPooled(_repool,mem)
	law	a0,__repool
	lwz	a0,0(a0)
		#a1 contains mem
	bl	_FreeVecPooled
ReDispose_else:
	unlk	a5
	blr

	.type	_ReDispose,2
	.size	_ReDispose,$-_ReDispose





	.global	_ReDisposeAll
_ReDisposeAll:
	link	a5,0
#DeletePoolPPC(_repool)
	law	a0,__repool
	lwz	r4,0(a0)
	tstw	r4
	beq	ReDisposeAll_else
	law	a6,_PowerPCBase
	lwz	r3,0(a6)
	lwz	a6,-822+2(r3)
	mtlr	a6
	blrl
#_repool:=0
	liw	r0,0
	law	a0,__repool
	stw	r0,0(a0)
ReDisposeAll_else:
	unlk	a5
	blr

	.type	_ReDisposeAll,2
	.size	_ReDisposeAll,$-_ReDisposeAll




#a0 - pool
#d0 - size

	.global	_AllocVecPooled
_AllocVecPooled:
	link	a5,0
#IF mem:=AllocPooledPPC(pool,size+SIZEOF LONG)
	mr	r4,a0
	addiw	r5,d0,4
	push	r5	# store size
	law	a6,_PowerPCBase
	lwz	r3,0(a6)
	lwz	a6,-828+2(r3)
	mtlr	a6
	blrl
	tstw	d0
	beq	AllocVecPooled_else
#mem[]++:=size
	pop	r5	# restore size
	stw	r5,0(d0)
	addi	d0,d0,4
AllocVecPooled_else:
#ENDPROC mem
	unlk	a5
	blr

	.type	_AllocVecPooled,2
	.size	_AllocVecPooled,$-_AllocVecPooled





#a0 - pool
#a1 - mem

	.global	_FreeVecPooled
_FreeVecPooled:
	link	a5,0
#FreePooledPPC(pool,mem-4,mem[-1])
	mr	r4,a0
	subiw	r5,a1,4
	lwz	r6,0(r5)
	law	a6,_PowerPCBase
	lwz	r3,0(a6)
	lwz	a6,-834+2(r3)
	mtlr	a6
	blrl
	unlk	a5
	blr

	.type	_FreeVecPooled,2
	.size	_FreeVecPooled,$-_FreeVecPooled



	.extern	_Raise
	.align	2
	.global	__repool
__repool:	.ualong	0



	.section .tocd
	.extern	_PowerPCBase_
	.global	_ReNewR_
_ReNewR_:	.long	_ReNewR
	.global	_ReDispose_
_ReDispose_:	.long	_ReDispose
	.global	_ReDisposeAll_
_ReDisposeAll_:	.long	_ReDisposeAll
	.global	_AllocVecPooled_
_AllocVecPooled_:	.long	_AllocVecPooled
	.global	_FreeVecPooled_
_FreeVecPooled_:	.long	_FreeVecPooled
	.global	__repool_
__repool_:	.long	__repool

