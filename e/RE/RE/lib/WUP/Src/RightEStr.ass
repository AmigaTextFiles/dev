
# RightEStr(a0:PTR TO CHAR,a1:PTR TO CHAR,d0:LONG)

	.include	ppcmacros.pasm
	.text
	.global	_RightEStr

_RightEStr:
.set	len,r8
.set	maxlen,r8
	mr	a6,a0		# store address
	tstw	d0
	bge	.do
	li	d0,0		# IF d0<0 THEN d0:=0
.do:	lhz	len,-2(a1)
	cmplw	d0,len
	blt	.skip0
	mr	d0,len		# Min(len,d0)
.skip0:
	add	a1,a1,len
	sub	a1,a1,d0	# a1 := (a1+len)-d0

	li	d0,-1
	lhz	maxlen,-4(a0)
	cmplw	d0,maxlen
	blt	.skip
	mr	d0,maxlen	# Min(maxlen,d0)
.skip:
	mtctr	d0
	subi	a0,a0,1
	subi	a1,a1,1
.loop:	lbzu	r0,1(a1)	# get char from source
	mr.	r0,r0		# source end?
	beq	.end
	stbu	r0,1(a0)	# put char to destination
	bdnz	.loop

.end:	li	r0,0		# finish with zero byte
	stbu	r0,1(a0)
	sub	len,a0,a6
	sth	len,-2(a6)
	mr	d0,a6
	blr			# d0 contains destination string pointer

	.type	_RightEStr,@function
	.size	_RightEStr,$-_RightEStr
