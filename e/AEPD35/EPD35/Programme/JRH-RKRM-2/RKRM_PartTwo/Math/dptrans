lf.file ,
										'->/////////////////////////////////////////////////////////////////////////////\n' +
										'->///////////////////////////////////////////////////////// DEF ... = ... /////\n' +
										'->/////////////////////////////////////////////////////////////////////////////\n'	)

				first_global_var := FALSE

			ENDIF

			Fputs( self.file , 'EXPORT DEF ' )
			Fputs( self.file , self.vars[ i ].ident )
			FputC( self.file , "\n" )

		ENDIF

	ENDFOR

	IF locale

		IF first_global_var

			IF environment THEN	Fputs( self.file ,
									'->/////////////////////////////////////////////////////////////////////////////\n' +
									'->///////////////////////////////////////////////////////// DEF ... = ... /////\n' +
									'->/////////////////////////////////////////////////////////////////////////////\n'	)

			first_global_var := FALSE

		ELSE

			FputC( self.file , "\n" )

		ENDIF

		Fputs( self.file , 'EXPORT DEF cat : PTR TO ' )
		Fputs( self.file ,  catalog_name )
		FputC( self.file , "\n" )

	ENDIF

	IF first_global_var = FALSE THEN Fputs( self.file , '\n\n' )

ENDPROC


->/////////////////////////////////////////////////////////////////////////////
->/////////////////////////////////////////// PROC put_create_declaration /////
->/////////////////////////////////////////////////////////////////////////////
PROC put_create_declaration( application ) OF gui_file

	Fputs( self.file ,
		'->/////////////////////////////////////////////////////////////////////////////\n'	+
		'->/////////////////////////////////////////////////////////// PROC create /////\n'	+
		'->/////////////////////////////////////////////////////////////////////////////\n'	+
		'PROC create('																		)

	IF application

		IF self.hook_funcs

			Fputs( self.file , ' display : PTR TO ' )
			Fputs( self.file , self.main_object_ident )
			Fputs( self.file , '_display ,\n            ' )

		ENDIF

		Fputs( self.file ,
			' icon  = NIL ,\n'									+
			'             arexx = NIL : PTR TO app_arexx ,\n'	+
			'             menu  = NIL '							)

	ELSE

		IF self.hook_funcs

			Fputs( self.file , ' display : PTR TO ' )
			Fputs( self.file , self.main_object_ident )
			Fputs( self.file , '_display ' )

		ENDIF

	ENDIF

	Fputs( self.file , ') OF ' )
	Fputs( self.file , self.main_object_ident )
	Fputs( self.file , '_obj\n\n' )

ENDPROC


->/////////////////////////////////////////////////////////////////////////////
->//////////////////////////////////////////// PROC put_create_local_defs /////
->/////////////////////////////////////////////////////////////////////////////
PROC put_create_local_defs() OF gui_file

	DEF i , defs_string[ 70 ] : STRING , local_vars = FALSE

	FOR i := 0 TO ( self.number_vars - 1 )

		IF self.vars[ i ].type = TYPEVAR_LOCAL_PTR

			local_vars := TRUE

			IF ( EstrLen( defs_string ) + StrLen ( self.vars[ i ].ident ) ) <= 57

				IF EstrLen( defs_string ) <> 0 THEN StrAdd( defs_string , ' , ' )
				StrAdd( defs_string , self.vars[ i ].ident )

			ELSE

				Fputs( self.file, '\tDEF ' )
				Fputs( self.file, defs_string )
				FputC( self.file , "\n" )
				SetStr( defs_string , 0 )
				StrAdd( defs_string , self.vars[ i ].ident )

			ENDIF

		ENDIF
			
	ENDFOR

	IF EstrLen( defs_string ) <> 0

		Fputs( self.file , '\tDEF ' )
		Fputs( self.file , defs_string )
		FputC( self.file , "\n" )

	ENDIF

	IF local_vars THEN FputC( self.file , "\n" )

ENDPROC


->/////////////////////////////////////////////////////////////////////////////
->/////////////////////////////////////// PROC put_create_initialisations /////
->/////////////////////////////////////////////////////////////////////////////
PROC put_create_initialisations( locale , getstring_func : PTR TO CHAR ) OF gui_file

	DEF i , j , initialisations = FALSE
	DEF strptr : PTR TO CHAR

	FOR i := 0 TO ( self.number_vars - 1 )

		IF self.vars[ i ].type = TYPEVAR_STRING

			initialisations := TRUE

			Fputs( self.file , '\tself.' )
			Fputs( self.file , self.vars[ i ].ident )
			indent_defs( self.file , self.vars[ i ].ident , self.ident_length_max )
			Fputs( self.file , ' := ' )

			IF Char( self.vars[ i ].init ) <> 0

				IF locale

					Fputs( self.file , 'cat.' )
					Fputs( self.file , self.vars[ i ].init )
					FputC( self.file , "." )
					Fputs( self.file , getstring_func )
					Fputs( self.file , '()\n' )

				ELSE

					Fputs( self.file , string_convert( self.vars[ i ].init ) )
					FputC( self.file , "\n" )

				ENDIF

			ELSE

				Fputs( self.file , 'NIL\n' )

			ENDIF

		ENDIF

	ENDFOR

	FOR i := 0 TO ( self.number_vars - 1 )

		IF self.vars[ i ].type = TYPEVAR_TABSTRING

			initialisations := TRUE

			Fputs( self.file , '\tself.' )
			Fputs( self.file , self.vars[ i ].ident )
			indent_defs( self.file , self.vars[ i ].ident , self.ident_length_max )
			Fputs( self.file , ' := [\n' )
			strptr := self.vars[ i ].init

			FOR j := 1 TO self.vars[ i ].size

				Fputs( self.file , '\t\t' )

				IF locale

					Fputs( self.file , 'cat.' )
					Fputs( self.file , strptr )
					FputC( self.file , "." )
					Fputs( self.file , getstring_func )
					Fputs( self.file , '() ,\n' )

				ELSE

					Fputs( self.file , string_convert( strptr ) )
					Fputs( self.file , ' ,\n' )

				ENDIF

				strptr := strptr + StrLen( strptr ) + 1

			ENDFOR

			Fputs( self.file , '\t\tNIL ]\n' )

		ENDIF

	ENDFOR

	IF initialisations THEN FputC( self.file , "\n" )

ENDPROC


->/////////////////////////////////////////////////////////////////////////////
->///////////////////////////////////////////////////////// PROC put_code /////
->/////////////////////////////////////////////////////////////////////////////
PROC put_code( getstring_func : PTR TO CHAR , muistrings : PTR TO LONG ) OF gui_file

	DEF type , code
	DEF indent_level = 1 , func_level = 0
	DEF return = TRUE , objfunction = FALSE , inobj = FALSE
	DEF makeobject = 0

	Mb_GetNextCode( {type} , {code} )

	WHILE type <> -1

		SELECT type

			CASE TC_CREATEOBJ

				indent_code( self.file , indent_level , return )
				Fputs( self.file , muistrings[ Val( code ) ] )
				Fputs( self.file , ' ,\n' )
				INC indent_level
				return := TRUE
				inobj := TRUE

				IF Val( code ) = 113

					indent_code( self.file , indent_level , return )
					Fputs( self.file , '( IF icon THEN MUIA_Application_DiskObject ELSE TAG_IGNORE ) , icon ,\n' )

					indent_code( self.file , indent_level , return )
					Fputs( self.file , '( IF arexx THEN MUIA_Application_Commands ELSE TAG_IGNORE ) , ( IF arexx THEN arexx.commands ELSE NIL ) ,\n' )

					indent_c