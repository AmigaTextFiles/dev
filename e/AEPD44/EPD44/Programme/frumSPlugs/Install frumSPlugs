;------------------------------------------------------------------------
;
; Installer script for frumSPlugs
;
; $VER: Install frumSPlugs 1.0 (10.5.96)
;
; (C)1996 Stephen Sinclair
;
;------------------------------------------------------------------------

(onerror (CLEANUP))

;------------------------------------------------------------------------
(set @app-name "frumSPlugs")

;------------------------------------------------------------------------
(set #wrongversion "You need at least version 42.9 of Installer!")
(set #welcome "Welcome to the installation of frumSPlugs!")
(set #no-diskspace "You need more HD space.")

;------------------------------------------------------------------------
(procedure CLEANUP
  (delete (tackon @default-dest ".tempinstall") (all) (optional "force"))
) ; CLEANUP

;------------------------------------------------------------------------

(welcome #welcome)
(complete 0)

(set moddir
  (askdir
    (prompt
      (cat "Select a directory in which to install the Amiga E module files.  "
           "A directory named 'Plugins' will be created there if it doesn't "
           "exist already."))
    (default "EModules:")
    (help @askdir-help)
  )
)

(set plugdir (tackon moddir "Plugins"))

(if (= (exists plugdir) 0)
  (makedir plugdir)
)

(copyfiles
  (prompt "Choose Plugin Modules to install:")
  (help   @copyfiles-help)
  (source "Modules/Plugins")
  (dest   plugdir)
  (all)
  (confirm)
)

(set docdir
  (askdir
    (prompt "Select directory for frumSPlugins.guide:")
    (default "E:Docs")
    (help @askdir-help)
  )
)

(copyfiles
  (source "frumSPlugs.guide")
  (dest   docdir)
  (infos)
)

(set instxpl
  (askbool
    (prompt "\n\n\nWould you like to install the examples?")
    (default 1)
    (choices "Yes" "No")
    (help @askbool-help)
  )
)

(if instxpl
  (
    (set xpldir
      (askdir
        (prompt "Select directory for examples.  A drawer called 'PluginExamples' will be created there.")
        (default "E:Src/Tools/EasyGUI")
        (help @askdir-help)
      )
    )
    (makedir (tackon xpldir "PluginExamples"))
    (copyfiles
      (source "Examples")
      (dest   (tackon xpldir "PluginExamples"))
      (all)
    )
  )
)

(CLEANUP)
(exit)

