 /*  Ecompile.edge  -  Robert Bromley  -  March 6, 1995
     Run the AmigaE compiler from within the Edge text editor.
     On my system, this macro is mapped to <Alternate - E>                */

                    /* change these 2 variables to reflect your own setup */
working_dir   = 'RAD:'
compiler_path = 'DH1:Amiga_E_V3.1a/Bin/EC'

OPTIONS RESULTS

                     /* get the name of the source file you're working on */
GetEnvVar _FE_DosName ; filename = RESULT
PARSE VAR filename Efile '.e'                      /* strip the extension */
Save                                       /* give EC the file to compile */

ADDRESS COMMAND
/* copy the compiler to your working directory, if it's not already there */
IF ~EXISTS(working_dir || 'EC') THEN 'Copy' compiler_path 'TO' working_dir

    /* delete the old version so it won't be Run if this one has an error */
IF EXISTS(Efile) THEN 'Delete' Efile

OPTIONS FAILAT 30

    /* ERRBYTE = return the byte offset, if there was a compilation error */
'RAD:EC ERRBYTE' Efile ; errorbyte = rc
IF EXISTS(Efile) THEN 'Run' Efile

ADDRESS
IF errorbyte > 0 THEN 'GoToByte' errorbyte      /* jump to point of error */
EXIT 0
