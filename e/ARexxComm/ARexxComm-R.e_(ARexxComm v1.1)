/* ARexxComm-R - a program to demo/test the module ARexxComm */

MODULE 'CSH/arexxcomm'

->how to create a port, send a message, wait for a message, & close a port
PROC main() HANDLE
 DEF portinfo=NIL:PTR TO rxcom, portname=NIL:PTR TO CHAR,
     command=NIL:PTR TO CHAR, args=NIL:PTR TO CHAR,
     cmdknown, quit=FALSE, wasack=FALSE
	portname:='ExampleHost'
	WriteF('ARexxComm-R program\nPress Ctrl-C to quit, or send the QUIT command\n')
	WriteF('1.To send a command type at another shell: Sys:RexxC/Rx "Address \a\s\a \aCOMMAND\a"\n2.Known commands: SAYYO, SAYHI, QUIT\n\n',portname)

	NEW portinfo.createPort(portname,{portinfo})
	IF portinfo
		IF portinfo.portExists('TestHost')
			WriteF('I see that "TestHost" is already running!\n')
		ELSE
			WriteF('Why not try running ARexxComm-S ("TestHost")?\n')
		ENDIF
		 ->portinfo.sendRexxMsg('GOLDED.1','UP')
		 ->portinfo.sendRexxMsg('REXX','scriptname')
		 ->portinfo.sendRexxMsg(portname,'QUIT')

		WriteF('Sent RexxMaster a command to run "list ram:"\n')
		portinfo.sendRexxMsg('REXX','"ADDRESS COMMAND \alist ram:\a"')

		REPEAT
			cmdknown:=FALSE
			command,args:=portinfo.waitForRexxMsg()			->get a command (uses zero CPU time)
			->command,args,wasack:=portinfo.waitForRexxMsg(TRUE)	->get a command (uses zero CPU time), may instead return wasack=TRUE (recieved acknowledge of our message)
			/*REPEAT						->get a command (uses LOTS of CPU time - "busy waiting")
				command,args,wasack:=portinfo.waitForRexxMsg(TRUE)
			UNTIL (command<>NIL) OR wasack*/

			IF wasack=TRUE		->don't need this IF statement if waitRexxMsg() is used above
				WriteF('  Happy! Happy! Joy! Joy!  Recieved acknowledgement our message was recieved!\n')
			ELSE
				 /* code to deal with commands */
				IF command				->you *must* check for this! (command=NIL if recieve Ctrl-C)
					WriteF('#I recieved the "\s" Rexx command.\n',command)
					IF StrCmp('SAYYO',command)
						WriteF('Yo')
						IF args[0]<>"\0" THEN WriteF(' "\s"!\n',args) ELSE WriteF('!\n')
						cmdknown:=TRUE
					ELSEIF StrCmp('SAYHI',command)
						WriteF('Hi')
						IF args[0]<>"\0" THEN WriteF(' "\s"!\n',args) ELSE WriteF('!\n')
						cmdknown:=TRUE
					ELSEIF StrCmp('QUIT',command)
						quit:=TRUE
						cmdknown:=TRUE
					ENDIF
					portinfo.ackRexxMsg(cmdknown)
				ELSE
					quit:=TRUE
				ENDIF
			ENDIF

			->this sends ourself a message that is never read (waited for or polled), as we quit soon after; this used to cause END to freeze miggy
			/*IF quit OR (command=FALSE)
				WriteF('Sent myself a SAYYO command\n')
				portinfo.sendRexxMsg(portname,'SAYYO ExampleHost')
			ENDIF*/
		UNTIL quit

		END portinfo
	ENDIF
EXCEPT
	WriteF('ERROR:  ')
	SELECT exception
		CASE "LIB";	WriteF('Could not open library "\s"!\n',exceptioninfo)
		CASE "PORT";	WriteF('Could not open ARexx port "\s"!\n',exceptioninfo)
		CASE "RXNA";	WriteF('Did not acknowledge previous ARexx message "\s"!\n',exceptioninfo)
		CASE "RXNM";	WriteF('No ARexx message to acknowledge!\n')
		CASE "RXCM";	WriteF('\s!\n')
		DEFAULT;	IF exceptioninfo
					WriteF('"\c"; \s.\n',exception,exceptioninfo)
				ELSE
					WriteF('"\c".\n',exception)
				ENDIF
	ENDSELECT
	IF portinfo THEN END portinfo		->only close port if it has not been closed already
ENDPROC
