/*
**  select.gadget PLUGIN f¸r NewGUI
**  Version: 1.5
**  PLUGIN © by Lemsman
**
**  NewGUI © 1994-1998 THE DARK FRONTIER Softwareentwicklung 
**          
*****************************************************************************  
** Anmerkung: Wenn die Tags SGA_QUIET und SGA_SYMBOLONLY der Taglist mit
**            ¸bergeben werden und beide auf TRUE gesetzt sind, sollte
**            das Parammeter "resize" auf FALSE gesetzt werden. 
**  \_|_/     Da sich das Symbol sowieso nicht in der Breite ‰ndern l‰st. 
**   o_o
**    |       Die Labeldefinition MUﬂ immer mit NIL beendet werden.
**   \_/      Da ansonsten das Ende der Labeldefinition NICHT erkannt wird
**            und es dadurch zum Absturz kommen kann.
*****************************************************************************   
*/
OPT     PREPROCESS,OSVERSION=37,LARGE

#define TEST        -> Wenn Test aktiviert ist wir ein Startf‰higes Programm
                    -> erzeugt. und wenn nicht dann das Plugin f¸r NewGUI

#ifndef TEST
OPT     MODULE
OPT     EXPORT
#endif

MODULE  'newgui/newgui'
MODULE  'Gadgets/Select','Gadgets/SelectClass'
MODULE  'tools/textlen',
        'intuition/intuition',
        'intuition/classes',
        'intuition/gadgetclass',
        'graphics/rastport',
        'utility/tagitem',
        'Libraries/GadTools',
        'Utility'


CONST   SELGAD = PLUGIN

OBJECT  selgad OF plugin
   selected:LONG            -> Gew‰hles Label 
PRIVATE
    sel_obj:PTR TO object   -> Adresse des selectgadget_Objects
    selectgadgetbase:LONG   -> 
    labels:PTR TO LONG      -> Labels
    tags:PTR TO LONG        -> Tags
    ausnahme:LONG           -> SGA_QUIT und SGA_SYMBOLONLY = TRUE
    resize:LONG             -> Grˆﬂenver‰nderbar (NewGUI FLAGS)
ENDOBJECT

#ifdef  TEST
DEF s1=NIL:PTR TO selgad
DEF s2=NIL:PTR TO selgad

PROC main() HANDLE
DEF tags=NIL , label=NIL
DEF tags2=NIL, label2=NIL

 tags:=[SGA_TEXTPLACE,          PLACETEXT_ABOVE,
        SGA_SEPARATOR,          TRUE,
        SGA_ITEMSPACING,        2,
        SGA_FOLLOWMODE,         SGFM_NONE,
        SGA_MINTIME,            60,
        SGA_MAXTIME,            60,
        SGA_PANELMODE,          SGPM_WINDOW,
        SGA_ACTIVE,             1,
        SGA_STICKY,             TRUE,
        SGA_ACTIVEBOX,          TRUE,
        SGA_BORDERSIZE,         TRUE,
        NIL]

tags2:=[SGA_SYMBOLONLY, TRUE, SGA_QUIET,TRUE,
        NIL]

    label:=['Eins','Zwei','Drei','Vierundvierzig',NIL]
    label2:=['Enabel', 'Disable', NIL]

    newguiA([
        NG_WINDOWTITLE, 'NewGUI-Plugin',     
        NG_GUI,
            [ROWS,
                [TEXT,    'Select.gadget Test', NIL, TRUE, 1],
                [SELGAD, {saction}, NEW s1.selgad(label,tags)],
                [SBUTTON, {toggle_enabled}, 'Disable'],
                [SELGAD, {saction2}, NEW s2.selgad(label2,tags2,NIL,FALSE)]
                    ]])
EXCEPT DO
    IF s1 THEN END s1
    IF s2 THEN END s2
    CleanUp(exception)
ENDPROC

PROC dummy() IS EMPTY
PROC toggle_enabled() 
    s2.disable(s2.dis=FALSE)
ENDPROC

PROC saction()
    WriteF('Label: \d gew‰hlt.\n',s1.selected)
    s2.setaction(s1.selected)
ENDPROC

PROC saction2()         
    IF s2.selected = 0 THEN s1.disable(FALSE)
    IF s2.selected = 1 THEN s1.disable(TRUE)
ENDPROC
#endif

PROC selgad(labels=NIL,tags=NIL,disabled=FALSE,resize=RESIZEX)  OF selgad

    utilitybase:=OpenLibrary('utility.library',36)
    selectgadgetbase:=OpenLibrary('Gadgets/select.gadget', 40)
    IF (selectgadgetbase=NIL) THEN selectgadgetbase := OpenLibrary('Gadgets/select.gadget',40)
    IF (selectgadgetbase=NIL) THEN selectgadgetbase := OpenLibrary('Classes/Gadgets/select.gadget',40)

    self.selectgadgetbase:=selectgadgetbase
    IF self.selectgadgetbase = NIL THEN Raise("sel")
    self.labels:=labels
    self.tags:=tags
    self.dis:=disabled
    self.selected:=GetTagData(SGA_ACTIVE, 0, tags)

/* ‹berpr¸fung der Tags SGA_QUIET und SGA_SYMBOLONLY.
** Wenn beide auf TRUE gesetzt sind tritt der Ausnahme Fall ein.
** Denn das Gadget ist dann NUR 21 bits groﬂ.
*/   
    IF (GetTagData(SGA_QUIET, FALSE, tags) = TRUE) AND (GetTagData(SGA_SYMBOLONLY, FALSE, tags) = TRUE) 
        self.ausnahme:=TRUE
    ELSE
        self.ausnahme:=FALSE
    ENDIF
    self.resize:=resize
ENDPROC

PROC end() OF selgad
    IF self.selectgadgetbase THEN CloseLibrary(self.selectgadgetbase)
ENDPROC

PROC min_size(ta,fh) OF selgad
DEF dd,err, check=FALSE, z=0,xw=NIL, d
/*
** Hier wird das l‰ngste Label String ermittelt und 
** als minimale Weite angesehen
*/
    IF self.ausnahme = FALSE
        REPEAT
            dd:=self.labels[z]
            IF dd = NIL
                check:=TRUE
            ELSE
                d:=textlen(self.labels[z],ta)
                IF d > xw THEN xw:=d
            ENDIF
            IF CtrlC() = TRUE THEN check:=TRUE
            INC z
        UNTIL check=TRUE
        xw:=xw+34   /* Die Symbolbreite und ein kleiner freiraum  muﬂ mit eingerechnet werden */
    ELSE
        xw:=21 
    ENDIF
ENDPROC xw,fh+8

PROC will_resize() OF selgad IS self.resize

PROC render(ta,x,y,xs,ys,win:PTR TO window) OF selgad

    self.sel_obj:=NewObjectA(NIL, 'selectgclass',
                    [GA_TOP,        y,  GA_LEFT,        x,                
                    GA_WIDTH,       xs, GA_HEIGHT,      ys,
                    GA_RELVERIFY,   TRUE,
                    GA_DISABLED,    self.dis,
                    SGA_LABELS,     self.labels,
                    TAG_MORE,       self.tags,
                    NIL])
    IF self.sel_obj = NIL THEN Raise("sel")

    AddGList(win,self.sel_obj ,-1, -1, NIL) 
    RefreshGList(self.sel_obj, win, NIL, -1)
ENDPROC

PROC clear_render(win:PTR TO window) OF selgad
  IF self.sel_obj
    RemoveGList(win,self.sel_obj,1)
    DisposeObject(self.sel_obj)
  ENDIF
ENDPROC

PROC message_test(imsg:PTR TO intuimessage,win:PTR TO window) OF selgad
    IF imsg.class=IDCMP_GADGETUP THEN RETURN imsg.iaddress=self.sel_obj
ENDPROC FALSE

PROC message_action(class,qual,code,win:PTR TO window) OF selgad
DEF dd=NIL
    GetAttr(SGA_ACTIVE, self.sel_obj, {dd})
    self.selected:=dd
    
ENDPROC TRUE

PROC disable(disabled=TRUE) OF selgad
    SetGadgetAttrsA(self.sel_obj, self.gh.wnd, NIL,[GA_DISABLED, disabled,NIL])
    self.dis:=disabled
ENDPROC

PROC setaction(nr=0) OF selgad
    SetGadgetAttrsA(self.sel_obj, self.gh.wnd, NIL,[SGA_ACTIVE, nr, NIL])
    GetAttr(SGA_ACTIVE, self.sel_obj,  self.selected)
ENDPROC