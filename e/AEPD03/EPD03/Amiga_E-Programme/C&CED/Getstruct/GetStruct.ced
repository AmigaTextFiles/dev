/*
 * rexx:getstruct.ced
 *
 * $VER: v1.0 Rick Younie Wed Apr  7 1993
 *
 *	 please send comments, questions or improvements:
 *			rick@emma.panam.wimsey.bc.ca	1:153/765.9
 *			rick@emma.tfbbs.wimsey.bc.ca
 *
 * SYNOPSIS:
 *	 - display the include file that contains the structure name under
 *		the cursor in CED
 *
 *	 - assigns:
 *		 include_h: - contains the C header files
 *		 include_i: - contains the assembler includes
 *		 include_e: - the 'ShowModule'd output of the *.e defines
 *
 *		 INDEX		- ParseInc makes it
 *					- an index file of structure names/header files
 *					- found in each directory (each type of header file
 *						  uses slightly different structure/member names)
 *
 *	 - assign a CED function key to 'rexx:getstruct h' (or i or e
 *		depending on which type of include file you want to use).
 *
 */
	signal on FAILURE
	signal on NOVALUE
	signal on SYNTAX
	signal on BREAK_C
	signal on HALT

/* constants and assigns */

	tab			= '09'x
	lf			= '0a'x
	space		= '20'x
	hardspace	= 'a0'x 
	transin		= '22'x'27'x'<>():;*.,|~=/+-' || hardspace || lf || tab
	indexFile	= 'INDEX'
	type		= 'h'

Main:
	parse arg type						/* e,h or i */
	headerDir = 'include_'type':'

	options results
	'status 55'; line = translate(result,,transin)
	'status 87'; cursorpos = result

	line = substr(line,1,pos(space,line,cursorpos+1))
	command = word(line, words(line))

	/* read in index file */
	if ~open('indexFile', headerDir || indexFile, 'Read') then 
		call ErX 'can''t find' headerDir || indexFile
	indexString = readch('indexFile', 65000)

	do forever
		indexPos = index(indexString,lf || upper(command)' ') + 1

		if indexPos = 1 then do	/* no doc for this object */
			'getstring "' '"' '"'type": can't find >"command"<... type one in?" '"'
			if symbol('result') = 'LIT' then exit
			if (result = '') then exit
			command = strip(result)
		end

		else do	  /* is doc - display it */

			entryLen = index(indexString,lf,indexPos)-indexPos
			fileEntry = substr(indexString,indexPos,entryLen)
			headFileName = word(fileEntry,2)

			if ~exists(headerDir || headFileName) then
				call ErX '..sorry..can''t find "'headFileName'"'lf,
					'in "'headerDir'"'

			/* if header file already in a view, use it */
			parse var headFileName '/' hName
			'status 66'
			views = result
			do i = views to 1 by -1
				'status 21'
				if result = hName then leave i
				'next view'
			end
			if i~=0 then 'beg of file'
			else do
				'open new'
				'open' headerDir || headFileName
			end

			/* try and position the cursor on the structure */
			select
				when type='h' then do
					'search for "struct 'command || lf'"'
					if result=0 then 'search for "'command' {"'
					if result=0 then 'search for "'lf'struct 'command'"'
				end
				when type='e' then do
					'search for "OBJECT  'command || lf'"'
					if result=0 then 'search for "OBJECT 'command || lf'"'
				end
				when type='i' then do
					'search for "STRUCTURE  'command'"'
					if result=0 then 'search for "STRUCTURE'tab || command'"'
					if result=0 then 'search for' command','
				end
				otherwise nop
			end
			exit 0
		end  /* display it */
	end  /* forever */
	exit


ErX: PROCEDURE
	'okay1' arg(1)
NOVALUE:
SYNTAX:
FAILURE:
	if symbol('SIGL') ~= 'LIT' then 'okay1' 'error in line' SIGL,
		'of GetStruct.ced'
HALT:
BREAK_C:
	exit
