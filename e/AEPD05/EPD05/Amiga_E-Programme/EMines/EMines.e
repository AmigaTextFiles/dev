
/* E Source generated by SRCGEN v0.1 */

/*
**      $Filename: SchiffeVersenken
**      $Version : 0.4c
**      $Date    : 21.3.94 13:34
**      $Status  : !!! Unreleased Version !!!
**      $Author  : Daniel van Gerpen, Alter Postweg 3, 26759 Hinte
**      $Others  : None
**
**
**
**
*/

OPT OSVERSION=37

MODULE 'gadtools','libraries/gadtools','intuition/intuition','exec/memory',
       'intuition/screens', 'intuition/gadgetclass', 'graphics/text'

ENUM NONE,NOCONTEXT,NOGADGET,NOWB,NOVISUAL,OPENGT,NOWINDOW,NOMENUS


CONST EINER=5,ZWEIER=4,DREIER=2

DEF     project0wnd:PTR TO window, x,y, arou, zugs,
        project0glist, ready,
        infos:PTR TO gadget,
        scr:PTR TO screen,
        visual=NIL,
        offx,offy,tattr,rx,ry,fx,fy,

        spielfeld[100]: ARRAY,
        history  [100]: ARRAY,
        kind     [100]: ARRAY,
        ges, i,j, tonemem, mi, se, mr,sr,
        eifer, zwfer, drfer, fertig


PROC get_time(m,s)
DEF time[3]:ARRAY OF LONG
   DateStamp(time)
   ^m:=Mod(time[1],60)
   ^s:=time[2]/50
ENDPROC

PROC setupscreen()
  IF (gadtoolsbase:=OpenLibrary('gadtools.library',37))=NIL THEN RETURN OPENGT
  IF (scr:=LockPubScreen('Workbench'))=NIL THEN RETURN NOWB
  IF (visual:=GetVisualInfoA(scr,NIL))=NIL THEN RETURN NOVISUAL
  offy:=scr.wbortop+Int(scr.rastport+58)-10
  tattr:=['topaz.font',8,0,0]:textattr
ENDPROC

PROC closedownscreen()
  IF visual THEN FreeVisualInfo(visual)
  IF scr THEN UnlockPubScreen(NIL,scr)
  IF gadtoolsbase THEN CloseLibrary(gadtoolsbase)
ENDPROC

PROC openproject0window()
  DEF g:PTR TO gadget, x,y

  IF (g:=CreateContext({project0glist}))=NIL THEN RETURN NOCONTEXT

  FOR x:=0 TO 9
  FOR y:=0 TO 9
  
   IF (g:=CreateGadgetA(BUTTON_KIND,g,
     [offx+17+(x*24),offy+18+(y*13),24,13,'??',tattr,0,0,visual,0]:newgadget,
     [NIL]))=NIL THEN RETURN NOGADGET

  ENDFOR
  ENDFOR
 IF (project0wnd:=OpenW(174,51,offx+380,offy+160,$24C077E,$102E,
    'EMines © by Daniel van Gerpen',NIL,1,project0glist))=NIL THEN Raise(NOWINDOW)

  Gt_RefreshWindow(project0wnd,NIL)
ENDPROC


PROC init()
FOR i:=0 TO 9 
FOR j:=0 TO 9 
  setze(i,j,FALSE)
  setkind(i,j,0)
ENDFOR
ENDFOR
ENDPROC

PROC lese(xpos,ypos)

DEF back

IF xpos<0 THEN xpos:=0
IF xpos>9 THEN xpos:=9
IF ypos<0 THEN ypos:=0
IF ypos>9 THEN ypos:=9

back:=spielfeld[(ypos*10)+xpos]

ENDPROC back

PROC leskind(xpos,ypos)

DEF back

IF xpos<0 THEN xpos:=0
IF xpos>9 THEN xpos:=9
IF ypos<0 THEN ypos:=0
IF ypos>9 THEN ypos:=9

back:=kind[(ypos*10)+xpos]

ENDPROC back

PROC radius(xpos,ypos)
DEF flag
flag:=FALSE

IF (lese(xpos,ypos)) OR
   (lese(xpos+1,ypos)) OR
   (lese(xpos-1,ypos)) OR
   (lese(xpos,ypos+1)) OR
   (lese(xpos,ypos-1)) OR
   (lese(xpos+1,ypos-1)) OR
   (lese(xpos+1,ypos+1)) OR
   (lese(xpos-1,ypos-1)) OR
   (lese(xpos-1,ypos+1)) THEN flag:=TRUE

ENDPROC flag

PROC look(xpos,ypos)

arou:=0

IF xpos<0 THEN xpos:=0
IF xpos>9 THEN xpos:=9
IF ypos<0 THEN ypos:=0
IF ypos>9 THEN ypos:=9

IF xpos+1<10                    THEN IF (lese(xpos+1,ypos))   THEN arou:=arou+1 
IF xpos-1>=0                     THEN IF (lese(xpos-1,ypos))   THEN arou:=arou+1 
IF ypos+1<10                    THEN IF (lese(xpos,ypos+1))   THEN arou:=arou+1 
IF ypos-1>=0                     THEN IF (lese(xpos,ypos-1))   THEN arou:=arou+1
IF (xpos+1<10)  AND (ypos-1>=0)  THEN IF (lese(xpos+1,ypos-1)) THEN arou:=arou+1
IF (xpos+1<10)  AND (ypos+1<10) THEN IF (lese(xpos+1,ypos+1)) THEN arou:=arou+1
IF (xpos-1>=0)   AND (ypos-1>=0)  THEN IF (lese(xpos-1,ypos-1)) THEN arou:=arou+1
IF (xpos-1>=0)   AND (ypos+1<10) THEN IF (lese(xpos-1,ypos+1)) THEN arou:=arou+1

ENDPROC


PROC setze(xpos,ypos,wert)

spielfeld[(ypos*10)+xpos]:=wert

ENDPROC


PROC setkind(xpos,ypos,wert)

kind[(ypos*10)+xpos]:=wert

ENDPROC





PROC setships()

  DEF ze,sp,z2,s2,s3,z3,i,j,k,seed

  MOVE.L $dff006,seed
  Rnd(seed*-1)

   i:=0; k:=0                        /* Dreier */
   WHILE (i<>DREIER) AND (k<500)
    ze:=Rnd(10)
    sp:=Rnd(10)
    j:=Rnd(4)
    z2:=ze; s2:=sp
    z3:=z2; s3:=s2
    k:=k+1

    SELECT j
  
        CASE 0; z2:=ze+1; z3:=z2+1
        CASE 1; z2:=ze-1; z3:=z2-1
        CASE 2; s2:=sp+1; s3:=s2+1
        CASE 3; s2:=sp-1; s3:=s2-1

    ENDSELECT

    IF (z2>0) AND (z2<10) AND (s2>0) AND (s2<10) AND
       (z3>0) AND (z3<10) AND (s3>0) AND (s3<10) 

        IF (radius(ze,sp)=FALSE) AND
           (radius(z2,s2)=FALSE) AND 
           (radius(z3,s3)=FALSE)

         setze(ze,sp,TRUE); setkind(ze,sp,3)
         setze(z2,s2,TRUE); setkind(z2,s2,3)
         setze(z3,s3,TRUE); setkind(z3,s3,3)
         i:=i+1

         TextF(260+offx,30+offy,'Dreier:  /\d',i)        
        ENDIF
    ENDIF
   ENDWHILE

   i:=0; k:=0                       /* Zweier */
   WHILE (i<>ZWEIER) AND (k<500)
    ze:=Rnd(10)
    sp:=Rnd(10)
    j:=Rnd(4)
    z2:=ze; s2:=sp
    k:=k+1    

    SELECT j
  
        CASE 0; z2:=ze+1
        CASE 1; z2:=ze-1
        CASE 2; s2:=sp+1
        CASE 3; s2:=sp-1

    ENDSELECT

    IF (z2>0) AND (z2<10) AND (s2>0) AND (s2<10)
        IF (radius(ze,sp)=FALSE) AND (radius(z2,s2)=FALSE)
         setze(ze,sp,TRUE); setkind(ze,sp,2)
         setze(z2,s2,TRUE); setkind(z2,s2,2)
         i:=i+1

         TextF(260+offx,42+offy,'Zweier:  /\d',i)        

        ENDIF
    ENDIF
   ENDWHILE

   i:=0; k:=0                          /* Einer */
   WHILE (i<>EINER) AND (k<500)
    ze:=Rnd(10)
    sp:=Rnd(10)
    k:=k+1

    IF radius(ze,sp)=FALSE 
     setze(ze,sp,TRUE); setkind(ze,sp,1)
     i:=i+1

     TextF(260+offx,54+offy,'Einer :  /\d',i)        

    ENDIF
   ENDWHILE

  
ENDPROC


PROC closeproject0window()
  IF project0wnd THEN CloseWindow(project0wnd)
  IF project0glist THEN FreeGadgets(project0glist)
ENDPROC

PROC ask_ready()
DEF zf,df

ready:=TRUE
eifer:=0
zwfer:=0
drfer:=0
zf:=0
df:=0

FOR i:=0 TO 9
FOR j:=0 TO 9

IF spielfeld[i*10+j] THEN IF history[i*10+j]=FALSE THEN ready:=FALSE

IF history[i*10+j]

x:=kind[10*i+j]

SELECT x

  CASE 1; eifer:=eifer+1
  CASE 2; zf:=zf+1; IF Even(zf) THEN zwfer:=zwfer+1
  CASE 3; df:=df+1; IF (Mod(df,3))=0 AND (df<>0) THEN drfer:=drfer+1

ENDSELECT

ENDIF

ENDFOR
ENDFOR

ENDPROC ready




PROC wait4message(win:PTR TO window)
  DEF mes:PTR TO intuimessage,type, punkte

  REPEAT
  type:=0

  rx:=MouseX(project0wnd)-17
  ry:=MouseY(project0wnd)-18
 
  IF rx<0 THEN rx:=0
  IF rx>216 THEN rx:=216
  IF ry<0 THEN ry:=0
  IF ry>117 THEN ry:=117
 
  fx:=(rx/24)
  fy:=(ry/13)

  Colour(1,0)
  TextF(260+offx,100+offy,'X:\d Y:\d  ',fx+1,fy+1)
  IF fertig=FALSE
  get_time({mi},{se})
  IF zugs>0
  TextF(260+offx,88+offy,'Zeit :')
  SetTopaz(9)
  TextF(317+offx,88+offy,'\d:\z\d[2] ',IF se-sr>=0 THEN mi-mr ELSE mi-mr-1,IF se-sr>=0 THEN (se-sr) ELSE (60+(se-sr)))
  SetTopaz(8)
    punkte:=(IF se-sr>=0 THEN mi-mr ELSE mi-mr-1)*60
    punkte:=punkte+(IF se-sr>=0 THEN se-sr ELSE 60+se-sr)
    punkte:=punkte+Abs(zugs*3/2)
    punkte:=400-punkte
    IF punkte<0 THEN punkte:=0

    TextF(260+offx,76+offy,'Punkte:  \d ',punkte)
  ENDIF

  ENDIF

    IF mes:=Gt_GetIMsg(win.userport)
      type:=mes.class
      IF type=IDCMP_MENUPICK
        infos:=mes.code
      ELSEIF (type=IDCMP_GADGETDOWN) OR (type=IDCMP_GADGETUP)
        infos:=mes.iaddress
      ELSEIF type=IDCMP_REFRESHWINDOW
        Gt_BeginRefresh(win)
        Gt_EndRefresh(win,TRUE)
        type:=0
      ELSEIF type<>IDCMP_CLOSEWINDOW  /* remove these if you like */
        type:=0
      ENDIF
      Gt_ReplyIMsg(mes)
    ELSE
      WaitPort(win.userport)
    ENDIF
  UNTIL type
ENDPROC type

PROC reporterr(er)
  DEF erlist:PTR TO LONG
  IF er
    erlist:=['get context','create gadget','lock wb','get visual infos',
      'open "gadtools.library" v37+','open window','create menus']
    EasyRequestArgs(0,[20,0,0,'Could not \s!','ok'],0,[erlist[er-1]])
  ENDIF
ENDPROC er
PROC play(f,d)

/* This procedure is probably very BAD... Writing to the hardware an so on...
I didn't know how to do it otherwise. Maybe someone else could help. */

        DEF period,sample[16]:LIST

        MOVE.W  #$00F,$DFF096           /* OFF WITH SOUND DMA */
        MOVE.L  tonemem,$DFF0B0         /* AUD1LOC */
        MOVE.W  #16,$DFF0b4                     /* AUD1LEN */
        period:=3579545/(16*f)
        MOVE.L  period,D0
        MOVE.W  D0,$dff0b6                      /* aud1per */
        MOVE.W  #64,$DFF0B8                     /* AUD1VOL */
        MOVE.W  #$8202,$DFF096          /* TURN ON DMA */

        Delay(d)

        MOVE.W  #$0003,$DFF096          /* SHUT SOUND DMA OFF AGAIN*/
        MOVE.W  #$0011,$DFF09E          /* RESTORE ADKCON */

        sample:=[10,20,30,40,50,60,70,80,70,60,50,40,30,20,10,0] /* Sample */

        CopyMem({sample},tonemem,16)

ENDPROC

PROC main()

  IF (tonemem:=AllocMem(16,MEMF_CHIP))=0
       CleanUp(5)
  ENDIF


  IF reporterr(setupscreen())=0
    reporterr(openproject0window())

REPEAT
  fertig:=FALSE
  zugs:=0

  FOR i:=1 TO 100 DO history[i]:=FALSE

  Colour(1,0)

  init()
  setships()


FOR fx:=0 TO 9
FOR fy:=0 TO 9
Colour(3,0)
  IF spielfeld[fy*10+fx] 
   TextF(21+offx+(fx*24),27+offy+(fy*13),'- ')
  ENDIF
  IF spielfeld[fy*10+fx]=FALSE THEN TextF(21+offx+(fx*24),27+offy+(fy*13),'- ')
Colour(1,0)
ENDFOR
ENDFOR

wait4message(project0wnd)

  REPEAT

  rx:=MouseX(project0wnd)-17
  ry:=MouseY(project0wnd)-18

  fx:=(rx/24)
  fy:=(ry/13)

  TextF(260+offx,112+offy,'X:\d Y:\d  ',fx+1,fy+1)

IF history[fy*10+fx]=FALSE
  IF (spielfeld[fy*10+fx])
   Colour(2,1)
   TextF(21+offx+(fx*24),27+offy+(fy*13),'XX')
   play(1198,3)
   Delay(1)
   TextF(21+offx+(fx*24),27+offy+(fy*13),'++')
   play(1228,1)
   Delay(1)
   TextF(21+offx+(fx*24),27+offy+(fy*13),'--')
   play(1258,2)
   TextF(21+offx+(fx*24),27+offy+(fy*13),' \d',leskind(fx,fy))
   play(1208,2)
   history[fy*10+fx]:=TRUE
   zugs:=zugs+1
   Colour(1,0)
  ENDIF
  IF (spielfeld[fy*10+fx]=FALSE)
    look(fx,fy)
    IF arou=0 THEN Colour(1,0) ELSE Colour(1,2)
    play(930,2)
    TextF(21+offx+(fx*24),27+offy+(fy*13),'oo')
    play(960,1)
    TextF(21+offx+(fx*24),27+offy+(fy*13),'öö')
    play(1000,3)
    TextF(21+offx+(fx*24),27+offy+(fy*13),' \d',arou)
    history[fy*10+fx]:=TRUE
    zugs:=zugs+1
    Colour(1,0)
  ENDIF
ELSE  
  IF (spielfeld[fy*10+fx])
   Colour(2,1)
   TextF(21+offx+(fx*24),27+offy+(fy*13),' \d',leskind(fx,fy))
  ENDIF
  IF (spielfeld[fy*10+fx]=FALSE)
    look(fx,fy)
    IF arou=0 THEN Colour(1,0) ELSE Colour(1,2)
    TextF(21+offx+(fx*24),27+offy+(fy*13),' \d',arou)
  ENDIF
  Colour(1,0)
ENDIF

  TextF(260+offx,124+offy,'Züge: \d',zugs)


  IF zugs=1 THEN get_time({mr},{sr})
  IF ask_ready() 
    Delay(5)
    play(930,2)
    play(1160,1)
    play(1080,2)
    play(930,3)
    play(1160,4)
    play(1100,3)
    play(1160,1)
    Delay(7)
    play(1160,4)
    play(1160,1)
    Delay(7)
    play(1160,4)
    play(950,20)
    play(1160,1)
    Colour(0,1)
    TextF(270+offx,136+offy,'  FERTIG  ')
    Colour(1,0)
    fertig:=TRUE
  ELSE
    TextF(260+offx,136+offy,'  Weiter    ') 
  ENDIF

  TextF(320+offx,30+offy,'\d',drfer)        
  TextF(320+offx,42+offy,'\d',zwfer)        
  TextF(320+offx,54+offy,'\d',eifer)        


  UNTIL (wait4message(project0wnd)=IDCMP_CLOSEWINDOW) OR (fertig=TRUE)
  UNTIL fertig<>TRUE
    closeproject0window()
    FreeMem(tonemem,16)
    IF CtrlC() THEN BRA t
  ENDIF
  t: closedownscreen()
ENDPROC

