; PowerD installer 1st august 2010
cd >t:powerd_source

echo "select a directory, where you want to install the PowerD to"
echo "PowerD directory will be created there"

failat 11
requestfile drawersonly title "select directory" positive "here" negative "nowhere" noicons initialvolumes >t:powerd_path
if warn
  echo "canceled"
  skip end
endif

; create PowerD directory
echo "creating PowerD directory..."
echo "cd " noline >t:powerd_tmp
join t:powerd_tmp t:powerd_path as t:powerd_tmp2
execute t:powerd_tmp2
makedir PowerD

; copy all files
echo "copying PowerD files..."
echo "copy to PowerD all quiet from " noline >t:powerd_tmp
join t:powerd_tmp t:powerd_source as t:powerd_tmp2
execute t:powerd_tmp2

; update s:user-startup
requestchoice title "update s:user-startup?" body "s:user-startup must be updated to add few lines" gadgets "do it|skip|break" >env:powerd_choice
if $powerd_choice eq 0
  echo "canceled"
  skip end
endif
if $powerd_choice eq 2
  echo "no s:user-startup edited"
  skip no_user_startup
endif
echo "updating s:user-startup (a backup is being made)"
copy s:user-startup to s:user-startup.backup quiet
echo >>s:user-startup
echo "; PowerD" >>s:user-startup
echo "assign d: " noline >>s:user-startup
cd PowerD
path bin add
assign d: ""
assign dmodules: modules
cd >t:powerd_path
join s:user-startup t:powerd_path as s:user-startup.powerd
delete s:user-startup quiet
rename s:user-startup.powerd to s:user-startup
echo "assign dmodules: d:modules" >>s:user-startup
echo "path d:bin add" >>s:user-startup
echo "; PowerD end" >>s:user-startup
lab no_user_startup

; prepare MorphOS/WarpOS launchers
requestchoice title "build MorphOS/WarpOS launchers?" body "these are needed if You will write WarpOS/MorphOS PowerPC applications" gadgets "both|MorphOS|WarpOS|skip" >env:powerd_choice
if $powerd_choice eq 0
  echo "skipped"
  skip end
endif
if $powerd_choice eq 3
  skip warpos_only
endif

echo "building MorphOS launchers (about 2 minutes on G4/1GHz, 6 minutes on 68060/50)..."
date
execute install4mos
date
if $powerd_choice eq 2
  skip warpos_skip
endif

lab warpos_only
echo "building WarpOS launchers (about 3 minutes on G4/1GHz, 6:30 minutes on 68060/50)..."
date
execute install4wos
date
lab warpos_skip

; get back to source directory
lab end
echo "cd " noline >t:powerd_tmp
join t:powerd_tmp t:powerd_source as t:powerd_tmp2
execute t:powerd_tmp2

; delete temporary files
path d:bin remove
assign d:
assign dmodules:
delete t:powerd_(tmp|tmp2|path|source) quiet
echo "thanks for using PowerD installer"
echo "please reboot now..."
wait 60
