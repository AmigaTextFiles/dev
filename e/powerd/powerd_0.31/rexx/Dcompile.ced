/* Dcompile.rexx: run D compiler from ced.
   thanks to Rick Younie for improvements. */

OPTIONS RESULTS
ADDRESS COMMAND

ADDRESS 'rexx_ced'

'status 19'					/* ask ced filename */
file = result

'status 18'
IF result ~= 0 THEN DO				/* save if changed */
  'save' file
  SAY 'saving changes..'
END
ELSE SAY 'no changes..'

PARSE VAR file comparg '.d'			/* strip the extension */
SAY 'invoking D compiler with file' comparg'.d'

ADDRESS
OPTIONS FAILAT 1000000
'dc' comparg				/* run compiler */
ebyte = rc

IF EXISTS(comparg) THEN comparg			/* run exe */

ADDRESS
pull						/* wait for a <cr> */
'cedtofront'
IF ebyte>0 THEN 'jump to byte' ebyte		/* jump to spot of error */
exit 0
