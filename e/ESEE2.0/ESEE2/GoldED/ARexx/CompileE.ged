/* $VER: CompileE.ged v0.5 (30.9.94)
   by Leon Woestenberg (leon@stack.urc.tue.nl)

   Function: Invokes the EC v3 compiler on the source in the active window,
   and reports back success, or error. Upon error, the cursor pinpoints to
   the exact spot of error, and the status bar shows the error description.

   Usage: Call from GoldED menu as ARexx script, and set output to 'NIL:'
*/

ARG ec

OPTIONS RESULTS

IF (LEFT(ADDRESS(),6)~="GOLDED") THEN ADDRESS 'GOLDED.1'
'LOCK CURRENT QUIET'
IF rc THEN EXIT

options='ERRBYTE' 'QUIET'

'QUERY USER11 VAR RESULT'
IF (result='TRUE') THEN options=options 'LARGE'
'QUERY USER12 VAR RESULT'
IF (result='TRUE') THEN options=options 'SYM'
'QUERY USER13 VAR RESULT'
IF (result='TRUE') THEN options=options 'LINEDEBUG'
'QUERY USER14 VAR RESULT'
IF (result='TRUE') THEN options=options 'IGNORECACHE'
'QUERY USER15 VAR RESULT'
IF (result='TRUE') THEN options=options 'OPTI'

'QUERY ANYTEXT'
IF (result='TRUE') THEN
DO
  'QUERY DOC VAR FILEPATH'
  IF (UPPER(RIGHT(result,2))='.E') THEN
  DO
    'QUERY MODIFY'
    IF (result='TRUE') THEN
    DO
      'REQUEST STATUS="Saving changes..."'
      'SAVE ALL'
    END
    'REQUEST STATUS="Compiling source..."'
    ADDRESS COMMAND ec filepath options '>T:EC_Report'
    errorbyte=rc
    IF errorbyte>0 THEN
    DO
      'FOLD OPEN=TRUE ALL'
      'GOTO UNFOLD=TRUE BYTE=' || errorbyte
      'PING 9'
    END
    IF OPEN(filehandle,'T:EC_Report','Read')~=0 THEN
    DO
      importance=0
      DO WHILE ~EOF(filehandle) & importance~='ERROR'
        lastline=READLN(filehandle)
        /* messages ordered in accending importance */
        IF (INDEX(lastline,'UNREFERENCED:')~=0) & (importance<1) THEN
        DO
          importance=1
          message=lastline
        END
        IF (INDEX(lastline,'WARNING:')~=0) & (importance<2) THEN
        DO
          importance=2
          message=lastline
        END
        IF (INDEX(lastline,'ERROR:')~=0) & (importance<3) THEN
        DO
          importance=3
          message=lastline
        END
        IF (INDEX(lastline,'EC INTERNAL ERROR')~=0) & (importance<4) THEN
        DO
          importance=4
          message=lastline
        END
      END
      ok=CLOSE(filehandle)
      IF importance=0 THEN message='Compilation succesful.'
      IF importance>=3 THEN 'BEEP'
      message=TRANSLATE(message,'''','"')
      'FIX VAR message'
      'REQUEST STATUS="' || message ||'"'
    END
  END
  ELSE
  DO
    'REQUEST STATUS="Source has no .e extension!"'
  END
END
ELSE
DO
  'REQUEST STATUS="Say what?! Try typing some e source first :)"'
END
'UNLOCK'
EXIT

