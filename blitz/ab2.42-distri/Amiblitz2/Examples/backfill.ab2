WBStartup
;Put following lines  at begin of your program
!initasm
!basic
optimize 7
Syntax 2
!tklibs_include
Runerrsoff
Statement image_blitbackfill {image.l,x.l,y.l,x2.l,y2.l,xoffset.l,yoffset.l,destbitmap_ptr.l}
SHARED imagedat()
USEPATH imagedat(image)
If \bitmap_ptr=False Then image_initbmap {image}
If \bitmap_ptr><False AND destbitmap_ptr
  dx.l  = x
  px.l  = (x - xoffset) MOD \img_width
  pxs.l = \img_width-px
  x2+1
  y2+1
  While dx<x2
    If dx+pxs>x2 Then pxs = x2-dx
    dy.l  = y
    py.l  = (y - yoffset) MOD \img_height
    pys.l = \img_height-py

    While dy<y2
      If dy+pys>y2 Then pys = y2-dy
      BltBitMap_ \bitmap_ptr,px,py,destbitmap_ptr,dx,dy,pxs,pys,$c0,$ff,0
      dy+pys
      pys = \img_height
      py = 0
    Wend
    dx+pxs
    pxs = \img_width
    px = 0
  Wend
End If
End Statement
DEFTYPE.w _bf_x1,_bf_y1,_bf_x2,_bf_y2
DEFTYPE.l _bf_layer,_bf_rp
BRA begin
backfillfunc:
!basica7
MOVE.l a2,_bf_rp@(a5)
MOVE.l  0(a1),_bf_layer@(a5)
MOVE.w  4(a1),_bf_x1@(a5)
MOVE.w  6(a1),_bf_y1@(a5)
MOVE.w  8(a1),_bf_x2@(a5)
MOVE.w 10(a1),_bf_y2@(a5)
*layer.Layer = _bf_layer.l
*rp.RastPort = _bf_rp.l
If image_isused{0}
  image_blitbackfill {0,_bf_x1,_bf_y1,_bf_x2,_bf_y2,*layer\bounds\MinX,*layer\bounds\MinY,*rp\BitMap}
End If
!asma7
RTS
.begin:Runerrson
;here come the user program
ret.l=image_load{0,"//thilo_includes/gfx/pattern"}
If ret Then WindowBackFillHook ?backfillfunc ;only if pic is found or it crash badly
FindScreen (0)
Stop
foo.l=GTButton(0,1,20,20,100,15,"Normal",#PLACETEXT_IN)
Window 0,20,100,200,280,$1004f,"",0,1
AttachGTList 0,0
Repeat
ev.l=WaitEvent
Until ev=#IDCMP_CLOSEWINDOW
End


