
; Blitz miniature lisp interpreter by Lorence Lombardo.

; This is a miniature lisp interpreter to demonstrate Dietmar Eilert's

; "lisp.library" from the Distributed LISP package from here:-

; http://www.softwareandcircuits.com/division/amiga/products/lisp/index.html

; This has been deliberatley made breef for demonstration purposes only.

; It has been intedened only to demonstrate the examples provided in
; "lisp/examples" of the package from this current date.

; 3-Oct-2012

; Argument parsing has not been supported because none of the "#?.lisp"
; examples in the package require this at this stage.

; No guarantee can be given that this interpreter will be suitable for
; future updates of Dietmar Eilert's Distributed LISP package.


np.b=NumPars : f$="" : If np>0 Then f$=Par$(1)

If np<1 OR f$="?" Then NPrint " Usage: lisp.exe <lisp_file> [D=detached]" : End

INCLUDE "lisp.include.bb2"

If lisp_InitLib{0}=0 Then NPrint "Can not open lisp.library!" : End

Dim load_tags.TagItem(1) : err$=String$(Chr$(0), #LISP_MAXERRORSIZE)

load_tags(0)\ti_Tag = #LISP_FILENAME, &f$
load_tags(1)\ti_Tag = #TAG_END


det.b=0 : If np>1 Then If UCase$(Par$(2))="D" Then det=1


If det=1
   !lisp_run {ret.l, &load_tags(0)\ti_Tag, &err$}
   If ret=0 Then NPrint "Lisp error: "+Peek$(&err$)
   lisp_FreeLib{} : End
EndIf


Dim create_tags.TagItem(1) : br.b=0

create_tags(0)\ti_Tag = #LISP_CONSOLE, 0
create_tags(1)\ti_Tag = #TAG_END

!lisp_create {*machine, &create_tags(0)\ti_Tag, &err$}

If *machine
   !lisp_load {ret.l, *machine, &load_tags(0)\ti_Tag, &err$}
   If ret
      While br=0
         !lisp_iterate {ret.l, *machine, &err$}
         If ret=0
            err$=Peek$(&err$) : br=1
            If err$<>"" Then NPrint "Lisp error: "+err$
         EndIf
      Wend
   Else
      NPrint "Lisp error: "+Peek$(&err$)
   EndIf
   !lisp_dispose{bla.l, *machine}
Else
   NPrint "Lisp error: "+Peek$(&err$)
EndIf

lisp_FreeLib{} : End


