
; ValLM{}  (C) 2010-2012  Lorence Lombardo

; Multi Val very compatible with Vallong including the identifying
; characters, also having the "#" character for octal as a bonus.
 
; Works with new Blitz and classic Blitz

; Bonus function to ValF{} NG  30-Aug-2012

; With this ValLM{} the sign can be before or after the ID.

; The original ValLM{} can be found in "ValL.ab2" from the archive
; "SANE.lha" on the Aminet.


Function.l ValLM {a$}
   a$=Replace$(a$,",","") : a$=Replace$(a$," ","")
   c.w=0 : vl.l=0 : sign.b=1 : base.b=10 : nmax.b=58
   s$=Left$(a$,1) : If s$="-" OR s$="+" Then a$=Mid$(a$,2)
   ntype$=Left$(a$,1) : If s$="-" Then sign=-1
   If ntype$<>"%" AND ntype$<>"$" AND ntype$<>"#" Then ntype$=""
   If ntype$<>"" Then a$=Mid$(a$,2)
   s$=Left$(a$,1) : If s$="-" OR s$="+" Then a$=Mid$(a$,2)
   slen.w=Peek.w(&a$-2) : If s$="-" Then sign=-1
   If ntype$="%" Then base=2 : nmax=50
   If ntype$="$" Then base=16 : a$=UCase$(a$)
   If ntype$="#" Then base=8 : nmax=56
   Repeat
      v.b=Peek.b(&a$+c) : c+1
      If v>47 AND v<nmax
         vl=vl*base+v-48
      Else
         If base=16 AND v>64 AND v<71
            vl=vl*16+v-55
         Else
            c=slen
         EndIf
      EndIf
   Until c=>slen
   Function Return vl*sign
End Function


;----------------------------------------------------------------------------------


NPrint ValLM{"3333.4444"}

NPrint ValLM{" 2000000000"}

NPrint ValLM{"$ffff"}

NPrint ValLM{"%1001"}

NPrint ValLM{"#515"}

NPrint ValLM {"-$7a.c"}

NPrint ValLM {"$-7a.c"}

NPrint ValLM{"1000000001"} + ValLM{"999999999"}

End

