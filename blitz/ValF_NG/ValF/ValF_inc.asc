
; ValF{}  NextGeneration  (C) 2012-2013  Lorence Lombardo
; This is an alternative to the "Val" command with some advantages.

; ValF{} NG works with or without FPU. ValF{} NG works with new Blitz
; and Classic Blitz
; Similar to the original "Val" command now supporting hex, binary
; and octal fractions.

; With ValF{} NG the sign can be before or after the ID.

; Previous versions of ValF{} can be found in the archives "SANE.lha"
; and "num2str.lha" from the Aminet. Its possible previous versions
; were faster but speed was not the issue here.

; 26-Aug-2012 to 22-May-2013


CNIF #FPU=1
Function.d ValF {valf$}
   fract.d=0 : fl.d=0
CELSE
Function.f ValF {valf$}
   fract.f=0 : fl.f=0
CEND
   nmax.b=58 : base.b=10 : sign.b=1
   valf$=Replace$(valf$,",","") : valf$=Replace$(valf$," ","")
   sign$=Left$(valf$,1) : If sign$="-" Then sign= -1
   If sign$="-" OR sign$="+" Then valf$=Mid$(valf$,2)
   ntype$=Left$(valf$,1)
   If ntype$<>"%" AND ntype$<>"$" AND ntype$<>"#" Then ntype$=""
   If ntype$<>"" Then valf$=Mid$(valf$,2)
   sign$=Left$(valf$,1) : If sign$="-" Then sign= -1
   If sign$="-" OR sign$="+" Then valf$=Mid$(valf$,2)
   xp.w=Instr(valf$,".") : fraclen.w=0
   If ntype$="%" Then base=2 : nmax=50
   If ntype$="$" Then base=16 : valf$=UCase$(valf$)
   If ntype$="#" Then base=8 : nmax=56
   If xp>0
      fract$=Mid$(valf$,xp+1) : fract$=StripTrail$(fract$,48)
      intlen.w=xp-1 : fraclen=Len(fract$)
      valf$=Left$(valf$, intlen)
   Else
      intlen.w=Len(valf$)
   EndIf
   slen.w=fraclen : v$=fract$
   For x.b=1 To 2
      If x=2 Then slen.w=intlen : v$=valf$
      If slen>0
         c.w=0 : calc.b=x
         Repeat
            v.b=Peek.b(&v$+c) : c+1
            If v>47 AND v<nmax
               vl.b=v-48
            Else
               If base=16 AND v>64 AND v<71 Then vl.b=v-55 Else c=slen : calc=0
            EndIf
            If calc=1 Then fract = vl * (base^(c*-1)) + fract
            If calc=2 Then fl = fl * base + vl
         Until c=>slen
      EndIf
   Next x
   Function Return (fl+fract)*sign
End Function



