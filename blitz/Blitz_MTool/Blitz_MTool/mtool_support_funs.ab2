
; Blitz mtool library support functions

; (C) 2011  Lorence Lombardo.

; Only supply clean numbers. ;)


INCLUDE "tool.include.bb2"

If Tool_InitLib{0}=0 Then NPrint "Could not initialize mtool lib...!!!" : End


; Converts a double float to an exponent free numeric string.
; replace StrToLong_ with "Val" for "ROM 1.3" & classic Blitz
; ie. dig.l=Val(dig$)

Function.s dub2str{addy.l}
   SHARED *_ToolBase : digits.b=16
   buf$=String$(Chr$(0), 64) : !Tool_UmwFtoS {&buf$, addy, digits}
   buf$=Peek$(&buf$) : x1.w=Instr(buf$,".") : x2.w=Instr(buf$,"d")
   If x1>0 AND x2=0
      buf$=StripTrail$(buf$, 48) : buf$=StripTrail$(buf$, 46)
   EndIf
   If x2>0
      a$=Left$(buf$,x1-1) : b$=Mid$(buf$,x1+1,x2-x1-1)
      If Mid$(buf$,x2+1,1)="-"
         dig$=Mid$(buf$,x2+2) : StrToLong_ &dig$, &dig.l
         If Left$(a$,1)="-"
            buf$="-."+String$("0",dig-Len(a$)+1)+Mid$(a$,2)+b$
         Else
            buf$="."+String$("0",dig-Len(a$))+a$+b$
         EndIf
      Else
         dig$=Mid$(buf$,x2+1) : StrToLong_ &dig$, &dig.l : digt.w=dig-Len(b$)
         If digt<0
            blen.w=Len(b$)+digt : b$=Left$(b$,blen)+"."+Mid$(b$,blen+1)
         EndIf
         buf$=a$+b$+String$("0",digt)
      EndIf
      If Instr(buf$,".")>0
         buf$=StripTrail$(buf$, 48) : buf$=StripTrail$(buf$, 46)
      EndIf
   EndIf
   If buf$="" OR buf$="-" Then buf$="0"
   Function Return buf$
End Function


; Converts exponent free numeric strings to a double float,
; some of which would not be possible by "UmwStoF" alone.
; Add Replace$'s if you wish to increase robustness at the
; cost of speed. I personally dont see myself supplying
; un-clean numbers.

Statement str2dub{dub_addy.l, fstr$}
   SHARED *_ToolBase : slen.w=Len(fstr$)
   x1.w=Instr(fstr$,".") : x2.w=Instr(fstr$,"d")
   If x1>0 AND x1<4 AND x2=0
      chek$="" : sm$="" : If x1=1 Then sm$=Mid$(fstr$,2,2)
      If x1=2
         chek$=Left$(fstr$,1)
         If chek$="-" OR chek$="+" OR chek$="0" Then sm$=Mid$(fstr$,3,2)
      EndIf
      If x1=3
         chek$=Left$(fstr$,2)
         If chek$="-0" OR chek$="+0" Then sm$=Mid$(fstr$,4,2)
      EndIf
      If sm$="00"
         a$=Mid$(fstr$,x1+1) : b$=StripLead$(a$,48) : x2=1
         blen.w=Len(a$)-Len(b$) : chek$=StripTrail$(chek$,48)
         fstr$=chek$+Left$(b$,1)+"."+Mid$(b$,2,17)+"d-"+Str$(blen+1)
      EndIf
   EndIf
   If x1>18 AND x2=0 Then slen=x1-1 : fstr$=Left$(fstr$, slen) : x1=0
   If x1>0 AND slen>20 AND x2=0 Then slen=19 : fstr$=Left$(fstr$, slen)
   If slen>27 AND x1=0 AND x2=0
      fstr$=Left$(fstr$,1)+"."+Mid$(fstr$,2,17)+"d"+Str$(slen-1)
   EndIf
   !Tool_UmwStoF{dub_addy, &fstr$}
End Statement


; end of functions

;---------------------------------------------------------------------------------------

; functions demo


IEEEDPFix_ 1,1  ; <- This allows the FPU version of the lib to work also.
                ;    It can be removed if you are going to use the IEEEDP#?
                ;    functions anyway.


NEWTYPE .dub : dub0.l : dub1.l : End NEWTYPE


b$="1635592999999999999999999.55555555555555555555"

str2dub{&test.dub, b$}

NPrint b$
NPrint dub2str{&test}

b$="163559299999999999999999999999999"

str2dub{&test, b$}

NPrint b$
NPrint dub2str{&test}

b$="1635599299999999.555555555555555555555"

str2dub{&test, b$}

NPrint b$
NPrint dub2str{&test}

b$=".000000000000016074893838524315"

str2dub{&test, b$}

NPrint b$
NPrint dub2str{&test}


b$="-.0016074893815"

str2dub{&test, b$}

NPrint b$
NPrint dub2str{&test}

b$="32.66666666666666666666666666666"

str2dub{&test, b$}

NPrint b$
NPrint dub2str{&test}

Tool_FreeLib{} : End


