
; A simple sprintf function for Blitz with bonus prints{}

; (c) 2012  Lorence Lombardo

; 14-Aug-2012


Function.s sprintf{fmt$, args.l}
   vspfPCP.l = $16c04e75  ; <- magic number obtained from hb2
   buff.l=AllocMem_(1024, #MEMF_PUBLIC) : buf$=""
   If buff
      RawDoFmt_ &fmt$, args, &vspfPCP, buff
      buf$=Peek$(buff) : FreeMem_ buff, 1024
   EndIf
   Function Return buf$
End Function

Statement prints{s$}
   Write_ Output_, &s$, Len(s$)
End Statement

; Here are some basic types that can be used with sprintf{} :-

; %ld = decimal
; %lu = unsigned ROM2.0+ very handy  v37+
; %lx = hexadecimal
; %lc = character 0-255
; %s  = string

; For a more detailed explanation please refer to RKM RawDoFmt
; Also C/C++ printf/sprintf could be of assistance


;----------------------------------------------------------------------------


a$="is this gona work ?"

Dim sargs.l(4)

sargs(0)=20
sargs(1)=-1
sargs(2)=&a$
sargs(3)=10

bla$=sprintf{ "test %ld %lx %s %lc", &sargs(0) }
prints{bla$}

If ExecVersion=>37
   sargs(0)=-1 : sargs(1)=10 : sargs(2)=10
   bla$=sprintf{ "%lu %lc %lc", &sargs(0)}
   prints{bla$}
EndIf

; With Lotan ArOfLong or Tags this can be done as follows:-

;bla$=sprintf{ "test %ld %lx %s %lc", ArOfLong(20,-1,&a$,10) }
;prints{bla$}

;bla$=sprintf{ "test %ld %lx %s %lc", Tags(20,-1,&a$,10) }
;prints{bla$}

End


