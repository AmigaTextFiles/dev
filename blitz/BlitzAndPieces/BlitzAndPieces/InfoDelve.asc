; infodelve by james l boyd - program & source PD
;--------------------------------------------------
; a requester to show icon information (allows easy editing
; of icons deep down in your hard drive directories,esp with
; swazinfo installed :)

v$="$VER: InfoDelve 1.0 (28.10.98) James L Boyd"

; function to return the file part of a file-&-path name

Function$ StripFile{p$}
  *fileptr.l = FilePart_(&p$)
  f$=Peek$(*fileptr)
Function Return f$
End Function

WBStartup:NoCli ; run from an icon if required,no CLI window

;setup requester strings
MaxLen p$=192:MaxLen f$=192
p$="RAM:"

WBenchToFront_:FindScreen 0 ; gimme the WB! (this call works on any
                            ; blitz screen,though...)

*scr.Screen=Peek.l(Addr Screen(0)) ; get screen structure for call

selecticon$=ASLFileRequest$("Select icon to edit/view",p$,f$,"#?.info")

If selecticon$="" OR f$="" Then End ; choose an icon or end

icon$=StripFile{selecticon$} ; get the file part

If Right$(icon$,5)=".info" Then icon$=Left$(icon$,Len(icon$)-5)
; strip ".info" if it's there (usually will be,since we've set the
; requester pattern to show only #?.info files)

dir$=Left$(selecticon$,Len(selecticon$)-Len(StripFile{selecticon$}))
; get the directory name

*lok.l=Lock_(&dir$,#ACCESS_READ) ; get a lock on the directory for the call

If *scr ; make sure we've got a screen structure

  If *lok ; and a directory lock

    If WBInfo_(*lok,&icon$,*scr) ; the Icon Info call!
    EndIf

  UnLock_ *lok ; free our directory lock

  EndIf

EndIf

End

; duh...
