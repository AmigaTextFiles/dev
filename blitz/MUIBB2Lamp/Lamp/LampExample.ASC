;
; Lamp.mcc Blitz Demo.
; (C) Rob Hutchinson, 25/5/1999 - SDSoftware!
;
; For more info contact: loki@sds.in2home.co.uk
;
; And go see:
;   HTTP://www.satanicdreams.freeserve.co.uk/
;
; Requires mui.res and MUILamp.RES resident in the compiler menu!
;
; Also shows you how to use text.mui :) (if you didn`t already know :))
;
; BTW This COULD be done with notification, but I cant be arsed :)
;

WBStartup
WbToScreen 0

  XINCLUDE "StackAppendOpen.BB2"   ;make our stack bigger

  MUIApplicationTitle       "Lamp.mcc example"
  MUIApplicationVersion     "$VER: Lamp Example 1.00 (25.05.99)"
  MUIApplicationCopyright   "(C)1999, SDSoftware."
  MUIApplicationAuthor      "Rob Hutchinson"
  MUIApplicationDescription "Example of how to use Lamp.mcc with Blitz!"
  MUIApplicationBase        "LAMPEXAMP"

  ;--- Generate Text and lamp objects
  ;
  Tit$="Example of how to use Lamp.mcc with Blitz."
  MUIAddTags   1,#MUIA_Lamp_Type,#MUIV_Lamp_Type_Huge,#MUIA_Lamp_Red,$00000000
  MUINewObject 1,"Lamp.mcc",0
  MUIAddTags   2,#MUIA_Frame,#MUIV_Frame_Text
  MUIAddTags   2,#MUIA_Text_PreParse,Null(Chr$(27)+"c"),#MUIA_Text_Contents,Null(Tit$) ;Centered!
  MUINewObject 2,"Text.mui",0
  ;------------------------------------
  MUIAddObjsHGroup 3,2,1
  MUIAddTags       3,#MUIA_Group_Columns,2
  MUICreateHGroup  3
  ;====================================

  ;--- Create our 3 buttons
  ;
  MUISimpleButton 4,"Full Red"
  MUISimpleButton 5,"Full Green"
  MUISimpleButton 6,"Full Blue"
  MUISimpleButton 7,"Lamp OFF!"
  ;------------------------------------
  MUIAddObjsHGroup 8,4,5,6,7
  MUIAddTags       8,#MUIA_Group_Columns,4
  MUICreateHGroup  8
  ;====================================

  MUIMakeObject 12,#MUIO_HBar,1,0

  ;--- Extra lamps
  ;
  Tit$="Example of how to use Lamp.mcc with Blitz."
  MUIAddTags   13,#MUIA_Lamp_Type,#MUIV_Lamp_Type_Tiny,#MUIA_Lamp_Green,$ffffffff
  MUINewObject 13,"Lamp.mcc",0
  MUIAddTags   14,#MUIA_Lamp_Type,#MUIV_Lamp_Type_Small,#MUIA_Lamp_Green,$ffffffff
  MUINewObject 14,"Lamp.mcc",0
  MUIAddTags   15,#MUIA_Lamp_Type,#MUIV_Lamp_Type_Medium,#MUIA_Lamp_Red,$ffffffff
  MUINewObject 15,"Lamp.mcc",0
  MUIAddTags   16,#MUIA_Lamp_Type,#MUIV_Lamp_Type_Big,#MUIA_Lamp_Blue,$ffffffff
  MUINewObject 16,"Lamp.mcc",0
  MUIAddTags   17,#MUIA_Lamp_Type,#MUIV_Lamp_Type_Huge,#MUIA_Lamp_Blue,$ffffffff
  MUINewObject 17,"Lamp.mcc",0
  MUIAddTags   18,#MUIA_Frame,#MUIV_Frame_Text
  MUIAddTags   18,#MUIA_Text_PreParse,Null(Chr$(27)+"c"),#MUIA_Text_Contents,Null("<-- Other types of Lamps.") ;Centered!
  MUINewObject 18,"Text.mui",0
  ;------------------------------------
  MUIAddTags       30,#MUIA_Frame,#MUIV_Frame_Group
  MUIAddTags       30,#MUIA_Group_Columns,6
  MUIAddObjsHGroup 30,13,14,15,16,17,18
  MUICreateHGroup  30

  ;--- Put them into a vertical group...
  ;
  MUIAddObjsVGroup 9,3,8,12,30
  MUICreateVGroup  9

  MUICreateWindow 10,"Lamp Example V1.00","LMPE",9
  MUIAddSubWindow 10

  If MUICreateApplication<>True Then End
  MUINotifyApp 10,#MUIA_Window_CloseRequest,1,$200
  MUINotifyApp 4 ,#MUIA_Pressed            ,0,$4
  MUINotifyApp 5 ,#MUIA_Pressed            ,0,$5
  MUINotifyApp 6 ,#MUIA_Pressed            ,0,$6
  MUINotifyApp 7 ,#MUIA_Pressed            ,0,$7
  MUIOpenWindow 10

  ;--- Main Loop
  ;
  Repeat
    ev.l=MUIWaitEvent
    Select ev
      Case $4
        MUISet 1,#MUIA_Lamp_Red,$ffffffff,#MUIA_Lamp_Green,$00000000,#MUIA_Lamp_Blue,$00000000
      Case $5
        MUISet 1,#MUIA_Lamp_Red,$00000000,#MUIA_Lamp_Green,$ffffffff,#MUIA_Lamp_Blue,$00000000
      Case $6
        MUISet 1,#MUIA_Lamp_Red,$00000000,#MUIA_Lamp_Green,$00000000,#MUIA_Lamp_Blue,$ffffffff
      Case $7
        MUISet 1,#MUIA_Lamp_Color,#MUIV_Lamp_Color_Off
    End Select
  Until ev=$200
  MUICloseWindow 10

  XINCLUDE "StackAppendClose.BB2"  ;change our stack back before we leave :)
End



