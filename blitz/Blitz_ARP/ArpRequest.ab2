
; ARP developement kit for Blitz

; (C) 2010 Lorence Lombardo

; 7-May-2010 to 11-May-2010

; Run from CLI


; INCLUDE "arp.include.bb2"  ; <- for AB2/3

INCLUDE "Blitz2:My_proggy's/arp.include.bb2"  ; <- complete path required required by classic Blitz

If arp_InitLib{0}=0 Then End

NEWTYPE .ArpFileRequester
   *fr_Hail.b
   *fr_File.b
   *fr_Dir.b
   *fr_Window.Window
   fr_FuncFlags.b
   fr_Flags2.b
   *fr_Function.l
   fr_LeftEdge.w
   fr_TopEdge.w
End NEWTYPE

DEFTYPE .ArpFileRequester my_req

If ExecVersion=>36
   wb$="Workbench"
   *scr.Screen=LockPubScreen_(&wb$)
Else
   GetScreenData_ &scr.Screen, SizeOf.Screen, #WBENCHSCREEN, 0
   *scr.Screen = scr.Screen
EndIf

myfile$=String$(Chr$(0),512)
path$=String$(Chr$(0),512)

Poke$ &myfile$,"bla"
Poke$ &path$, "RAM:"
tit$="Pick a file."

my_req\fr_Hail=&tit$
my_req\fr_File=&myfile$
my_req\fr_Dir=&path$
my_req\fr_Window = *scr\FirstWindow
my_req\fr_LeftEdge=50
my_req\fr_TopEdge=50

!arp_FileRequest {ret.l, my_req}

myfile$=Peek$(&myfile$)  ; <- we can get rid of trailing zero's now.
path$=Peek$(&path$)

NPrint myfile$
NPrint path$
NPrint "ret = ",ret

If ExecVersion=>36 Then UnlockPubScreen_ 0,*scr

arp_FreeLib {}

End



