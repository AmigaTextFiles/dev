
; "SimpleDecrunchDemo.ab2" for Blitz was based on
; "HB2 SimpleDecrunchDemo.bas" from the HB2 XFD package,
; implementing my own style into it. ;)

; No consideration has been made for password protected files.

; Done by Lorence Lombardo in 2008  (lombi@iprimus.com.au)

; See the archives "XFDDevBas.lha" and "xfdmaster_dev.lha" on the
; aminet for related interest.

INCLUDE "XFD_constants.bb2"

#R=1005: #W=1006 : l$=Chr$(10)

np=NumPars : If np>0 Then f$=Par$(1)
If np=0 OR f$="?"
   NPrint l$+" Usage:-"+l$+l$+" SimpleDecrunchDemo <crunched_file>"+l$
   End
EndIf

infile.l=Open_(&f$, #R)
If infile
   ExamineFH_ infile, fib.FileInfoBlock : sz.l=fib\fib_Size
   ptr.l = AllocMem_(sz, #MEMF_PUBLIC) : If ptr <=0 Then Close_ infile : End
   Read_ infile, ptr, sz : Close_ infile

   xfdbi.l=xfdAllocObject_(#XFDOBJ_BUFFERINFO)
   If xfdbi
      Poke.l (xfdbi + #xfdbi_SourceBuffer), ptr
      Poke.l (xfdbi + #xfdbi_SourceBufLen), sz
      Poke.w (xfdbi + #xfdbi_Flags), #XFDFF_RECOGEXTERN

      r1.l = xfdRecogBuffer_(xfdbi)   ; is this a recognized compression ?
      If r1
         cli$ = Peek$( Peek.l(xfdbi+#xfdbi_PackerName) )
         NPrint l$+" User client:- "+cli$+l$
         Poke.l (xfdbi + #xfdbi_TargetBufMemType), #MEMF_PUBLIC
         r2.l = xfdDecrunchBuffer_(xfdbi)
         If r2
            out_ptr.l = Peek.l (xfdbi + #xfdbi_TargetBuffer)
            out_len.l = Peek.l (xfdbi + #xfdbi_TargetBufSaveLen)
            NPrint " Original: ",sz," bytes --> Decompressed: ",out_len," bytes"
            out$=f$+".decrunched" : Print l$+" Saving file as `"+out$+"'..."
            outfile.l=Open_(&out$, #W)
            If outfile
               Write_ outfile, out_ptr, out_len : Close_ outfile
               NPrint " Done...!!!"+l$
            Else
               NPrint l$+l$+" Unable to write file."+l$
            EndIf
            FreeMem_ out_ptr, out_len
         Else
            NPrint " Unable to decrunch file."+l$
         EndIf
      Else
         NPrint l$+" File has no known compression."+l$
      EndIf

      xfdFreeObject_ xfdbi
   EndIf

   FreeMem_ ptr, sz
Else
   NPrint l$+" File does not exist."+l$
EndIf


