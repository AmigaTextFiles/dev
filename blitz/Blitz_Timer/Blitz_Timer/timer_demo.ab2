
; "timer_demo.ab2"  (C) 2011-2012  Lorence Lombardo

; Demo 1

; NB: Format has no effect on UStr$


INCLUDE "timer.include.bb2"

l$=Chr$(10) : NPrint l$+" Demo_1:-"

If timer_Init{#UNIT_MICROHZ}=0 Then NPrint l$+" Could not initialize timer...!!!"+l$ : End

!timer_GetSysTime {&aa.timeval}

NPrint l$+" SysTime1: ",aa\tv_secs," secs ",aa\tv_micro," micros."

; Delay_ 75  ; <-- 1.5 second delay  OR  with TimerWait {} below

TimerWait {1, 500000}  ; <- Use TimerWait{} when Delay_ is not small enough

; Delay_ 50*3360  ; also tested with a 3360 second delay, ie. 56 minutes

!timer_GetSysTime {&bb.timeval}

NPrint l$+" SysTime2: ",bb\tv_secs," secs ",bb\tv_micro," micros."+l$

!timer_SubTime{&bb,&aa} : Format "000000"

NPrint " Total_time: "+UStr$(bb\tv_secs)+"."+Str$(bb\tv_micro)+" secs"+l$

timer_Free {} : Format ""


;-------------------------------------------------------------------------------------------


; Demo 2


NPrint l$+" Demo_2:-" : FloatMode -1

*time1.EClockVal=0 : *time2.EClockVal=0 : E_Freq.l=0

If timer_Init{#UNIT_ECLOCK}
   *time1=AllocVec_(SizeOf.EClockVal, #MEMF_PUBLIC)
   *time2=AllocVec_(SizeOf.EClockVal, #MEMF_PUBLIC)
Else
   NPrint l$+" Could not initialize timer...!!!"+l$ : End
EndIf

If *time1=0 OR *time2=0
   NPrint l$+" Could not initialize timer...!!!"+l$
   Goto exit
EndIf


; The following function was taken from "unsign.ab2" from "SANE.lha"
; on the aminet.

; Converts a signed long to an unsigned float.
; Change the ".f" type to a ".d" type if greater precision is required.

Function.f unsL{long.l}
   If long<0 Then ret.f=256^4+long Else ret.f=long
   Function Return ret
End Function


Macro prtime
   thi.f = unsL{*time2\ev_hi} - unsL{*time1\ev_hi}
   tlo.f = unsL{*time2\ev_lo} - unsL{*time1\ev_lo}
   sec.f=(65536.0^2 * thi + tlo) / unsL{E_Freq}
   NPrint " Operation took ",sec," seconds."+l$
End Macro

NPrint l$+" Now reading EClock."+l$

!timer_ReadEClock {E_Freq, *time1}

Delay_ 25  ; <-- 1/2 second delay

; Delay_ 50*3360  ; also tested with a 3360 second delay, ie. 56 minutes

!timer_ReadEClock {E_Freq, *time2}

!prtime

exit:
If *time1 Then FreeVec_(*time1)
If *time2 Then FreeVec_(*time2)
timer_Free {}
End


