
#Output=51:#Text=52:#String=53
#Number=54:#MX=55:#Cyc=56
#CheckBox=57:#Button=58:#w0=0
#GadAid=59


#MOUSEMOVE=$10:#INTUITICKS=$400000

DEFTYPE .ExtGadget *EX,*ExtGad

LoadFont 0,"Xen.font",8
Use IntuiFont 0

NEWTYPE .item
  a.w
  b$
End NEWTYPE

Dim List results.item(1000)


.main
  AddIDCMP #MOUSEMOVE+#INTUITICKS

.setupui:
  WbToScreen 0
  GTButton   #w0,#Button  , 70,  7,110, 21,"BUTTON"  ,16
  GTNumber   #w0,#Number  , 70, 37,110, 14,"Number"  ,1,0
  GTText     #w0,#Text    , 70, 57,110, 14,"Text"    ,1,""
  GTTags  #GTCB_Scaled,True
  GTCheckBox #w0,#CheckBox, 70, 77, 26, 11,"HELP ON",1
  GTCycle    #w0,#Cyc     , 70, 97,110, 17,"Cycle"   ,1," One | Two |Three"
  GTMX       #w0,#MX      , 70,117, 49, 28,"MX"      ,2,"One|Two|Poo"
  GTListView #w0,#Output  ,200, 13,258,160,"Results" ,4,results()
  GTText     #w0,#GadAid  ,200,170,258, 12,""        ,1,""
  DefaultIDCMP $2|$4|$8|$10|$20|$40|$100|$200|$400|$40000|$80000|$400000|$800000|$1000000|$4000000
  Window #w0,0,11,500,200,$1|$2|$4|$8|$1000,"HELP Demo",1,2,0
  AttachGTList #w0,#w0
  *w.Window=Peek.l(Addr Window(#w0))
  HelpOn.b = 0
  Result.l = HelpControl_(*w,0)
  Test.w   = 0

  Repeat
    ev.l=WaitEvent
    If ev<>#MOUSEMOVE AND ev<>#INTUITICKS Then Gosub addevent
  Until ev=$200
  End



.addevent
  Format "000"
  GTChangeList #w0,#Output
  *EX = EventiAddress
  GadSel.l = *EX\GadgetID
  If ev =#IDCMP_GADGETHELP
    GadAid$ = ""
    If AddFirst(results())
      results()\b="Gadget#"+Str$(GadSel)+" : help bubble"
    EndIf
    Test=*EX\GadgetType AND $F0
    If *EX <> *w
      Select Test
      Case #GTYP_CLOSE
        GadAid$="Window close"
      Case #GTYP_WDRAGGING
        GadAid$="Window bar and dragging"
      Case #GTYP_WUPFRONT
        GadAid$="Window to back/front"
      Case #GTYP_WDOWNBACK
        GadAid$="Window zoom"
      Case #GTYP_SDRAGGING
        GadAid$="Screen bar and dragging"
      Case #GTYP_SUPFRONT
        GadAid$="Screen to front"
      Case #GTYP_SDOWNBACK
        GadAid$="Screen to back"
      Case #GTYP_SIZING
        GadAid$="Window sizing"
      Default
        GadSel.l = *EX\GadgetID
        Select GadSel
        Case #Number
          GadAid$="Number gadget"
        Case #Button
          GadAid$="Button gadget"
        Case #Output
          GadAid$="Output gadget"
        Case #Cyc
          GadAid$="Cycle gadget"
        Case #Text
          GadAid$="Text gadget"
        Case #GadAid
          GadAid$="String gadget"
        Case #MX
          GadAid$="MX (Radio buttons) gadget"
        End Select
      End Select
      GTSetString 0,#GadAid,GadAid$
    EndIf
  Else
    If ev=#IDCMP_GADGETUP
      Select GadgetHit
      Case #CheckBox
        If HelpOn.b = 0
          HelpOn.b = 1              ; turn help on
        Else
          HelpOn.b = 0              ; turn help off
        EndIf
        Result.l = HelpControl_(*w,HelpOn)
      End Select
    EndIf
    If AddFirst(results())
      results()\b="Gadget#"+Str$(GadSel)+" : Event#"+UStr$(ev)+" Code#"+UStr$(EventCode)
    EndIf
  EndIf
  GTChangeList #w0,#Output,results()
  Return



