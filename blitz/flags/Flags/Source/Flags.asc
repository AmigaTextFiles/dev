;Read the section in the guide on how to add new topics -
;that's what true-multi-tasking is for - it's dead easy
;
;WARNING: there is no error checking, as I wrote it for my
;system, and it assumes that the default font is 8x8 - all
;window/gadget size calculations are based on this - the
;Ysize of gadgets is generally 10, and the size of windows
;is the titlebar + 10*the number of lines of text to print.
;
;doesn't check the screen it's called on either - don't call
;in a paint package, etc.
;
;I think there must be a problem with the commodities library
;because this program does not free up all of the memory that
;it uses when quitted - even if you run it, and then quit it
;from exchange - thereby not opening any windows or creating
;any gadgets.

ver$="$VER: FLAGS 2.0 Vic E Babes 5/6/98 (updated 30/05/99)"

.numtops
#numtopics=19
WBStartup
NoCli

;-------this bit creates the commodity-----------

comsucc= MakeCommodity("Flags","Flags 2.0 - Vic E Babes (98/99)","Online Flag info. for Blitz Basic 2.1")
keysucc=SetHotKey(0,"lalt lshift f")
If comsucc AND keysucc
  quitted=False
  Repeat
    Repeat
      VWait
      cev.l=CommodityEvent
      hev.l=HotKeyHit
    Until cev OR hev=0
    evtype.l=ExchangeMessage
    If hev=0 OR evtype=19
      Gosub showtopics
    Else
    Select evtype
      Case #CXCMD_KILL    : quitted=True
      Case #CXCMD_DISABLE : SetStatus Off
      Case #CXCMD_ENABLE  : SetStatus On
      Case #CXCMD_UNIQUE  : Gosub mycloud
    End Select
    EndIf
  Until quitted
  End
Else
  End
EndIf

.mycloud
FindScreen 0
  body$=" Hey - you - get offa my cloud!!!!"+Chr$(10)+Chr$(10)
  body$+"Help!!  Another commodity is trying"+Chr$(10)
  body$+"to launch with the same name as me!"
  res=Request("Flags",body$,"DOWNER")
Return
;----------------------------end of commodity bit

;---------------------------start of main program
.showtopics
FindScreen 0
*scr.Screen=Peek.l(Addr Screen(0))
scrhite.w=*scr\Height        ;no you won't fool, the peeker
scrwidf.w=*scr\Width         ;of the resolution, no you...
fy=*scr\Font\ta_YSize

  Restore gads
  GTButton 0,61,0,0,75,10,"Quit",$10
  GTButton 0,62,75,0,75,10,"Hide",$10
  For nogads=1 To #numtopics
    Read gadname$
    GTButton 0,62+nogads,0,nogads*10,150,10,gadname$,$10
  Next

;all windows are automatically centered on the screen by dividing
;the screenwidth by 2 (scrwidf lsr 1) and then taking half the
;window's width away.  Same with height - but includes titlebar.

winhite=fy+5+((#numtopics+1)*10)
Window 0,(scrwidf LSR 1)-77,(scrhite LSR 1)-(winhite LSR 1),158,winhite,$1002,"Flags",2,1
;        ---------------centre the window------------------
AttachGTList 0,0
  Repeat
    keytested=False                      ;flag - see below
    Repeat
      ev=WaitEvent
    Until ev AND $240              ;until closed or gadgeted
    If GadgetHit>62                ;if not quit/hide
      Restore gads                 ;topic titles
      For i=1 To GadgetHit-62
        Read gadname$
      Next
      Select GadgetHit
        Case 63 : Restore scrflags       ;restore data pointer
        Case 64 : Restore scrtags        ;to appropriate data
        Case 65 : Restore winflags       ;according to gadget
        Case 66 : Restore idcmpflags     ;selected
        Case 67 : Restore gadflags
        Case 68 : Restore gadtags
        Case 69 : Restore typesizes
        Case 70 : Restore initcop
        Case 71 : Restore intrtypes
        Case 72 : Restore rqflags
        Case 73 : Restore rawkeys
        Case 74 : Gosub testkey          ;keytested flag set here
        Case 75 : Restore strcomms
        Case 76 : Restore cxevents
        Case 77 : Restore menuitemflags
        Case 78 : Restore printer
        Case 79 : Restore CD32
        Case 80 : Restore gurus
        Case 81 : Restore other
.select
      End Select
      If keytested=False Gosub showdata
      Use Window 0
      Activate 0
    Else
      If GadgetHit=61 Then quitted=True
    EndIf
    FlushEvents
  Until GadgetHit<63       ;until quit/hide
  CloseWindow 0
  Free GTList 0
Return                     ;go away until hotkey pressed again
;--------------------------------testkey
.testkey
winhite=56+fy           ;fy is font taYsize
Window 1,(scrwidf LSR 1)-85,(scrhite LSR 1)-(winhite LSR 1),170,winhite,$100E,"Press keys",2,1
WColour 1
Repeat
  tkev=WaitEvent
  If tkev=$400
    b$=Inkey$
    WLocate 0,0
    NPrint ""
    NPrint " Char       : ",b$
    NPrint ""
    NPrint " Ascii      : ",Asc(b$),"  "
    NPrint " RawKey     : $",UnRight$(Hex$(RawKey),4)
    NPrint " Qualifier  : $"+UnRight$(Hex$(Qualifier),4)
  EndIf
Until tkev=$200
keytested=True          ;so that doesn't go into print-loop
CloseWindow 1           ;when it goes back as no data to print
Return
;--------------------------------------
.showdata
  Read numlines.b
  winhite=fy+5+(numlines LSL 3)
  Window 1,(scrwidf LSR 1)-200,(scrhite LSR 1)-(winhite LSR 1),400,winhite,$100A,gadname$,2,1
  WColour 1
  For i=1 To numlines
    Read txt$
    If txt$="x"
      WColour 2
      Read txt$
      NPrint txt$
      WColour 1
    Else
      NPrint txt$
    EndIf
  Next
  Repeat
  Until WaitEvent=$200      ;until closed
  CloseWindow 1
  Use Window 0
  Activate 0
Return

;--------------------------------data

.datatitles             ;for topic-buttons on main window
gads:
Data$ "Screen Flags"
Data$ "Screen Tags"
Data$ "Window Flags"
Data$ "IDCMP Flags"
Data$ "Gadget Flags"
Data$ "Gadget Tags"
Data$ "Type Sizes"
Data$ "InitCoplist Flags"
Data$ "Interrupt Types"
Data$ "Req Flags"
Data$ "RawKeys"
Data$ "Test Keys"
Data$ "$# Commands"
Data$ "CX Events"
Data$ "Menu Item Flags"
Data$ "Printer Stuff"
Data$ "Joystick/Joypad"
Data$ "Guru Meditations"
Data$ "Misc stuff"

scrflags                ;start of topic data
Data.b 11
Data$ "x"
Data$ "MODE - for short version:"
Data$ "  1-6  For bitplanes"
Data$ "  +$0  LoRes"
Data$ "  +$8  HiRes (only 1-4 bitplanes)"
Data$ "  +$10 Interlaced"
Data$ "x"
Data$ "VIEWMODE - for long version"
Data$ "  $0    Lores"
Data$ "  $4    Interlaced"
Data$ "  $80   HBrite"
Data$ "  $800  Ham"
Data$ "  $8000 HiRes"

scrtags
Data.b 22
Data$ " $80000000+"
Data$ " $21  Left           36  Show title"
Data$ " $22  Top            37  Behind"
Data$ " $23  Width          38  Quiet (no title)"
Data$ " $24  Height         39  Autoscroll"
Data$ " $25  Depth          3A  Pens"
Data$ " $26  Detail pen     3B  Full palette"
Data$ " $27  Block pen      3C  Colour map entries"
Data$ " $28  Title          3D  Parent"
Data$ " $29  Colours        3E  Draggable"
Data$ " $2A  Error code     3F  Exclusive"
Data$ " $2B  Font           40  Show pens"
Data$ " $2C  Sys font       41  Back Fill"
Data$ " $2D  Type           42  Interleaved"
Data$ " $2E  Bitmap         43  Colours 32"
Data$ " $2F  Pub name       44  Video control"
Data$ " $30  Pub sig        45  Front child"
Data$ " $31  Pub task       46  Back child"
Data$ " $32  Display ID     47  Like Workbench"
Data$ " $33  DClip          48  Reserved"
Data$ " $34  Overscan"
Data$ " $35  Obsolete1"

winflags
Data.b 21
Data$ " $1     Attach sizing gadget"
Data$ " $2     Draggable window"
Data$ " $4     Depth gadget"
Data$ " $8     Close gadget"
Data$ " $10    Right margin sizing gadget"
Data$ " $20    Bottom margin sizing gadget"
Data$ " $80    SuperBitMap"
Data$ " $100   Backdrop - behind other windows can't"
Data$ "        have $4 as well."
Data$ " $200   Reports mouse moved (w IDCMP Mouse_Move)"
Data$ " $400   GimmeZeroZero - Border separate - can't"
Data$ "        be drawn on - has the effect of making"
Data$ "        the origin (coords) for gfx (wline etc)"
Data$ "        the same as for text/gadgets - so"
Data$ "        WLine 10,10,2 will be the same location"
Data$ "        as WLocate 10,10.  Uses more mem though"
Data$ " $800   No border"
Data$ " $1000  Activated when first opened"
Data$ " $10000 Catch RMB events"
Data$ " $40000 Extension data provided"
Data$ " $200000 New-look menus"


idcmpflags

Data.b 28
Data$ " $1     SizeVerify - won't change size"
Data$ "        until your program allows it - to"
Data$ "        give it chance to finish what it's"
Data$ "        doing"
Data$ " $2     Reported when window size changed"
Data$ " $4     Contents corrupted - need redraw"
Data$ " $8     Either mouse button hit"
Data$ " $10    Mouse moved"
Data$ " $20    Gadget pressed (w Immediate GadTag)"
Data$ " $40    Gadget released"
Data$ " $80    Reports requester opened"
Data$ " $100   Menu operation occurred"
Data$ " $200   Close gadget selected"
Data$ " $400   Keypressed"
Data$ " $800   Reports requester about to open but"
Data$ "        requires your attention first so you"
Data$ "        can stop your program drawing, etc."
Data$ " $1000  Report requester cleared"
Data$ " $2000  Like $800 but for menus"
Data$ " $8000  Disk inserted"
Data$ " $10000 Disk removed"
Data$ " $40000 Window activated"
Data$ " $80000 Window de-activated"
Data$ " $400000 Intuiticks"
Data$ " $1000000 MenuHelp - Help key pressed during"
Data$ "          menu operation"
Data$ " $2000000 Changewindow - moved, etc"
Data$ "x"
Data$ "       See page 345 of ROM Kernel"

gadflags

Data.b 14
Data$ " $0      No label"
Data$ " $1      Label left"
Data$ " $2      Label right"
Data$ " $4      Label above"
Data$ " $8      Label below"
Data$ " $10     Label in"
Data$ " $20     Label is white for certain gadgets"
Data$ " $40     Gadget disabled initially"
Data$ " $80     Immediate gadget-down flag used mainly"
Data$ "         for slider and listview gadgets."
Data$ " $100    Gadget is on initially"
Data$ " $200    Scale arrowsize on scroller"
Data$ " $400    Proportional gadget is vertical"
Data$ " $400000 Needed for scroller/slider gadgets"

gadtags

Data.b 25
Data$ " $80080000 should be made #tag constant at top"
Data$ "           to make things easier.  Add those"
Data$ "           listed below to #tag"
Data$ "x"
Data$ "      i.e. GTGetAttrs(gtlist#,GTid#,#tag+flag)"
Data$ "x"
Data$ "        or GTSetAttrs(gtlist#,GTid#,#tag+flag)"
Data$ " Usually need to 'Redraw' gadget after 'Setting'"
Data$ " +4   State of checkbox"
Data$ " +5   Top visible in Listview"
Data$ " +7   True if listview is read only"
Data$ " +10  Active one in MX gadget"
Data$ " +11  GTMX text to display !!?"
Data$ " +13  GTNM number to display"
Data$ " +15  Active one in cycle gadget"
Data$ " +17  Chosen palette colour"
Data$ " +18  First colour to use in palette"
Data$ " +21  Top visible in scroller"
Data$ " +22  Total in scroller area"
Data$ " +23  Number visible in scroller"
Data$ " +40  Slider level"
Data$ " +41  Max length of printed level"
Data$ " +42 *Format string for level '%2ld' L-not-one"
Data$ " +43 *Where level should be placed, above etc."
Data$ " +54  Set ordinal number of selected GTLV"
Data$ " +61 *Added to font height in GTMX for spacing"
Data$ "x"
Data$ "   * can't be set once gadget initialized"

typesizes

Data.b 7
Data$ "x"
Data$ " Type Suff  Range           Accuracy   #Bytes"
Data$ ""
Data$ " Byte  .b  +/-128             Int         1"
Data$ " Word  .w  +/-32768           Int         2"
Data$ " Long  .l  +/-2147483648      Int         4"
Data$ " Quick .q  +/-32768.00000   1/65536       2"
Data$ " Float .f  +/-9*10^18       1/10^18       4"

initcop

Data.b 21
Data$ " $1-8   No. of bitplanes"
Data$ ""
Data$ " $10    Smooth-scrolling"
Data$ " $20    Enable dual playfield"
Data$ " $40    Forces 6 bitplane display to ehb mode"
Data$ " $80    Dispaly in ham mode"
Data$ ""
Data$ " $000   LoRes"
Data$ " $100   HiRes"
Data$ " $200   SuperHiRes"
Data$ ""
Data$ " $400   LoRes sprites"
Data$ " $800   HiRes sprites"
Data$ " $c00   SuperHiRes sprites"
Data$ ""
Data$ " $0000  Fetchmode 0"
Data$ " $1000  Fetchmode 1"
Data$ " $2000  Fetchmode 2"
Data$ " $3000  Fetchmode 3"
Data$ ""
Data$ " $10000 AGA colours"

intrtypes
Data.b 14
Data$ " 0  Serial transmit buffer empty"
Data$ " 1  Disk block read/written"
Data$ " 2  Software interupt"
Data$ " 3  Cia ports interrupt"
Data$ " 4  Copper interrupt"
Data$ " 5  Vertical blank"
Data$ " 6  Blitter finished"
Data$ " 7  Audio channel 0 pointer/length fetched"
Data$ " 8  Audio channel 1 pointer/length fetched"
Data$ " 9  Audio channel 1 pointer/length fetched"
Data$ " 10 Audio channel 3 pointer/length fetched"
Data$ " 11 Serial receive buffer full"
Data$ " 12 Floppy disk sinc"
Data$ " 13 External interrupt"

rqflags
Data.b 21
Data$ " REQOUTPUT #Window - or requester will appear"
Data$ " on default public screen"
Data$ " REQFILELOC returns address of req.library"
Data$ " file structure"
Data$ ""
Data$ "x"
Data$ "   Path$=ReqFileRequest([title$[,flags])"
Data$ ""
Data$ " $1    Show .info files"
Data$ " $2    Extended select"
Data$ " $4    Directory caching"
Data$ " $8    Font requester"
Data$ " $10   Hide .info files gadget"
Data$ " $20   No show/hide string gadgets"
Data$ " $40   Use abs x,y instead of mouse pos"
Data$ " $80   Purge cache on datestamp change"
Data$ " $100  Don't cache dir unless read in"
Data$ " $200  Don't sort directories"
Data$ " $400  No drag and depth gadgets"
Data$ " $800  Selecting a file to SAVE TO"
Data$ " $1000 Selecting a file to LOAD FROM"
Data$ " $2000 User to choose dir not file"

rawkeys
Data.b 22
Data$ "x"
Data$ "HEX CODES"
Data$ " 00-0D  ' - \        44 Enter"
Data$ " 10-1B  Q - ]        45 Escape"
Data$ " 20-2A  A - #        46 Del >"
Data$ " 31-3A  Z - /        4C Up"
Data$ " 40     Space        4D Down"
Data$ " 41     Del <        4E Right"
Data$ " 42     Tab          4F Left"
Data$ "NUMPAD            50-59 F keys"
Data$ "  5A,5B,5C,5D        5F Help"
Data$ "  3D,3E,3F,4A     60-61 L/R Shift"
Data$ "  20,2E,2F,5E        62 Caps-lock"
Data$ "  1D,1E,1F,          63 Ctrl"
Data$ "    0F  3C,43     64-65 L/R Alt"
Data$ "                  66-67 L/R Amiga"
Data$ "x"
Data$ " QUALIFIERS       Left  Right"
Data$ " Unqualified      $0000 $0000"
Data$ " Shift            $0001 $0002"
Data$ " Caps-lock down   $0004 $0004"
Data$ " Control          $0008 $0008"
Data$ " Alt              $0010 $0020"
Data$ " Amiga            $0040 $0080"

strcomms
Data.b 27
Data$ " Although these appear on the menu-bar when you"
Data$ " press Help, they are not listed in the Library"
Data$ " of associated commands if, for example you"
Data$ " typed 'Chr$' and pressed RAmiga Help, and then"
Data$ " clicked on library - these wouldn't appear."
Data$ " Type the command and press Help to see full"
Data$ " syntax on menu-bar."
Data$ "x"
Data$ "$ commands"
Data$ " Bin#(Bin$)  Converts Binary$ to Decimal"
Data$ " CharCount($,65) would count 'A's (65) in $"
Data$ " Cipher$($) Encrypts/decrypts $"
Data$ " Hex#(Hex$) Converts Hex$ to Decimal"
Data$ " PeekTo$(addr,byte) Terminator is chosen byte"
Data$ " Repeats($) '...1'=3, 'Test'=1, ''=0"
Data$ " SearchBegin($,byte,begin) Like Instr, but will"
Data$ "   return position of byte in $ from begin pos."
Data$ " Search End($,byte,end) Like above but from end"
Data$ " Space$(#) Returns a string with # spaces"
Data$ "x"
Data$ "# commands"
Data$ " Avg, .l, .q(#1,#2) Returns 'type' average"
Data$ "   10=Avg(5,15), 10=Avg(5,16), 10.5=Avg.q(5,16)"
Data$ " IsEven(#) Would 'If # & 1' (IsOdd) be quicker?"
Data$ " Largest, .l, .q(#1,#2) =largest # of 'type'"
Data$ " Max(#1,#2) returns largest 'Integer'"
Data$ " Min(#1,#2) returns smallest 'Integer'"
Data$ " Smallest, .l, .q(#1,#2) =smallest # of 'type'"
Data$ " Xor(#1,#2) returns exclusive or of #1 and #2"

cxevents
Data.b 12
Data$ "x"
Data$ "CONSTANT        VAL: MESSAGE-MEANING"
Data$ ""
Data$ "#CXCMD_DISABLE   15  disable commodity"
Data$ "#CXCMD_ENABLE    17  enable commodity"
Data$ "x"
Data$ "                 Use SetStatus with the above"
Data$ "#CXCMD_APPEAR    19  show interface if possible"
Data$ "#CXCMD_DISAPPEAR 21  hide interface"
Data$ "#CXCMD_KILL      23  quit"
Data$ "#CXCMD_UNIQUE    25  another commodity trying to"
Data$ "                     launch with same name: let"
Data$ "                     user know/show interface"
Data$ "#CXCMD_LIST_CHG  27  broker list changed"

menuitemflags
Data.b 5
Data$ "0  Standard select item"
Data$ "1  Toggle-item with check-mark"
Data$ "2  Mutually-exclusive toggle-item w check-mark"
Data$ "3  Like 1 - but item will be 'on' initially"
Data$ "4  Like 2 - but item will be 'on' initially"

printer
Data.b 4
Data$ "x"
Data$ "CheckPrt results:"
Data$ "-1 Printer Off"
Data$ "-3 Printer Off-Line"
Data$ "-4 Printer Ready"

CD32
Data.b 28
Data$ "x"
Data$ " GameB values         JoyR values"
Data$ " 1   Play/Pause            0"
Data$ " 2   << Reverse         7     1"
Data$ " 4   >> Forward       6    8    2"
Data$ " 8   Green              5     3"
Data$ " 16  Yellow                4"
Data$ " 32  Red"
Data$ " 64  Blue"
Data$ " "
Data$ "x"
Data$ " 4-Player adaptor commands"
Data$ "x"
Data$ " Zone-Joy Library"
Data$ " AllFire - af.b=AllFire [(bit pattern)]"
Data$ "           bit 0: Joy 1 (JoyPort)"
Data$ "           bit 1: Joy 2 (MousePort)"
Data$ "           bit 2: Joy 3 (Adapter)"
Data$ "           bit 3: Joy 4 (Adapter)"
Data$ "   optional bit-pattern tests specific"
Data$ "   joysticks - Allfire (%0011) will test"
Data$ "   firebuttons of joysticks 1 and 2."
Data$ " "
Data$ " JFire  - jf.b=JFire (#joy)"
Data$ "          returns -1 (pressed), 0 (not)"
Data$ " "
Data$ " JHoriz - jh.b=JHoriz (#joy)"
Data$ "          Like JoyX"
Data$ " "
Data$ " JVert  - jv.b=JVert (#joy)"
Data$ "          Like JoyY"

gurus
Data.b 12
Data$ "01  Fault found by Exec library"
Data$ "02  Graphics library: out of mem?"
Data$ "03  Layers library: out of mem?"
Data$ "04  Intuition library: programming error"
Data$ "05  Maths problem; FPU or libraries"
Data$ "07  Dos library: disk or messaging fault"
Data$ "10  Audio device problem"
Data$ "13  Keyboard device problem"
Data$ "14  Trackdisk: disk drive fault?"
Data$ "15  Timer: CIA or power problem?"
Data$ "31  Workbench has found something wrong"
Data$ "80  Dead end - normally detected by CPU"

other
Data.b 4
Data$ "x"
Data$ " USEFUL BITS"
Data$ " To poke the background colour in Blitz mode:"
Data$ "     MOVE.w $Colour,$DFF180"
Data$ "  i.e $F00 = red."
;start new data here

.dataend
