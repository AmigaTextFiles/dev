
; "Suite.ab2" a C to Blitz adaptation by Lorence Lombardo.
; Based on Allenbrand Brice's "Suite.c" example.


INCLUDE "Bignum.include.bb2"

If Bignum_InitLib {37}=0 Then NPrint "Could not open lib...!!!" :End

jmax.l=0 : l$=Chr$(10) : DEFTYPE .BigNum *z,*Un

Macro Test
   ii.l=`1 : jj.l=0
   !BigNumInit {*z}
   !BigNumInit {*Un}
   !BigNumIntToBigNum {*Un,1}
   !BigNumIntToBigNum {*z,ii}

   !BigNumFastCompare {BNFC.w, *z, *Un}
   While BNFC

      !BigNumIsEven {BNIE.w,*z}
      If BNIE
         !BigNumRightShift {*z,1}
      Else
         !BigNumDigitMul {*z,3} : !BigNumDigitAdd {*z,1}
      EndIf
      jj+1

      !BigNumFastCompare {BNFC,*z,*Un}

      If jj>jmax
         jmax=jj : BNFC=0
         PutStr_ Str$(ii)+" - "+Str$(jj)+l$
      EndIf

   Wend
   !BigNumFree {2}
End Macro


DEFTYPE .l i, j, k, mm, zz : #SIZE=1000000 : Dim *buf.l(#SIZE)


mm=0 ; <- This actually tests !Test{k}
; mm=1431655765 ; <- This makes "Suite.ab2" behave more like the default "Suite.c"

; When mm=0 for "Suite.ab2" and "Suite.c" the numerical output is the same,
; other than the timer. ;)  ie. without any args.


If NumPars>0
   arg$=Par$(1) : StrToLong_ &arg$, &zz
Else
   zz=100000
EndIf

For k=3 To zz-1 Step 2
   i=0 : j=k
   While j<>1

      If j<#SIZE AND *buf(j)<>0 Then i=i + *buf(j) : j=1

      If j<>1
         If j AND 1
            If j=>mm Then !Test{k} : j=1 Else j=j*3+1
         Else
            j=j LSR 1 : i+1
         EndIf
      EndIf

   Wend

   If i>jmax Then PutStr_ Str$(k)+" -- "+Str$(i)+l$ : jmax=i
   If k<#SIZE Then *buf(k)=i

Next k

Bignum_FreeLib {}

End


