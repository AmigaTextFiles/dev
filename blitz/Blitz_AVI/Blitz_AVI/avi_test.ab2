
; "avi_test.ab2" a C to Blitz adaptation by Lorence Lombardo.
; Based on "test.c" from the "avilib.library" package.

; Requires an FPU

; CLI Usage:- avi_test_fpu.exe <AVI_file>

optimize 7

If NumPars<1 Then End

INCLUDE "avilib.include.bb2"

If avilib_InitLib{1}=0 Then NPrint " AVI lib not available...!!!" : End

avi_file$=Par$(1) : bla.l=0 : l$=Chr$(10)

!avilib_AVI_open_input_file {*avifile.avi_t, &avi_file$, 1}

If *avifile

   !avilib_AVI_video_frames {fframes.l, *avifile}
   !avilib_AVI_video_width {width.l, *avifile}
   !avilib_AVI_video_height {height.l, *avifile}

   !avilib_AVI_frame_rate {fps.d, *avifile}

   !avilib_AVI_video_compressor {addy.l, *avifile}

   Print l$+"[avilib] V: ",fps," fps, codec="+Peek$(addy)
   NPrint ", frames=",fframes,", width=",width,", height=",height

   !avilib_AVI_audio_tracks {tracks.l, *avifile}

   !avilib_AVI_get_audio_track{tmp.l, *avifile}

   For j.l=0 To tracks-1

      !avilib_AVI_set_audio_track {bla, *avifile, j}

      !avilib_AVI_audio_rate {rate.l, *avifile}
      !avilib_AVI_audio_format {form.l, *avifile}
      !avilib_AVI_audio_channels {chan.l, *avifile}
      !avilib_AVI_audio_bits {bits.l, *avifile}
      !avilib_AVI_audio_mp3rate {mp3rate.l, *avifile}

      !avilib_AVI_audio_chunks {chunks.l, *avifile}
      !avilib_AVI_audio_bytes {tot_bytes.l, *avifile}

      If chan>0
         Print "[avilib] A: ",rate," Hz, format=$"+StripLead$(Hex$(form),48)
         NPrint ", bits=",bits,", channels=",chan,", bitrate=",mp3rate," kbps"
         NPrint "[avilib]    ",chunks," chunks, ",tot_bytes," bytes"+l$
      Else
         NPrint l$+"[avilib] A: no audio track found"+l$
      EndIf

   Next j

   !avilib_AVI_set_audio_track {bla, *avifile, tmp}

   !avilib_AVI_close {bla, *avifile}

Else

   NPrint l$+" AVI failed to open."+l$

EndIf

avilib_FreeLib {}

End


