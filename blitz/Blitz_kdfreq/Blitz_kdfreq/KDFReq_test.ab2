
; "KDFReq_test.ab2" a C to Blitz adaptation by Lorence Lombardo.

; Based on Khalid Aldoseri's "test.c".


INCLUDE "KDFReq.include.bb2"

If KDFReq_InitLib{3}=0 Then NPrint "Could not initialize KDFReq lib...!!!" : End

msg1$="Usr But'n"
msg2$="An Extra Button"
msg3$="for that special function"
msg4$="in your program"

eb.ExtraButton\Nxt=0,0,0,&msg1$,&msg2$,&msg3$,&msg4$,0,0


!KDFReq_CreateFRequest {*fr.FRequest}

If *fr=0
   NPrint "Could not Create File Request...!!!"
   KDFReq_FreeLib{} : End
EndIf

tit$="Lombi's AB2 FR Test" : *fr\reqtitle=&tit$

*fr\flags = #FR_STDFLAGS | #FR_SELECTLIST | #FR_EXTRABUTTONS | #FR_USEPENS

eb\button_function = ?but

*fr\extras\button = &eb

Poke$ *fr\pattern,"#?"
Poke$ *fr\extras\Hide,"#?.o"

*fr\extras\pen0 = 1
*fr\extras\pen1 = 1
*fr\extras\pen2 = 2
*fr\extras\pen3 = 3


MOVE.l a5,globalvars ; <- this is only needed for the user defined button


!KDFReq_NewFReq {*fyl.b, *fr}
NPrint ""

If *fyl
   NPrint " Selected: "+Peek$(*fyl)
Else
   NPrint " Canceled."
EndIf
NPrint ""

NPrint "Path    : "+Peek$(*fr\fullname)
NPrint "Title   : "+Peek$(*fr\reqtitle)
NPrint "Dir     : "+Peek$(*fr\directory)
NPrint "File    : "+Peek$(*fr\filename)
NPrint "Show Pat: "+Peek$(*fr\pattern)
NPrint "Hide Pat: "+Peek$(*fr\extras\Hide)
NPrint "Position: ",*fr\extras\LeftEdge," , ",*fr\extras\TopEdge
NPrint "Size    : ",*fr\extras\Width," x ",*fr\extras\Height

NPrint ""

If *fr\extras\SelectList
   NPrint "Select List:" : NPrint ""
   Repeat
      !KDFReq_NextSelectEntry {*entry.b, *fr}
      If *entry Then NPrint Peek$(*entry)
   Until *entry=0
   NPrint ""
EndIf

!KDFReq_DeleteFRequest {*fr}

KDFReq_FreeLib{} : End


; This below stuff is only required if your having a user defined button.

but:
MOVEM.l   d1-d7/a0-a6,-(a7)
MOVE.l globalvars,a5

;  Put your stuff here.
NPrint""
NPrint "Hey!  The user clicked on the extra button!!"
NPrint "Button: "+Peek$(eb\text)
NPrint "Window: $"+Hex$(eb\Win)
NPrint""
GetReg d0, #EB_CONTINUE  ; <- tells requester to continue
;  End of your stuff

MOVEM.l   (a7)+,d1-d7/a0-a6
RTS


globalvars:
Dc.l 0


