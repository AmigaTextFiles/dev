
; "crmscrunch.ab2" a C to Blitz adaptation by Lorence Lombardo.

; Based on Michael Mutschler's "crmscrunch.c" example.

; "crmscrunch.exe" simply crunch's a file in LZH & sample mode.


If NumPars<2 Then NPrint "Usage: crmscrunch <infile> <outfile>" : End

INCLUDE "CrM.include.bb2"

If CrM_InitLib{0}=0 Then NPrint "Could not initialize CrM lib...!!!" : End

Dim mytags.TagItem(1) : arg1$=Par$(1) : arg2$=Par$(2)

lock.l=0 : fmem.l=0 : fh.l=0 : newlen.l=0 : *fib.FileInfoBlock=0

mytags(0)\ti_Tag = #CMCS_Algo, #cm_LZH | #cmF_Sample | #cmF_Overlay
mytags(1)\ti_Tag = #TAG_DONE

!CrM_cmAllocCrunchStructA {*crunchstruct.cmCrunchStruct, &mytags(0)\ti_Tag}
If *crunchstruct=0 Then NPrint "Couldn't allocate struct!" : Goto exit

lock = Lock_(&arg1$, #ACCESS_READ)
If lock=0 Then NPrint "file not found!" : Goto exit

*fib = AllocMem_(SizeOf.FileInfoBlock, #MEMF_PUBLIC)
If *fib=0 Then NPrint "No Mem!" : Goto exit

Examine_ lock, *fib

fmem = AllocMem_(*fib\fib_Size, #MEMF_PUBLIC)
If fmem=0 Then NPrint "No Mem!!" : Goto exit

UnLock_(lock) : lock=0

fh = Open_(&arg1$, #MODE_OLDFILE)
rlen.l = Read_(fh, fmem, *fib\fib_Size)
If rlen<>*fib\fib_Size Then NPrint "Error while reading!" : Goto exit
Close_(fh) : fh=0

*crunchstruct\cmcr_Src     = fmem
*crunchstruct\cmcr_SrcLen  = *fib\fib_Size
*crunchstruct\cmcr_Dest    = fmem
*crunchstruct\cmcr_DestLen = *fib\fib_Size
*crunchstruct\cmcr_DataHdr = &dataheader.DataHeader

!CrM_cmCrunchData{newlen, *crunchstruct}
If newlen=0 Then NPrint "crunching failed!" : Goto exit

fh = Open_(&arg2$, #MODE_NEWFILE)
If fh=0 Then NPrint "Dest File open failed!" : Goto exit

wlen.l = Write_(fh, &dataheader, SizeOf.DataHeader)
If wlen<>SizeOf.DataHeader Then NPrint "Error while writing!" : Goto exit

wlen = Write_(fh, fmem, newlen)
If wlen<>newlen Then NPrint "Error while writing!!" : Goto exit

NPrint "oldfile: "+arg1$+"  length: ",*fib\fib_Size
NPrint "newfile: "+arg2$+"  length: ",newlen

exit:
If fh Then Close_(fh)
If *fib Then FreeMem_ *fib, SizeOf.FileInfoBlock
If fmem Then FreeMem_ fmem, *fib\fib_Size
If lock Then UnLock_(lock)
If *crunchstruct Then !CrM_cmFreeCrunchStruct{*crunchstruct}
CrM_FreeLib{} : End


