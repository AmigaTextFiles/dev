;
; AHI Sub Library for Blitz Basic.
;
; (c) 1998 AlphaSOUND - Fantaisie Software


INCLUDE "AHI_Include.bb2"

FREQUENCY.l  = 16000          ; You can modify these value
MY_TYPE.l    = #AHIST_M8S     ;

DEFTYPE .MsgPort    *AHImp
DEFTYPE .AHIRequest *AHIio, *MyLINK


.AHI_CleanUP
Statement AHI_CleanUP{rc.l}
  SHARED *AHIDevice, *AHIio, *AHImp

  If *AHIDevice=0
    CloseDevice_ *AHIio
  EndIf

  DeleteIORequest_ *AHIio
  DeleteMsgPort_   *AHImp
End Statement


.AHI_LoadSound
Function.l AHI_LoadSound{FileName.s}
  SHARED NFileLen.l

  *MyFile.l=Open_ (FileName,1005)
  If *MyFile
    NFileLen.l=Seek_(*MyFile,0,1)  ; Here we have the real size of
    NFileLen.l=Seek_(*MyFile,0,1)  ; the file.

    Seek_ *MyFile,0,-1  ; Begining of the file.

    *MemPtr.l=AllocMem_(NFileLen,0) ; Get an buffer of ANY memory
    If *MemPtr
      Read_ *MyFile, *MemPtr, NFileLen
    EndIf

    Close_ *MyFile
  EndIf

  Function Return *MemPtr
End Function


.AHI_Init
Function.l AHI_Init{}
  SHARED *AHImp, *AHIio, *AHIDevice, *AHIio2, AHINAME

  Ok.l=1

  *AHImp = CreateMsgPort_
  If *AHImp

    *AHIio=CreateIORequest_(*AHImp, SizeOf .AHIRequest)
    If *AHIio
      *AHIio\ahir_Version = 4
      *AHIDevice=OpenDevice_(&AHINAME, 0, *AHIio, 0)
      SendIO_ *AHIio ; This avoid crashe when closing AHI and no sound were played
    Else
      Ok=0
    EndIf

    If *AHIDevice <> 0
      AHI_CleanUP{0}
      Ok=0
    EndIf
  Else
    Ok=0
  EndIf

  Function Return Ok
End Function


.AHI_PlaySound
Statement AHI_PlaySound{*p1.l, leng.l, ASync.l}
  SHARED *AHIio, FREQUENCY, *MyLINK, MY_TYPE, pri

  *AHIio\ahir_Std\io_Message\mn_Node\ln_Pri = pri
  *AHIio\ahir_Std\io_Command  = #CMD_WRITE
  *AHIio\ahir_Std\io_Data     = *p1
  *AHIio\ahir_Std\io_Length   = leng
  *AHIio\ahir_Std\io_Offset   = 0
  *AHIio\ahir_Frequency       = FREQUENCY
  *AHIio\ahir_Type            = MY_TYPE
  *AHIio\ahir_Volume          = $10000          ; Full Volume
  *AHIio\ahir_Position        = $1000           ; Centered
  *AHIio\ahir_Link            = *MyLINK
  SendIO_ *AHIio

  If ASync = 0
    WaitIO_ *AHIio
  EndIf
End Statement


.AHI_FreeSound
Statement AHI_FreeSound{*MySound.l, SoundLen.l}
  FreeMem_ *MySound, SoundLen
End Statement


.AHI_Close
Statement AHI_Close{}
  SHARED *AHIio

  If *AHIio<>0
    AbortIO_ *AHIio
    WaitIO_  *AHIio

    AHI_CleanUP{0}

    *AHIio = 0
  EndIf
End Statement
