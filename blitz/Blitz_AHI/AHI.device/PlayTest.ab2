;/*
;** This program uses the device interface To play a sampled Sound.
;** The input is Read from THE Default INPUT, make sure you
;** start it with "PlayTest pri < mysample.raw" !
;** Where pri is a number from -128 To +127 (may be omitted)
;** The sample should be 8 bit signed, mono (see Type).
;*/
;
; An Litteral C/C++ -> Blitz 2 Adaptation.
;
;     Done by AlphaSOUND - Fantaisie Software


INCLUDE "AHI_Include.bb2"

#SIGBREAKF_CTRL_C = 1 ASL 12

#RETURN_ERROR = 2
#RETURN_FAIL  = 1
#RETURN_OK    = 0

#FREQUENCY  = 16726
#MY_TYPE    = #AHIST_M8S
#BUFFERSIZE = 20000

DEFTYPE .MsgPort    *AHImp
DEFTYPE .AHIRequest *AHIio
DEFTYPE .AHIRequest *AHIio2

AHIDevice.b = -1

;#My_Buffer1[#BUFFERSIZE]
;#My_Buffer2[#BUFFERSIZE]

*MyBuffer1 = AllocMem_(#BUFFERSIZE, 0)
*MyBuffer2 = AllocMem_(#BUFFERSIZE, 0)


Statement cleanup{rc.l}
  SHARED *AHIDevice, *AHIio, AHIio2, *AHImp, *MyBuffer1, *MyBuffer2

  If *AHIDevice=0
    CloseDevice_ *AHIio
  EndIf

  DeleteIORequest_ *AHIio
  DeleteIORequest_ *AHIio2
  DeleteMsgPort_   *AHImp

  ; The following added by Lombi

  FreeMem_ *MyBuffer1 ,#BUFFERSIZE
  FreeMem_ *MyBuffer2 ,#BUFFERSIZE

  NPrint "" : NPrint ""
  If rc=1 Then NPrint " Failed...!!!"
  If rc=0 Then NPrint " Done...!!!"
  NPrint ""

  ; End Lombi ;)

End Statement


*p1=*MyBuffer1
*p2=*MyBuffer2

DEFTYPE .l signals, leng, priority

DEFTYPE .AHIRequest *MyLINK

DEFTYPE.b pri

pri = 1  ; Priority

NPrint "Usage : PlayTest < YourSound.RAW"

NPrint "Sound priority: ", pri

*AHImp = CreateMsgPort_()
If *AHImp

  *AHIio=CreateIORequest_(*AHImp, SizeOf .AHIRequest)
  If *AHIio
    *AHIio\ahir_Version = 4
    *AHIDevice=OpenDevice_(&AHINAME, 0, *AHIio, 0)
  EndIf

  If *AHIDevice <> 0
    NPrint "Unable to open ", AHINAME, " version 4"
    cleanup{#RETURN_FAIL}
    End
  EndIf

; Make a copy of the Request (For double buffering)

  *AHIio2 = AllocMem_ (SizeOf .AHIRequest, #MEMF_ANY)
  If *AHIio2 = 0
    cleanup{#RETURN_FAIL}
    End
  EndIf

  CopyMem_ *AHIio, *AHIio2, SizeOf .AHIRequest

  SetIoErr_ 0

  Ok=1
  While Ok=1

; Fill Buffer
    leng = Read_(Input_, *p1, #BUFFERSIZE)

; Play Buffer
    *AHIio\ahir_Std\io_Message\mn_Node\ln_Pri = pri
    *AHIio\ahir_Std\io_Command  = #CMD_WRITE
    *AHIio\ahir_Std\io_Data     = *p1
    *AHIio\ahir_Std\io_Length   = leng
    *AHIio\ahir_Std\io_Offset   = 0
    *AHIio\ahir_Frequency       = #FREQUENCY
    *AHIio\ahir_Type            = #MY_TYPE
    *AHIio\ahir_Volume          = $10000          ; Full Volume
    *AHIio\ahir_Position        = $8000           ; Centered
    *AHIio\ahir_Link            = *MyLINK
    SendIO_ *AHIio

    If *MyLINK

      Print "Ok "

      ; Wait Until the last Buffer is finished (== the new Buffer is started)

      signals=Wait_(#SIGBREAKF_CTRL_C | (1 LSL *AHImp\mp_SigBit))

      ; Check For Ctrl-C AND abort If pressed
      If (signals & #SIGBREAKF_CTRL_C) <> 0
        SetIoErr_ 1 ;#ERROR_BREAK
        Ok=0 ; End
      EndIf

      ; Remove the reply AND abort On error
      If WaitIO_(*MyLINK)
        SetIoErr_ 2 ;#ERROR_WRITE_PROTECTED
        Ok=0 ; End
      EndIf
    EndIf

    ; Check For End-of-Sound, AND Wait Until it is finished before aborting
    If leng <> #BUFFERSIZE
      WaitIO_ *AHIio
      Ok=0 ; End
    EndIf

    If Ok=1  ; Not Abort
      *MyLINK = *AHIio

      ; Swap Buffer and Request pointers, and restart

      Exchange *p1, *p2  ; Easier and FASTER !!
      Exchange *AHIio, *AHIio2
    EndIf

  Wend

  ; Abort any pending iorequests

  AbortIO_ *AHIio
  WaitIO_  *AHIio

  If *MyLINK  ; Only If the second Request was started
    AbortIO_ *AHIio2
    WaitIO_ *AHIio2
  EndIf

  ;If IoErr_
  ;  PrintFault_(IoErr(), argv[0] )
  ;  cleanup{#RETURN_ERROR}
  ;EndIf

  cleanup{#RETURN_OK}
EndIf

End

