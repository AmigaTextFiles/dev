
; /*
; ** This program uses the device interface to sample sound data.
; ** The output is written to THE DEFAULT OUTPUT, make sure you
; ** start it with "RecordTest > mysample.raw" !
; */
;
; A C/C++ to AmiBlitz Adaptation
;
;     Done by Lorence Lombardo in 2008
;
; Complementing the work of AlphaSOUND
;
; Use "PlayTest.exe" to play back your sound.
;


INCLUDE "AHI_Include.bb2"

#SIGBREAKF_CTRL_C = 1 ASL 12

#RETURN_ERROR = 2
#RETURN_FAIL  = 1
#RETURN_OK    = 0

#FREQUENCY  = 16726
#MY_TYPE    = #AHIST_M8S
#BUFFERSIZE = 20000

DEFTYPE .MsgPort    *AHImp
DEFTYPE .AHIRequest *AHIio

AHIDevice.b = -1

;#My_Buffer1[#BUFFERSIZE]
;#My_Buffer2[#BUFFERSIZE]

*MyBuffer1 = AllocMem_(#BUFFERSIZE, 0)
*MyBuffer2 = AllocMem_(#BUFFERSIZE, 0)


Statement cleanup{rc.l}
  SHARED *AHIDevice, *AHIio, *AHImp, *MyBuffer1, *MyBuffer2

  If *AHIDevice=0
    CloseDevice_ *AHIio
  EndIf

  DeleteIORequest_ *AHIio
  DeleteMsgPort_   *AHImp

  ; The following added by Lombi

  FreeMem_ *MyBuffer1 ,#BUFFERSIZE
  FreeMem_ *MyBuffer2 ,#BUFFERSIZE

  If rc=1 Then NPrint " Failed...!!!"

  ; End Lombi ;)

End Statement


*p1=*MyBuffer1
*p2=*MyBuffer2

DEFTYPE .l signals, leng, priority

*AHImp = CreateMsgPort_()
If *AHImp

   *AHIio=CreateIORequest_(*AHImp, SizeOf .AHIRequest)
   If *AHIio
      *AHIio\ahir_Version = 4
      *AHIDevice=OpenDevice_(&AHINAME, 0, *AHIio, 0)
   EndIf

   If *AHIDevice <> 0
      cleanup{#RETURN_FAIL}
      End
   EndIf

   ; Play Buffer
   *AHIio\ahir_Std\io_Command  = #CMD_READ
   *AHIio\ahir_Std\io_Data     = *MyBuffer1
   *AHIio\ahir_Std\io_Length   = #BUFFERSIZE
   *AHIio\ahir_Std\io_Offset   = 0
   *AHIio\ahir_Frequency       = #FREQUENCY
   *AHIio\ahir_Type            = #MY_TYPE

   If NOT DoIO_(*AHIio)

      SetIoErr_ 0 : Ok=1

      While Ok=1

         leng.l=*AHIio\ahir_Std\io_Actual

         *AHIio\ahir_Std\io_Data     = *p2
         *AHIio\ahir_Std\io_Length   = #BUFFERSIZE
         *AHIio\ahir_Frequency       = #FREQUENCY
         *AHIio\ahir_Type            = #MY_TYPE

         SendIO_(*AHIio)

         If (Write_(Output_, *p1, leng))<1 Then Ok=0

         signals=Wait_(#SIGBREAKF_CTRL_C | (1 LSL *AHImp\mp_SigBit))

         ; Check For Ctrl-C AND abort If pressed
         If (signals & #SIGBREAKF_CTRL_C) <> 0
            SetIoErr_ 1 ;#ERROR_BREAK
            Ok=0 ; End
         EndIf

         If WaitIO_(*AHIio)
            SetIoErr_ #ERROR_READ_PROTECTED
            Ok=0 ; End
         EndIf

         If Ok=1  ; Not Abort
            ; Swap Buffer and Request pointers, and restart
            Exchange *p1, *p2  ; Easier and FASTER !!
         EndIf

      Wend

   EndIf

; Abort any pending iorequests

  AbortIO_ *AHIio
  WaitIO_  *AHIio


  ;If IoErr_
  ;  PrintFault_(IoErr(), argv[0] )
  ;  cleanup{#RETURN_ERROR}
  ;EndIf

  cleanup{#RETURN_OK}
EndIf

End

