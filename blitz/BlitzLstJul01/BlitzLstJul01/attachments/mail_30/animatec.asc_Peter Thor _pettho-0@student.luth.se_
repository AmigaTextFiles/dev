;
; Example of animation of characters.
;
; This code is PD and I hereby allow you to use it in any
; way you like.
;
; Peter Thor, 27/6 - 2001
;

  #WARRIOR  = 0                         ; different enemytypes
  #ARCHER   = 1                         ; I only use #WARROR thought

  #WALK     = 0                         ; different types of movement
  #ATTACK   = 1

  #DOWN   = 0                           ; constants for the directions
  #RIGHT  = 1                           ; must be carefully selected
  #UP     = 2
  #LEFT   = 3

  #DIRECTIONS         = 4               ; 4 directions
  #SHAPESPERDIRECTION = 8               ; 8 shapes/direction
  #CHARACTERS         = 1               ; 1 characters
  #CHARACTERTYPES     = 1               ; only 1 charactertype
  #NROFCHARACTERS     = 1               ; 1 character

NEWTYPE .characterdirframes             ; the number of the frames
    walk.w     [4]                      ; for each of the 4
    attack.w [4]                        ; directions to use
End NEWTYPE

NEWTYPE .character
    x.w
    y.w
    movement.b                          ; WALK or ATTACK or ..
    direction.b                         ; UP or DOWN or ..
    framecount.b                        ; 0 to 8-1 (eight shapes/dir)
End NEWTYPE

;--

  ; Initialize the character(s)
  Dim Characters.character(#NROFCHARACTERS-1)
  Dim CharacterDirs.characterdirframes (#CHARACTERTYPES-1)

  ; Now, for each direction, initialize the number
  ; of the shapenr each character-type's direction STARTS at.
  ; the \framecount in .character is added to this value
  ; so that the next shapenr in the animsequence is shown.
  ; the #DIRECTIONS*SHAPESPERDIRECTION is the offset to the
  ; attack-Frames after the walkframes
  ; (4 directions and 8 shapes in each direction).
  ; Remember, we have only got 1 charactertype (#WARRIOR)
  ; This For-loop very is important to understand, it is used in
  ; .AnimateCharacters.
  ;
  For n.w = 0 To #DIRECTIONS-1
      CharacterDirs(#WARRIOR)\walk[n] = n * #SHAPESPERDIRECTION
      CharacterDirs(#WARRIOR)\attack[n] = n * #SHAPESPERDIRECTION + #DIRECTIONS*SHAPESPERDIRECTION
  Next

  ;--

  ; Ok now when we have defined all the framenumbers to use
  ; in each direction we should try and use them.
  ; Something like this could be used:

.
.MAINLOOP
  Repeat

    ; change the \direction and see what happens to the shapenr.w in
    ; AnimateCharacters

    Characters(#WARRIOR)\direction = #LEFT

    If Characters(#WARRIOR)\framecount < (#SHAPESPERDIRECTION-1)
      Characters(#WARRIOR)\framecount + 1  ; increase framenr
    Else
      Characters(#WARRIOR)\framecount = 0  ; Animated one loop, so reset it
    EndIf

    ; check for joystick/keyboard and set ShouldMove = True
    ; if something was pressed.. I've skipped it thought

    ShouldMove = True

    ;--

    If ShouldMove
        Gosub AnimateCharacters
        ShouldMove = False
    EndIf

    If RawStatus($45) Then Quit.b = True

  Until Quit = True

  End

;--

.AnimateCharacters
  ;
  ; Get hold and blit the shapenr of the character in the animsequence
  ;

  framecount.b = Characters(#WARRIOR)\framecount    ; what frame?
  direction.b = Characters(#WARRIOR)\direction            ; what dir?

  Select Characters(#WARRIOR)\movement        ; how is it moving?
      Case #WALK
          ; get hold of the shapenr to blit
         shapenr.w = CharacterDirs(#WARRIOR)\walk[direction]

      Case #ATTACK
          shapenr.w = CharacterDirs(#WARRIOR)\attack[direction]
  End Select

  shapenr + framecount
  x.w = Characters(#WARRIOR)\x
  y.w = Characters(#WARRIOR)\x
;  QBlit 0, shapenr, x,y

Return
;
