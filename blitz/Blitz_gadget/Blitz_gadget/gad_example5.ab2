
; "gad_example5.ab2" a C to Blitz adaptation by Lorence Lombardo.
; Based on Steffen Gutmann's "example5.c".

; I suprised myself when I got the text labels working in this one. ;)


INCLUDE "gadget.include.bb2"

If gadget_InitLib{38}=0 Then NPrint " Could not initialize v38 gadget lib...!!!" : End

DEFTYPE .NewWindow NewWin : tit$="gadget.library example 5"

NewWin\LeftEdge    = 10
NewWin\TopEdge     = 10
NewWin\Width       = 300
NewWin\Height      = 160
NewWin\DetailPen   = #AUTOFRONTPEN
NewWin\BlockPen    = #AUTOBACKPEN
NewWin\IDCMPFlags  = #GAD_IDCMPFlags | #IDCMP_CLOSEWINDOW
NewWin\Flags       = #WFLG_CLOSEGADGET | #WFLG_DRAGBAR | #WFLG_DEPTHGADGET | #WFLG_ACTIVATE
NewWin\FirstGadget = 0
NewWin\CheckMark   = 0
NewWin\Title       = &tit$
NewWin\Screen      = 0
NewWin\BitMap      = 0
NewWin\MinWidth    = 100
NewWin\MinHeight   = 50
NewWin\MaxWidth    = -1
NewWin\MaxHeight   = -1
NewWin\Type        = #WBENCHSCREEN

Dim gadtags1.TagItem(7), gadtags2.TagItem(6), gadtags3.TagItem(10)
Dim cyclabels$(12) , mxlabels$(4) , cyclabels_ptr.l(12) , mxlabels_ptr.l(4)
*win.Window=0 : *gad1.Gadget=0 : *gad2.Gadget=0 : *gad3.Gadget=0

For b.b=0 To 11 : Read cyclabels$(b) : cyclabels_ptr(b)=&cyclabels$(b) : Next b
For b.b=0 To 3 : Read mxlabels$(b) : mxlabels_ptr(b)=&mxlabels$(b) : Next b

txt$="C_ycle"
gadtags1(0)\ti_Tag = #GA_Left, 100
gadtags1(1)\ti_Tag = #GA_Top, 20
gadtags1(2)\ti_Tag = #GA_Text, &txt$
gadtags1(3)\ti_Tag = #GADCYC_Labels, &cyclabels_ptr(0)
gadtags1(4)\ti_Tag = #GADCYC_Active, 1
gadtags1(5)\ti_Tag = #GA_Previous, &NewWin\FirstGadget
gadtags1(6)\ti_Tag = #TAG_END

!gadget_gadAllocGadgetA {*gad1, #GAD_CYCLE_KIND, &gadtags1(0)\ti_Tag}

gadtags2(0)\ti_Tag = #GA_Left, 100
gadtags2(1)\ti_Tag = #GA_Top, 50
gadtags2(2)\ti_Tag = #GA_Previous, &*gad1\NextGadget
gadtags2(3)\ti_Tag = #GADMX_Labels, &mxlabels_ptr(0)
gadtags2(4)\ti_Tag = #GADMX_Active, 2
gadtags2(5)\ti_Tag = #TAG_END

!gadget_gadAllocGadgetA {*gad2, #GAD_MX_KIND, &gadtags2(0)\ti_Tag}

txt$="_Color"
gadtags3(0)\ti_Tag = #GA_Left, 100
gadtags3(1)\ti_Tag = #GA_Top, 120
gadtags3(2)\ti_Tag = #GA_Width, 100
gadtags3(3)\ti_Tag = #GA_Height, 18
gadtags3(4)\ti_Tag = #GA_Previous, &*gad2\NextGadget
gadtags3(5)\ti_Tag = #GA_Text, &txt$
gadtags3(6)\ti_Tag = #GADPA_Depth, 2
gadtags3(7)\ti_Tag = #GADPA_ColorOffset, 1
gadtags3(8)\ti_Tag = #GADPA_IndicatorWidth, 36
gadtags3(9)\ti_Tag = #TAG_END

!gadget_gadAllocGadgetA {*gad3, #GAD_PALETTE_KIND, &gadtags3(0)\ti_Tag}

If *gad1=0 OR *gad2=0 OR *gad3=0 Then Goto exit

*win=OpenWindow_(&NewWin) : class.l=0

If *win
   Repeat
      *imsg.IntuiMessage=GetMsg_(*win\UserPort)
      If *imsg
         class=*imsg\Class
         !gadget_gadFilterMessage {bla.l, *imsg, 0}
         ReplyMsg_ *imsg
      Else
         Wait_(1 LSL *win\UserPort\mp_SigBit)
      EndIf
   Until class = #IDCMP_CLOSEWINDOW
EndIf

exit:
If *win Then CloseWindow_(*win)
If NewWin\FirstGadget Then !gadget_gadFreeGadgetList {bla.l, NewWin\FirstGadget}
gadget_FreeLib{}
End


Data$ "January", "February", "March", "April", "May", "June"
Data$ "July", "August", "September", "October", "November", "December"

Data$ "_8 Bit", "_7 Bit", "_6 Bit", "_other"

