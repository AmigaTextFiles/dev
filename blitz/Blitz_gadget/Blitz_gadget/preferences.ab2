
; "preferences.ab2" a C to Blitz adaptation by Lorence Lombardo.
; Based on Steffen Gutmann's "preferences.c".

; This is complete-ish ;)


INCLUDE "gadget.include.bb2"

If gadget_InitLib{38}=0 Then NPrint " Could not initialize v38 gadget lib...!!!" : End

DEFTYPE .NewWindow NewWin : tit$="Preferences (Example)"


#RETURN=13
#ESC=27
#PREF_SAVE=1
#PREF_USE=2
#PREF_LOAD=3
#PREF_CANCEL=0


NewWin\LeftEdge    = 10
NewWin\TopEdge     = 10
NewWin\Width       = 570
NewWin\Height      = 190
NewWin\DetailPen   = #AUTOFRONTPEN
NewWin\BlockPen    = #AUTOBACKPEN
NewWin\IDCMPFlags  = #GAD_IDCMPFlags | #IDCMP_CLOSEWINDOW
NewWin\Flags       = #WFLG_DRAGBAR | #WFLG_DEPTHGADGET | #WFLG_ACTIVATE | #WFLG_CLOSEGADGET
NewWin\FirstGadget = 0
NewWin\CheckMark   = 0
NewWin\Title       = &tit$
NewWin\Screen      = 0
NewWin\BitMap      = 0
NewWin\MinWidth    = 100
NewWin\MinHeight   = 50
NewWin\MaxWidth    = -1
NewWin\MaxHeight   = -1
NewWin\Type        = #WBENCHSCREEN

*win.Window=0

Insert1.l=True : Insert2.l=False : Keypad.l=True : AutoIndent.l=True
Backup.l=True : ImmediateScroll.l=True : NormalPri.l=True : Pri_Alternative.l=1
datahyphen.l=13 : varhyphen.l=9 : memohyphen.l=0 : quotes.l=True

editw.l = 28*8 : edith.l = 6*14
genw.l = editw : genh.l = 4*14
taskw.l = 36*8 : taskh.l = 4*14
impw.l = taskw : imph.l = 6*14
mainw.l = 16+editw+8+taskw+16
mainh.l = 8 + Max(edith+genh, taskh+imph) + 8

mainx.l = 8 : mainy.l = 10
editx.l = mainx+16 : edity.l = mainy+8
genx.l = editx : geny.l = edity+edith
taskx.l = editx+editw+8 : tasky.l=edity
impx.l = taskx : impy.l = tasky+taskh

DEFTYPE .Gadget *main, *eedit, *gen, *task, *imp, *ins1, *ins2, *key, *aut, *back
DEFTYPE .Gadget *imme, *def, *alt, *pri, *ddata, *fiel, *memo, *quot, *save, *uuse
DEFTYPE .Gadget *load, *cancel
DEFTYPE .l x, y, w, h, ende, prefresult, storage
Dim *gadptr.Gadget(3), gadtags.TagItem(10)


gadtags(0)\ti_Tag = #GA_Left, mainx
gadtags(1)\ti_Tag = #GA_Top, mainy
gadtags(2)\ti_Tag = #GA_Width, mainw
gadtags(3)\ti_Tag = #GA_Height, mainh
gadtags(4)\ti_Tag = #GADBB_Recessed, 0
gadtags(5)\ti_Tag = #GA_Previous, &NewWin\FirstGadget
gadtags(6)\ti_Tag = #TAG_END

!gadget_gadAllocGadgetA {*main, #GAD_BEVELBOX_KIND, &gadtags(0)\ti_Tag}

txt$="Edit"
gadtags(0)\ti_Tag = #GA_Left, editx
gadtags(1)\ti_Tag = #GA_Top, edity
gadtags(2)\ti_Tag = #GA_Width, editw
gadtags(3)\ti_Tag = #GA_Height, edith
gadtags(4)\ti_Tag = #GA_Previous, &*main\NextGadget
gadtags(5)\ti_Tag = #GA_Text, &txt$
gadtags(6)\ti_Tag = #TAG_END

!gadget_gadAllocGadgetA {*eedit, #GAD_BEVELBOX_KIND, &gadtags(0)\ti_Tag}

txt2$="General"
gadtags(0)\ti_Tag = #GA_Left, genx
gadtags(1)\ti_Tag = #GA_Top, geny
gadtags(2)\ti_Tag = #GA_Width, genw
gadtags(3)\ti_Tag = #GA_Height, genh
gadtags(4)\ti_Tag = #GA_Text, &txt2$
gadtags(5)\ti_Tag = #GA_Previous, &*eedit\NextGadget
gadtags(6)\ti_Tag = #TAG_END

!gadget_gadAllocGadgetA {*gen, #GAD_BEVELBOX_KIND, &gadtags(0)\ti_Tag}

txt3$="Taskpriority"
gadtags(0)\ti_Tag = #GA_Left, taskx
gadtags(1)\ti_Tag = #GA_Top, tasky
gadtags(2)\ti_Tag = #GA_Width, taskw
gadtags(3)\ti_Tag = #GA_Height, taskh
gadtags(4)\ti_Tag = #GA_Text, &txt3$
gadtags(5)\ti_Tag = #GA_Previous, &*gen\NextGadget
gadtags(6)\ti_Tag = #TAG_END

!gadget_gadAllocGadgetA {*task, #GAD_BEVELBOX_KIND, &gadtags(0)\ti_Tag}

txt4$="Import/Export"
gadtags(0)\ti_Tag = #GA_Left, impx
gadtags(1)\ti_Tag = #GA_Top, impy
gadtags(2)\ti_Tag = #GA_Width, impw
gadtags(3)\ti_Tag = #GA_Height, imph
gadtags(4)\ti_Tag = #GA_Text, &txt4$
gadtags(5)\ti_Tag = #GA_Previous, &*task\NextGadget
gadtags(6)\ti_Tag = #TAG_END

!gadget_gadAllocGadgetA {*imp, #GAD_BEVELBOX_KIND, &gadtags(0)\ti_Tag}

txt5$="_Insert Mode?" : x = editx+editw-50 : y = edity+14
gadtags(0)\ti_Tag = #GA_Left, x
gadtags(1)\ti_Tag = #GA_Top, y
gadtags(2)\ti_Tag = #GA_Text, &txt5$
gadtags(3)\ti_Tag = #GA_Previous, &*imp\NextGadget
gadtags(4)\ti_Tag = #TAG_END

!gadget_gadAllocGadgetA {*ins1, #GAD_CHECKBOX_KIND, &gadtags(0)\ti_Tag}

txt6$="I_nsert Mode (Memo)?" : y = y+14
gadtags(0)\ti_Tag = #GA_Left, x
gadtags(1)\ti_Tag = #GA_Top, y
gadtags(2)\ti_Tag = #GA_Text, &txt6$
gadtags(3)\ti_Tag = #GA_Previous, &*ins1\NextGadget
gadtags(4)\ti_Tag = #TAG_END

!gadget_gadAllocGadgetA {*ins2, #GAD_CHECKBOX_KIND, &gadtags(0)\ti_Tag}

txt7$="_Keypad = Special?" : y = y+14
gadtags(0)\ti_Tag = #GA_Left, x
gadtags(1)\ti_Tag = #GA_Top, y
gadtags(2)\ti_Tag = #GA_Text, &txt7$
gadtags(3)\ti_Tag = #GA_Previous, &*ins2\NextGadget
gadtags(4)\ti_Tag = #TAG_END

!gadget_gadAllocGadgetA {*key, #GAD_CHECKBOX_KIND, &gadtags(0)\ti_Tag}

txt8$="_AutoIndent (Memo)?" : y = y+14
gadtags(0)\ti_Tag = #GA_Left, x
gadtags(1)\ti_Tag = #GA_Top, y
gadtags(2)\ti_Tag = #GA_Text, &txt8$
gadtags(3)\ti_Tag = #GA_Previous, &*key\NextGadget
gadtags(4)\ti_Tag = #TAG_END

!gadget_gadAllocGadgetA {*aut, #GAD_CHECKBOX_KIND, &gadtags(0)\ti_Tag}

txt9$="_Backup in *.BAK?" : y = geny+14
gadtags(0)\ti_Tag = #GA_Left, x
gadtags(1)\ti_Tag = #GA_Top, y
gadtags(2)\ti_Tag = #GA_Text, &txt9$
gadtags(3)\ti_Tag = #GA_Previous, &*aut\NextGadget
gadtags(4)\ti_Tag = #TAG_END

!gadget_gadAllocGadgetA {*back, #GAD_CHECKBOX_KIND, &gadtags(0)\ti_Tag}

txt10$="Immediate _Scrolling?" : y = y+14
gadtags(0)\ti_Tag = #GA_Left, x
gadtags(1)\ti_Tag = #GA_Top, y
gadtags(2)\ti_Tag = #GA_Text, &txt10$
gadtags(3)\ti_Tag = #GA_Previous, &*back\NextGadget
gadtags(4)\ti_Tag = #TAG_END

!gadget_gadAllocGadgetA {*imme, #GAD_CHECKBOX_KIND, &gadtags(0)\ti_Tag}

txt11$="D_efault" : y = tasky+14
gadtags(0)\ti_Tag = #GA_Left, taskx+taskw/2-50
gadtags(1)\ti_Tag = #GA_Top, y
gadtags(2)\ti_Tag = #GA_Text, &txt11$
gadtags(3)\ti_Tag = #GA_Previous, &*imme\NextGadget
gadtags(4)\ti_Tag = #GA_UserData, &*gadptr(0)
gadtags(5)\ti_Tag = #GAD_CallBack, ?deftaskpri
gadtags(6)\ti_Tag = #TAG_END

!gadget_gadAllocGadgetA {*gadptr(0), #GAD_RADIOBUTTON_KIND, &gadtags(0)\ti_Tag}
*def=*gadptr(0)

txt12$="Al_ternative" : x=taskx+taskw-58
gadtags(0)\ti_Tag = #GA_Left, x
gadtags(1)\ti_Tag = #GA_Top, y
gadtags(2)\ti_Tag = #GA_Text, &txt12$
gadtags(3)\ti_Tag = #GA_Previous, &*def\NextGadget
gadtags(4)\ti_Tag = #GA_UserData, &*gadptr(0)
gadtags(5)\ti_Tag = #GAD_CallBack, ?alttaskpri
gadtags(6)\ti_Tag = #GA_UserData, &*def
gadtags(7)\ti_Tag = #TAG_END

!gadget_gadAllocGadgetA {*gadptr(1), #GAD_RADIOBUTTON_KIND, &gadtags(0)\ti_Tag}
*alt=*gadptr(1)

txt13$="Alternative _Priority:" : y=y+14
gadtags(0)\ti_Tag = #GA_Left, x
gadtags(1)\ti_Tag = #GA_Top, y
gadtags(2)\ti_Tag = #GA_Text, &txt13$
gadtags(3)\ti_Tag = #GA_Previous, &*alt\NextGadget
gadtags(4)\ti_Tag = #GADSTR_Min, -9
gadtags(5)\ti_Tag = #GADSTR_Max, 9
gadtags(6)\ti_Tag = #TAG_END

!gadget_gadAllocGadgetA {*gadptr(2), #GAD_INTEGER_KIND, &gadtags(0)\ti_Tag}
*pri=*gadptr(2)

txt14$="Hyphen between _Datasets:" : y=impy+14
gadtags(0)\ti_Tag = #GA_Left, x
gadtags(1)\ti_Tag = #GA_Top, y
gadtags(2)\ti_Tag = #GA_Text, &txt14$
gadtags(3)\ti_Tag = #GA_Previous, &*pri\NextGadget
gadtags(4)\ti_Tag = #GADSTR_Min, 0
gadtags(5)\ti_Tag = #GADSTR_Max, 255
gadtags(6)\ti_Tag = #TAG_END

!gadget_gadAllocGadgetA {*ddata, #GAD_INTEGER_KIND, &gadtags(0)\ti_Tag}

txt15$="Hyphen between _Fields:" : y=y+14
gadtags(0)\ti_Tag = #GA_Left, x
gadtags(1)\ti_Tag = #GA_Top, y
gadtags(2)\ti_Tag = #GA_Text, &txt15$
gadtags(3)\ti_Tag = #GA_Previous, &*ddata\NextGadget
gadtags(4)\ti_Tag = #GADSTR_Min, 0
gadtags(5)\ti_Tag = #GADSTR_Max, 255
gadtags(6)\ti_Tag = #TAG_END

!gadget_gadAllocGadgetA {*fiel, #GAD_INTEGER_KIND, &gadtags(0)\ti_Tag}

txt16$="Hyphen between _Memolines:" : y=y+14
gadtags(0)\ti_Tag = #GA_Left, x
gadtags(1)\ti_Tag = #GA_Top, y
gadtags(2)\ti_Tag = #GA_Text, &txt16$
gadtags(3)\ti_Tag = #GA_Previous, &*fiel\NextGadget
gadtags(4)\ti_Tag = #GADSTR_Min, 0
gadtags(5)\ti_Tag = #GADSTR_Max, 255
gadtags(6)\ti_Tag = #GADSTR_EndGadget, True
gadtags(7)\ti_Tag = #TAG_END

!gadget_gadAllocGadgetA {*memo, #GAD_INTEGER_KIND, &gadtags(0)\ti_Tag}

txt17$="Use Double _Quotes?" : y=y+14
gadtags(0)\ti_Tag = #GA_Left, x
gadtags(1)\ti_Tag = #GA_Top, y
gadtags(2)\ti_Tag = #GA_Text, &txt17$
gadtags(3)\ti_Tag = #GA_Previous, &*memo\NextGadget
gadtags(4)\ti_Tag = #TAG_END

!gadget_gadAllocGadgetA {*quot, #GAD_CHECKBOX_KIND, &gadtags(0)\ti_Tag}

txt18$="Save" : x=mainx+4 : y=mainy+mainh : w=88 : h=14
gadtags(0)\ti_Tag = #GA_Left, x
gadtags(1)\ti_Tag = #GA_Top, y
gadtags(2)\ti_Tag = #GA_Width, w
gadtags(3)\ti_Tag = #GA_Height, h
gadtags(4)\ti_Tag = #GA_Text, &txt18$
gadtags(5)\ti_Tag = #GA_Previous, &*quot\NextGadget
gadtags(6)\ti_Tag = #GAD_CallBack, ?saveprefs
gadtags(7)\ti_Tag = #GA_UserData, &prefresult
gadtags(8)\ti_Tag = #GAD_ShortCut, #RETURN
gadtags(9)\ti_Tag = #TAG_END

!gadget_gadAllocGadgetA {*save, #GAD_BUTTON_KIND, &gadtags(0)\ti_Tag}

txt19$="_Use" : x=x+(mainw-mainx-w)/3
gadtags(0)\ti_Tag = #GA_Left, x
gadtags(1)\ti_Tag = #GA_Top, y
gadtags(2)\ti_Tag = #GA_Width, w
gadtags(3)\ti_Tag = #GA_Height, h
gadtags(4)\ti_Tag = #GA_Text, &txt19$
gadtags(5)\ti_Tag = #GA_Previous, &*save\NextGadget
gadtags(6)\ti_Tag = #GAD_CallBack, ?useprefs
gadtags(7)\ti_Tag = #GA_UserData, &prefresult
gadtags(8)\ti_Tag = #TAG_END

!gadget_gadAllocGadgetA {*uuse, #GAD_BUTTON_KIND, &gadtags(0)\ti_Tag}

txt20$="_Load" : x=x+(mainw-mainx-w)/3
gadtags(0)\ti_Tag = #GA_Left, x
gadtags(1)\ti_Tag = #GA_Top, y
gadtags(2)\ti_Tag = #GA_Width, w
gadtags(3)\ti_Tag = #GA_Height, h
gadtags(4)\ti_Tag = #GA_Text, &txt20$
gadtags(5)\ti_Tag = #GA_Previous, &*uuse\NextGadget
gadtags(6)\ti_Tag = #GAD_CallBack, ?loadprefs
gadtags(7)\ti_Tag = #GA_UserData, &prefresult
gadtags(8)\ti_Tag = #TAG_END

!gadget_gadAllocGadgetA {*load, #GAD_BUTTON_KIND, &gadtags(0)\ti_Tag}

txt21$="Cancel" : x=x+(mainw-mainx-w)/3
gadtags(0)\ti_Tag = #GA_Left, x
gadtags(1)\ti_Tag = #GA_Top, y
gadtags(2)\ti_Tag = #GA_Width, w
gadtags(3)\ti_Tag = #GA_Height, h
gadtags(4)\ti_Tag = #GA_Text, &txt21$
gadtags(5)\ti_Tag = #GA_Previous, &*load\NextGadget
gadtags(6)\ti_Tag = #GAD_CallBack, ?cancelprefs
gadtags(7)\ti_Tag = #GA_UserData, &prefresult
gadtags(8)\ti_Tag = #GAD_ShortCut, #ESC
gadtags(9)\ti_Tag = #TAG_END

!gadget_gadAllocGadgetA {*cancel, #GAD_BUTTON_KIND, &gadtags(0)\ti_Tag}


If *main=0 OR *eedit=0 OR *gen=0 OR *task=0 OR *imp=0 Then Goto exit
If *ins1=0 OR *ins2=0 OR *key=0 OR *aut=0 OR *back=0 Then Goto exit
If *imme=0 OR *def=0 OR *alt=0 OR *pri=0 OR *ddata=0 Then Goto exit
If *fiel=0 OR *memo=0 OR *quot=0 OR *save=0 OR *uuse=0 Then Goto exit
If *load=0 OR *cancel=0 Then Goto exit


MOVE.l a5,globalbase

*win=OpenWindow_(&NewWin) : If *win=0 Then Goto exit

class.l=0 : l$=Chr$(10) : ex.b=0 : *gad.Gadget=0 : *w.Window=0


Macro gadget_gadSetSelectedFlag
   gadtags(0)\ti_Tag = #GA_Selected, `4
   gadtags(1)\ti_Tag = #TAG_END
   !gadget_gadSetGadgetAttrsA{ret.l, `1, `2, `3, &gadtags(0)\ti_Tag}
End Macro


Macro gadget_gadSetDisabledFlag
   gadtags(0)\ti_Tag = #GA_Disabled, `4
   gadtags(1)\ti_Tag = #TAG_END
   !gadget_gadSetGadgetAttrsA{ret.l, `1, `2, `3, &gadtags(0)\ti_Tag}
End Macro


!gadget_gadSetSelectedFlag{*ins1, *win, 0, Insert1}
!gadget_gadSetSelectedFlag{*ins2, *win, 0, Insert2}
!gadget_gadSetSelectedFlag{*key, *win, 0, Keypad}
!gadget_gadSetSelectedFlag{*aut, *win, 0, AutoIndent}
!gadget_gadSetSelectedFlag{*back, *win, 0, Backup}
!gadget_gadSetSelectedFlag{*imme, *win, 0, ImmediateScroll}
; NormalPri=0  ; <- for testing
If NormalPri
   GetReg a0,*def : GetReg a1,*win : JSR deftaskpri  ; <- Gosub
Else
   GetReg a0,*alt : GetReg a1,*win : JSR alttaskpri
EndIf
gadtags(0)\ti_Tag = #GADSTR_LongVal, Pri_Alternative
gadtags(1)\ti_Tag = #TAG_END
!gadget_gadSetGadgetAttrsA{ret.l, *pri, *win, 0, &gadtags(0)\ti_Tag}
gadtags(0)\ti_Tag = #GADSTR_LongVal, datahyphen
gadtags(1)\ti_Tag = #TAG_END
!gadget_gadSetGadgetAttrsA{ret.l, *ddata, *win, 0, &gadtags(0)\ti_Tag}
gadtags(0)\ti_Tag = #GADSTR_LongVal, varhyphen
gadtags(1)\ti_Tag = #TAG_END
!gadget_gadSetGadgetAttrsA{ret.l, *fiel, *win, 0, &gadtags(0)\ti_Tag}
gadtags(0)\ti_Tag = #GADSTR_LongVal, memohyphen
gadtags(1)\ti_Tag = #TAG_END
!gadget_gadSetGadgetAttrsA{ret.l, *memo, *win, 0, &gadtags(0)\ti_Tag}
!gadget_gadSetSelectedFlag{*quot, *win, 0, quotes}

Repeat
   *imsg.IntuiMessage=GetMsg_(*win\UserPort)
   If *imsg
      class=*imsg\Class
      !gadget_gadFilterMessage {bla.l, *imsg, 0}
      If bla Then NPrint *gad,l$
      ReplyMsg_ *imsg
   Else
      Wait_(1 LSL *win\UserPort\mp_SigBit)
   EndIf
Until class = #IDCMP_CLOSEWINDOW OR ex=1

If *gad\UserData = #PREF_SAVE OR *gad\UserData = #PREF_USE

   If *ins1\Flags=134 Then Insert1=1 Else Insert1=0
   If *ins2\Flags=134 Then Insert2=1 Else Insert2=0
   If *key\Flags=134 Then Keypad=1 Else Keypad=0
   If *aut\Flags=134 Then AutoIndent=1 Else AutoIndent=0
   If *back\Flags=134 Then Backup=1 Else Backup=0
   If *imme\Flags=134 Then ImmediateScroll=1 Else ImmediateScroll=0
   If *def\Flags=134 Then NormalPri=1 Else NormalPri=0
   If *quot\Flags=134 Then quotes=1 Else quotes=0

   !gadget_gadGetGadgetAttr{bla.l, #GADSTR_LongVal, *pri, &storage}
   Pri_Alternative = storage
   !gadget_gadGetGadgetAttr{bla.l, #GADSTR_LongVal, *ddata, &storage}
   datahyphen = storage
   !gadget_gadGetGadgetAttr{bla.l, #GADSTR_LongVal, *fiel, &storage}
   varhyphen = storage
   !gadget_gadGetGadgetAttr{bla.l, #GADSTR_LongVal, *memo, &storage}
   memohyphen = storage

   NPrint "Insert1:     ", Insert1
   NPrint "Insert2:     ", Insert2
   NPrint "Keypad:      ", Keypad
   NPrint "AutoIndent:  ", AutoIndent
   NPrint "Backup:      ", Backup
   NPrint "ImmediateScroll: ", ImmediateScroll
   NPrint "NormalPri:   ", NormalPri
   NPrint "Pri_Alternative: ", Pri_Alternative
   NPrint "datahyphen   ", datahyphen
   NPrint "varhyphen:   ", varhyphen
   NPrint "memohyphen:  ", memohyphen
   NPrint "quotes:      ", quotes, l$

EndIf


exit:
If *win Then CloseWindow_(*win)
If NewWin\FirstGadget Then !gadget_gadFreeGadgetList {bla.l, NewWin\FirstGadget}
gadget_FreeLib{}
End

deftaskpri:
MOVEM.l   d1-d7/a0-a6,-(a7)
MOVE.l    globalbase,a5
PutReg a0,*gad : PutReg a1,*w
!gadget_gadSetSelectedFlag{*gadptr(0), *w, 0, 1}
!gadget_gadSetSelectedFlag{*gadptr(1), *w, 0, 0}
!gadget_gadSetDisabledFlag{*gadptr(2), *w, 0, 1}
NPrint " deftaskpri"
MOVEM.l   (a7)+,d1-d7/a0-a6
RTS

alttaskpri:
MOVEM.l   d1-d7/a0-a6,-(a7)
MOVE.l    globalbase,a5
PutReg a0,*gad : PutReg a1,*w
!gadget_gadSetSelectedFlag{*gadptr(0), *w, 0, 0}
!gadget_gadSetSelectedFlag{*gadptr(1), *w, 0, 1}
!gadget_gadSetDisabledFlag{*gadptr(2), *w, 0, 0}
NPrint " alttaskpri"
MOVEM.l   (a7)+,d1-d7/a0-a6
RTS

saveprefs:
MOVEM.l   d1-d7/a0-a6,-(a7)
MOVE.l    globalbase,a5
PutReg a0,*gad : PutReg a1,*w
NPrint " saveprefs" : ex=1 : *gad\UserData = #PREF_SAVE
MOVEM.l   (a7)+,d1-d7/a0-a6
RTS

useprefs:
MOVEM.l   d1-d7/a0-a6,-(a7)
MOVE.l    globalbase,a5
PutReg a0,*gad : PutReg a1,*w
NPrint " useprefs" : ex=1 : *gad\UserData = #PREF_USE
MOVEM.l   (a7)+,d1-d7/a0-a6
RTS

loadprefs:
MOVEM.l   d1-d7/a0-a6,-(a7)
MOVE.l    globalbase,a5
PutReg a0,*gad : PutReg a1,*w
NPrint " loadprefs" : *gad\UserData = #PREF_LOAD
MOVEM.l   (a7)+,d1-d7/a0-a6
RTS

cancelprefs:
MOVEM.l   d1-d7/a0-a6,-(a7)
MOVE.l    globalbase,a5
PutReg a0,*gad : PutReg a1,*w
NPrint " cancelprefs" : ex=1 : *gad\UserData = #PREF_CANCEL
MOVEM.l   (a7)+,d1-d7/a0-a6
RTS

globalbase: Dc.l 0

