
; "gad_example2.ab2" a C to Blitz adaptation by Lorence Lombardo.
; Based on Steffen Gutmann's "example2.c".


INCLUDE "gadget.include.bb2"

If gadget_InitLib{38}=0 Then NPrint " Could not initialize v38 gadget lib...!!!" : End

DEFTYPE .NewWindow NewWin : tit$="gadget.library example 2"

NewWin\LeftEdge    = 10
NewWin\TopEdge     = 10
NewWin\Width       = 280
NewWin\Height      = 120
NewWin\DetailPen   = #AUTOFRONTPEN
NewWin\BlockPen    = #AUTOBACKPEN
NewWin\IDCMPFlags  = #GAD_IDCMPFlags | #IDCMP_CLOSEWINDOW
NewWin\Flags       = #WFLG_CLOSEGADGET | #WFLG_DRAGBAR | #WFLG_DEPTHGADGET | #WFLG_ACTIVATE
NewWin\FirstGadget = 0
NewWin\CheckMark   = 0
NewWin\Title       = &tit$
NewWin\Screen      = 0
NewWin\BitMap      = 0
NewWin\MinWidth    = 100
NewWin\MinHeight   = 50
NewWin\MaxWidth    = -1
NewWin\MaxHeight   = -1
NewWin\Type        = #WBENCHSCREEN

Dim gadtags1.TagItem(5), gadtags2.TagItem(8), gadtags3.TagItem(7)
Dim gadtags4.TagItem(6), gadtags5.TagItem(5)

*win.Window=0 : *gad1.Gadget=0 : *gad2.Gadget=0 : *gad3.Gadget=0
*gad4.Gadget=0 : *gad5.Gadget=0

txt$="Test"
gadtags1(0)\ti_Tag = #GA_Left, 20
gadtags1(1)\ti_Tag = #GA_Top, 15
gadtags1(2)\ti_Tag = #GA_Previous, &NewWin\FirstGadget
gadtags1(3)\ti_Tag = #GA_Text, &txt$
gadtags1(4)\ti_Tag = #TAG_END

!gadget_gadAllocGadgetA {*gad1, #GAD_TEXT_KIND, &gadtags1(0)\ti_Tag}

txt$="Raw Bool Gadget"
gadtags2(0)\ti_Tag = #GA_Left, 100
gadtags2(1)\ti_Tag = #GA_Top, 14
gadtags2(2)\ti_Tag = #GA_Width, 160
gadtags2(3)\ti_Tag = #GA_Height, 14
gadtags2(4)\ti_Tag = #GA_Previous, &*gad1\NextGadget
gadtags2(5)\ti_Tag = #GA_Text, &txt$
gadtags2(6)\ti_Tag = #GAD_CallBack, ?boolcallback
gadtags2(7)\ti_Tag = #TAG_END

!gadget_gadAllocGadgetA {*gad2, #GAD_BOOL_KIND, &gadtags2(0)\ti_Tag}

gadtags3(0)\ti_Tag = #GA_Left, 20
gadtags3(1)\ti_Tag = #GA_Top, 40
gadtags3(2)\ti_Tag = #GA_Previous, &*gad2\NextGadget
gadtags3(3)\ti_Tag = #GAD_ShortCut, 120 ; <- "x"
gadtags3(4)\ti_Tag = #GAD_CallBack, ?arrowcallback
gadtags3(5)\ti_Tag = #GADAR_Which,  #UPIMAGE
gadtags3(6)\ti_Tag = #TAG_END

!gadget_gadAllocGadgetA {*gad3, #GAD_ARROW_KIND, &gadtags3(0)\ti_Tag}

txt$="_Solo"
gadtags4(0)\ti_Tag = #GA_Left, 100
gadtags4(1)\ti_Tag = #GA_Top, 40
gadtags4(2)\ti_Tag = #GA_Previous, &*gad3\NextGadget
gadtags4(3)\ti_Tag = #GA_Text, &txt$
gadtags4(4)\ti_Tag = #GAD_CallBack, ?buttoncallback
gadtags4(5)\ti_Tag = #TAG_END

!gadget_gadAllocGadgetA {*gad4, #GAD_BUTTON_KIND, &gadtags4(0)\ti_Tag}

gadtags5(0)\ti_Tag = #GA_Left, 20
gadtags5(1)\ti_Tag = #GA_Top, 75
gadtags5(2)\ti_Tag = #GA_Previous, &*gad4\NextGadget
gadtags5(3)\ti_Tag = #GAD_CallBack, ?getfilecallback
gadtags5(4)\ti_Tag = #TAG_END

!gadget_gadAllocGadgetA {*gad5, #GAD_GETFILE_KIND, &gadtags5(0)\ti_Tag}

If *gad1=0 OR *gad2=0 OR *gad3=0 OR *gad4=0 OR *gad5=0 Then Goto exit
*gad.Gadget=0 : *w.Window=0 : *req.Requester=0 : *pgi.gadScrollerInfo=0

MOVE.l a5,globalbase

*win=OpenWindow_(&NewWin) : class.l=0 : l$=Chr$(10)

If *win
   Repeat
      *imsg.IntuiMessage=GetMsg_(*win\UserPort)
      If *imsg
         class=*imsg\Class
         !gadget_gadFilterMessage {bla.l, *imsg, 0}
         If bla
            If *gad=*gad2 Then NPrint "gad2"+l$
            If *gad=*gad3 Then NPrint "gad3"+l$
            If *gad=*gad4 Then NPrint "gad4"+l$
            If *gad=*gad5 Then NPrint "gad5"+l$
         EndIf
         ReplyMsg_ *imsg
      Else
         Wait_(1 LSL *win\UserPort\mp_SigBit)
      EndIf
   Until class = #IDCMP_CLOSEWINDOW
EndIf

exit:
If *win Then CloseWindow_(*win)
If NewWin\FirstGadget Then !gadget_gadFreeGadgetList {bla.l, NewWin\FirstGadget}
gadget_FreeLib{}
End

boolcallback:
MOVEM.l   d1-d7/a0-a6,-(a7)
MOVE.l    globalbase,a5
PutReg a0,*gad : NPrint " bool"
MOVEM.l   (a7)+,d1-d7/a0-a6
RTS

arrowcallback:
MOVEM.l   d1-d7/a0-a6,-(a7)
MOVE.l    globalbase,a5
PutReg a0,*gad : NPrint " arrow"
MOVEM.l   (a7)+,d1-d7/a0-a6
RTS

buttoncallback:
MOVEM.l   d1-d7/a0-a6,-(a7)
MOVE.l    globalbase,a5
PutReg a0,*gad : NPrint " button"
MOVEM.l   (a7)+,d1-d7/a0-a6
RTS

getfilecallback:
MOVEM.l   d1-d7/a0-a6,-(a7)
MOVE.l    globalbase,a5
PutReg a0,*gad : NPrint " file"
MOVEM.l   (a7)+,d1-d7/a0-a6
RTS

globalbase: Dc.l 0

