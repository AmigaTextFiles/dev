
; "gad_example4.ab2" a C to Blitz adaptation by Lorence Lombardo.
; Based on Steffen Gutmann's "example4.c".


INCLUDE "gadget.include.bb2"

If gadget_InitLib{38}=0 Then NPrint " Could not initialize v38 gadget lib...!!!" : End

DEFTYPE .NewWindow NewWin : tit$="gadget.library example 4"

NewWin\LeftEdge    = 10
NewWin\TopEdge     = 10
NewWin\Width       = 300
NewWin\Height      = 80
NewWin\DetailPen   = #AUTOFRONTPEN
NewWin\BlockPen    = #AUTOBACKPEN
NewWin\IDCMPFlags  = #GAD_IDCMPFlags | #IDCMP_CLOSEWINDOW
NewWin\Flags       = #WFLG_CLOSEGADGET | #WFLG_DRAGBAR | #WFLG_DEPTHGADGET | #WFLG_ACTIVATE
NewWin\FirstGadget = 0
NewWin\CheckMark   = 0
NewWin\Title       = &tit$
NewWin\Screen      = 0
NewWin\BitMap      = 0
NewWin\MinWidth    = 100
NewWin\MinHeight   = 50
NewWin\MaxWidth    = -1
NewWin\MaxHeight   = -1
NewWin\Type        = #WBENCHSCREEN

Dim gadtags1.TagItem(7), gadtags2.TagItem(8)
*win.Window=0 : *gad1.Gadget=0 : *gad2.Gadget=0

txt$="String" : txt2$="TextVal"
gadtags1(0)\ti_Tag = #GA_Left, 100
gadtags1(1)\ti_Tag = #GA_Top, 20
gadtags1(2)\ti_Tag = #GA_Width, 100
gadtags1(3)\ti_Tag = #GA_Previous, &NewWin\FirstGadget
gadtags1(4)\ti_Tag = #GA_Text, &txt$
gadtags1(5)\ti_Tag = #GADSTR_TextVal, &txt2$
gadtags1(6)\ti_Tag = #TAG_END

!gadget_gadAllocGadgetA {*gad1, #GAD_STRING_KIND, &gadtags1(0)\ti_Tag}

txt$="Integer"
gadtags2(0)\ti_Tag = #GA_Left, 100
gadtags2(1)\ti_Tag = #GA_Top, 40
gadtags2(2)\ti_Tag = #GA_Previous, &*gad1\NextGadget
gadtags2(3)\ti_Tag = #GA_Text, &txt$
gadtags2(4)\ti_Tag = #GADSTR_Min, 0
gadtags2(5)\ti_Tag = #GADSTR_Max, 999
gadtags2(6)\ti_Tag = #GADSTR_LongVal, 10
gadtags2(7)\ti_Tag = #TAG_END

!gadget_gadAllocGadgetA {*gad2, #GAD_INTEGER_KIND, &gadtags2(0)\ti_Tag}

If *gad1=0 OR *gad2=0 Then Goto exit

*win=OpenWindow_(&NewWin) : class.l=0

If *win
   Repeat
      *imsg.IntuiMessage=GetMsg_(*win\UserPort)
      If *imsg
         class=*imsg\Class
         !gadget_gadFilterMessage {bla.l, *imsg, 0}
         ReplyMsg_ *imsg
      Else
         Wait_(1 LSL *win\UserPort\mp_SigBit)
      EndIf
   Until class = #IDCMP_CLOSEWINDOW
EndIf

exit:
If *win Then CloseWindow_(*win)
If NewWin\FirstGadget Then !gadget_gadFreeGadgetList {bla.l, NewWin\FirstGadget}
gadget_FreeLib{}
End


