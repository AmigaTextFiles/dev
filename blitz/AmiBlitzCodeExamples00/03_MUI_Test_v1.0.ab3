; XTRA
; Embedded .xtra Header
; 
; General Info
; -------------------------------------------------------
; ExePath         = "ram:"
; ExeFile         = "Empty"
; CreateIcon      = 0
; Residents       = "all.res"
; 
; Compiler
; -------------------------------------------------------
; StringBuffer    = 100000
; MakeSmallest    = 1
; FuncOptimize    = 1
; Version         = 0.0.0
; NumberOfBuilds  = 113
; 
; Debugger
; -------------------------------------------------------
; CliArgs         = ""
; StackSize       = 8192
; RuntimeDebug    = 0
; DebugInfo       = 0
; CreateDbgFile   = 0
; OverflowCheck   = 0
; AssemblerCheck  = 0
; InterruptCheck  = 1
; AutoRun         = 1
; 
; Editor
; -------------------------------------------------------
; CursorLine      = 1
; CursorColumn    = 1
; LabelSearch     = ""
; LabelRemark     = 0
; LabelAll        = 0
; LabelCase       = 0
; LabelPosition   = 0
; 
; Blitz Objects
; -------------------------------------------------------
; Max File        = 100
; Max GadgetList  = 100
; Max Shape       = 200
; Max Bank        = 1
; Max MenuList    = 100
; Max GTList      = 100
; Max Palette     = 4
; Max BitMap      = 10
; Max Screen      = 5
; Max IntuiFont   = 10
; Max BlitzFont   = 1
; Max Window      = 100
; Max IconInfo    = 1
; Max MUIObject   = 50
; Max AsyncReq    = 4
; Max Req-Lib     = 5
; Max GTMenuList  = 100
; Max Console     = 5
; Max TCPSock     = 5
; Max Tape        = 5
; Max TagList     = 5
; Max Database    = 16
; Max Sound       = 10
; Max MedModule   = 8
; Max Buffer      = 10
; Max Queue       = 10
; Max Sprite      = 20
; Max Module      = 5
; Max Slice       = 10
; Max Page        = 4
; Max CopList     = 10
; Max PTModule    = 5
; Max Anim        = 10
; Max NChunky     = 50
; Max Chunky      = 20
; Max Stencil     = 5
; Max XBSound     = 10
; /XTRA
; #######################################
; ### MUI Interface                   ###
; ### Version 0.001                   ###
; #######################################


; #######################################
; ### Global Stuff                    ###
; #######################################

; ### Sintax - All variables need to be declared (or var.type) ###
SYNTAX 2

; ### So it can be started from Workbench ###
WBStartup

; ### Workbench to AmiBlitz screen ###
WbToScreen 0

; ### Bring Workbench to front ###;
ShowScreen 0

; #######################################
; ###### Includes                  ######
; #######################################


; #######################################
; ###### MUI Init Stuff            ######
; #######################################

MUIApplicationTitle       "MUI_Tests"
MUIApplicationVersion     "$VER:MUI_Tests 0.000 (2025.01.17)"
MUIApplicationBase        "MUI_TESTS"
MUIApplicationAuthor      "Paulo Reis"
MUIApplicationDescription "Testing MUI in Blitz..."
MUIApplicationCopyright   "© 2025 Paulo Reis"

; #######################################
; ###### MUI Variables             ######
; #######################################

; ### MUI Object Constants ###

#MAINWIN          = 0
#MAINAPP          = 1

#GROUPMAIN        = 10
#GROUPFILE        = 11
#GROUPPREFS       = 12
#GROUPRENDER      = 13
#GROUPRENDERX     = 14
#GROUPRENDERY     = 15
#GROUPRENDERZ     = 16
#GROUPAMS         = 17
#GROUPAMSA        = 18

#GFBUTPATH        = 20
#GFTXTPATH        = 21
#GFBUTSAVE        = 22

#GPLABDIM         = 30
#GPCYCDIM         = 31
#GPLABPAL         = 32
#GPCYCPAL         = 33

#GRLABSLIX        = 40
#GRSLIDERX        = 41
#GRLABSLIY        = 42
#GRSLIDERY        = 43
#GRLABSLIZ        = 44
#GRSLIDERZ        = 45
#GRBUTRENDER      = 46

#GABUTABOUT       = 25
#GABUTABOUTMUI    = 26
#GABUTMUISET      = 27

; ### MUI Event Constantes ###

#EV_CLOSE         = -1
#EV_GABUTABOUT    = 1
#EV_GABUTABOUTMUI = 2
#EV_GABUTMUISET   = 3
#EV_GFBUTPATH     = 4
#EV_GFBUTSAVE     = 5
#EV_GRBUTRENDER   = 6

; ### MUI Interface Build ###

MUIKeyButton     #GABUTABOUT,    "About...", "a"
MUIKeyButton     #GABUTABOUTMUI, "About MUI...", "m"
MUIKeyButton     #GABUTMUISET,   "MUI Settings...", "s"

MUIAddObjsHGroup #GROUPAMSA, #GABUTABOUT, #GABUTABOUTMUI
MUICreateHGroup  #GROUPAMSA

MUIAddTags       #GROUPAMS, #MUIA_Frame, #MUIV_Frame_Group, #MUIA_FrameTitle, GetGlobalStrAddr("Project")
MUIAddObjsVGroup #GROUPAMS, #GROUPAMSA, #GABUTMUISET
MUICreateVGroup  #GROUPAMS

MUIKeyButton     #GFBUTPATH,     "Image Path:", "i"
MUIText          #GFTXTPATH,     "Ram:", True
MUIAddTags       #GFBUTSAVE, #MUIA_Disabled, True
MUIKeyButton     #GFBUTSAVE,     "Save Image", "s"

MUIAddTags       #GROUPFILE, #MUIA_Frame, #MUIV_Frame_Group, #MUIA_FrameTitle, GetGlobalStrAddr("Image Save Prefs")
MUIAddObjsVGroup #GROUPFILE, #GFBUTPATH, #GFTXTPATH, #GFBUTSAVE
MUICreateVGroup  #GROUPFILE

MUILabel         #GPLABDIM,      "Dimensions", #MUIO_Label_Centered
MUIKeyCycle      #GPCYCDIM,      "","Dim1","Dim2"
MUILabel         #GPLABPAL,      "Palette", #MUIO_Label_Centered
MUIKeyCycle      #GPCYCPAL,      "","Pal1","Pal2"

MUIAddTags       #GROUPPREFS, #MUIA_Frame, #MUIV_Frame_Group, #MUIA_FrameTitle, GetGlobalStrAddr("Window Settings")
MUIAddObjsVGroup #GROUPPREFS, #GPLABDIM, #GPCYCDIM, #GPLABPAL, #GPCYCPAL
MUICreateVGroup  #GROUPPREFS

MUILabel         #GRLABSLIX,     "Xpos:", #MUIO_Label_Centered
MUISlider        #GRSLIDERX,     1,1000,1
MUILabel         #GRLABSLIY,     "Ypos:", #MUIO_Label_Centered
MUISlider        #GRSLIDERY,     1,1000,1
MUILabel         #GRLABSLIZ,     "Zoom:", #MUIO_Label_Centered
MUISlider        #GRSLIDERZ,     1,32,1
MUIKeyButton     #GRBUTRENDER,   "Render", "r"

MUIAddObjsHGroup #GROUPRENDERX, #GRLABSLIX, #GRSLIDERX
MUICreateHGroup  #GROUPRENDERX

MUIAddObjsHGroup #GROUPRENDERY, #GRLABSLIY, #GRSLIDERY
MUICreateHGroup  #GROUPRENDERY

MUIAddObjsHGroup #GROUPRENDERZ, #GRLABSLIZ, #GRSLIDERZ
MUICreateHGroup  #GROUPRENDERZ

MUIAddTags       #GROUPRENDER, #MUIA_Frame, #MUIV_Frame_Group, #MUIA_FrameTitle, GetGlobalStrAddr("Render")
MUIAddObjsVGroup #GROUPRENDER, #GROUPRENDERX, #GROUPRENDERY, #GROUPRENDERZ, #GRBUTRENDER
MUICreateVGroup  #GROUPRENDER

MUIAddObjsVGroup #GROUPMAIN, #GROUPAMS, #GROUPFILE, #GROUPPREFS, #GROUPRENDER
MUICreateVGroup  #GROUPMAIN

MUICreateWindow  #MAINWIN, "MUI Tests", "MAIN", #GROUPMAIN
MUIAddSubWindow  #MAINWIN

If MUICreateApplication = False
  NPrint "Error creating application!"
  End
Else
  MUIApplicationObj #MAINAPP
End If

MUINotifyApp #MAINWIN, #MUIA_Window_CloseRequest, 1, #EV_CLOSE
MUINotifyApp #GABUTABOUT,  #MUIA_Pressed, 0, #EV_GABUTABOUT
MUINotifyApp #GABUTABOUTMUI,  #MUIA_Pressed, 0, #EV_GABUTABOUTMUI
MUINotifyApp #GABUTMUISET, #MUIA_Pressed, 0, #EV_GABUTMUISET
MUINotifyApp #GFBUTPATH, #MUIA_Pressed, 0, #EV_GFBUTPATH
MUINotifyApp #GFBUTSAVE, #MUIA_Pressed, 0, #EV_GFBUTSAVE
MUINotifyApp #GRBUTRENDER, #MUIA_Pressed, 0, #EV_GRBUTRENDER

; ### Open MUI Window ###
If MUIOpenWindow(#MAINWIN) = False
  NPrint "Error opening main window!"
  End
End If

quit.b = False
.mainloop
Repeat
  ev.l = MUIWaitEvent
  If ev
    ;Stop
    NPrint "Event: ", ev
    Select ev
      Case #EV_CLOSE
        NPrint "Quitting MUI_Tests application..."
        quit = True
      Case #EV_GABUTABOUT
        NPrint "About button pressed..."
        MUIRequest #MAINWIN,0,"About...","OK","MUI Tests v0.0 © 2025 Paulo Reis"
      Case #EV_GABUTABOUTMUI
        NPrint "About MUI button pressed..."
        error.w = MUIDoMethod(#MAINAPP, #MUIM_Application_AboutMUI, 0)
      Case #EV_GABUTMUISET
        NPrint "MUI Settings button pressed..."
        error.w = MUIDoMethod(#MAINAPP, #MUIM_Application_OpenConfigWindow, 0)
      Case #EV_GFBUTPATH
        NPrint "Path button pressed..."
      Case #EV_GFBUTSAVE
        NPrint "Save button pressed..."
      Case #EV_GRBUTRENDER
        NPrint "Render button pressed..."
        NPrint "Window:  ",MUIGetCycle (#GPCYCDIM)
        NPrint "Palette: ",MUIGetCycle (#GPCYCPAL)
        NPrint "x pos:   ",MUIGet (#GRSLIDERX,#MUIA_Slider_Level)
        NPrint "y pos:   ",MUIGet (#GRSLIDERY,#MUIA_Slider_Level)
        NPrint "zoom:    ",MUIGet (#GRSLIDERZ,#MUIA_Slider_Level)
        MUISet #GFBUTSAVE, #MUIA_Disabled, False
      Default
        NPrint "Unknown event"
    End Select
  End If
Until quit

MUICloseWindow #MAINWIN

End


