
; "joydemo.ab2" a C to Blitz adaptation by Lorence Lombardo.

; Based on Oliver Wagner's "joydemo.c".


INCLUDE "joystick.include.bb2"

If joystick_InitLib{0}=0 Then NPrint " Could not initialize joystick lib...!!!" : End

Statement printjoy {num.b, v.l, *rp.RastPort}
   If num=0 Then x.w=15
   If num=1 Then x.w=160
   l$="    " : If v AND #JOY_LEFT Then l$="LEFT"
   r$="    " : If v AND #JOY_RIGHT Then r$="RIGT"
   u$="    " : If v AND #JOY_UP Then u$=" UP "
   d$="    " : If v AND #JOY_DOWN Then d$="DOWN"
   f$="    " : If v AND #JOY_FIRE Then f$="FIRE"
   Move_ *rp,x,45 : Text_ *rp,&l$,4
   Move_ *rp,x+80,45 : Text_ *rp,&r$,4
   Move_ *rp,x+40,30 : Text_ *rp,&u$,4
   Move_ *rp,x+40,60 : Text_ *rp,&d$,4
   Move_ *rp,x+40,45 : Text_ *rp,&f$,4
End Statement

DEFTYPE .NewWindow nw : tit$="JoyLib DEMO"

nw\LeftEdge    = 20
nw\TopEdge     = 20
nw\Width       = 300
nw\Height      = 100
nw\DetailPen   = 3
nw\BlockPen    = 2
nw\IDCMPFlags  = #IDCMP_CLOSEWINDOW
nw\Flags       = #WFLG_CLOSEGADGET | #WFLG_DRAGBAR | #WFLG_DEPTHGADGET | #WFLG_RMBTRAP
nw\FirstGadget = 0
nw\CheckMark   = 0
nw\Title       = &tit$
nw\Screen      = 0
nw\BitMap      = 0
nw\MinWidth    = 0
nw\MinHeight   = 0
nw\MaxWidth    = 0
nw\MaxHeight   = 0
nw\Type        = #WBENCHSCREEN

*w.Window=OpenWindow_(&nw)
If *w=0 Then Goto exit

DEFTYPE.TextAttr font : fn$="topaz.font" : class.l=0

font\ta_Name  = &fn$
font\ta_YSize = 8
font\ta_Style = 0
font\ta_Flags = #FPF_DISKFONT
*font.TextFont = OpenDiskFont_ (font)
*rp.RastPort=*w\RPort

SetFont_ *rp, *font : SetAPen_ *rp,3 : SetBPen_ *rp,0 : SetDrMd_ *rp,#JAM2

Repeat
   *imsg.IntuiMessage = GetMsg_(*w\UserPort)
   If *imsg
      class=*imsg\Class : ReplyMsg_ *imsg
   Else
      !joystick_joy0{v.l} : printjoy{0, v, *rp}
      !joystick_joy1{v.l} : printjoy{1, v, *rp}
      WaitTOF_
   EndIf
Until class=#IDCMP_CLOSEWINDOW

CloseFont_ *font : CloseWindow_ *w

exit:
joystick_FreeLib {} : End


