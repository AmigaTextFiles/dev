
; Run-length encoding (RLE) functions for Blitz

; by Lorence Lombardo  28-Apr-2012

; Based on information obtained from here:-

; http://rosettacode.org/wiki/Run-length_encoding


Function.s RLE_enc {text$}
   slen.w=Len(text$) : n.w=1 : o$=""
   While n <= slen
      c$ = Mid$(text$, n, 1) : r.w=1 : n+1
      While c$ = Mid$(text$, n, 1) AND r < 127
         r+1 : n+1
      Wend
      If r<3 Then o$ = o$ + String$(c$,r) Else o$ = o$ + Chr$(128+r) + c$
   Wend
   Function Return o$
End Function


Function.s RLE_dec {text$}
   slen.w=Len(text$) : n.w=1 : o$=""
   While n <= slen
      c$ = Mid$(text$, n, 1) : n+1 : cn.w=Asc(c$)
      If cn > 128
         o$ = o$ + String$(Mid$(text$, n, 1) , cn-128) : n+1
      Else
         o$ = o$ + c$
      EndIf
   Wend
   Function Return o$
End Function


; End of functions

;-------------------------------------------------------------------------------------

; Functions demo begins here.


src$="This test string will not pack very well but the next test will."

dest$=RLE_enc {src$}
dtest$=RLE_dec {dest$}

NPrint ""

NPrint "input1: ", Len(src$), " "+src$
NPrint "Packed length:  ",Len(dest$)
NPrint "UnPack: ", Len(dtest$), " "+dtest$

src$="WWWWWWWWWWWWBWWWWWWWWWWWWBBBWWWWWWWWWWWWWWWWWWWWWWWWBWWWWWWWWWWWWWW"

dest$=RLE_enc {src$}
dtest$=RLE_dec {dest$}

NPrint ""

NPrint "input2: ", Len(src$), " "+src$
NPrint "Packed length:  ",Len(dest$)
NPrint "UnPack: ", Len(dtest$), " "+dtest$

NPrint "" : End


