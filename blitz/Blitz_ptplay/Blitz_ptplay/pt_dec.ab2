
; "pt_dec.ab2" a Blitz demonstration program by Lorence Lombardo
; for Ilkka Lehtoranta's "ptplay.library".

; "pt_dec.ab2" was based on a previous demo. ie. "hvl_dec.ab2"
; See "Blitz_HVL.lha" on the aminet for related interest.

; Also "ShellPlayer.c" provided some assistance which is avaialble
; from here:- http://aminet.net/package/mus/play/ShellPlayer


optimize 5

l$=Chr$(10) : np.b=NumPars

If np=0 Then NPrint l$+" Usage:- pt_dec <PT_file> [out_raw]"+l$ : End

f$=Par$(1) : #R=1005 : #W=1006 : buf.l=0

fre.l=44100 : PCM_BUFFER_SIZE.l=fre*4 : buf2.l=0 : modptr.l=0 : outfile.l=0

INCLUDE "ptplay_lib.include.bb2"

If ptplay_InitLib{0}=0 Then NPrint l$+" Could not initialize ptplay lib...!!!"+l$ : End

infile.l = Open_(&f$, #R)
If infile=0 Then NPrint l$+" File not found...!!!"+l$ : Goto exit

ExamineFH_ infile, &fib.FileInfoBlock

buf.l = AllocVec_ (fib\fib_Size, #MEMF_PUBLIC)
If buf=0 Then NPrint l$+" Could not allocate RAM...!!!"+l$ : Goto exit

Read_ infile, buf, fib\fib_Size : Close_ infile : infile=0

!ptplay_PtTest {typ.l, &f$, buf, 1200}

!ptplay_PtInit {modptr.l, buf, fib\fib_Size, fre, typ}
If modptr=0 Then NPrint l$+" Could not initialize mod...!!!"+l$ : Goto exit

buf2.l = AllocVec_ (PCM_BUFFER_SIZE, #MEMF_PUBLIC)
If buf2=0 Then NPrint l$+" Could not allocate RAM...!!!"+l$ : Goto exit

out$="AUDIO:B/16/F/44100/C/2/BUFFER/352800" : If np>1 Then out$=Par$(2)

!ptplay_PtGetAttr {bla.l, modptr, #PTPLAY_TotalTime, &songlen.l}

NPrint l$+" Length:- ~",songlen," secs  Type:- ",typ

!ptplay_PtGetAttr {bla.l, modptr, #PTPLAY_SongTitle, &addy.l}

NPrint l$+" Name:- "+Peek$(addy)

outfile.l = Open_(&out$, #W) : ex.b=0
If outfile=0 Then NPrint l$+" Unable to create output...!!!"+l$ : Goto exit

Dim mytags.TagItem(1)  ; <- need this for a clean ending, got it from Thilo. ;)

mytags(0)\ti_Tag = #PTPLAY_Flags, #MODF_DOSONGEND | #MODF_ALLOWFILTER | #MODF_ALLOWPANNING
mytags(1)\ti_Tag = #TAG_END

!ptplay_PtSetAttrs {modptr, &mytags(0)\ti_Tag} : ex.b=0

TimerReset : t1.l=Timer  ; <- comment this if you dont have it

Repeat
   !ptplay_PtGetAttr {bla.l, modptr, #PTPLAY_Flags, &flag.l}
   If flag AND #MODF_SONGEND Then ex=1

   If ex=0
      !ptplay_PtRender {modptr, buf2, buf2+2, 4, fre, 1, 16, 2}
      Write_ outfile, buf2, PCM_BUFFER_SIZE
   EndIf

Until ex=1 OR CtrlC<>0

tt.f=(Timer-t1)/50 : FloatMode -1 : NPrint l$+" ",tt,l$  ; <- comment this if you dont have it

exit:
If infile Then Close_ infile
If modptr Then !ptplay_PtCleanup{modptr}
If buf Then FreeVec_ buf
If buf2 Then FreeVec_ buf2
If outfile Then Close_ outfile
ptplay_FreeLib{} : End


