
; "hvl_dec.ab2" a Blitz demonstration program by Lorence Lombardo
; for Ilkka Lehtoranta's "hvlplay.library".

; INCLUDE version.

; NB: only the default file will be played for multi AHX's.


optimize 5

l$=Chr$(10) : np.b=NumPars

If np=0 Then NPrint l$+" Usage:- hvl_dec <AHX/HVL_file> [out_raw]"+l$ : End

f$=Par$(1) : buf$="123" : #R=1005 : #W=1006

infile.l = Open_(&f$, #R)
If infile=0 Then NPrint l$+" File not found...!!!"+l$ : End

fre.l=44100 : PCM_BUFFER_SIZE.l=fre*4

Read_ infile, &buf$, 3 : Close_ infile : infile=0

INCLUDE "hvlplay.include.bb2"

If hvlplay_InitLib{0}=0 Then NPrint " Could not initialize hvl lib...!!!" : End

If buf$="THX" OR buf$="HVL" Then !hvlplay_HVL_LoadTune {infile, &f$, fre, 4}

If infile=0 Then NPrint l$+" File is not recognised...!!!"+l$ : hvlplay_FreeLib{} : End

out$="AUDIO:B/16/F/44100/C/2/BUFFER/352800" : If np>1 Then out$=Par$(2)


!hvlplay_HVL_GetAttr {bla.l, #HVLPLAY_SongLength, infile, &songlen.l}

NPrint l$+" Length:- ",songlen," secs"

!hvlplay_HVL_GetAttr {bla.l, #HVLPLAY_SongName, infile, &addy.l}

NPrint " Name:- "+Peek$(addy)+l$ : buf.l=0 : u$=Chr$(11)+" "

outfile.l = Open_(&out$, #W)
If outfile=0 Then NPrint l$+" Unable to create output...!!!"+l$ : Goto exit

buf.l = AllocVec_ (PCM_BUFFER_SIZE, #MEMF_PUBLIC)
If buf=0 Then NPrint l$+" Could not allocate RAM...!!!"+l$ : Goto exit


For x.l=1 To songlen

   !hvlplay_HVL_Decode {infile, buf, buf+2, fre, 4}

   Write_ outfile, buf, PCM_BUFFER_SIZE

   perc.l=x*100.0/songlen : NPrint u$,perc," % "

   If CtrlC<>0 Then x=songlen

Next x

NPrint ""

exit:
If infile Then !hvlplay_HVL_FreeTune {infile}
If outfile Then Close_ outfile
If buf Then FreeVec_ buf
hvlplay_FreeLib{} : End


