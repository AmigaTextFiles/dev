
; "hvl_dec.ab2" a Blitz demonstration program by Lorence Lombardo
; for Ilkka Lehtoranta's "hvlplay.library".

; no INCLUDE version.

; NB: only the default file will be played for multi AHX's.


optimize 5

#HVLPLAY_SongName = #TAG_USER
#HVLPLAY_SongLength = #TAG_USER + 1
#HVLPLAY_ForceMono = #TAG_USER + 2

l$=Chr$(10) : np.b=NumPars

If np=0 Then NPrint l$+" Usage:- hvl_dec <AHX/HVL_file> [out_raw]"+l$ : End

f$=Par$(1) : buf$="123" : #R=1005 : #W=1006

infile.l = Open_(&f$, #R)
If infile=0 Then NPrint l$+" File not found...!!!"+l$ : End

fre.l=44100 : PCM_BUFFER_SIZE.l=fre*4

Read_ infile, &buf$, 3 : Close_ infile : infile=0

If buf$="THX" OR buf$="HVL" Then infile = HVL_LoadTune_ (&f$, fre, 4)

If infile=0 Then NPrint l$+" File is not recognised...!!!"+l$ : End

out$="AUDIO:B/16/F/44100/C/2/BUFFER/352800" : If np>1 Then out$=Par$(2)


bla.l = HVL_GetAttr_ (#HVLPLAY_SongLength, infile, &songlen.l)

NPrint l$+" Length:- ",songlen," secs"

bla.l = HVL_GetAttr_ (#HVLPLAY_SongName, infile, &addy.l)

NPrint " Name:- "+Peek$(addy)+l$ : buf.l=0 : u$=Chr$(11)+" "

outfile.l = Open_(&out$, #W)
If outfile=0 Then NPrint l$+" Unable to create output...!!!"+l$ : Goto exit

buf.l = AllocVec_ (PCM_BUFFER_SIZE, #MEMF_PUBLIC)
If buf=0 Then NPrint l$+" Could not allocate RAM...!!!"+l$ : Goto exit


For x.l=1 To songlen

   HVL_DecodeFrame_ infile, buf, buf+2, fre, 4

   Write_ outfile, buf, PCM_BUFFER_SIZE

   perc.l=x*100.0/songlen : NPrint u$,perc," % "

   If CtrlC<>0 Then x=songlen

Next x

NPrint ""

exit:
If infile Then HVL_FreeTune_ infile
If outfile Then Close_ outfile
If buf Then FreeVec_ buf
End


