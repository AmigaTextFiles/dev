
; "Image_Demo.ab2" a C to Blitz adaptation by Lorence Lombardo.

; Based on Pawen Marciniak's "Image_Demo.c" example.


WBStartup

INCLUDE "image_lib.include.bb2"

If image_InitLib{36}=0 Then NPrint " Could not initialize image lib...!!!" : End

PubScreenName$="Workbench"
APP_TitleWindow$="Image.library Demo Window"
APP_TitleScreenWindow$="Image.library Demo Screen"

*VisualInfo.l = 0 : *scr.Screen = 0 : *APP_Window.Window=0

Width.w=320
Height.w=150
MinWidth.w=50
MinHeight.w=30
MaxWidth.w=1600
MaxHeight.w=1200

DEFTYPE .TextAttr *Font, ScreenFont
DEFTYPE .TextFont *APP_Font

FontX.w = 0 ; WORD
FontY.w = 0 ; WORD
DEFTYPE .w OffX, OffY ; UWORD


;This is how "pal_dat" & "pic_dat" were generated:-

;Dim pal.b(384), dat.b(327680)

;For x.l=0 To 383 : Read pal(x) : Next x
;For x.l=0 To 327679 : Read dat(x) : Next x

;If WriteFile(0,"ram:pal_dat") Then WriteMem 0,&pal(0),384 : CloseFile 0
;If WriteFile(0,"ram:pic_dat") Then WriteMem 0,&dat(0),327680 : CloseFile 0


DEFTYPE .ChunkyImg homeimg

*scr = LockPubScreen_( &PubScreenName$ )
If *scr=0 Then Goto exit

*Font = &ScreenFont
*Font\ta_Name = *scr\RastPort\Font\tf_Message\mn_Node\ln_Name
*Font\ta_YSize = *scr\RastPort\Font\tf_YSize
FontY = *Font\ta_YSize
FontX = *scr\RastPort\Font\tf_XSize

OffX = *scr\WBorLeft
OffY = *scr\RastPort\TxHeight + *scr\WBorTop + 1

*APP_Font=OpenDiskFont_( &ScreenFont )
If *APP_Font=0 Then Goto exit

ComputeX.w = ( FontX * Width + 4 ) / 8
ComputeY.w = ( FontY * Height + 4 ) / 8

*VisualInfo.l = GetVisualInfoA_ ( *scr,0 )
If *VisualInfo=0 Then Goto exit

Dim mytags.TagItem(16)

mytags(0)\ti_Tag  = #WA_Left,        20
mytags(1)\ti_Tag  = #WA_Top,         20
mytags(2)\ti_Tag  = #WA_Width,       ComputeX + OffX + *scr\WBorRight
mytags(3)\ti_Tag  = #WA_Height,      ComputeY + OffY + *scr\WBorBottom
ComputeX.w = ( FontX * MinWidth + 4 ) / 8
mytags(4)\ti_Tag  = #WA_MinWidth,    ComputeX + OffX + *scr\WBorRight
ComputeX.w = ( FontX * MinHeight + 4 ) / 8
mytags(5)\ti_Tag  = #WA_MinHeight,   ComputeX + OffX + *scr\WBorRight
ComputeX.w = ( FontX * MaxWidth + 4 ) / 8
mytags(6)\ti_Tag  = #WA_MaxWidth,    ComputeX + OffX + *scr\WBorRight
ComputeY.w = ( FontY * MaxHeight + 4 ) / 8
mytags(7)\ti_Tag  = #WA_MaxHeight,   ComputeY + OffY + *scr\WBorBottom
mytags(8)\ti_Tag  = #WA_Title,       &APP_TitleWindow$
mytags(9)\ti_Tag  = #WA_ScreenTitle, &APP_TitleScreenWindow$
mytags(10)\ti_Tag = #WA_Flags,       #WFLG_DRAGBAR | #WFLG_CLOSEGADGET | #WFLG_SIZEGADGET | #WFLG_SMART_REFRESH
mytags(11)\ti_Tag = #WA_IDCMP,       #IDCMP_CLOSEWINDOW | #IDCMP_REFRESHWINDOW
mytags(12)\ti_Tag = #WA_Gadgets,     False
mytags(13)\ti_Tag = #WA_AutoAdjust,  False
mytags(14)\ti_Tag = #WA_PubScreen,   *scr
mytags(15)\ti_Tag = #TAG_DONE

*APP_Window=OpenWindowTagList_(0, &mytags(0)\ti_Tag)
If *APP_Window=0 Then Goto exit

GT_RefreshWindow_ *APP_Window, 0

SetWindowTitles_ *APP_Window, "Converce c2p wait...", True

homeimg\ci_Width=640
homeimg\ci_Height=512
homeimg\ci_NumColors=128
homeimg\ci_Palette=?paldat
homeimg\ci_ChunkyData=?picdat

mytags(0)\ti_Tag = #CTBM_Precision, #PRECISION_EXACT
mytags(1)\ti_Tag = #TAG_DONE

!image_ChunkyToBitMapA {*HomeBM.BitMap, *scr, &homeimg, &mytags(0)\ti_Tag}
If *HomeBM=0 Then Goto exit

Gosub ScaleBM

Repeat
  WaitPort_ ( *APP_Window\UserPort )
  *imsg.IntuiMessage = GT_GetIMsg_( *APP_Window\UserPort )

  If *imsg
     class = *imsg\Class : GT_ReplyIMsg_( *imsg )

     If class = #IDCMP_REFRESHWINDOW
        GT_BeginRefresh_( *APP_Window )
        GT_EndRefresh_ *APP_Window, True
        Gosub ScaleBM
     EndIf
  EndIf
Until class=#IDCMP_CLOSEWINDOW

!image_FreeChunky { *scr, *HomeBM }


exit:
If *VisualInfo Then FreeVisualInfo_ *VisualInfo
If *scr Then UnlockPubScreen_ 0,*scr
If *APP_Font Then CloseFont_ *APP_Font
If *APP_Window Then CloseWindow_ *APP_Window
image_FreeLib {} : End


ScaleBM:
SetWindowTitles_ *APP_Window, "Scaling...", True
ww.l = *APP_Window\Width - (OffX + *APP_Window\BorderRight)
hh.l = *APP_Window\Height - (OffY + *APP_Window\BorderBottom)
mytags(0)\ti_Tag = #SBA_SrcWidth, 640
mytags(1)\ti_Tag = #SBA_SrcHeight, 512
mytags(2)\ti_Tag = #SBA_DestWidth, ww
mytags(3)\ti_Tag = #SBA_DestHeight, hh
mytags(4)\ti_Tag = #TAG_DONE
!image_ScaleBitMapA {*ScaledBM.BitMap, *HomeBM, &mytags(0)\ti_Tag}
If *ScaledBM
   SetWindowTitles_ *APP_Window, "Drawing...", True
   !image_DrawBitMap{ *ScaledBM, OffX, OffY, ww, hh, *APP_Window\RPort}
   FreeBitMap_( ScaledBM ) : txt$="Image size: "+Str$(ww)+" x "+Str$(hh)
   SetWindowTitles_ *APP_Window, txt$, True
EndIf
Return


; Using "IncBin" for this large picutre instead of data makes this demo
; about 1/2 the compiled size. "pal_dat" & "pic_dat" were generated from
; "Data.b's". Although "Data.l" is almost as good as IncBin.

paldat:IncBin "pal_dat"  ; <- palette data for the picture
picdat:IncBin "pic_dat"  ; <- raw picture data


