
; "Image_Demo_2.ab2" a C to Blitz adaptation by Lorence Lombardo.

; Based on Pawen Marciniak's "Image_Demo_2.c" example.


WBStartup

INCLUDE "image_lib.include.bb2"

If image_InitLib{36}=0 Then NPrint " Could not initialize image lib...!!!" : End

PubScreenName$ = "Workbench" : l$=Chr$(10)
APP_TitleWindow$ = "Demo okno"
APP_TitleScreenWindow$ = "Demo ekran okno"

fn$="helvetica.font"

HelveticaFont13.TextAttr\ta_Name = &fn$, 13, 0, 0

*HelveticaFont.TextFont = 0

; Koniec opcjonalnego Fontu


DEFTYPE .TextAttr *Font, Attr
FontX.w = 0 ; WORD
FontY.w = 0 ; WORD
DEFTYPE .w OffX, OffY ; UWORD


Width.w = 520 ; WORD
Height.w = 200 ; WORD
MinWidth.w = 520 ; WORD
MinHeight.w = 150 ; WORD
MaxWidth.w = 520 ; WORD
MaxHeight.w = 375 ; WORD

Dim chunk.b(1482), pal.b(21)

For x.w=0 To 1481 : Read chunk(x) : Next x
For x.w=0 To 20 : Read pal(x) : Next x

DEFTYPE .ChunkyImg chunkyimg

Statement ShowText {text$, *rp.RastPort}
   SetAPen_ *rp, 1 : SetDrMd_ *rp, #JAM2
   Move_ *rp, 20, 384 : Text_ *rp, &text$, Len(text$)
End Statement

*VisualInfo.l = 0 : *scr.Screen = 0 : *APP_Window.Window=0

*scr = LockPubScreen_( &PubScreenName$ )
If *scr=0 Then Goto exit

*HelveticaFont=OpenDiskFont_( &HelveticaFont13 )
If *HelveticaFont=0 Then Goto exit

*Font = &*Attr
*Font\ta_Name = *scr\RastPort\Font\tf_Message\mn_Node\ln_Name
*Font\ta_YSize = *scr\RastPort\Font\tf_YSize
FontY = *Font\ta_YSize
FontX = *scr\RastPort\Font\tf_XSize

OffX = *scr\WBorLeft
OffY = *scr\RastPort\TxHeight + *scr\WBorTop + 1

ComputeX.w = ( FontX * Width + 4 ) / 8
ComputeY.w = ( FontY * Height + 4 ) / 8

*VisualInfo.l = GetVisualInfoA_ ( *scr,0 )
If *VisualInfo=0 Then Goto exit

Dim mytags.TagItem(16)

mytags(0)\ti_Tag  = #WA_Left,        20
mytags(1)\ti_Tag  = #WA_Top,         20
mytags(2)\ti_Tag  = #WA_Width,       ComputeX + OffX + *scr\WBorRight
mytags(3)\ti_Tag  = #WA_Height,      ComputeY + OffY + *scr\WBorBottom
mytags(4)\ti_Tag  = #WA_MinHeight,   OffY + 2
mytags(5)\ti_Tag  = #WA_MinWidth,    ComputeX + OffX + *scr\WBorRight
mytags(6)\ti_Tag  = #WA_MaxHeight,   ComputeY + OffY + *scr\WBorBottom
mytags(7)\ti_Tag  = #WA_MaxWidth,    ComputeX + OffX + *scr\WBorRight
mytags(8)\ti_Tag  = #WA_Title,       &APP_TitleWindow$
mytags(9)\ti_Tag  = #WA_ScreenTitle, &APP_TitleScreenWindow$
mytags(10)\ti_Tag = #WA_Flags,       #WFLG_DRAGBAR | #WFLG_CLOSEGADGET | #WFLG_SIZEGADGET | #WFLG_SMART_REFRESH
mytags(11)\ti_Tag = #WA_IDCMP,       #IDCMP_CLOSEWINDOW | #IDCMP_REFRESHWINDOW
mytags(12)\ti_Tag = #WA_Gadgets,     False
mytags(13)\ti_Tag = #WA_AutoAdjust,  False
mytags(14)\ti_Tag = #WA_PubScreen,   *scr
mytags(15)\ti_Tag = #TAG_DONE

*APP_Window=OpenWindowTagList_(0, &mytags(0)\ti_Tag)
If *APP_Window=0 Then Goto exit

GT_RefreshWindow_ *APP_Window, 0

*rp.RastPort=*APP_Window\RPort : SetAPen_ *rp, 1
ShowText{"Chunky to BitMap please wait...           ", *rp}

chunkyimg\ci_Width=39
chunkyimg\ci_Height=38
chunkyimg\ci_NumColors=7
chunkyimg\ci_Palette=&pal(0)
chunkyimg\ci_ChunkyData=&chunk(0)

!image_ChunkyToBitMapA {*BM.BitMap, *scr, &chunkyimg, 0}

ShowText{"Displaying BitMap please wait...        ", *rp}

!image_DrawBitMap {*BM, 20, 20, 39, 38, *rp}

ShowText{"Drawing Text please wait...             ", *rp}

txt$="1bimage.library"+l$+"© 1993 Paweî Marciniak"+l$+"e-mail <pmarciniak@lodz.home.pl>"
txt$=txt$+l$+"Idea i programowanie"+l$+"1Paweî Marciniak"

mytags(0)\ti_Tag = #DT_Texts, &txt$
mytags(1)\ti_Tag = #DT_FontA, *HelveticaFont
mytags(2)\ti_Tag = #DT_FontB, *HelveticaFont
mytags(3)\ti_Tag = #DT_RastPort, *rp
mytags(4)\ti_Tag = #DT_Top, 40
mytags(5)\ti_Tag = #DT_Left, 200
mytags(6)\ti_Tag = #TAG_DONE

!image_DrawTextA {bla.l, &mytags(0)\ti_Tag}

ShowText{"Drawing 3DBox please wait...                 ", *rp}

txt$="Draw3DBox"
mytags(0)\ti_Tag = #DBA_PosX,   80
mytags(1)\ti_Tag = #DBA_PosY,   30
mytags(2)\ti_Tag = #DBA_Width,  240
mytags(3)\ti_Tag = #DBA_Height, 100
mytags(4)\ti_Tag = #DBA_Text,   &txt$
mytags(5)\ti_Tag = #DBA_Align,  #ALIGN_CENTER
mytags(6)\ti_Tag = #TAG_DONE

!image_Draw3DBoxA { *rp, *HelveticaFont, &mytags(0)\ti_Tag}

ShowText{"Scaling BitMap please wait...                 ", *rp}

mytags(0)\ti_Tag = #SBA_SrcWidth,   39
mytags(1)\ti_Tag = #SBA_SrcHeight,  38
mytags(2)\ti_Tag = #SBA_DestWidth,  39
mytags(3)\ti_Tag = #SBA_DestHeight, 70
mytags(4)\ti_Tag = #TAG_DONE

!image_ScaleBitMapA {*DestBM.BitMap, *BM, &mytags(0)\ti_Tag}

If *DestBM
   !image_DrawBitMap {*DestBM, 340, 20, 39, 70, *rp}
   FreeBitMap_( *DestBM )
EndIf

DEFTYPE .Image MyImage

MyImage\LeftEdge = 0
MyImage\TopEdge = 0
MyImage\Width = 39
MyImage\Height = 38
MyImage\Depth = *scr\RastPort\BitMap\Depth
MyImage\ImageData = *BM\Planes
MyImage\PlanePick = 255
MyImage\PlaneOnOff = 0
MyImage\NextImage = 0

DrawImage_ *rp, &MyImage, 340, 100 : class.l=0

ShowText{"That's all folks                                    ", *rp}

Repeat
   WaitPort_ ( *APP_Window\UserPort )
   *imsg.IntuiMessage = GT_GetIMsg_( *APP_Window\UserPort )

   If *imsg
      class = *imsg\Class : GT_ReplyIMsg_( *imsg )

      If class = #IDCMP_REFRESHWINDOW    ;      <- re-draw the left image
         GT_BeginRefresh_( *APP_Window )
         !image_DrawBitMap {*BM, 20, 20, 39, 38, *rp}
         GT_EndRefresh_ *APP_Window, True
      EndIf
   EndIf
Until class=#IDCMP_CLOSEWINDOW

!image_FreeChunky { *scr, *BM }

exit:
If *VisualInfo Then FreeVisualInfo_ *VisualInfo
If *scr Then UnlockPubScreen_ 0,*scr
If *HelveticaFont Then CloseFont_ *HelveticaFont
If *APP_Window Then CloseWindow_ *APP_Window
image_FreeLib {} : End


CChunky:  ; 38x39 = 1482
Data.b 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
Data.b 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
Data.b 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,4,4,0,0,0,0,0,0,0,0,0,0,0,0,0,0
Data.b 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,4,4,4,4,4,0,0,0,0,0,0,0,0,0,0,0,0,0
Data.b 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,4,4,4,4,2,4,4,4,0,0,0,0,0,0,0,0,0,0,0,0
Data.b 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,4,4,4,4,2,2,2,2,4,4,0,0,0,0,0,0,0,0,0,0,0,0
Data.b 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,4,4,4,4,4,2,2,2,2,2,4,4,4,0,0,0,0,0,0,0,0,0,0,0
Data.b 0,0,0,0,0,0,0,0,0,0,0,0,0,4,4,4,4,4,2,2,2,2,2,2,2,2,4,4,0,0,0,0,0,0,0,0,0,0,0
Data.b 0,0,0,0,0,0,0,0,0,0,0,0,4,4,4,4,2,2,2,2,2,2,2,2,2,2,4,4,4,0,0,0,0,0,0,0,0,0,0
Data.b 0,0,0,0,0,0,0,0,0,0,4,4,4,4,4,4,2,2,2,2,2,2,2,2,2,2,2,4,4,0,0,0,0,0,0,0,0,0,0
Data.b 0,0,0,0,0,0,0,0,4,4,4,4,3,3,4,4,2,2,2,2,2,2,2,2,2,2,2,4,4,4,0,0,0,0,0,0,0,0,0
Data.b 0,0,0,0,0,0,4,4,4,4,3,3,3,3,4,4,4,2,2,2,2,2,2,2,2,2,2,2,4,4,0,0,0,0,0,0,0,0,0
Data.b 0,0,0,0,4,4,4,4,3,3,3,3,3,3,3,4,4,2,2,2,2,2,2,2,2,2,2,2,2,4,4,0,0,0,0,0,0,0,0
Data.b 0,0,4,4,4,4,3,3,3,3,3,3,3,3,3,3,4,4,2,2,2,2,2,2,2,2,2,2,4,4,4,0,0,0,0,0,0,0,0
Data.b 0,6,6,4,4,3,3,3,3,3,3,3,3,3,3,3,4,4,2,2,2,2,2,2,2,2,4,4,4,4,4,4,0,0,0,0,0,0,0
Data.b 0,6,6,4,4,3,3,3,3,3,3,3,3,3,3,3,3,4,4,2,2,2,2,2,4,4,4,4,5,5,4,4,0,0,0,0,0,0,0
Data.b 0,0,6,6,4,4,3,3,3,3,3,3,3,3,3,3,3,4,4,2,2,2,4,4,4,4,5,5,5,5,5,4,4,0,0,0,0,0,0
Data.b 0,0,6,6,4,4,3,3,3,3,3,3,3,3,3,3,3,3,4,4,4,4,4,4,5,5,5,5,5,5,5,4,4,0,0,0,0,0,0
Data.b 0,0,0,6,6,4,4,3,3,3,3,3,3,3,3,3,3,3,4,4,4,4,5,5,5,5,5,5,5,5,5,5,4,4,0,0,0,0,0
Data.b 0,0,0,6,6,4,4,3,3,3,3,3,3,3,3,3,4,4,4,4,4,5,5,5,5,5,5,5,5,5,5,5,4,4,0,0,0,0,0
Data.b 0,0,0,0,6,6,4,4,3,3,3,3,3,3,4,4,4,4,4,4,4,5,5,5,5,5,5,5,5,5,5,5,5,4,4,0,0,0,0
Data.b 0,0,0,0,6,6,4,4,3,3,3,3,3,4,4,4,4,1,1,1,4,4,5,5,5,5,5,5,5,5,5,5,5,4,4,4,0,0,0
Data.b 0,0,0,0,0,6,6,4,4,3,3,4,4,4,4,1,1,1,1,1,4,4,4,5,5,5,5,5,5,5,5,5,5,5,4,4,0,0,0
Data.b 0,0,0,0,0,6,6,4,4,4,4,4,4,1,1,1,1,1,1,1,1,4,4,5,5,5,5,5,5,5,5,5,5,5,4,4,4,0,0
Data.b 0,0,0,0,0,0,6,6,4,4,4,1,1,1,1,1,1,1,1,1,1,4,4,4,5,5,5,5,5,5,5,5,4,4,4,4,4,6,0
Data.b 0,0,0,0,0,0,6,6,4,4,4,1,1,1,1,1,1,1,1,1,1,1,4,4,5,5,5,5,5,5,4,4,4,4,4,6,6,6,6
Data.b 0,0,0,0,0,0,0,6,6,4,4,1,1,1,1,1,1,1,1,1,1,1,4,4,4,5,5,5,5,4,4,4,4,6,6,6,6,6,6
Data.b 0,0,0,0,0,0,0,6,6,4,4,4,1,1,1,1,1,1,1,1,1,1,1,4,4,5,5,4,4,4,4,6,6,6,6,6,6,0,0
Data.b 0,0,0,0,0,0,0,0,6,6,4,4,1,1,1,1,1,1,1,1,1,1,1,4,4,4,4,4,4,6,6,6,6,6,6,0,0,0,0
Data.b 0,0,0,0,0,0,0,0,6,6,4,4,4,1,1,1,1,1,1,1,1,1,1,1,4,4,4,4,6,6,6,6,6,6,0,0,0,0,0
Data.b 0,0,0,0,0,0,0,0,0,6,6,4,4,1,1,1,1,1,1,1,1,1,4,4,4,4,6,6,6,6,6,6,0,0,0,0,0,0,0
Data.b 0,0,0,0,0,0,0,0,0,0,6,6,4,4,1,1,1,1,1,1,4,4,4,4,6,6,6,6,6,6,0,0,0,0,0,0,0,0,0
Data.b 0,0,0,0,0,0,0,0,0,0,6,6,4,4,1,1,1,1,4,4,4,4,6,6,6,6,6,6,0,0,0,0,0,0,0,0,0,0,0
Data.b 0,0,0,0,0,0,0,0,0,0,0,6,6,4,4,1,4,4,4,4,6,6,6,6,6,6,0,0,0,0,0,0,0,0,0,0,0,0,0
Data.b 0,0,0,0,0,0,0,0,0,0,0,6,6,4,4,4,4,4,6,6,6,6,6,6,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
Data.b 0,0,0,0,0,0,0,0,0,0,0,0,6,6,4,4,6,6,6,6,6,6,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
Data.b 0,0,0,0,0,0,0,0,0,0,0,0,0,6,6,6,6,6,6,6,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
Data.b 0,0,0,0,0,0,0,0,0,0,0,0,0,0,6,6,6,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0

pallete:   ; 21
Data.b 0,0,0, 0,255,0, 255,0,0, 255,255,0, 255,255,255, 0,0,255, 127,127,127


