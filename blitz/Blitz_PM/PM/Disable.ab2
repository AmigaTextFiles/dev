
; "Disable.ab2" for Blitz was based on "HB2 Disable.bas"
; which was based on "Disable.c" from the "pop menu" package,
; implementing my own style into it. ;)

; Done by Lorence Lombardo in 2008  (lombi@iprimus.com.au)

; See the archives "pmDevBas.lha" and "pmdev.lha" on the
; aminet for related interest.


WBStartup

INCLUDE "PM_constants.bb2"

w.l=0 : im.l=0 : p.l=0 : r.l=True

Dim PMtags.TagItem(16) : Dim i.l(8)

; 1st menu item  (not visible)

PMtags(0)\ti_Tag = #PM_Hidden, True : PMtags(1)\ti_Tag = #TAG_DONE
i(0) = PM_MakeItemA_(&PMtags(0)\ti_Tag)

; 2nd menu item  (title)

tit$="Plain Simple Menu!" : PMtags(0)\ti_Tag = #PM_Title, &tit$
PMtags(1)\ti_Tag = #PM_NoSelect, True : PMtags(2)\ti_Tag = #PM_ShinePen, True
PMtags(3)\ti_Tag = #PM_Shadowed, True : PMtags(4)\ti_Tag = #PM_Center, True
PMtags(5)\ti_Tag = #TAG_DONE : i(1) = PM_MakeItemA_(&PMtags(0)\ti_Tag)

; 3rd menu item  (separator bar)

PMtags(0)\ti_Tag = #PM_WideTitleBar, True : PMtags(1)\ti_Tag = #TAG_DONE
i(2) = PM_MakeItemA_(&PMtags(0)\ti_Tag)

; 4th menu item

tit$="Enable quit?" : PMtags(0)\ti_Tag = #PM_Title, &tit$
PMtags(1)\ti_Tag = #PM_ID, 10 : PMtags(2)\ti_Tag = #PM_Checkit, True
PMtags(3)\ti_Tag = #TAG_DONE  : i(3) = PM_MakeItemA_(&PMtags(0)\ti_Tag)

; 5th menu item  (separator menu)

PMtags(0)\ti_Tag = #PM_TitleBar, True : PMtags(1)\ti_Tag = #TAG_DONE
i(4) = PM_MakeItemA_(&PMtags(0)\ti_Tag)

; 6th menu item  (last item for this menu followed by a sub item)

tit$="Quit" : PMtags(0)\ti_Tag = #PM_Title, &tit$
PMtags(1)\ti_Tag = #PM_Disabled, True : PMtags(2)\ti_Tag = #PM_UserData, 5
PMtags(3)\ti_Tag = #PM_ID, 15         : PMtags(4)\ti_Tag = #TAG_DONE
i(5) = PM_MakeItemA_(&PMtags(0)\ti_Tag)

; sub item

PMtags(0)\ti_Tag = #PM_Title, &tit$ : PMtags(1)\ti_Tag = #PM_Sub, i(5)
PMtags(2)\ti_Tag = #TAG_DONE        : i(6) = PM_MakeItemA_(&PMtags(0)\ti_Tag)

; Creating the popup menu

PMtags(0)\ti_Tag = #PM_Item, i(0) : PMtags(1)\ti_Tag = #PM_Item, i(1)
PMtags(2)\ti_Tag = #PM_Item, i(2) : PMtags(3)\ti_Tag = #PM_Item, i(3)
PMtags(4)\ti_Tag = #PM_Item, i(4) : PMtags(5)\ti_Tag = #PM_Item, i(6)
PMtags(6)\ti_Tag = #TAG_DONE      : p = PM_MakeMenuA_(&PMtags(0)\ti_Tag)

tit$="Disable & Enable"

; Window properties for an OS window
                    ; If using a Blitz window make sure you comment this section out.

PMtags(0)\ti_Tag = #WA_IDCMP, #IDCMP_CLOSEWINDOW | #IDCMP_MOUSEBUTTONS
PMtags(1)\ti_Tag = #WA_RMBTrap, True : PMtags(2)\ti_Tag = #WA_DragBar, True
PMtags(3)\ti_Tag = #WA_Width, 150    : PMtags(4)\ti_Tag = #WA_Height, 100
PMtags(5)\ti_Tag = #WA_Left, 0       : PMtags(6)\ti_Tag = #WA_Top, 0
PMtags(7)\ti_Tag = #WA_Title, &tit$  : PMtags(8)\ti_Tag = #WA_CloseGadget, True
PMtags(9)\ti_Tag = #TAG_DONE : w=OpenWindowTagList_(0, &PMtags(0)\ti_Tag)

; I've got it to work with a Blitz window
;                  Comment this section out if using an OS window.

;AddIDCMP #IDCMP_CLOSEWINDOW | #IDCMP_MOUSEBUTTONS
;Window 0,0,0,150,100,$8|$2,tit$,0,0
;w=Peek.l (Addr Window(0))


#IntuiMessage_sizeof=52 : #UserPort=86 : #Class=20 : #IntuiMessageCode=24

imsg$=String$(Chr$(0), #IntuiMessage_sizeof)

While r
   tmp.l = WaitPort_( Peek.l(w+#UserPort) )

   Repeat
      im = GetMsg_( Peek.l(w+#UserPort) )
      If im
         CopyMem_ im, &imsg$, Len(imsg$) : ReplyMsg_ im
         tmp = Peek.l(&imsg$ + #Class)

         If tmp = #IDCMP_CLOSEWINDOW Then r=False

         If tmp = #IDCMP_MOUSEBUTTONS
            PMtags(0)\ti_Tag = #PM_Menu, p
            PMtags(1)\ti_Tag = #PM_Dummy, &imsg$+#IntuiMessageCode
            PMtags(2)\ti_Tag = #TAG_DONE
            r = PM_OpenPopupMenuA_(w, &PMtags(0)\ti_Tag) - 5

            If PM_ItemChecked_ (p,10) Then state.b=False Else state.b=True
            PMtags(0)\ti_Tag = #PM_Disabled, state : PMtags(1)\ti_Tag = #TAG_DONE
            bla.l = PM_SetItemAttrsA_(PM_FindItem_(p,15), &PMtags(0)\ti_Tag)
         EndIf

      EndIf
   Until tmp=#IDCMP_CLOSEWINDOW OR tmp=#IDCMP_MOUSEBUTTONS OR NOT im

Wend

CloseWindow_ w    ; comment this if using a Blitz window.

;CloseWindow 0     ; comment this if using an OS window.

PM_FreePopupMenu_ p


