
; "Layout.ab2" for Blitz was based on "HB2 Layout.bas"
; which was based on "Layout.c" from the "pop menu" package,
; implementing my own style into it. ;)

; Done by Lorence Lombardo in 2008  (lombi@iprimus.com.au)

; See the archives "pmDevBas.lha" and "pmdev.lha" on the
; aminet for related interest.


WBStartup

INCLUDE "PM_constants.bb2"

w.l=0 : im.l=0 : p.l=0 : r.l=True

Dim PMtags.TagItem(30) : Dim i.l(10) : Dim i1.l(10) : Dim i2.l(10)

; 1st menu item  (not visible)

PMtags(0)\ti_Tag = #PM_Hidden, True : PMtags(1)\ti_Tag = #TAG_DONE
i(0) = PM_MakeItemA_(&PMtags(0)\ti_Tag)

; 2nd menu item  (title)

tit$="Group Layout" : PMtags(0)\ti_Tag = #PM_Title, &tit$
PMtags(1)\ti_Tag = #PM_NoSelect, True : PMtags(2)\ti_Tag = #PM_ShinePen, True
PMtags(3)\ti_Tag = #PM_Shadowed, True : PMtags(4)\ti_Tag = #PM_Center, True
PMtags(5)\ti_Tag = #TAG_DONE : i(1) = PM_MakeItemA_(&PMtags(0)\ti_Tag)

; 3rd menu item  (Separator bar)

PMtags(0)\ti_Tag = #PM_WideTitleBar, True : PMtags(1)\ti_Tag = #TAG_DONE
i(2) = PM_MakeItemA_(&PMtags(0)\ti_Tag)

; The first horizontal group with three elements (one is other group)

; 1st element

tit$="Left" : PMtags(0)\ti_Tag = #PM_Title, &tit$
PMtags(1)\ti_Tag = #PM_Center, True : PMtags(2)\ti_Tag = #TAG_DONE
i1(1) = PM_MakeItemA_(&PMtags(0)\ti_Tag)

; 2nd element  (a vertical group)

For i.b=1 To 6
   tit$="Item"+Str$(i) : PMtags(0)\ti_Tag = #PM_Title, &tit$
   PMtags(1)\ti_Tag = #PM_Center, True : PMtags(2)\ti_Tag = #TAG_DONE
   i2(i) = PM_MakeItemA_(&PMtags(0)\ti_Tag)
Next i

; Now, vertical grouping...

PMtags(0)\ti_Tag = #PM_Dummy, 0
For i=1 To 6
   PMtags(i)\ti_Tag = #PM_Item, i2(i)
Next i
PMtags(7)\ti_Tag = #TAG_DONE : i2(0) = PM_MakeMenuA_(&PMtags(0)\ti_Tag)

PMtags(0)\ti_Tag = #PM_NoSelect, True : PMtags(1)\ti_Tag = #PM_LayoutMode, #PML_Vertical
PMtags(2)\ti_Tag = #PM_Members, i2(0) : PMtags(3)\ti_Tag = #TAG_DONE
i1(2) = PM_MakeItemA_(&PMtags(0)\ti_Tag)

; Third element

tit$="Right" : PMtags(0)\ti_Tag = #PM_Title, &tit$
PMtags(1)\ti_Tag = #PM_Center, True : PMtags(2)\ti_Tag = #TAG_DONE
i1(3) = PM_MakeItemA_(&PMtags(0)\ti_Tag)

; And now, horizontal grouping...

PMtags(0)\ti_Tag = #PM_Dummy, 0    : PMtags(1)\ti_Tag = #PM_Item, i1(1)
PMtags(2)\ti_Tag = #PM_Item, i1(2) : PMtags(3)\ti_Tag = #PM_Item, i1(3)
PMtags(4)\ti_Tag = #TAG_DONE       : i1(0) = PM_MakeMenuA_(&PMtags(0)\ti_Tag)

PMtags(0)\ti_Tag = #PM_NoSelect, True : PMtags(1)\ti_Tag = #PM_LayoutMode, #PML_Horizontal
PMtags(2)\ti_Tag = #PM_Members, i1(0) : PMtags(3)\ti_Tag = #TAG_DONE
i(3) = PM_MakeItemA_(&PMtags(0)\ti_Tag)

; Separator bar

PMtags(0)\ti_Tag = #PM_TitleBar, True : PMtags(1)\ti_Tag = #TAG_DONE
i(4) = PM_MakeItemA_(&PMtags(0)\ti_Tag)

; The second group (vertical)... this contains
; five entries (horizontal groups with 10 items).

For j.b=0 To 4

   For i=0 To 9
      PMtags(0)\ti_Tag = #PM_ColourBox, i+(j*10) : PMtags(1)\ti_Tag = #TAG_DONE
      i2(i)=PM_MakeItemA_(&PMtags(0)\ti_Tag)
   Next i

   ; Creating the horizontal group

   PMtags(0)\ti_Tag =  #PM_Dummy, 0
   For i=1 To 9
      PMtags(i)\ti_Tag = #PM_Item, i2(i)
   Next i
   PMtags(10)\ti_Tag = #PM_Item, i2(0) : PMtags(11)\ti_Tag = #TAG_DONE
   i1(6+j) = PM_MakeMenuA_(&PMtags(0)\ti_Tag)

   PMtags(0)\ti_Tag = #PM_NoSelect, True : PMtags(1)\ti_Tag = #PM_LayoutMode, #PML_Horizontal
   PMtags(2)\ti_Tag = #PM_Members, i1(6+j) : PMtags(3)\ti_Tag = #TAG_DONE
   i1(1+j) = PM_MakeItemA_(&PMtags(0)\ti_Tag)

Next j


; Creating the vertical group

PMtags(0)\ti_Tag = #PM_Dummy, 0
For i=1 To 5
   PMtags(i)\ti_Tag = #PM_Item, i1(i)
Next i
PMtags(6)\ti_Tag = #TAG_DONE : i1(0) = PM_MakeMenuA_(&PMtags(0)\ti_Tag)

PMtags(0)\ti_Tag = #PM_NoSelect, True : PMtags(1)\ti_Tag = #PM_LayoutMode, #PML_Vertical
PMtags(2)\ti_Tag = #PM_Members, i1(0) : PMtags(3)\ti_Tag = #TAG_DONE
i(5) = PM_MakeItemA_(&PMtags(0)\ti_Tag)

; Separator bar

PMtags(0)\ti_Tag = #PM_TitleBar, True : PMtags(1)\ti_Tag = #TAG_DONE
i(6) = PM_MakeItemA_(&PMtags(0)\ti_Tag)

; Quit item

tit$="Quit" : PMtags(0)\ti_Tag = #PM_Title, &tit$
PMtags(1)\ti_Tag = #PM_UserData, 2 : PMtags(2)\ti_Tag = #TAG_DONE
i(7) = PM_MakeItemA_(&PMtags(0)\ti_Tag)

; Creating the popup menu

For i=0 To 7
   PMtags(i)\ti_Tag = #PM_Item, i(i)
Next i
PMtags(8)\ti_Tag = #TAG_DONE : p = PM_MakeMenuA_(&PMtags(0)\ti_Tag)

tit$="Layout"

; Window properties for an OS window
                    ; If using a Blitz window make sure you comment this section out.

PMtags(0)\ti_Tag = #WA_IDCMP, #IDCMP_CLOSEWINDOW | #IDCMP_MOUSEBUTTONS | #IDCMP_VANILLAKEY
PMtags(1)\ti_Tag = #WA_RMBTrap, True : PMtags(2)\ti_Tag = #WA_DragBar, True
PMtags(3)\ti_Tag = #WA_Width, 150    : PMtags(4)\ti_Tag = #WA_Height, 100
PMtags(5)\ti_Tag = #WA_Left, 0       : PMtags(6)\ti_Tag = #WA_Top, 100
PMtags(7)\ti_Tag = #WA_Title, &tit$  : PMtags(8)\ti_Tag = #WA_CloseGadget, True
PMtags(9)\ti_Tag = #TAG_DONE : w=OpenWindowTagList_(0, &PMtags(0)\ti_Tag)

; I've got it to work with a Blitz window
;                  Comment this section out if using an OS window.

;AddIDCMP #IDCMP_CLOSEWINDOW | #IDCMP_MOUSEBUTTONS | #IDCMP_VANILLAKEY
;Window 0,0,100,150,100,$8|$2,tit$,0,0
;w=Peek.l (Addr Window(0))


#IntuiMessage_sizeof=52 : #UserPort=86 : #Class=20 : #IntuiMessageCode=24

imsg$=String$(Chr$(0), #IntuiMessage_sizeof)

While r
   bla.l = WaitPort_( Peek.l(w+#UserPort) )

   Repeat
      im = GetMsg_( Peek.l(w+#UserPort) )
      If im
         CopyMem_ im, &imsg$, Len(imsg$) : ReplyMsg_ im
         tmp.l = Peek.l(&imsg$ + #Class)

         If tmp = #IDCMP_MOUSEBUTTONS
            PMtags(0)\ti_Tag = #PM_Menu, p : PMtags(1)\ti_Tag = #TAG_DONE
            r = PM_OpenPopupMenuA_(w, &PMtags(0)\ti_Tag) - 2
         EndIf

         If tmp = #IDCMP_CLOSEWINDOW Then r=False

      EndIf
   Until tmp=#IDCMP_CLOSEWINDOW OR tmp=#IDCMP_MOUSEBUTTONS OR NOT im

Wend

CloseWindow_ w    ; comment this if using a Blitz window.

;CloseWindow 0     ; comment this if using an OS window.

PM_FreePopupMenu_ p


