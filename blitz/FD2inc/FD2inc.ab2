
optimize 5 : qt$=Chr$(34) : l$=Chr$(10) : tb$=Chr$(9) : np=NumPars

startfile.s="" : f_ok.b=0 : If np>0 Then startfile = Par$(1)

If np<1 OR startfile="?"
   NPrint l$+" Usage:"+l$+l$+" FD2inc <fd-file> [clib-file]"
   NPrint " Converts an FD lib file into a Blitz include."+l$
   End
EndIf

If np>1 Then f$=Par$(2)


; string functions

Function.s dos_FilePart {filename.s}
   fptr.l = FilePart_(&filename.s)
   If fptr
     filepart.s = Peek.s(fptr)
   Else
     filepath.s = ""
   EndIf
   Function Return filepart.s
End Function

Function.s dos_RemExt {filename.s}
   m.l = Len(filename.s)
   While w$<>"."
      w$=Mid$(filename.s,m,1) : m-1
      If m=0 OR (Len(filename.s)-m)>8 Then w$="." : m=Len(filename.s)
   Wend
   filename.s = Left$(filename.s,m)
   Function Return filename.s
End Function

Function.s dos_SetExt {filename.s,extention.s}
   If extention.s<>""
      filename.s = dos_RemExt {filename.s}
      filename.s + "." + extention.s
   EndIf
   Function Return filename.s
End Function

; end of string functions


NEWTYPE.fdengine
   bias.l
   public.l
   basename.s
   funccount.l
   libname.s
   includename.s
   prefix.s
End NEWTYPE

NEWTYPE.func
   name.s
   param.s[32]
   reg.s[32]
   lvo.l
   public.l
   paramcount.l
   isret.l
End NEWTYPE

DEFTYPE .fdengine fd

fd\bias        = 30
fd\public      = True
fd\basename    = dos_FilePart{dos_RemExt{startfile}}
fd\libname     = dos_SetExt{Replace$(fd\basename,"_lib",""),"library"}
fd\basename    = "_"+fd\basename+"Base"
fd\funccount   = 0
fd\includename = dos_RemExt{fd\libname} +".include"
fd\prefix      = dos_RemExt{fd\libname}

Dim List func.func(0)
Dim List noret$(0)


If np>1
   If ReadFile(0,f$)
      FileInput 0 : f_ok=1
      Repeat
         lin.s = Edit$(2000) : lin=Replace$(lin,tb$," ")
         InitArgParse lin : arg$=UCase$(NextArg$)
         If arg$="VOID"
            arg2$=NextArg$ : x.l=Instr(arg2$,"(")
            If x>0 Then arg2$=Left$(arg2$,x-1)
            AddLast noret$() : noret$()=UCase$(arg2$)
         EndIf
      Until Eof(0)
      CloseFile 0
   EndIf
EndIf


If ReadFile(0,startfile)
  quitme.l = False : FileInput 0
  While quitme = False AND Eof(0)=False
    lin.s = Edit$(2000) : If lin.s = "" Then quitme = True
    compos.l = Instr(lin,"*")
    If compos>0 Then lin = Left$(lin,compos-1)
    If Left$(lin.s,2)="##"
      InitArgParse lin : wrd$=NextArg$ : wrd$=Mid$(wrd$,3)
      wrd$=LCase$(wrd$) : arg2$=NextArg$
      Select wrd$
        Case "base"
          fd\basename = arg2$
        Case "bias"
          fd\bias = Vallong(arg2$) : fd\funccount=0
        Case "public"
          fd\public = True
        Case "private"
          fd\public = False
        Case "end"
          quitme = True
        Default
          NPrint "; *********************************************"
          NPrint "Unknown instruction: "+wrd$
          NPrint "; *********************************************"
      End Select
    Else
      pos1.l = Instr(lin,"(")
      pos2.l = Instr(lin,")")
      If pos1>0 AND pos2>0
        name.s = Left$(lin,pos1-1)
        name=StripTrail$(name,32) : name=StripLead$(name,32)
        param.s = Mid$(lin,pos1+1,pos2-pos1-1)
        reg.s   = Right$(lin,Len(lin)-pos2);: NPrint reg
        pos1.l = Instr(reg,"(")
        pos2.l = Instr(reg,")")
        reg = Right$(reg,Len(reg)-pos1)
        reg = Left$(reg,Len(reg)-1)

        If AddItem(func())
          If UCase$(Left$(name,Len(fd\prefix))) <> UCase$(fd\prefix)
             func()\name = fd\prefix+"_"+name
          Else
             func()\name = name
          EndIf
          isret.b=1
          If f_ok=1
             ResetList noret$() : uname$=UCase$(name)
             Repeat
                nxi.l=NextItem(noret$())
                If nxi
                   If uname$=noret$()
                      isret=0 : KillItem noret$() : nxi=0
                   EndIf
                EndIf
             Until nxi=0
          EndIf
          func()\isret = isret
          While param<>""
            pos.l = Instr(param,",")
            If pos<=0
               thisparam.s = param : param=""
            Else
               thisparam=Left$(param,pos-1) : param = Right$(param,Len(param)-pos)
            EndIf
            If thisparam<>""
              func()\param[func()\paramcount] = thisparam
              func()\paramcount+1
            Else
              param = ""
            EndIf
          Wend
          n.l=0
          While reg<>""
            pos.l = Instr(reg,",")
            pos2.l = Instr(reg,"/")
            If pos2>0 Then If pos=0 Then pos = pos2 Else pos = Min(pos,pos2)
            If pos<=0
              thisreg.s = reg : reg=""
            Else
              thisreg=Left$(reg,pos-1) : reg = Right$(reg,Len(reg)-pos)
            End If
            If thisreg<>""
               func()\reg[n] = thisreg : n+1
               If n>func()\paramcount Then func()\paramcount+1 : func()\param[n-1]="#?"
            Else
               reg = ""
            EndIf
          Wend
          func()\lvo = fd\bias + fd\funccount*6 : fd\funccount+1
          func()\public = fd\public
        EndIf
      EndIf
    EndIf
  Wend

  ; print out ...
  NPrint l$+"; "+fd\includename+".bb2 generated by FD2inc."+l$

  NPrint l$+"DEFTYPE .Library *"+fd\basename+l$
  ResetList func()
  While NextItem(func())
    Print "Macro "+func()\name+" ; {"
    If func()\isret=1 Then Print "ret" : If func()\paramcount>0 Then Print ","
    For n.l=0 To func()\paramcount-1
       Print func()\param[n] : If n<func()\paramcount-1 Then Print ","
    Next n
    NPrint "}" : c.w=0
    For n.l=0 To func()\paramcount-1
       If c=0 Then Print "   "
       Print "Getreg "+func()\reg[n]+",`"
       If func()\isret=1 Then n$=Str$(n+2) : If n>7 Then n$=Chr$(89+n)
       If func()\isret=0 Then n$=Str$(n+1) : If n>8 Then n$=Chr$(88+n)
       Print n$ : c+1 : If c<5 AND n<func()\paramcount-1 Then Print " : "
       If c=5 OR n=func()\paramcount-1 Then c=0 : NPrint ""
    Next n
    Print "   Getreg a6,*"+fd\basename+" : JSR -"+Str$(func()\lvo)+"(a6)"
    If func()\isret=1 Then Print " : PutReg d0,`1"
    NPrint l$+"End Macro"+l$
  Wend
  NPrint l$+"Function.l "+fd\prefix+"_InitLib {minVersion.l}"
  NPrint "   SHARED *"+fd\basename
  NPrint "   If *"+fd\basename+" Then Function Return *"+fd\basename
  NPrint "   If minVersion<0 Then minVersion = 0"
  NPrint "   *"+fd\basename+" = OpenLibrary_ ("+qt$+fd\libname+qt$+",minVersion)"
  NPrint "   Function Return *"+fd\basename
  NPrint "End Function"+l$+l$
  NPrint "Statement "+fd\prefix+"_FreeLib {}"
  NPrint "   SHARED *"+fd\basename
  NPrint "   If *"+fd\basename+" Then CloseLibrary_ *"+fd\basename+" : *"+fd\basename+" = 0"
  NPrint "End Statement"+l$+l$
  CloseFile 0
Else
  NPrint l$+" File not found...!!!"+l$
EndIf

End

