
; "SelGadgTest_hi.ab2" by Lorence Lombardo 9-July-2010.

; This is a higher level version of "SelGadgTest.ab2".

; OS Window and OS Message functions have been replaced with
; Blitz Window commands.

; At the moment I'm not quite sure if its possible to replace the
; OS GT functions with the Blitz GT commands. If you have any
; ideas about this then by all means feel free to let me know.

; Run from CLI.


INCLUDE "selectgadget_cl.include.bb2"

If selectgadget_cl_InitLib{0}=0 Then End

Dim labels1.TagItem(2), labels2.TagItem(2), mytags.TagItem(17)

a$="First option" : b$="Second option"
c$="Third option" : d$="Fourth option"

labels1(0)\ti_Tag = &a$, &b$ : labels1(1)\ti_Tag = &c$, &d$
labels1(2)\ti_Tag = #TAG_DONE

e$ = "This is an" : f$ = "example of" : g$ = "my BOOPSI"
h$ = "pop-up" : i$ = "gadget class"

labels2(0)\ti_Tag = &e$, &f$ : labels2(1)\ti_Tag = &g$, &h$
labels2(2)\ti_Tag = &i$, #TAG_DONE

j$="select.gadget test"

AddIDCMP #IDCMP_CLOSEWINDOW | #IDCMP_GADGETUP | #IDCMP_REFRESHWINDOW | #IDCMP_VANILLAKEY
Window 0,0,20,640,160,$1000|$8|$2|$1,j$,0,0

*win.Window = Peek.l (Addr Window(0))

*dri.DrawInfo = GetScreenDrawInfo_(*win\WScreen)

k$="_Click me"

mytags(0)\ti_Tag = #GA_Left,           40
mytags(1)\ti_Tag = #GA_Top,            40 + *win\BorderTop
mytags(2)\ti_Tag = #GA_RelVerify,      True
mytags(3)\ti_Tag = #GA_DrawInfo,       *dri
mytags(4)\ti_Tag = #GA_Text,           &k$
mytags(5)\ti_Tag = #GA_ID,             1
mytags(6)\ti_Tag = #SGA_TextPlace,     #PLACETEXT_ABOVE
mytags(7)\ti_Tag = #SGA_Labels,        &labels1(0)\ti_Tag
mytags(8)\ti_Tag = #SGA_Separator,     False
mytags(9)\ti_Tag = #SGA_ItemSpacing,   2
mytags(10)\ti_Tag =#SGA_FollowMode,    #SGFM_FULL
mytags(11)\ti_Tag =#SGA_MinTime,       200
mytags(12)\ti_Tag =#SGA_MaxTime,       200
mytags(13)\ti_Tag =#SGA_PanelMode,     #SGPM_DIRECT_NB
mytags(14)\ti_Tag =#SGA_Underscore,    95
mytags(15)\ti_Tag =#TAG_DONE

*gad1.Gadget = NewObjectA_(0, "selectgclass", &mytags(0)\ti_Tag)

l$="Me, _too!"

mytags(0)\ti_Tag = #GA_Previous,        *gad1
mytags(1)\ti_Tag = #GA_Top,             80 + *win\BorderTop
mytags(2)\ti_Tag = #GA_RelVerify,       True
mytags(3)\ti_Tag = #GA_DrawInfo,        *dri
mytags(4)\ti_Tag = #GA_Text,            &l$
mytags(5)\ti_Tag = #GA_ID,              2
mytags(6)\ti_Tag = #SGA_Labels,         &labels2(0)\ti_Tag
mytags(7)\ti_Tag = #SGA_PopUpPos,       #SGPOS_RIGHT
mytags(8)\ti_Tag = #SGA_Quiet,          True
mytags(9)\ti_Tag = #SGA_Separator,      False
mytags(10)\ti_Tag = #SGA_ReportAll,     True
mytags(11)\ti_Tag = #SGA_BorderSize,    8
mytags(12)\ti_Tag = #SGA_FullPopUp,     True
mytags(13)\ti_Tag = #SGA_PopUpDelay,    1
mytags(14)\ti_Tag = #SGA_DropShadow,    True
mytags(15)\ti_Tag = #SGA_ListJustify,   #SGJ_LEFT
mytags(16)\ti_Tag = #SGA_Underscore,    95
mytags(17)\ti_Tag = #TAG_DONE

*gad2.Gadget = NewObjectA_(0, "selectgclass", &mytags(0)\ti_Tag)

mytags(0)\ti_Tag = #GA_Left, *gad1\LeftEdge + *gad1\Width - *gad2\Height - 10
mytags(1)\ti_Tag = #GA_Width, *gad2\Height + 10
mytags(2)\ti_Tag = #TAG_DONE
SetAttrsA_ *gad2, &mytags(0)\ti_Tag

m$="Sticky b_utton"

mytags(0)\ti_Tag = #GA_Previous,      *gad2
mytags(1)\ti_Tag = #GA_Top,           40 + *win\BorderTop
mytags(2)\ti_Tag = #GA_RelVerify,     True
mytags(3)\ti_Tag = #GA_DrawInfo,      *dri
mytags(4)\ti_Tag = #GA_Text,          &m$
mytags(5)\ti_Tag = #GA_ID,            3
mytags(6)\ti_Tag = #SGA_Underscore,   95
mytags(7)\ti_Tag = #SGA_Labels,       &labels1(0)\ti_Tag
mytags(8)\ti_Tag = #SGA_Active,       3
mytags(9)\ti_Tag = #SGA_ItemSpacing,  4
mytags(10)\ti_Tag = #SGA_SymbolOnly,  True
mytags(11)\ti_Tag = #SGA_SymbolWidth, -21
mytags(12)\ti_Tag = #SGA_Sticky,      True
mytags(13)\ti_Tag = #SGA_PopUpPos,    #SGPOS_BELOW
mytags(14)\ti_Tag = #SGA_BorderSize,  4
mytags(15)\ti_Tag = #SGA_PopUpDelay,  1
mytags(16)\ti_Tag = #TAG_DONE

*gad3.Gadget = NewObjectA_(0, "selectgclass", &mytags(0)\ti_Tag)

mytags(0)\ti_Tag = #GA_Left,   *win\Width - *gad3\Width - 40
mytags(1)\ti_Tag = #TAG_DONE
SetAttrsA_ *gad3, &mytags(0)\ti_Tag

n$="S_imple"

mytags(0)\ti_Tag = #GA_Previous,    *gad3
mytags(1)\ti_Tag = #GA_Top,         80 + *win\BorderTop
mytags(2)\ti_Tag = #GA_RelVerify,   True
mytags(3)\ti_Tag = #GA_DrawInfo,    *dri
mytags(4)\ti_Tag = #GA_Text,        &n$
mytags(5)\ti_Tag = #GA_ID,          4
mytags(6)\ti_Tag = #SGA_Underscore, 95
mytags(7)\ti_Tag = #SGA_Labels,     &labels1(0)\ti_Tag
mytags(8)\ti_Tag = #TAG_DONE

*gad4.Gadget = NewObjectA_(0, "selectgclass", &mytags(0)\ti_Tag)

mytags(0)\ti_Tag = #GA_Left,   *win\Width - *gad4\Width - 40
mytags(1)\ti_Tag = #TAG_DONE
SetAttrsA_ *gad4, &mytags(0)\ti_Tag

AddGList_ *win, *gad1, True, 4, True : ln$=Chr$(10)
RefreshGList_ *gad1, *win, 0, True

Repeat
   class.l=WaitEvent
   If class = #IDCMP_GADGETUP
      PutStr_ "Gadget:"+Str$(GadgetHit)+" Item:"+Str$(EventCode)+ln$
   EndIf
   If class = #IDCMP_VANILLAKEY
      ec.l=EventCode : kc$=UCase$(Chr$(ec)) : id.l=0
      If kc$="C" Then id=*gad1
      If kc$="T" Then id=*gad2
      If kc$="U" Then id=*gad3
      If kc$="I" Then id=*gad4
      If id Then ActivateGadget_ id,*win,0 : PutStr_ "Key: "+Str$(ec)+ln$
   EndIf
   If class = #IDCMP_REFRESHWINDOW
      BeginRefresh_ *win : EndRefresh_ *win, True
   EndIf
Until class = #IDCMP_CLOSEWINDOW

RemoveGList_ *win, *gad1, 4

DisposeObject_ *gad1 : DisposeObject_ *gad2 : DisposeObject_ *gad3 : DisposeObject_ *gad4

FreeScreenDrawInfo_ *win\WScreen, *dri : CloseWindow 0

selectgadget_cl_FreeLib {}

End




