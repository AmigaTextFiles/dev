;
; SDSBFBPlayer V1.00 (C) Robert Hutchinson (22/04/1999)
; Uses routines written by Robert Hutchinson & David McMinn.
; BFBPlayMaster.library and sublibs are (C) Jarkko Vatjus-Anttila
;
; This source requires:
;
;   o bfbplaymaster.library1 in your BlitzLibs:AmigaLibs/ (+ compiled deflibs)
;   o the bfbplaymaster.library in LIBS: and Replayers/ in LIBS:
;   o The SDSBFB.RES file resident in the compiler menu.
;   o The AmigaLibs.RES file resident in compiler menu.
;   o The Blitz Support Suite would be nice ;)
;
; Satanic Dreams Software 1999, Visit:
;   http://www.satanicdreams.freeserve.co.uk/
;
; NOTE!: The BFBPrevPattern{} and BFBNextPattern{} commands cause
;        overflow errors according to the debugger, but are perfectly
;        safe (honest ;)). Just switch overflow checking off when using the
;        debugger ;) (BTW - It`s only the error code ;))
;

WBStartup
WbToScreen 0
WBenchToFront_

XINCLUDE "Blitz2:BlitzBFBPlayer/BFB_Include.BB2"  ;Our include!

*WBScreen.Screen=Peek.l(Addr Screen (0))    ;Grab some stuff from the
FontHeight = *WBScreen\Font\ta_YSize        ;screen structure.
BarHeight  = *WBScreen\BarHeight

NoMod$     = "No Module loaded!"      ;Some presets.
Shrunk     = 1
FullSize   = 68+FontHeight
ShrunkSize = 20+FontHeight
InfoHeight = 149+FontHeight

  If FromCLI AND Par$(1)="?"
    DefaultOutput
    NPrint "SDSBFBPlayer V1.00 (C) Robert Hutchinson 1999"
    NPrint "A perfect example of how to use our BFB conversion ;)"
    NPrint ""
    NPrint "  CLI USAGE: SDSBFBPlayer <Mod_Path$>"
    NPrint ""
    End
  EndIf

  Statement GadToggle{Win,GTL,IDENT,OOSwitch} ;for toggling GT gadgets <--
    If OOSwitch=On
      GTEnable GTL,IDENT
    Else
      GTDisable GTL,IDENT
    EndIf
    Redraw   Win,IDENT
  End Statement

  ; ######################################################[ Our GUI ]##

  ;-- Our Menu Lists!
  ;
  GTMenuTitle 0,0,"Project"
    GTMenuItem 0,0,0,0,"About","?"
    GTMenuItem 0,0,0,1,"About Satanic Dreams!","$"
    GTMenuItem 0,0,0,2,"About the BFB Libs."
    GTMenuItem 0,0,0,3
    GTMenuItem 0,0,0,4,"Fold <-> UnFold","U"
    GTMenuItem 0,0,0,5,"Quit","Q"

  GTMenuTitle 0,1,"PlayBack"
    GTMenuItem 0,0,1,0,"Play","P"
    GTMenuItem 0,0,1,1,"Stop","S"
    GTMenuItem 0,0,1,2,"Continue","C"
    GTMenuItem 0,0,1,3
    GTMenuItem 0,0,1,4,"Forward Pattern Jump","F"
    GTMenuItem 0,0,1,5,"Backward Pattern Jump","B"
    GTMenuItem 0,0,1,6
    GTMenuItem 0,0,1,7,"Eject","E"

  ;--- Our gadget list!
  ;
  GTButton 0,50,  0, 0, 27,15,">" ,$5 ;Top set
  GTButton 0,51, 27, 0, 27,15,"<>",$5
  GTButton 0,52, 54, 0, 27,15,"O" ,$5
  GTButton 0,53, 81, 0, 27,15,"<<",$5
  GTButton 0,54,108, 0, 27,15,">>",$5
  GTButton 0,55,135, 0, 27,15,"^" ,$5
  GTButton 0,62,162, 0, 30,15,"?" ,$5

  GTButton 0,56,  0,18, 90,15,"ModInfo",$5 ;Info bar
  GTText   0,59, 90,18, 25,15,"",$1,"00"
  GTText   0,60,125,18, 25,15,"",$1,"00"
  GTText   0,61,150,18, 42,15,"",$1,"0%"

  GTText   0,57,  0,33,192,15,"",$1,NoMod$ ;more Infos
  GTText   0,58,  0,48,192,15,"",$1,NoMod$

  Window 0,ScreenWidth/2-(200/2),ScreenHeight/2-(ShrunkSize/2),200,ShrunkSize,$1000|$2|$4|$8,"SDSBFBPlayer (V1.00)",1,0
  GTSetMenu 0
  If NOT AddAppWindow(0)
    sel.b=RTEZRequest("Error!","ERROR: Unable to generate appwindow!","Terminate!")
    End
  EndIf

  AttachGTList 0,0
  If FromCLI AND NumPars=1
    NewFile$=Par$(1)
    Gosub LoadUpFile
  EndIf

  Gosub SystemCheck

  ; #[ Main Loop ]########################################[ End Our GUI ]##

  Repeat
    Delay_ 1
    EV.l=Event
    If EV=$40           ;Check the gadgets
      Select GadgetHit
        Case 50
          Gosub PlayMe
        Case 51
          succ.b=BFBContModule{}
        Case 52
          BFBStopModule{}
        Case 53
          Gosub PatPrev
        Case 54
          Gosub PatNext
        Case 55
          BFBStopModule{}
          BFBFreeModule{}
          Gosub NoModule
          Gosub SystemCheck
        Case 62
          Gosub FoldUnFold
        Case 56
          Gosub InitAddit
      End Select
    EndIf

    If EV=$100           ;Check the menus
      Select MenuHit
        Case 0
          Select ItemHit
            Case 0:Gosub About
            Case 1:Gosub AboutSDS
            Case 2:Gosub AboutBFB

            Case 4:Gosub FoldUnFold
            Case 5:EV=$200
          End Select
        Case 1
          Select ItemHit
            Case 0:Gosub PlayMe
            Case 1:BFBStopModule{}
            Case 2:succ.b=BFBContModule{}
            Case 4:Gosub PatPrev
            Case 5:Gosub PatNext
            Case 7
              BFBStopModule{}
              BFBFreeModule{}
              Gosub NoModule
              Gosub SystemCheck
          End Select
      End Select
    EndIf

    If AppEvent
      NewFile$=AppFile(1)
      Gosub LoadUpFile
    EndIf

    Gosub ReThinkPosition

  Until EV=$200

  ; #[ Program Ends ]##################################[ End Main Loop ]##

  ; Program ends!
  BFBStopModule{}
  BFBFreeModule{}

End

  ; #[ Sub-Routines ]#####################################################


  SetupDisplay
    GTSetString 0,57,BFBModuleName{}
    GTSetString 0,58,BFBModuleType{}

    GTSetString 0,59,Str$(BFBCurrentPosition{})
    GTSetString 0,60,Str$(BFBMaxPositions{})
    GTSetString 0,61,Str$(BFBPlayPercentage{})+"%"
  Return

  ReThinkPosition
    If BFBModLoaded{}=True
      NewPos=BFBCurrentPosition{}
      If CurPos<>NewPos
        CurPos=NewPos
        GTSetString 0,59,Str$(CurPos)
        GTSetString 0,61,Str$(BFBPlayPercentage{})+"%"
      EndIf
    EndIf
  Return

  NoModule
    GTSetString 0,57,NoMod$
    GTSetString 0,58,NoMod$

    GTSetString 0,59,"00"
    GTSetString 0,60,"00"
    GTSetString 0,61,"0%"
  Return

  GeneralModErr
    lock.l=RTLockWindow(0)
    sel.b=RTEZRequest("Error!","ERROR: "+BFBParseError{succ},"Ok!")
    RTUnlockWindow 0,lock
  Return

  PatternErr
    lock.l=RTLockWindow(0)
    sel.b=RTEZRequest("Error!","ERROR: Module doesn`t support pattern jumping!","Ok!")
    RTUnlockWindow 0,lock
  Return

  FoldUnFold
    Use Window 0
    DetachGTList 0
    If Shrunk=1
      If (WindowY+FullSize)>WBHeight         ;Protect from bottom of screen
        WMove WindowX,WBHeight-(FullSize)
      EndIf
      WSize 200,FullSize
      WindowOutput 0:Use Window 0:Wline 118,48,128,33,1 ;Draw the forward slash.
      Shrunk=0
      AttachGTList 0,0
      GTBevelBox 0,4,BarHeight+16,192,3,$0
      wbarheight
    Else
      WSize 200,ShrunkSize
      Shrunk=1
      AttachGTList 0,0
    EndIf

    If BFBModLoaded{}=True
      Gosub SetupDisplay
    Else
      Gosub NoModule
    EndIf

  Return

  InitAddit
    If BFBModLoaded{}=True
      Gosub AdditionalInfoWin
    EndIf
  Return

  PatNext
    succ.b=BFBNextPattern{}
    If succ=-1
      Gosub PatternErr
    EndIf
  Return

  PatPrev
    succ.b=BFBPrevPattern{}
    If succ=-1
      Gosub PatternErr
    EndIf
  Return

  PlayMe
    succ.b=BFBPlayModule{}
    If succ<>0
      Gosub GeneralModErr
    EndIf
  Return

  About
    lock.l=RTLockWindow(0)
    InnerTxt$="SDSBFBPlayer V1.00"+Chr$(10)+Chr$(10)
    InnerTxt$+"This program (apart from being really groovy ;)) is"+Chr$(10)
    InnerTxt$+"an example of how easy it is to use the bfbplaymaster.library"+Chr$(10)
    InnerTxt$+"through Blitz2 with our C->Blitz conversion and includes."+Chr$(10)
    InnerTxt$+"The package has 22 commands for use with BFB."+Chr$(10)+Chr$(10)
    InnerTxt$+"The BFB conversion was written by:"+Chr$(10)
    InnerTxt$+"   Robert Hutchison &"+Chr$(10)
    InnerTxt$+"   David McMinn"+Chr$(10)+Chr$(10)
    InnerTxt$+"DATE: 22/04/1999"+Chr$(10)
    InnerTxt$+"Have fun!"
    sel.b=RTEZRequest("About this program",InnerTxt$,"Ok!")
    RTUnlockWindow 0,lock
  Return

  AboutSDS
    lock.l=RTLockWindow(0)
    InnerTxt$="Satanic Dreams Software"+Chr$(10)+Chr$(10)
    InnerTxt$+"SDS was a designgroup, but shortly, we will be working"+Chr$(10)
    InnerTxt$+"on a possible commercial game, and are dropping the "+Chr$(10)
    InnerTxt$+"design group image."+Chr$(10)+Chr$(10)
    InnerTxt$+" Visit our website:"+Chr$(10)
    InnerTxt$+"   HTTP://www.satanicdreams.freeserve.co.uk"+Chr$(10)+Chr$(10)
    InnerTxt$+" Or Email Admin:"+Chr$(10)
    InnerTxt$+"   admin@satanicdreams.freeserve.co.uk"
    sel.b=RTEZRequest("About Satanic Dreams Software",InnerTxt$,"Ok!")
    RTUnlockWindow 0,lock
  Return

  AboutBFB
    lock.l=RTLockWindow(0)
    InnerTxt$="About BFB"+Chr$(10)+Chr$(10)
    InnerTxt$+"BFB consists of a 13 Amiga shared libraries, 1 master lib"+Chr$(10)
    InnerTxt$+"and 12 sublibs. The master lib is used to control the sub"+Chr$(10)
    InnerTxt$+"libraries automatically. And it is very easy to use."+Chr$(10)+Chr$(10)
    InnerTxt$+"BFB is (C): Jarkko Vatjus-Anttila"+Chr$(10)+Chr$(10)
    InnerTxt$+"Replayers are (C) to their respective authors."+Chr$(10)
    sel.b=RTEZRequest("About BFB",InnerTxt$,"Ok!")
    RTUnlockWindow 0,lock
  Return

  AdditionalInfoWin
    lock.l=RTLockWindow(0)
    FlushEvents

    ClearList BFBSamples()
    ResetList BFBSamples()
    AddFirst BFBSamples()
    BFBInitSamples{}
    DefaultOutput
    BFBMS=BFBMaxSamples{}
    If BFBMS=0 OR BFBMS=1
      BFBSamples()\Text="   **********************************":AddItem BFBSamples()
      BFBSamples()\Text="   *** No sample texts availible! ***":AddItem BFBSamples()
      BFBSamples()\Text="   **********************************"
    Else
      For TSP=0 To BFBMS
        If TSP<10
          SN$="0"+Str$(TSP)+": "
        Else
          SN$=Str$(TSP)+": "
        EndIf
        BFBSamples()\Text=SN$+BFBGetSampleName{TSP}
        If TSP<>BFBMS
          AddItem BFBSamples()
        EndIf
      Next
    EndIf

    ;--- Our gadget list!
    ;
    GTText     1,50, 70,  0,272,15,"  Name:",$1,BFBModuleName{}
    GTText     1,51, 70, 15,272,15,"Format:",$1,BFBModuleType{}
    GTText     1,52, 70, 30,272,15,"Length:",$1,Str$(BFBModuleLength{})

    GTListView 1,53,  0, 45,342,86,"",$1,BFBSamples()

    GTText     1,54, 70,129,272,15," Songs:",$1,Str$(BFBMaxSongs{})

    DefaultIDCMP $40027C|$2|$4|$8|$10|$20|$40|$100|$200|$400|$8000|$10000|$40000|$80000
    Window 1,ScreenWidth/2-(350/2),ScreenHeight/2-(InfoHeight/2),350,InfoHeight,$1000|$2|$4|$8,"SDSBFBPlayer Info",1,0

    AttachGTList 1,1

    Repeat
      Delay_ 1
      EV.l=WaitEvent
      If EV=$200
        If EventWindow=1
          quitflag=1
        EndIf
      EndIf
    Until quitflag=1
    quitflag=0
    EV=0
    DetachGTList 1
    Free GTList 1
    CloseWindow 1
    FlushEvents

    RTUnlockWindow 0,lock
  Return

  SystemCheck
    If BFBModLoaded{}=True
      Switch=On
    Else
      Switch=Off
    EndIf

    GTMenuState 0,1,0,Switch
    GTMenuState 0,1,1,Switch
    GTMenuState 0,1,2,Switch
    GTMenuState 0,1,4,Switch
    GTMenuState 0,1,5,Switch
    GTMenuState 0,1,7,Switch
    For TSP=50 To 56
      GadToggle{0,0,TSP,Switch}
    Next
  Return

  LoadUpFile
    BFBStopModule{}
    BFBFreeModule{}

    succ=BFBLoadModule{NewFile$}
    If succ<>0
      Gosub GeneralModErr
      Gosub NoModule
    Else
      Gosub SetupDisplay
    EndIf

    succ.b=BFBPlayModule{}
    If succ<>0
      Gosub GeneralModErr
    EndIf
    Gosub SystemCheck
  Return

  ; ################################################[ End Sub-Routines ]##



