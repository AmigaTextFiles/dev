
; SDS TCP functions by Lorence Lombardo.
; Version 1.2  (C) 2009   26/1/09

; single socket version

; eoff = End of File flag


; like OpenFile and Open_
Function.b ConnectTCP{host$, port.w, blok.b}
   ret.b=-1
   If TCPOpen
      If TCPCreateSocket(0, blok, #TCP_SOCK_Read|#TCP_SOCK_Write, 10, 0) = #TCP_SOCK_Ok
         If TCPConnectSocket(0,host$,port) = #TCP_CONN_CONNECTING
            Repeat
               Delay_ 1 : tcp_ev.l=TCPEvent : br=CtrlC
               If tcp_ev>0
                  revent.l=TCPIsReadEvent(0) : wevent.l=TCPIsWriteEvent(0)
                  If revent<>0 OR wevent<>0
                     If TCPSocketError(0)<>0 Then ret=0
                  EndIf
               EndIf
               If br<>0 Then tcp_ev=1 : revent=1 : ret=0
            Until tcp_ev>0 AND (revent<>0 OR wevent<>0)
         Else
            ret=0
         EndIf
      Else
         ret=0
      EndIf
   Else
      ret=0
   EndIf
   Function Return ret
End Function


; like Inkey$() , but is asynchronous instead
Function.s ReadTCP{}
   SHARED eoff.b : eoff=0 : ret$=""
   If TCPEvent>0 AND TCPIsReadEvent(0)
      If TCPSocketError(0)=0
         ret$=TCPReadSocket$(0) : If ret$="" Then eoff=-1
      Else
         eoff=-1
      EndIf
   EndIf
   Function Return ret$
End Function


; like Inkey$() and is synchronous
Function.s ReadTCPs{}
   SHARED eoff.b : eoff=0 : ret$=""
   Repeat
      If TCPEvent>0 AND TCPIsReadEvent(0)
         If TCPSocketError(0)=0
            ret$=TCPReadSocket$(0) : If ret$="" Then eoff=-1
         Else
            eoff=-1
         EndIf
      Else
         Delay_ 5 ; <----------------------+ this is only 1/10th adjust to your liking
      EndIf                                ; This I found used very little CPU & the
   Until ret$<>"" OR eoff                  ; time difference was insignificant when
   Function Return ret$                    ; compared with 0 delay and 100% CPU usage.
End Function


; like NPrint
Statement WriteTCP{tex$}
   TCPPrint 0, tex$+Chr$(10)
End Statement


; like CloseFile() and Close_
Statement CloseTCP{}
   TCPRemoveSocket 0 : TCPClose
End Statement


; End of functions.

; Functions demo begins here.

; Lame TCP example by Lombi :)
; This is just a lame example of using TCP fuctions.
; Reading my regional weather forcast details.
; Pretending to be a browser
; Use from CLI

l$=Chr$(10): cr$=Chr$(13): NPrint l$+" Lombi's lame TCP demo :)"

switch.b=On  ; <- switch for blocking mode. *****************************************

mode$="on" : If switch=Off Then mode$="off"

NPrint l$+" Testing with blocking mode "+mode$+"."

Print l$+" Attempting connection to Bureau of Meteorology..."


If ConnectTCP{"www.bom.gov.au",80,switch}

   NPrint " ok..!!"+l$+l$+" Reading forcast..."+l$+l$

   WriteTCP{"GET /cgi-bin/wrap_fwo.pl?IDV10450.txt HTTP/1.0"+cr$}
   WriteTCP{"User-Agent: Mozilla/6.0; (Spoofed by Lombi's TCP example)"+cr$}
   WriteTCP{"Accept: */*;q=1"+cr$}
   WriteTCP{"Host: www.bom.gov.au"+cr$}
   WriteTCP{"Pragma: no-cache"+cr$}
   WriteTCP{"Referer: http://www.bom.gov.au/cgi-bin/wrap_fwo.pl?IDV10450.txt"+cr$}
   WriteTCP{cr$}

   args$=""
   Repeat : rea$=ReadTCPs{} : args$=args$+rea$ : br=CtrlC
   Until Len(args$)>20 OR br<>0 OR eoff

   If br<>0 Then CloseTCP{}: NPrint " User Abort.1"+args$: End
   If eoff Then CloseTCP{}: NPrint " Premature EOF...!!!": End
   x=Instr(args$," 200 OK")
   If x=0 Then CloseTCP{}: NPrint args$+l$+"Unable to read weather details.": End

   Print args$: x=0
   Repeat
      rea$=ReadTCPs{} : Print rea$ : br=CtrlC
   Until eoff OR br<>0

   If br<>0 Then NPrint l$+" User Abort." Else NPrint l$+l$+" Read complete."
   CloseTCP{}
Else
   NPrint l$+" Unable to connect."
EndIf
End




