; (EF)BoopsiDoor
; (c) 1992/93, Stefan Stuntz
; Blitz Basic 2 version (c) 1997/98 by Vivid Imagination

WBStartup

;Definitions from gadgets/colorwheel.h
#WHEEL_Dummy=(#TAG_USER+$04000000)
#WHEEL_Hue=(#WHEEL_Dummy+1)
#WHEEL_Saturation=(#WHEEL_Dummy+2)
#WHEEL_Screen=(#WHEEL_Dummy+9)

Macro InfoGauge ;obj#
  MUIAddTags `1,#MUIA_Frame,#MUIV_Frame_Gauge
  MUIAddTags `1,#MUIA_Background,#MUII_BACKGROUND
  MUIAddTags `1,#MUIA_Gauge_Max,16384
  MUIAddTags `1,#MUIA_Gauge_Divide,262144
  MUIAddTags `1,#MUIA_Gauge_Horiz,1
  MUINewObject `1,"Gauge.mui",0
End Macro

*ColorWheelBase.Library=OpenLibrary_("gadgets/colorwheel.gadget",0)
If *ColorWheelBase=0 Then End

MUIApplicationTitle        "BoopsiDoor"
MUIApplicationVersion      "$VER: BoopsiDoor 19.5 (12.02.97)"
MUIApplicationCopyright    "(c)1992/93, Stefan Stuntz"
MUIApplicationAuthor       "Stefan Stuntz"
MUIApplicationDescription  "Show a boopsi colorwheel with MUI."
MUIApplicationBase         "BOOPSIDOOR"

  MUISetMaximumTags 40

  MUIAddTags 0,#MUIA_Group_Columns,2

  MUILabel 1,"Hue:",0

  !InfoGauge{2}

  MUILabel 3,"Saturation:",0

  !InfoGauge{4}

  MUIAddTags 5,#MUIA_Weight,0
  MUINewObject 5,"Rectangle.mui",0,0

  MUINewObject 6,"Scale.mui",0,0

MUIAddObjsVGroup 0,1,2,3,4,5,6
MUICreateVGroup 0
MUIAddObjVGroup 10,0

MUIAddTags 7,#MUIA_Frame,#MUIV_Frame_Group
MUIAddTags 7,#MUIA_Boopsi_ClassID,Null("colorwheel.gadget")
MUIAddTags 7,#MUIA_Boopsi_MinWidth,30
MUIAddTags 7,#MUIA_Boopsi_MinHeight,30
MUIAddTags 7,#MUIA_Boopsi_Remember,#WHEEL_Saturation
MUIAddTags 7,#MUIA_Boopsi_Remember,#WHEEL_Hue
MUIAddTags 7,#MUIA_Boopsi_TagScreen,#WHEEL_Screen
MUIAddTags 7,#WHEEL_Screen,0
MUIAddTags 7,#GA_Left,0,#GA_Top,0,#GA_Width,0,#GA_Height,0
MUIAddTags 7,#ICA_TARGET,#ICTARGET_IDCMP
MUIAddTags 7,#WHEEL_Saturation,0
MUIAddTags 7,#MUIA_FillArea,1
MUINewObject 7,"Boopsi.mui",0
MUIAddObjVGroup 10,7

MUICreateVGroup 10

MUICreateWindow 8,"BoopsiDoor","BOOP",10

MUIAddSubWindow 8

If MUICreateApplication<>True
  CloseLibrary_ *ColorWheelBase
  End
EndIf

MUINotifyApp 8,#MUIA_Window_CloseRequest,1,-1

MUIOpenWindow 8

h.l=0:s.l=0

Repeat
  VWait
  ev.l=MUIEvent
  h=MUIGet(7,#WHEEL_Hue)              ;It is different
  MUISet 2,#MUIA_Gauge_Current,h,0    ;from the
  s=MUIGet(7,#WHEEL_Saturation)       ;C source
  MUISet 4,#MUIA_Gauge_Current,s,0    ;but it works.
Until ev=-1

MUICloseWindow 8

CloseLibrary_ *ColorWheelBase

End
