
; "zlibexample.ab2" a D to Blitz adaptation by Lorence Lombardo,
; with some additional checking added.

; Based on Michel 'DMX' Bagmeijer's "zlibexample.d" example,
; from here:- http://aminet.net/package/util/pack/zlibexample

; Just type "zlibexample.exe" for a usage explanation.


l$=Chr$(10) : np.b=NumPars

If np<1
   NPrint l$+" Usage:-"+l$+l$+" zlibexample <SRC> [DEST] [D]"+l$
   NPrint " D=DECOMPRESS, <SRC> only for a check."+l$ : End
EndIf


src$=Par$(1) : dest$="" : opt$="" : If np>1 Then dest$=Par$(2)
If np>2 Then opt$=UCase$(Par$(3))


; file size function

Function.l fsz{fyl$}
   fyl.l=Open_(&fyl$, #MODE_OLDFILE): sz.l=0
   If fyl
      If ExamineFH_ (fyl, fib.FileInfoBlock) Then sz=fib\fib_Size
      Close_ (fyl)
   EndIf
   Function Return sz
End Function


infile.l=Open_(&src$, #MODE_OLDFILE)
If infile
   Read_ infile, &test.w, 2 : Close_ (infile) : infile=0 : sz.l=fsz{src$}
   Print l$+" Source file appears to be " : If test<>8075 Then Print "NOT "
   NPrint "Gzip'd."+l$+l$+" Source file size is ",sz," bytes."
Else
   NPrint l$+" Source file not found...!!!"+l$ : End
EndIf


INCLUDE "zlib.include.bb2"

If zlib_InitLib{3}=0 Then NPrint l$+" Could not initialize v3 zlib...!!!"+l$ : End


; (np=1)  Check file only for 1 arg

If np=1
   !zlib_GZ_Open{infile.l, &src$, #MODE_OLDFILE, 0, 0} : c.l=0 : br.b=0
   If infile
      !zlib_GZ_FileLength{srclen.l, infile}
      NPrint l$+" Unpacked file check detected = ",srclen," bytes."+l$
      buf.l=AllocVec_(16384,#MEMF_PUBLIC)
      If buf
         Repeat
            !zlib_GZ_Read{rl.l, infile, buf, 16384} : If rl>0 Then c=c+rl
            If CtrlC<>0 Then br=1
         Until rl<1 OR br=1
         If br=0
            NPrint " Unpacked file check actual = ",c," bytes."+l$
         Else
            NPrint " Unpacked file check test not complete."+l$
         EndIf
         FreeVec_ buf
      Else
         NPrint " Could not allocate RAM!"+l$
      EndIf
      !zlib_GZ_Close{bla.l, infile}
   Else
      NPrint l$+" File not found...!!!"+l$
   EndIf
EndIf


; (np=2)  Compress file for 2 args

If np=2
   infile.l=Open_(&src$, #MODE_OLDFILE)
   If infile
      If sz>0
         buf.l=AllocVec_(sz,#MEMF_PUBLIC)
         If buf
            Read_ infile, buf, sz
            !zlib_GZ_Open{outfile.l, &dest$, #MODE_NEWFILE, #GZ_STRATEGY_FILTERED, #GZ_COMPRESS_BEST}
            If outfile
               !zlib_GZ_Write{bla.l, outfile, buf, sz}
               !zlib_GZ_Close{bla.l, outfile}
               NPrint l$+" Packed File = ",fsz{dest$}," bytes."+l$
            Else
               NPrint l$+" Could not open the dest file!"+l$
            EndIf
            FreeVec_ buf
         Else
            NPrint l$+" Could not allocate RAM!"+l$
         EndIf
      Else
         NPrint l$+" Source file size is not more than zero...!!!"+l$
      EndIf
      Close_ (infile)
   Else
      NPrint l$+" File not found...!!!"+l$
   EndIf
EndIf


; (np>2 and opt$="D")  De-compress file for opt$="D"

If opt$="D"
   !zlib_GZ_Open{infile.l, &src$, #MODE_OLDFILE, 0, 0}
   If infile
      !zlib_GZ_FileLength{srclen.l, infile}
      If srclen>0
         buf.l=AllocVec_(srclen, #MEMF_PUBLIC)
         If buf
            !zlib_GZ_Read{rl.l, infile, buf, srclen}
            If rl>0
               outfile.l=Open_(&dest$, #MODE_NEWFILE)
               If outfile
                  Write_ outfile, buf, rl : Close_ (outfile)
                  NPrint l$+" Unpacked File detected = ",srclen," bytes."
                  NPrint l$+" Unpacked File actual = ",rl," bytes."+l$
               Else
                  NPrint l$+" Could not open the dest file!"+l$
               EndIf
            Else
               NPrint l$+" Could not unpack source file!"+l$
            EndIf
            FreeVec_ buf
         Else
            NPrint l$+" Could not allocate RAM!"+l$
         EndIf
      Else
         NPrint l$+" Unpacked file size is not more than zero...!!!"+l$
      EndIf
      !zlib_GZ_Close{bla.l, infile}
   Else
      NPrint l$+" File not found...!!!"+l$
   EndIf
EndIf

zlib_FreeLib{} : End


