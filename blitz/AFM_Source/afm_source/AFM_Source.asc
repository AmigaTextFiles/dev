;The full source code for AFM Version 1.00 in Blitz Basic! I  hope 
;you Blitzers enjoy this little Christmas candy...

;The  code  is copyrighted to Rui Carvalho a.k.a. gRiM. The
;compilation and distribution of the code is forbidden. I only
;authorize the use of parts of it, not the hole.

;Merry Christmas...

;Aminet FTP by Mail

a$="$VER: AFM V1.01"

WBStartup

If FindPort_("REXX")=0
 a$="REXX is NOT running!"+Chr$(10)
 a$+Chr$(10)
 a$+"Please run it..."+Chr$(10)
 Request "ERROR",a$,"DAMM"
 End
EndIf

If FindPort_("YAM")=0
 a$="YAM is NOT running!"+Chr$(10)
 a$+Chr$(10)
 a$+"Please run it..."+Chr$(10)
 Request "ERROR",a$,"DAMM"
 End
EndIf

Execute_"rx yam:rexx/AFM/scripts/get.rexx",0,0

NEWTYPE.en
  *entrie.b[4]
End NEWTYPE
DEFTYPE.en *buf

Dim *en.b(8192)
Dim *entr.b(2048)
Dim *entr2.b(2048)

*titulo=Null("New Files On Aminet")
*titulo1=Null("Selected Files")
Delay_(15)

If Exists ("t:AFM.tmp")
 If ReadFile (0,"t:AFM.tmp") Then
 c0=0
 c1=0
 count=0
Else
 a$="SORRY! Unable to find an Aminet Upload Mail!"+Chr$(10)
 Request "ERROR",a$,"DAMM"
 End
EndIf

FileInput 0

While NOT Eof(0)
 a$=Edit$("",255)
 *en(c1+2)=Null(Left$(a$,19))
 *en(c1)=Null(Mid$(a$,20,11))
 *en(c1+1)=Null(Mid$(a$,31,4))
 *en(c1+3)=Null(Mid$(a$,36))
 *entr(c0)=&*en(c1)
 c1+5
 c0+1
Wend
CloseFile 0
Execute_"delete t:AFM.tmp",0,0

MUIApplicationTitle       "Aminet FTP MAIL"
MUIApplicationVersion     "$VER: AFM 1.01"
MUIApplicationCopyright   "Rui Carvalho"
MUIApplicationAuthor      "gRiM"
MUIApplicationDescription "Get FTP by Mail from Aminet"
MUIApplicationBase        "AFM"

MUISimpleButton 1,"_Select"
MUISimpleButton 2,"_Unselect"
MUISimpleButton 4,"Send _Request"
MUISimpleButton 3,"_Quit"

MUIAddObjsHGroup 30,1,2,4,3
MUICreateHGroup 30

MUIListHook On
MUIAddTags 5,#MUIA_Frame,#MUIV_Frame_InputList
MUIList 5,"BAR,BAR,BAR,",*entr()
MUIListView 6,5

MUIAddTags 32,#MUIA_Frame,#MUIV_Frame_Group
MUIAddTags 32,#MUIA_FrameTitle,*titulo
MUIAddObjsHGroup 32,6
MUICreateHGroup 32

MUIAddTags 7,#MUIA_Frame,#MUIV_Frame_InputList
MUIList 7,"BAR,BAR,BAR,",*entr2()
MUIListView 8,7

MUIAddTags 31,#MUIA_Frame,#MUIV_Frame_Group
MUIAddTags 31,#MUIA_FrameTitle,*titulo1
MUIAddObjsHGroup 31,8
MUICreateHGroup 31


MUIAddObjsVGroup 33,31,32,30
MUICreateVGroup 33

MUICreateWindow 10,"AFM","AFM",33
MUIAddSubWindow 10
MUIAddTags 10,#MUIA_Window_Width,300

If MUICreateApplication<>True Then End

MUINotifyApp 1,#MUIA_Pressed,0,1
MUINotifyApp 1,#MUIM_DragDrop,0,1
MUINotifyApp 2,#MUIA_Pressed,0,2
MUINotifyApp 3,#MUIA_Pressed,0,-1
MUINotifyApp 4,#MUIA_Pressed,0,3
MUINotifyApp 5,#MUIA_List_Active,#MUIV_EveryTime,5
MUINotifyApp 7,#MUIA_List_Active,#MUIV_EveryTime,7
MUINotifyApp 10,#MUIA_Window_CloseRequest,1,#MUIV_Application_ReturnID_Quit

MUIOpenWindow 10

a$="Aminet FTP by Mail"+Chr$(10)
a$+Chr$(10)
a$+"This is version 1.01"+Chr$(10)
a$+Chr$(10)
a$+Chr$(27)+"bTHIS SOFTWARE IS HITWARE"+Chr$(10)
a$+Chr$(27)+"n"+Chr$(10)
a$+"Dark Dreams Home: Http://ip.pt/"+Chr$(126)+"ip234558"+Chr$(10)
a$+"My E-Mail: Rui Carvalho <grim@ip.pt>"+Chr$(10)
a$+Chr$(10)+"Compilation date: 12.11.1998"+Chr$(10)
a$+Chr$(10)+"Made in Blitz Basic"

MUIRequest 10,0,"Dark Dreams Designs","OK",Chr$(27)+"c"+a$

Repeat

ev.l=MUIWaitEvent

Select ev
  Case 1
   MUIDoMethod 5,#MUIM_List_GetEntry,#MUIV_List_GetEntry_Active,&*buf
   MUIDoMethod 5,#MUIM_List_Remove,#MUIV_List_Remove_Selected
   MUIDoMethod 7,#MUIM_List_InsertSingle,*buf,#MUIV_List_Insert_Sorted
   MUIDoMethod 7,#MUIM_List_Redraw,#MUIV_List_Redraw_Active
   count+1

  Case 2
   MUIDoMethod 7,#MUIM_List_GetEntry,#MUIV_List_GetEntry_Active,&*buf
   MUIDoMethod 7,#MUIM_List_Remove,#MUIV_List_Remove_Selected
   MUIDoMethod 5,#MUIM_List_InsertSingle,*buf,#MUIV_List_Insert_Sorted
   MUIDoMethod 5,#MUIM_List_Redraw,#MUIV_List_Redraw_Active
   count-1

  Case 3
  If count>0
   If WriteFile (0,"T:body.tmp")
    FileOutput 0
    NPrint "size huge"
    NPrint "cd /"
    For b=0 To count-1
     MUIDoMethod 7,#MUIM_List_GetEntry,b,&*buf
     NPrint "cd pub/aminet/"+Peek$(*buf\entrie[0])
     NPrint "get "+Peek$(*buf\entrie[2])
    Next
    NPrint "quit"
    CloseFile 0
    For b=count-1 To 0 Step-1
     MUIDoMethod 7,#MUIM_List_Remove,b
    Next
    count=0
    Execute_ "rx yam:rexx/AFM/scripts/put.rexx",0,0
   EndIf
  Else
  MUIRequest 10,0,"Error!","OK",Chr$(27)+"cNo Files Selected!"
  EndIf
End Select
Until ev=-1

MUICloseWindow 10
End



