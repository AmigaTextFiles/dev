; NOTES :

; not all of this is totally necessary, just bullet-proofing it ;)

; create executable JumpTo and run it from CLI/Execute Command
; as < JumpTo pubscreenname > or go to Compiler menu/CLI Arguments...
; and type the name of the screen before running.

; IMPORTANT!!! Won't work directly from Blitz unless you enter the
; public screen name you want in the CLI Argument requester, but you
; can pass no argument once it's compiled...

; TEST - try closing the program you've invaded :)

; Examples:

; Blitz           : SuperTED1,2 etc
; Workbench       : Workbench

; oh, yeah, it's NOT case-sensitive and I made "" = default pubscreen

; I've done it the way the autodocs say :

; lock pubscreen
; open my window
; unlock pubscreen
; screen can't close until our window closes

; make sure you check it was successful before
; doing anything with your window!

pubname$=Par$(1)

If pubname$<>""                                     ; no name?
*scr.Screen=LockPubScreen_ (&pubname$)              ; lock the screen so it
                                                    ; can't get away!

Else *scr.Screen=LockPubScreen_ (0)                 ; default pubscreen
pubname$="Default"
EndIf

If *scr                                             ; did we get it?

  title$=Peek$(*scr\Title)                          ; get TITLE (not name!)
  FindScreen 0,title$                               ; FindScreen 0,<title>
  ScreenToFront_ *scr                               ; bring to front
  Window 0,0,0,500,100,$a,"JumpTo "+pubname$,1,2    ; open our window
  UnlockPubScreen_ &pubname$,0                      ; unlock screen

; now we've got our window open on the screen, it can't close till
; we say!

Else Request "","Failed to find public screen : "+pubname$,"Doh!"
EndIf ; from If *scr...

; so we do our stuff here :

If *scr

  NPrint ""
  NPrint "Excuse me, I'm looking for the ",pubname$," screen."
  NPrint ""
  NPrint "I think this is the right place... :)"

  Repeat
    VWait
  Until Event=$200

EndIf

End
