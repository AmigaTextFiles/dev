;ReadDirs
;by Curt Esser
;camge@ix.netcom.com
;Example program
;last modified feb 10 1998
;Reads all directories and subdirectories from
;the selected starting point
;and stores full pathname of all files in an array
;and directory names in another array
;use and distribute as you wish

WBStartup
NoCli
WBenchToFront_
WbToScreen 0
#Mx=1000                           ;maximum number of files to be read
Window 0,50,20,WBWidth-100,WBHeight-40,$4|$8|$1000|$400,"<- to Exit",1,0
MaxLen pa$=192                     ;needed for the path requestor
pa$="SYS:"                         ;default starting point
pa$=ASLPathRequest$ ("Select starting directory",pa$)

NPrint " Reading Directories:"
NPrint" "
NPrint "   ",pa$

.setup

ChDir pa$                          ;CD to selected path to start
Dim fil$(#Mx)                      ;for storing file names
Dim dir$(#Mx)                      ;for storing directory names
drawer$=pa$                        ;start with current directory
fils=0                             ;number of files found
dirs=0                             ;number of directories found
dirsread=0                         ;number of directories read

.read_it

Repeat                             ;reading loop

  ChDir drawer$                    ; CD to selected directory

  While MoreEntries AND fils<#Mx   ; check if there's any more entries

    If Event=$200 Then End         ; cancel program if close gadget hit
    File_Name$=EntryName$          ; get its name
    If EntryDIR                    ; check if its a directory
      dirs+1                       ;yes, so increase the directory count
      dir$(dirs)=drawer$+File_Name$+"/"
      NPrint "   ",dir$(dirs)      ;print directory names in the window
      WPrintScroll                 ;scroll the window if needed
    Else
      fil$(fils)=drawer$+File_Name$
      ;fil$(fils)=File_Name$       ;use this if you only want the file name
                                   ;and not the full path
      fils+1                       ;and update the count
    EndIf

  Wend

  If dirs>dirsread                 ;are there more directories to read?
    dirsread+1                     ;yes, so read next one
    drawer$=dir$(dirsread)         ;re-set the reading path
  Else
    doneflag=1                     ;no more, time to leave!
  EndIf

Until doneflag                     ;keep going till all sub-directories read

NPrint " "                         ;now print files to the window
WPrintScroll
NPrint " Files found:"
WPrintScroll
NPrint " "
WPrintScroll

For i = 0 To fils                  ;this prints list of files found
If Event = $200 Then End           ;stop if close gadget is hit
NPrint "   ",fil$(i)               ;print out all the files found
WPrintScroll                       ;into the window
Next

Repeat
  ev.l=WaitEvent                   ;wait till close button hit
Until ev=$200
End









