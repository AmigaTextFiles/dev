;-> RandomNumbers by Stefano Maria Regattin
;i> 17 febbraio 2022
;--------------------
JMP ProgramStart
Dc.b "$VER: RandomNubers 1.0 (17.02.2022)"
Even
ProgramStart

#MouseButtonPressed=8

#Columns=10
#Rows=20
#RNWindow=0
#WinFont=0
#WorkbenchScreen=0

Dim Number.w(#Columns*#Rows-1)

WinTitle$="Random Numbers 1.0"
ScrTitle$="Random Numbers made in Italy by Stefano Maria Regattin on Thu 17 Feb 2022"

OpenFontPrefs
 WinFont$=DefaultFontName(1)
 WinFont.b=DefaultFontHeight(1)
CloseFontPrefs
If WinFont$="" AND WinFont=0 Then Winfont$="topaz.font":WinFont=8
LoadFont #WinFont,WinFont$,WinFont
WbToScreen #WorkbenchScreen:MenusOff
WinFontWidth.b=8:Asterisks.w=(#Columns*32+(#Columns-1)*8)/WinFontWidth
ScrAddress.l=Peek.l(Addr Screen(#WorkbenchScreen))
XLenScr.w=Peek.w(ScrAddress+12):YLenScr.w=Peek.w(ScrAddress+14)
XLenWin.w=4+#Columns*WinFontWidth*4+(#Columns-1)*WinFontWidth+4
YLenWin.w=11+(#Rows+1+#Rows)*WinFont+2
XPosWin.w=(XLenScr-XLenWin)/2:YPosWin.w=(YLenScr-YLenWin)/2
Window #RNWindow,XPosWin,YPosWin,XLenWin,YLenWin,$1400,WinTitle$,-1,-1
WinFontWidth=TextLength_ (RastPort(#RNWindow)," ",1)
If WLeftOff<>4 OR WTopOff<>11 OR WinFont<>8
 XLenWin=4+#Columns*WinFontWidth*4+(#Columns-1)*WinFontWidth+4
 YLenWin=11+(#Rows+1+#Rows)*WinFont+2
 XLenWin+XLenWin-4-#Columns*WinFontWidth*4-(#Columns-1)*WinFontWidth-4
 YLenWin+YLenWin-11-(#Rows+1+#Rows)*WinFont-2
 XPosWin.w=(XLenScr-XLenWin)/2:YPosWin.w=(YLenScr-YLenWin)/2
EndIf
CloseWindow #RNWindow
Window #RNWindow,XPosWin,YPosWin,XLenWin,YLenWin,$1400,WinTitle$,-1,-1
WTitle WinTitle$,ScrTitle$
WindowFont #WinFont
WLocate 0,#Rows*WinFont
ProgramExit.b=False
For Asterisk.b=0 To Asterisks-1
 Gosub WindowEvents
 If ProgramExit=True Then Pop For:Goto ProgramExit
 Print "*":VWait
Next Asterisk
Format "000#":WLocate 0,0
RandomNumbers.w=0
For RandomNumber.w=0 To #Columns*#Rows-1
 Gosub WindowEvents
 If ProgramExit=True Then Pop For:Goto ProgramExit
 Print RandomNumber:Delay_ 20
 If RandomNumber MOD #Columns=#Columns-1
  NPrint ""
 Else
  Print " "
 EndIf
 Number(RandomNumber)=-1:RandomNumbers+1
Next RandomNumber
For RandomNumber=0 To #Columns*#Rows-1
 Gosub WindowEvents
 If ProgramExit=True Then Pop For:Goto ProgramExit
 NumberFound.w=Rnd(RandomNumbers-1)+1
 For Number.w=0 To #Columns*#Rows-1
  If Number(Number)=-1
   NumberFound-1
   If NumberFound=0
    Number(Number)=RandomNumber:RandomNumbers-1:Pop If:Pop For:Goto NumberFound
   EndIf
  EndIf
 Next Number
NumberFound
Next RandomNumber
For RandomNumber=0 To #Columns*#Rows-1
 Gosub WindowEvents
 If ProgramExit=True Then Pop For:Goto ProgramExit
 XNumber.w=RandomNumber MOD #Columns*5*WinFontWidth
 YNumber.w=RandomNumber/#Columns*WinFont+(#Rows+1)*WinFont
 WLocate XNumber,YNumber:Print Number(RandomNumber):Delay_ 20
Next RandomNumber
WaitEvent
ProgramExit
CloseWindow #RNWindow
End
WindowEvents
WindowEvent.l=Event
If EventWindow=#RNWindow AND WindowEvent=#MouseButtonPressed
 ProgramExit=True
EndIf
Return
