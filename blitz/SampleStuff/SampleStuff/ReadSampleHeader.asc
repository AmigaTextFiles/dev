;ReadSampleHeader
;by Curt Esser
;camge@ix.netcom.com
;last modified Dec 2/98
;reads the file header of an 8svx file and presents this in a window
;PD - do what you like with it...

;The first 12 bytes of an IFF file are used to distinguish between an Amiga
;picture (FORM-ILBM), an Amiga sound sample (FORM-8SVX), or other file
;conforming to the IFF specification.  The middle 4 bytes is the count of
;bytes that follow the "FORM" and byte count longwords.  (Numbers are stored
;in M68000 form, high order byte first.)

;FutureSound audio file, 15000 samples at 10.000KHz, file is 15048 bytes long.

;0000: 464F524D 00003AC0 38535658 56484452    FORM..:.8SVXVHDR
;      F O R M     15040 8 S V X  V H D R

;0010: 00000014 00003A98 00000000 00000000    ......:.........
;            20    15000        0        0

;0020: 27100100 00010000 424F4459 00003A98    '.......BODY..:.
;     10000 1 0    1.0   B O D Y     15000

;0000000..03 = "FORM", identifies this as an IFF format file.
;FORM+00..03 (ULONG) = number of bytes that follow.  (Unsigned long int.)
;FORM+03..07 = "8SVX", identifies this as an 8-bit sampled voice.

;????+00..03 = "VHDR", Voice8Header, describes the parameters for the BODY.
;VHDR+00..03 (ULONG) = number of bytes to follow.
;VHDR+04..07 (ULONG) = samples in the high octave 1-shot part.
;VHDR+08..0B (ULONG) = samples in the high octave repeat part.
;VHDR+0C..0F (ULONG) = samples per cycle in high octave (if repeating), else 0.
;VHDR+10..11 (UWORD) = samples per second.  (Unsigned 16-bit quantity.)
;VHDR+12     (UBYTE) = number of octaves of waveforms in sample.
;VHDR+13     (UBYTE) = data compression (0=none, 1=Fibonacci-delta encoding).
;VHDR+14..17 (FIXED) = volume.  (The number 65536 means 1.0 or full volume.)

;????+00..03 = "BODY", identifies the start of the audio data.
;BODY+00..03 (ULONG) = number of bytes to follow.
;BODY+04..NNNNN      = Data, signed bytes, from -128 to +127.


WBStartup
WBenchToFront_
NoCli

; grab the workbench screen and open our window on it
WbToScreen 0
winhi=400
If winhi>WBHeight Then winhi = WBHeight
Window 0,75,20,500,winhi,$1000|$400,"  8SVX File Header Info",1,2
MaxLen pa$=250   ;need these for the ASL filerequestor
MaxLen fi$=250
;====================MAIN LOOP===============================
Repeat
;-----------open an ASL requestor to select a file-----------
  fi$="Cancel to Exit"
  fil$=ASLFileRequest$ ("Select 8SVX sample",pa$,fi$,100,10,200,winhi)
  If fil$="" Then End      ;cancel selected, so quit program
  If ReadFile(0,fil$)
    FileInput 0
    a$=Inkey$(12)
    If Left$(a$,4)="FORM" AND Right$(a$,4)="8SVX"

      look$=""
      totln.l=Cvl(Mid$(a$,5,4))+8

      While look$<>"VHDR"        ;find the Voice Header chunk
        a$=Inkey$(1)
        If a$="V"
          look$=a$+Inkey$(3)
        EndIf
      Wend
      bytes.l=Cvl(Inkey$(4))     ;bytes to follow
      oneshot.l=Cvl(Inkey$(4))   ;one shot length
      horepeat.l=Cvl(Inkey$(4))  ;high octave repeat
      smpcycle.l=Cvl(Inkey$(4))  ;samples per cycle

      While look$<>"BODY"        ;find the Body chunk
        a$=Inkey$(1)
        If a$="B"
          look$=a$+Inkey$(3)
        EndIf
      Wend

      dtaln.l=Cvl(Inkey$(4))     ;bytes of actual data

      CloseFile 0
      Use Window 0

;-------- OK, show the results -----------------------------

      NPrint fil$,":"
      NPrint "Total Length = ",totln
      WPrintScroll
      NPrint "Data Bytes   = ",dtaln
      WPrintScroll
      NPrint "One shot     = ",oneshot
      WPrintScroll
      NPrint "Repeat       = ",horepeat
      WPrintScroll
      NPrint "Per cycle    = ",smpcycle
      WPrintScroll

    Else                         ;Error!  not an 8svx file

      CloseFile 0
      Use Window 0
      NPrint fil$
      WPrintScroll
      NPrint "is Not an 8SVX sample..."
      WPrintScroll
    EndIf
    NPrint "========================================="
    WPrintScroll
  EndIf
Forever

