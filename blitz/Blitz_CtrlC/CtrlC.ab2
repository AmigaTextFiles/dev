
; Ctrl-C breaking methods for Blitz  22-June-2013

; (C) 2013  Lorence Lombardo

; Works on all Blitz flavours on Amiga and is ROM 1.3 tested also.


#METH=1  ; <- CtrlC method constant should be between
         ;    1 and 4 inclusive.


; This is the leanest of the 1st 3 methods,
; with help from the OS.

CNIF #METH=1

; #SIGBREAKF_CTRL_C=4096

Function.b CtrlCK{}
   br.b=0 : If (SetSignal_(0,4096) & 4096) Then br=1
   Function Return br
End Function

CEND


; This method is slightly larger than the previous,
; and peeks into the keyboards hardware address.

CNIF #METH=2

Function.b CtrlCK{}
   SHARED br_bk.b : br.b=0 : brk.b=Peek.b($BFEC01)
   If br_bk=57 AND brk=-103 Then br=1
   br_bk=brk : Function Return br
End Function

CEND


; This method is the largest, but if you are going to
; use RawStatus in your program anyway, then this
; becomes not so large.

CNIF #METH=3

Function.b CtrlCK{}
   br.b=0 : If RawStatus($63) AND RawStatus($33) Then br=1
   Function Return br
End Function

CEND


; This method pauses the program efficiently and may be quite a
; good choice if you have a back ground thread running already.

CNIF #METH=4

Function.b CtrlCK{}
   SetSignal_ 0,4096 : Wait_ 4096 : Function Return 1
End Function

CEND


;------------------------------------------------------------------------------------

; CtrlC test begins here.


NPrint ""

Repeat
   NPrint " I am in loop method " , #METH
   Delay_ 1
Until CtrlCK{}<>0

NPrint "" : End


