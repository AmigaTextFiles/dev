
; Use this as guide to add other variable types if you wish

; long & float array combined

Function.l long_RA{sz.l}
   Function Return AllocVec_ (sz*4+4, #MEMF_PUBLIC)
End Function

Statement long_RA_add_item{ra.l, item.l, ele.l}
   Poke.l ra+(item*4), ele
End Statement

Function.l long_RA_get_item{ra.l, item.l}
   Function Return Peek.l (ra+(item*4))
End Function

Statement float_RA_add_item{ra.l, item.l, ele.f} ; <- can b FFP or IEEE depending on
   Poke.f ra+(item*4), ele                       ; optimzer setting
End Statement

Function.f float_RA_get_item{ra.l, item.l}
   Function Return Peek.f (ra+(item*4))
End Function


;----------------------------------------------------


NPrint "" : NPrint " Long & Float array combined" : NPrint ""

myRA.l=long_RA{10} : If myRA=0 Then End

For x.b=0 To 10
   long_RA_add_item{myRA, x, 40000+x}   ; <- put some stuff in the array
Next x

For x.b=0 To 10
   NPrint long_RA_get_item{myRA, x}     ; <- retrieve the stuff
Next x

NPrint ""

; now we can do float in the same array

For x.b=0 To 10
   float_RA_add_item{myRA, x, Pi+x}
Next x

For x.b=0 To 10
   NPrint float_RA_get_item{myRA, x}
Next x

FreeVec_ myRA  ; <- Not required but good practice.
               ; If you use AllocMem_ instead of AllocVec_ for ROM 1.3
               ; then FreeMem_ will be required & u might want to wrap it
               ; in a statement. ie. FreeMem_ myRA, sz*4+4 ; <- just as
               ; it was allocated.


;----------------------------------------------------


word_RA_lab:

Function.l word_RA{sz.l}
   Function Return AllocVec_ (sz*2+2, #MEMF_PUBLIC)
End Function

Statement word_RA_add_item{ra.l, item.l, ele.w}
   Poke.w ra+(item*2), ele
End Statement

Function.w word_RA_get_item{ra.l, item.l}
   Function Return Peek.w (ra+(item*2))
End Function


;----------------------------------------------------


NPrint "" : NPrint "" : NPrint " Word array" : NPrint ""

myRA.l=word_RA{10} : If myRA=0 Then End

For x.b=0 To 10
   word_RA_add_item{myRA, x, 200+x}
Next x

For x.b=0 To 10
   NPrint word_RA_get_item{myRA, x}
Next x

FreeVec_ myRA


;----------------------------------------------------


byte_RA_lab:

Function.l byte_RA{sz.l}
   Function Return AllocVec_ (sz+1, #MEMF_PUBLIC)
End Function

Statement byte_RA_add_item{ra.l, item.l, ele.b}
   Poke.b ra+item, ele
End Statement

Function.b byte_RA_get_item{ra.l, item.l}
   Function Return Peek.b (ra+item)
End Function


;----------------------------------------------------


NPrint "" : NPrint "" : NPrint " Byte array" : NPrint ""

myRA.l=byte_RA{10} : If myRA=0 Then End

For x.b=0 To 10
   byte_RA_add_item{myRA, x, x+1}
Next x

For x.b=0 To 10
   NPrint byte_RA_get_item{myRA, x}
Next x

FreeVec_ myRA : NPrint "" : End


