
; 80bit SANE Normalized and numeric functions v1.5c

; classic version

; See "www.mactech.com/articles/mactech/Vol.06/06.01/SANENormalized"
; for related interest.



; Begin numeric functions.




; The calcualtion of the following function was based on some PHP sources
; from:- http://getid3.sourceforge.net or www.getid3.org
; It probably looks nothing like it any more. :)

Function.f sane2float {sane$}
   sbin$ = Bin$(Peek.l(&sane$)) + Bin$(Peek.l(&sane$+4)) + Right$(Bin$(Peek.l(&sane$+6)),16)
   expon.l = Val( "%"+Mid$(sbin$, 2, 15) )
   norm.b = Val(Mid$(sbin$,17,1)) : sh.l=2^21 ; shift
   F1.l=Val( "%"+Mid$(sbin$, 18, 21) )
   F2.l=Val( "%"+Mid$(sbin$, 39, 21) )
   F3.l=Val( "%"+Right$(sbin$, 21) )
   sign.l=1 : If Left$(sbin$, 1)="1" Then sign = -1
   float.f = (2^(expon-16383)) * (((F1*(sh^2))+(F2*sh)+F3)/(2^63)+norm) * sign
   Function Return float
End Function


; The following function should not be used with FPU.

; returns an 80bit SANE string from an Amiga FFP float value
Function.s float2sane {ffp.f}
   sbin$=Bin$( Peek.l(&ffp) ) : sign$ = Mid$(sbin$,25,1)
   exponent.l = Val( "%"+Right$(sbin$, 7) ) + 16318  ;  16383 - 64 = 16319
   exponent$ = Right$(Bin$(exponent),15)
   fraction.l = Val( "%"+Left$(sbin$, 24) ) - 8388608
   fraction_bin$ = Right$(Bin$(fraction),23) + String$("0",8)
   V1.w=Val( "%"+sign$+exponent$ )
   V2.w=Val( "%1"+Left$(fraction_bin$,15) )
   V3.w=Val( "%"+Right$(fraction_bin$,16) )
   Function Return Mki$(V1)+Mki$(V2)+Mki$(V3)+Mkl$(0)
End Function


; End of numeric functions.


; Here is a demo of using the "sane2float{}" function required for reading
; the frequencies of AIFF files.
; Usage:- aif_info <AIFF/AIFC_file>

fre.l=0
If ReadFile(0,Par$(1))
   FileInput 0: in$=Inkey$(100): CloseFile 0: l$=Chr$(10)
   smp$=Mid$(in$,9,4): xp=Instr(in$,"COMM")
   chan=Cvi(Mid$(in$,xp+8,2)): bit=Cvi(Mid$(in$,xp+14,2))
   fre.l = sane2float {Mid$(in$,xp+16,10)}
   NPrint l$+" "+smp$+" ",bit,"bit ",fre,"Hz chan=",chan,l$
EndIf

If fre>0
   fre$=float2sane {fre}: NPrint Len(fre$)
   NPrint sane2float {fre$}, l$
EndIf


; See "http://developer.apple.com/documentation/mac/Sound/Sound-61.html" for AIFF-C
; file information.

; NB: In "Figure 2-7 A sample AIFF-C file", I think the byte size of the sample rate
;     should be 10 bytes rather than 8 bytes.  :)


