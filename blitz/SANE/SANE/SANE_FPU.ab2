
; 80bit SANE Normalized and numeric functions v1.3b

; FPU only version

; See "www.mactech.com/articles/mactech/Vol.06/06.01/SANENormalized"
; & "www.psc.edu/general/software/packages/ieee/ieee.html"
; for related interest.


optimize 3  ; turn on FPU + integer optimizer


; The following functions were based on information which was obtained from
; this site:-  www.codeproject.com/csharp/longdouble.asp

; Support for de-normalized numbers has not been made available as I dont see
; the point, since I am not able to produce a 64bit de-normalized number with
; Blitz. However if you think I should support de-normalized numbers and you
; have a good argument then I am prepared to listen. :)


Function.d sane2float64 {sane$}
   sbin$ = Bin$(Peek.l(&sane$)) + Bin$(Peek.l(&sane$+4)) + Right$(Bin$(Peek.l(&sane$+6)),16)
   sign$ = Left$(sbin$,1)
   exponent.l = Vallong( "%"+Mid$(sbin$, 2, 15) ) - 15360  ; 16383 - 1023 = 15360
   exponent$ = Right$(Bin$(exponent),11) : fraction_bin$ = Mid$(sbin$, 18, 52)
   val1.l=Vallong("%"+sign$+exponent$+Left$(fraction_bin$,20))
   val2.l=Vallong("%"+Right$(fraction_bin$,32)): sane$=Mkl$(val1)+Mkl$(val2)
   Function Return Peek.d(&sane$)
End Function



; returns an 80bit SANE string from a float 64 value
Function.s float2sane {fl64.d}
   sbin$ = Bin$(Peek.l(&fl64)) + Bin$(Peek.l(&fl64+4)) : sign$ = Left$(sbin$,1)
   exponent.l = Vallong( "%"+Mid$(sbin$, 2, 11) ) + 15360
   exponent$ = Right$(Bin$(exponent),15)
   fraction_bin$ = Right$(sbin$, 52) + String$("0",11)
   val1.l=Vallong("%"+sign$+exponent$+"1"+Left$(fraction_bin$,15))
   val2.l=Vallong("%"+Mid$(fraction_bin$, 16, 32))
   val3.w=Vallong("%"+Right$(fraction_bin$, 16))
   Function Return Mkl$(val1)+Mkl$(val2)+Mki$(val3)
End Function


; End of numeric functions.


; Here is a demo of using the "sane2float{}" function required for reading
; the frequencies of AIFF files.
; Usage:- aif_info <AIFF/AIFC_file>

fre.l = 0
If ReadFile(0,Par$(1))
   FileInput 0: in$=Inkey$(100): CloseFile 0: l$=Chr$(10)
   smp$=Mid$(in$,9,4): xp=Instr(in$,"COMM")
   chan=Cvi(Mid$(in$,xp+8,2)): bit=Cvi(Mid$(in$,xp+14,2))
   fre.l = sane2float64 {Mid$(in$,xp+16,10)}
   NPrint l$+" "+smp$+" ",bit,"bit ",fre,"Hz chan=",chan,l$
EndIf


If fre>0
   fre$=float2sane {fre}: NPrint Len(fre$)
   NPrint sane2float64 {fre$}, l$
EndIf


; See "http://developer.apple.com/documentation/mac/Sound/Sound-61.html" for AIFF-C
; file information.

; NB: In "Figure 2-7 A sample AIFF-C file", I think the byte size of the sample rate
;     should be 10 bytes rather than 8 bytes.  :)


