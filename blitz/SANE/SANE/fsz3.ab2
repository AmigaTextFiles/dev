
; 9-2-2010

; This demonstrates how to obtain precise file sizes of up to 4GB.
; Using some slightly stripped down functions from "bint.ab2".
; Without requiring the use of floats or an FPU.


If NumPars=0 Then End

l$=Chr$(10): #R=1005: f$=Par$(1)

Function.s add_int0{a$,b$}
   of.b = 0 :  l.w = Max( Peek.w(&a$-2), Peek.w(&b$-2) )
   a$=RSet$(a$,l) : b$=RSet$(b$,l) : c$=""
   Repeat
      erg.l = Vallong(Right$(a$,9)) + Vallong(Right$(b$,9)) + of
      ans$=Str$(erg) : ansl.w=Peek.w(&ans$-2)
      If ansl<9 Then ans$=String$("0",9-ansl)+ans$
      If ansl>9 Then of=1 : ans$=Right$(ans$,9) Else of=0
      c$=ans$+c$ : l=l-9 : a$=Left$(a$,l) : b$=Left$(b$,l)
   Until l<1
   If of>0 Then c$=Str$(of)+c$
   c$=StripLead$(c$,48) : If c$="" Then c$="0"
   Function Return c$
End Function

Function.s mul_int{a$,b$}
   c$="0" : If a$="" OR b$="" Then a$="0"
   If a$<>"0" AND b$<>"0"
      l1.w=Peek.w(&a$-2) : l2.w=Peek.w(&b$-2) : If s1$<>s2$ Then sign$="-"
      If l2>l1 Then Exchange a$,b$ : Exchange l1,l2
      For x.w=l2 To 1 Step -1
         tmp$=String$("0",l2-x) : v2.b = Vallong(Mid$(b$, x, 1))
         a2$=a$ : l3.w=l1 : of.b=0
         Repeat
            v1.l = Vallong(Right$(a2$, 8)) : l3=l3-8 : a2$=Left$(a2$,l3)
            ans$ = Str$(v1 * v2 + of) : ansl.w=Peek.w(&ans$-2)
            If ansl<8 Then ans$=String$("0",8-ansl)+ans$
            If ansl>8
               of = Vallong(Left$(ans$, 1)) : ans$=Right$(ans$, 8)
            Else
               of = 0
            EndIf
            tmp$ = ans$ + tmp$
         Until l3<1
         If of>0 Then tmp$ = Str$(of) + tmp$
         c$ = add_int0{c$,tmp$}
      Next x
   EndIf
   Function Return sign$+c$
End Function


Function.s fszs{fyl$}
   fyl.l=Open_(&fyl$, #R): sz.l=0
   If fyl
      If ExamineFH_ (fyl, fib.FileInfoBlock) Then sz=fib\fib_Size
      Close_ (fyl)
   EndIf
   If sz<0
      w1.l=PeekUW(&sz) : w2.l=PeekUW(&sz+2)
      fs$=mul_int{Str$(w1),"65536"}
      fs$=add_int0{fs$,Str$(w2)}
   Else
      fs$=Str$(sz)
   EndIf
   Function Return fs$
End Function

fsz$=fszs{f$}
NPrint l$+"The "+f$+" file size is "+fsz$+" bytes."+l$



