
; Read/Convert AMIGA FFP data to IEEE v1.3 :)
; (C) 2007-2008  Lorence Lombardo.

; Requires FPU

; Diagram partially derived from trial and error. :)


;       AMIGA FFP binary diagram in LOMBI logic ;)
; MSB                                                 LSB
;   ___________________________________________________
;  |                                                   |
;  | MMMMMMMM    MMMMMMMM    MMMMMMMM    S    EEEEEEE  |
;  | 1      8    9      16  17      24  25   26     32 |
;  |___________________________________________________|
;
; M = Mantissa (fractional part) ; S = Sign ; E = Exponent
;
; MSB = Most Significant Bit ; LSB = Least Significant Bit
;
; Amiga FFP exponent has a bias of +64


optimize 3  ; switch on FPU + integer optimizer


; converts Amiga FFP data to IEEE float
Function.f ffp2ieee {ffp$}
   sbin$=Bin$( Peek.l(&ffp$) )  :  expon.l = Vallong( "%"+Right$(sbin$, 7) )
   fract.l = Vallong( "%"+Left$(sbin$, 24) )
   sign.l = 1 : If Mid$(sbin$,25,1) = "1" Then sign = -1
   float.f = ( 2^(expon-64) ) * (fract/16777216) * sign
   Function Return float
End Function


; A different way of converting Amiga FFP data to IEEE float, just for fun pun ;)
Function.f ffp2ieee2 {ffp$}
   sbin$=Bin$( Peek.l(&ffp$) ) : sign$ = Mid$(sbin$,25,1)
   expon.l = Vallong( "%"+Right$(sbin$, 7) ) + 62
   exponent$=Right$(Bin$(expon),8)
   fraction.l = Vallong( "%"+Left$(sbin$, 24) ) - 8388608
   fraction_bin$ = Right$(Bin$(fraction),23)
   ie.l=Vallong("%"+sign$+exponent$+fraction_bin$)
   Function Return Peek.f(&ie)
End Function


; returns a 32bit FFP string from a 32bit IEEE float
Function.s MkFFP {fl32.f}
   sbin$=Bin$( Peek.l(&fl32) ) : sign$ = Left$(sbin$,1)
   expon.l = Vallong( "%"+Mid$(sbin$, 2, 8) ) - 62
   exponent$=Right$(Bin$(expon),7)
   fraction.l = Vallong( "%"+Right$(sbin$, 23) ) + 8388608
   fraction_bin$ = Right$(Bin$(fraction),24)
   ffp.l=Vallong("%"+fraction_bin$+sign$+exponent$)
   Function Return Mkl$(ffp)
End Function


; End of functions

; Begin functions demo


ffp1$=Mkl$(-1431655614)  ;  FFP for 8/3,  derived with following program with no FPU :-

                         ; ffp.f = 8/3 : NPrint ffp
                         ; NPrint Peek.l(&ffp)


ffp2$=Mkl$(-920350142)   ;  FFP for 22/7, a quick pie ;)

ffp3$=Mkl$(-1431655616)  ;  FFP for 2/3

ffp4$=Mkl$(-1431655617)  ;  FFP for 1/3


l$=Chr$(10): NPrint l$+" Amiga FFP to IEEE:-"+l$

NPrint ffp2ieee {ffp1$} ," = 8/3"+l$
NPrint ffp2ieee {ffp2$} ," = 22/7"+l$
NPrint ffp2ieee {ffp3$} ," = 2/3"+l$
NPrint ffp2ieee {ffp4$} ," = 1/3"+l$


NPrint l$+" MkFFP test:-"+l$

ffp5$=MkFFP{22/7}
NPrint ffp2ieee {ffp5$} ," = ~PIE"+l$
NPrint ffp2ieee2 {ffp5$} ," = ~PIE2"+l$


