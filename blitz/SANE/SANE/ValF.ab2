
; ValF{} v1.2  (C) 2008  Lorence Lombardo
; This is an alternative to the "Val" command with some advantages.

; ValF{} works with or without FPU.
; Similar to the original "Val" command now supporting hex and binary
; fractions.

; Known issues:-
; When ValF{} is in big number mode (ie. more than 9 didgits for decimal
; integer) invalid characters will be treated as zero's. Comma's and
; spaces are not an issue since they are stripped.


; optimize 3   ; If you have an FPU and greater precission is required
               ; use "optimize 3" and change the ".f" type to a ".d" type.


Function.f ValF {valf$}
   fract.f=0 : fl.f=0 : valt.f=0
   chlen.l=9 : c.l=0 : base.l=10 : sign.l=1
   valf$=Replace$(valf$,",","") : valf$=Replace$(valf$," ","")
   sign$=Left$(valf$,1) : If sign$="-" Then sign= -1
   If sign$="-" OR sign$="+" Then valf$=Mid$(valf$,2)
   xp.l=Instr(valf$,".") : ntype$=Left$(valf$,1) : intlen.l=Len(valf$)
   If ntype$<>"%" AND ntype$<>"$" Then ntype$=""
   If ntype$="%" Then chlen.l=31 : base=2
   If ntype$="$" Then chlen.l=8 : base=16
   If xp>0
      fraclen.l=chlen : If ntype$<>"" Then fraclen=chlen-1
      fract$=Mid$(valf$,xp+1,fraclen) : frac1.l=Vallong( ntype$+fract$ )
      If ntype$="" Then fract2$=Str$(frac1)
      If ntype$="$" Then fract2$=StripLead$( Hex$(frac1),48 )
      If ntype$="%" Then fract2$=StripLead$( Bin$(frac1),48 )
      fraclen = Len(fract2$) + Instr(fract$,fract2$) - 1
      fract = frac1 / Vallong( ntype$+"1"+String$("0",fraclen) )
      intlen=xp-1 : valf$=Left$(valf$, intlen)
   EndIf
   If intlen>chlen
      If ntype$<>"" Then valf$=Mid$(valf$,2) : chlen-1
      Repeat
         fl = Vallong( ntype$+Right$(valf$, chlen) ) * ((base^chlen)^c) + fl
         valf$ = Left$( valf$, Len(valf$)-chlen ) : c+1
      Until valf$=""
      valt=(fl+fract)*sign
   Else
      valt=(Vallong(valf$)+fract)*sign
   EndIf
   Function Return valt
End Function


; ValF{} demo starts here. ;)

FloatMode -1

NPrint ValF {".0033333333"}

NPrint ValF {"1.000222222"}

NPrint ValF {"4.0000999999999999"}

NPrint ValF {"3.0000088888"}

NPrint ValF {"1.999999999999999999999"}

NPrint ValF {"1.3333333333"}

NPrint ValF {"2.66666666666666"}

NPrint ValF {"0.66666"}

NPrint ValF {"bla.75"}

NPrint ValF {"$7A.C"}

NPrint ValF {"%101.11"}

NPrint ValF {"    $6A.5"}

NPrint ValF {"4 300 000 000"}

NPrint ValF {"20,000,000,000"}

NPrint ValF {"990,000,000,000,000,000"}

NPrint ValF {"20,000,000 jjj 000"}  ; The "j's" will be treated as a zero's here.

NPrint ValF {"-99,000 000,000 000"}

NPrint ""

NPrint " Big binary test:-"

bla$=String$("1",48)
NPrint bla$
NPrint ValF {"%"+bla$}

NPrint " Big HEX test:-"
bla$=String$("F",12)
NPrint bla$
NPrint ValF {"$"+bla$}


