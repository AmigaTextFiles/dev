
; Read/Convert IEEE data to Amiga FFP, without an FPU
; v1.5a
; (C) 2007-2008  Lorence Lombardo.

; See "http://en.wikipedia.org/wiki/IEEE_754" for IEEE details

; Keep in mind that its an Intel point of view.
; So you need swap 0 with 31 in the diagrams,
; since Motorla is MSB to LSB, and Intel is LSB to MSB.
; However other than the decimal numbers the diagrams are infact Motorla MSB to LSB.
; Motorla MSB to LSB seems to be an easier way of explaining how things work
; for Big-endian and Little-endian machines.

; I presonally prefer to start from 1 rather than 0 in this case. :)
; So that the 32nd bit is infact 32 rather than 31. :)


; Also see "www.psc.edu/general/software/packages/ieee/ieee.html" for extra info.

; I have not considered de-normalized numbers since I am unable produce these in
; Blitz on my current computer system at the moment.


optimize 1  ; switch on integer optimizer


; converts IEEE data to Amiga FFP
Function.f ieee2ffp {ieee$}
   sbin$=Bin$( Peek.l(&ieee$) )
   expon.l = Vallong( "%"+Mid$(sbin$, 2, 8) )
   fract.l = Vallong( "%"+Right$(sbin$, 23) )
   sign.l=1 : If Left$(sbin$, 1)="1" Then sign = -1
   float.f = ( 2^(expon-127) ) * (fract/8388608+1) * sign
   Function Return float
End Function


; A different way of converting IEEE data to Amiga FFP, just for fun pun ;)
Function.f ieee2ffp2 {ieee$}
   sbin$=Bin$( Peek.l(&ieee$) )  :  sign$ = Left$(sbin$, 1)
   expon.l = Vallong( "%"+Mid$(sbin$, 2, 8) ) - 62
   exponent$=Right$(Bin$(expon),7)
   fraction.l = Vallong( "%"+Right$(sbin$, 23) ) + 8388608
   fraction_bin$ = Right$(Bin$(fraction),24)
   ffp.l = Vallong( "%"+fraction_bin$+sign$+exponent$ )
   Function Return Peek.f(&ffp)
End Function


; converts double IEEE data to Amiga FFP
Function.f double2ffp {ieed$}
   sbin$ = Bin$( Peek.l(&ieed$) ) + Bin$( Peek.l(&ieed$+4) )
   expon.l = Vallong( "%"+Mid$(sbin$, 2, 11) )
   frac1.l = Vallong( "%"+Mid$(sbin$, 13,26) )
   frac2.l = Vallong( "%"+Right$(sbin$, 26) )
   sign.l=1 : If Left$(sbin$, 1)="1" Then sign = -1
   float.f = (2^(expon-1023)) * ((frac1*67108864+frac2)/(2^52)+1) * sign
   Function Return float
End Function


; returns a 32bit IEEE float string from an Amiga FFP float value
Function.s MkIEEE {ffp.f}
   sbin$=Bin$( Peek.l(&ffp) ) : sign$ = Mid$(sbin$,25,1)
   expon.l = Vallong( "%"+Right$(sbin$, 7) ) + 62
   exponent$=Right$(Bin$(expon),8)
   fraction.l = Vallong( "%"+Left$(sbin$, 24) ) - 8388608
   fraction_bin$ = Right$(Bin$(fraction),23)
   ie.l = Vallong( "%"+sign$+exponent$+fraction_bin$ )
   Function Return Mkl$(ie)
End Function


; returns a 64bit IEEE float string from an Amiga FFP float value
Function.s MkIEED {ffp.f}
   sbin$=Bin$( Peek.l(&ffp) ) : sign$ = Mid$(sbin$,25,1)
   exponent.l = Vallong( "%"+Right$(sbin$, 7) ) + 958  ;  1023 - 64 = 959
   exponent$ = Right$(Bin$(exponent),11)
   fraction.l = Vallong( "%"+Left$(sbin$, 24) ) - 8388608
   fraction_bin$ = Right$(Bin$(fraction),23) + String$("0",29)
   val1.l=Vallong("%"+sign$+exponent$+Left$(fraction_bin$,20))
   val2.l=Vallong("%"+Right$(fraction_bin$, 32))
   Function Return Mkl$(val1)+Mkl$(val2)
End Function


; End of functions

; Begin functions demo


; Single float demo stuff

l$=Chr$(10): NPrint l$+" Single float to FFP:-"+l$

ieee1$=Mkl$(1076538027)  ;  IEEE for 8/3,  derived with following program with FPU :-

                         ; optimize 2
                         ; ieee.f = 8/3 : NPrint ieee
                         ; NPrint Peek.l(&ieee)

ieee2$=Mkl$(1078535314)  ;  IEEE for 22/7, a quick pie ;)

ieee3$=Mkl$(1059760811)  ;  IEEE for 2/3

ieee4$=Mkl$(1051372203)  ;  IEEE for 1/3


NPrint ieee2ffp {ieee1$} ," = 8/3"+l$
NPrint ieee2ffp {ieee2$} ," = 22/7"+l$
NPrint ieee2ffp {ieee3$} ," = 2/3"+l$
NPrint ieee2ffp {ieee4$} ," = 1/3"+l$



; Double float demo stuff

NPrint l$+" Double float to FFP:-"+l$

ieed1$=Mkl$(1074091349)+Mkl$(1431655765)  ;  double IEEE for 8/3
                                          ;  derived with following program with FPU :-

                                          ; optimize 2
                                          ; ieed.d = 8/3 : NPrint ieed
                                          ; NPrint Peek.l(&ieed) : NPrint Peek.l(&ieed+4)


ieed2$=Mkl$(1074341010)+Mkl$(1227133513)  ;  double IEEE for 22/7, a quick pie ;)

ieed3$=Mkl$(1071994197)+Mkl$(1431655765)  ;  double IEEE for 2/3

ieed4$=Mkl$(1070945621)+Mkl$(1431655765)  ;  double IEEE for 1/3


NPrint double2ffp {ieed1$} ," = 8/3"+l$
NPrint double2ffp {ieed2$} ," = 22/7"+l$
NPrint double2ffp {ieed3$} ," = 2/3"+l$
NPrint double2ffp {ieed4$} ," = 1/3"+l$


NPrint l$+" MkIEEE test:-"+l$

ieee5$=MkIEEE{22/7}
NPrint ieee2ffp {ieee5$} ," = ~PIE"+l$
NPrint ieee2ffp2 {ieee5$} ," = ~PIE2"+l$


NPrint l$+" MkIEED test:-"+l$

ieed5$=MkIEED{1/3}
NPrint double2ffp {ieed5$} ," = 1/3"+l$


