
; Read/Convert IEEE data to Amiga FFP, without an FPU
; v1.0
; (C) 2007  Lorence Lombardo.

; See "http://en.wikipedia.org/wiki/IEEE_754" for IEEE details

; Keep in mind that its an Intel point of view.
; So you need swap 0 with 31 in the diagrams,
; since Motorla is MSB to LSB, and Intel is LSB to MSB.
; However other than the decimal numbers the diagrams are infact Motorla MSB to LSB.
; Motorla MSB to LSB seems to be an easier way of explaining how things work
; for Big-endian and Little-endian machines.

; I presonally prefer to start from 1 rather than 0 in this case. :)
; So that the 32nd bit is infact 32 rather than 31. :)


; Also see "www.psc.edu/general/software/packages/ieee/ieee.html" for extra info.

; I have not considered de-normalized numbers since I am unable produce these in
; Blitz on my current computer system at the moment.


;optimize 2


SHARED exp0.l, exponent.f, frac0.l, fract.f

; converts a character string to a binary string
Function.s chr2bin {char$}
   bins$=""
   For x.l=1 To Len(char$)
      bins$ = bins$ + Right$(Bin$(Asc(Mid$(char$,x,1))),8)
   Next x
   Function Return bins$
End Function


; converts IEEE data to Amiga FFP
Function.f ieee2ffp {ieee$}
   sbin$=chr2bin{ieee$}  :  sign$ = Left$(sbin$, 1)
   exp0=Val( "%"+Mid$(sbin$, 2, 8) )
   exponent.f = 2 ^ ( Val( "%"+Mid$(sbin$, 2, 8) ) - 127 )
   fraction_bin$ = Right$(sbin$, 23): frac0=Val( "%"+fraction_bin$ )
   fract.f = Val( "%"+fraction_bin$ ) / Val("%1"+String$("0",Len(fraction_bin$)))
   float.f = exponent * (1+fract) :  If sign$="1" Then float=float * -1
   Function Return float
End Function




; End of functions


; Single float demo stuff

l$=Chr$(10): NPrint l$+" Single float to FFP:-"+l$

ieee1$=Mkl$(1076538027)  ;  IEEE for 8/3,  derived with following program with FPU :-

                         ; optimize 2
                         ; ieee.f = 8/3 : NPrint ieee
                         ; NPrint Peek.l(&ieee)

ieee2$=Mkl$(1078535314)  ;  IEEE for 22/7, a quick pie ;)

ieee3$=Mkl$(1059760811)  ;  IEEE for 2/3

ieee4$=Mkl$(1051372203)  ;  IEEE for 1/3


NPrint ieee2ffp {ieee1$} ," = 8/3; exp1=",exp0," exp2=",exponent," frac1=",frac0," frac2=",fract,l$
NPrint ieee2ffp {ieee2$} ," = 22/7; exp1=",exp0," exp2=",exponent," frac1=",frac0," frac2=",fract,l$
NPrint ieee2ffp {ieee3$} ," = 2/3; exp1=",exp0," exp2=",exponent," frac1=",frac0," frac2=",fract,l$
NPrint ieee2ffp {ieee4$} ," = 1/3; exp1=",exp0," exp2=",exponent," frac1=",frac0," frac2=",fract,l$
