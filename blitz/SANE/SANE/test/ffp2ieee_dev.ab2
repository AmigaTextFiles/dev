
; Read/Convert AMIGA FFP data to IEEE v1.0 :)
; (C) 2007  Lorence Lombardo.

; Requires FPU

; Diagram partially derived from trial and error. :)


;       AMIGA FFP binary diagram in LOMBI logic ;)
; MSB                                                 LSB
;   ___________________________________________________
;  |                                                   |
;  | MMMMMMMM    MMMMMMMM    MMMMMMMM    S    EEEEEEE  |
;  | 1      8    9      16  17      24  25   26     32 |
;  |___________________________________________________|
;
; M = Mantissa (fractional part) ; S = Sign ; E = Exponent
;
; MSB = Most Significant Bit ; LSB = Least Significant Bit
;
; Amiga FFP exponent has a bias of +64


optimize 2  ; switch on FPU

SHARED exp0.l, exponent.f, frac0.l, fract.f

; converts a character string to a binary string
Function.s chr2bin {char$}
   bins$=""
   For x.l=1 To Len(char$)
      bins$ = bins$ + Right$(Bin$(Asc(Mid$(char$,x,1))),8)
   Next x
   Function Return bins$
End Function


; converts Amiga FFP data to IEEE float
Function.f ffp2ieee {ffp$}
   sbin$=chr2bin{ffp$}  :  sign$ = Mid$(sbin$,25,1)
   exp0=Val( "%"+Right$(sbin$, 7) )
   exponent.f = 2 ^ ( Val( "%"+Right$(sbin$, 7) ) -64 )
   fraction_bin$ = Left$(sbin$, 24): frac0=Val( "%"+fraction_bin$ )
   fract.f = Val( "%"+fraction_bin$ ) / Val("%1"+String$("0",Len(fraction_bin$)))
   float.f = exponent * fract :  If sign$="1" Then float=float * -1
   Function Return float
End Function


ffp1$=Mkl$(-1431655614)  ;  FFP for 8/3,  derived with following program with no FPU :-

                         ; ffp.f = 8/3 : NPrint ffp
                         ; NPrint Peek.l(&ffp)


ffp2$=Mkl$(-920350142)   ;  FFP for 22/7, a quick pie ;)

ffp3$=Mkl$(-1431655616)  ;  FFP for 2/3

ffp4$=Mkl$(-1431655617)  ;  FFP for 1/3


l$=Chr$(10): NPrint l$+" Amiga FFP to IEEE:-"+l$

NPrint ffp2ieee {ffp1$} ," = 8/3; exp1=",exp0," exp2=",exponent," frac1=",frac0," frac2=",fract,l$
NPrint ffp2ieee {ffp2$} ," = 22/7; exp1=",exp0," exp2=",exponent," frac1=",frac0," frac2=",fract,l$
NPrint ffp2ieee {ffp3$} ," = 2/3; exp1=",exp0," exp2=",exponent," frac1=",frac0," frac2=",fract,l$
NPrint ffp2ieee {ffp4$} ," = 1/3; exp1=",exp0," exp2=",exponent," frac1=",frac0," frac2=",fract,l$




