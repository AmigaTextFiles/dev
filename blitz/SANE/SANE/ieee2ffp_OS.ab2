
; Read/Convert IEEE data to Amiga FFP, without an FPU
; v1.7
; (C) 2007-2008  Lorence Lombardo.

; OS version

; optimize 1  ; switch on integer optimizer if you want


; converts IEEE data to Amiga FFP
Function.f ieee2ffp {ieee$}
   ffpl.l = SPFieee_( Peek.l(&ieee$) )
   Function Return Peek.f(&ffpl)
End Function


; converts double IEEE data to Amiga FFP
Function.f double2ffp {ieed$}
   ieee.l = IEEEDPTieee_ ( Peek.l(&ieed$) , Peek.l(&ieed$+4) )
   ffpl.l = SPFieee_(ieee)
   Function Return Peek.f(&ffpl)
End Function


; returns a 32bit IEEE float string from an Amiga FFP float value
Function.s MkIEEE {ffp.f}
   ieee.l = SPTieee_( Peek.l(&ffp) )
   Function Return Mkl$(ieee)
End Function


; returns a 64bit IEEE float string from an Amiga FFP float value
; I use a Macro instead of a Function because I could not get PutReg
; to work in a Function.  NB: Macro's use global variables.
Macro MkIEED
   ffp.f=`1 : ieee.l = SPTieee_( Peek.l(&ffp) )
   ie1.l = IEEEDPFieee_ ( ieee ) : PutReg d1, ie2.l
   buf$ = Mkl$(ie1) + Mkl$(ie2)
End Macro


; End of functions

; Begin functions demo


; Single float demo stuff

l$=Chr$(10): NPrint l$+" Single float to FFP:-"+l$

ieee1$=Mkl$(1076538027)  ;  IEEE for 8/3,  derived with following program with FPU :-

                         ; optimize 2
                         ; ieee.f = 8/3 : NPrint ieee
                         ; NPrint Peek.l(&ieee)

ieee2$=Mkl$(1078535314)  ;  IEEE for 22/7, a quick pie ;)

ieee3$=Mkl$(1059760811)  ;  IEEE for 2/3

ieee4$=Mkl$(1051372203)  ;  IEEE for 1/3


NPrint ieee2ffp {ieee1$} ," = 8/3"+l$
NPrint ieee2ffp {ieee2$} ," = 22/7"+l$
NPrint ieee2ffp {ieee3$} ," = 2/3"+l$
NPrint ieee2ffp {ieee4$} ," = 1/3"+l$



; Double float demo stuff

NPrint l$+" Double float to FFP:-"+l$

ieed1$=Mkl$(1074091349)+Mkl$(1431655765)  ;  double IEEE for 8/3
                                          ;  derived with following program with FPU :-

                                          ; optimize 2
                                          ; ieed.d = 8/3 : NPrint ieed
                                          ; NPrint Peek.l(&ieed) : NPrint Peek.l(&ieed+4)


ieed2$=Mkl$(1074341010)+Mkl$(1227133513)  ;  double IEEE for 22/7, a quick pie ;)

ieed3$=Mkl$(1071994197)+Mkl$(1431655765)  ;  double IEEE for 2/3

ieed4$=Mkl$(1070945621)+Mkl$(1431655765)  ;  double IEEE for 1/3


NPrint double2ffp {ieed1$} ," = 8/3"+l$
NPrint double2ffp {ieed2$} ," = 22/7"+l$
NPrint double2ffp {ieed3$} ," = 2/3"+l$
NPrint double2ffp {ieed4$} ," = 1/3"+l$


NPrint l$+" MkIEEE test:-"+l$

ieee5$=MkIEEE{22/7}
NPrint ieee2ffp {ieee5$} ," = 22/7"+l$


NPrint l$+" MkIEED test:-"+l$

!MkIEED{1/3} : ieed5$=buf$

NPrint double2ffp {ieed5$} ," = 1/3"+l$



