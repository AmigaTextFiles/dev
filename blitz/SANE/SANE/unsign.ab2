
; "unsign.ab2" is a set of functions for converting signed numbers to
; unsigned numbers.

; (C) 12-2-2010  Lorence Lombardo.

; Its not that often that we require unsigned numbers in Blitz but
; since Blitz does not have unsigned number types I felt that it
; warrented a set of functions for dealing with them.

; In many cases it is necessary to store unsigned numbers in their
; signed equivalent initially, especially in the case of NEWTYPE's.
; Then you need to take them up to the next size up in order to
; drop the sign.

; There are currently two choices for dealing with unsigned longs.
; This could also be used to obtain file sizes of up to 4GB.


optimize 2     ; <- FPU is the default.

; FloatMode -1 ; <- If you dont use the FPU then you might want to use this.
               ;    Also you should consider changing the ".d" type to the
               ;    ".f" type for a slightly more correct syntax.


; Converts a signed byte to an unsigned word.

Function.w unsB{byte.b}
   If byte<0 Then ret.w=256+byte Else ret.w=byte
   Function Return ret
End Function


; Converts a signed word to an unsigned long.

Function.l unsW{wrd.w}
   If wrd<0 Then ret.l=65536+wrd Else ret.l=wrd
   Function Return ret
End Function


; Converts a signed 24bit to an unsigned long.

Function.l uns24{L24.l}
   If L24<0 Then ret.l=16777216+L24 Else ret.l=L24
   Function Return ret
End Function


; Converts a signed long to an unsigned double float.

Function.d unsL{long.l}
   If long<0 Then ret.d=256^4+long Else ret.d=long
   Function Return ret
End Function


; Slightly stripped function from "bint.ab2" required for the next
; function. Replace it with the classic version for classic Blitz.

Function.s sub_int0{a$,b$}
   of.b = 0 :  l.w = Max( Peek.w(&a$-2), Peek.w(&b$-2) )
   a$=RSet$(a$,l) : b$=RSet$(b$,l) : c$=""
   Repeat
      erg.l = Vallong(Right$(a$,9)) - Vallong(Right$(b$,9)) - of
      If erg<0
         erg = erg + 1000000000 : of=1
      Else
         of=0
      EndIf
      ans$=Str$(erg) : ansl.w=Peek.w(&ans$-2)
      If ansl<9 Then ans$=String$("0",9-ansl)+ans$
      c$=ans$+c$ : l=l-9 : a$=Left$(a$,l) : b$=Left$(b$,l)
   Until l<1
   c$=StripLead$(c$,48) : If c$="" Then c$="0"
   Function Return c$
End Function


; Converts a signed long to an unsigned decimal string.

Function.s unsLS{long.l}
   ret$=Str$(long)
   If long<0 Then ret$=sub_int0{"4294967296", Mid$(ret$,2)}
   Function Return ret$
End Function


; End of functions

; ***********************************************************************************

; Function's demo begins here.


NPrint unsB{127}

NPrint unsB{-1}

NPrint unsW{-1}

NPrint uns24{-1}

NPrint unsL{-1}

NPrint unsL{-1519438842}

NPrint unsLS{-1519438842}


