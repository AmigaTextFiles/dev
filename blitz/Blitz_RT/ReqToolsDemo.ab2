
; "ReqToolsDemo.ab2" for Blitz was based on "HB2 ReqToolsDemo.bas"
; which was based on "demo.c" from the reqtools package, implementing
; my own style into it. ;)

; Done by Lorence Lombardo in 2008  (lombi@iprimus.com.au)

; See the archives "hsb_reqt_inc.lha" and "ReqToolsDev.lha" on the
; aminet for related interest.


INCLUDE "RT_constants.bb2"

l$=Chr$(10) : workbuf$=String$(Chr$(0), 512) ; allocate string space

; Goto lab  ; un-comment this to test a specific section
            ; also place a "lab:" label at begining of the section
            ; and an "End" statement at the end of the section.
            ; sections are clearly marked with *'s.


; *** Startup *********************************************************

msg$="ReqTools 2.0 Demo"+l$+"~~~~~~~~~~~~~~~~~"+l$+"'reqtools.library' offers several"
msg$=msg$+l$+"different types of requesters:" : but$="Let's see them"

bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)

msg$="NUMBER 1:"+l$+"The larch :-)" : but$="Be serious!"
bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)


; *** rtGetStringA ****************************************************

msg$="String requester"+l$+"function: rtGetStringA_" : but$="Show me"

bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)

wbuf$=workbuf$ : maxchars.l=Len(wbuf$)

def$ = "A bit of text" : tit$="Enter anything:"

CopyMem_ &def$, &wbuf$, Len(def$)

If rtGetStringA_ (&wbuf$,maxchars,&tit$,0,0)
   msg$="You entered this string:"+l$+"'"+Peek.s(&wbuf$)+"'" : but$="So I did"
   bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)
Else
   msg$="You entered nothing :-(" : but$="I'm sorry"
   bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)
EndIf


msg$="These are two new features of ReqTools 2.0:"+l$
msg$=msg$+"Text above the entry gadget and more than"+l$+"one response gadget."

Dim RTags.TagItem(3) : but$=" _Ok |New _2.0 feature!|_Cancel"
RTags(0)\ti_Tag = #RTGS_GadFmt,    &but$
RTags(1)\ti_Tag = #RTGS_TextFmt,   &msg$
RTags(2)\ti_Tag = #RT_Underscore, 95
RTags(3)\ti_Tag = #TAG_DONE

If rtGetStringA_ (&wbuf$, maxchars, &tit$, 0, &RTags(0)\ti_Tag)
   msg$="Yep, this is a new"+l$+"ReqTools 2.0 feature!" : but$="Oh boy!"
   bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)
EndIf


msg$="New is also the ability to switch off the"+l$
msg$=msg$+"backfill pattern.  You can also center the"+l$+"text above the entry gadget."
msg$=msg$+l$+"These new features are also available in"+l$
msg$=msg$+"the rtGetLong() requester."

Dim RTags.TagItem(5) : but$=" _Ok |_Abort|_Cancel"
RTags(0)\ti_Tag = #RTGS_GadFmt,    &but$
RTags(1)\ti_Tag = #RTGS_TextFmt,   &msg$
RTags(2)\ti_Tag = #RTGS_BackFill,  False
RTags(3)\ti_Tag = #RTGS_Flags, #GSREQ_CENTERTEXT|#GSREQ_HIGHLIGHTTEXT
RTags(4)\ti_Tag = #RT_Underscore, 95
RTags(5)\ti_Tag = #TAG_DONE

If rtGetStringA_ (&wbuf$, maxchars, &tit$, 0, &RTags(0)\ti_Tag) = 2
   msg$="What!! You pressed abort!?!"+l$+"You must be joking :-)" : but$="Ok, Continue"
   bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)
EndIf


; *** rtGetLongA ****************************************************

msg$="NUMBER 2:"+l$+"Number requester"+l$+"function: rtGetLongA_" : but$="Show me"
bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)

Dim RTags.TagItem(3) : tit$="Enter a number:"
RTags(0)\ti_Tag = #RTGL_ShowDefault, False
RTags(1)\ti_Tag = #RTGL_Min, 0
RTags(2)\ti_Tag = #RTGL_Max, 999
RTags(3)\ti_Tag = #TAG_DONE

If rtGetLongA_ (&longnum.l, &tit$, 0, &RTags(0)\ti_Tag)
   msg$="The number you entered was:"+l$+Str$(longnum) : but$="So it was"
   bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)
Else
   msg$="You entered nothing :-(" : but$="I'm sorry"
   bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)
EndIf


; *** rtEZRequestA ****************************************************

msg$="NUMBER 3:"+l$+"Message requester, the requester"+l$+"you've been using all the time!"
msg$=msg$+l$+"function: rtEZRequestA_" : but$="Show me more"
bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)

msg$="Simplest usage: some body text and"+l$+"a single centered gadget."
but$="Go it" : bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)

msg$="You can also use two gadgets to"+l$+"ask the user something."
msg$=msg$+l$+"Do you understand?" : but$="Of course|Not really"

msg2$="You are not one of the brightest are you?"+l$+"We'll try again..." : but2$="Ok"

While rtEZRequestA_ (&msg$, &but$, 0, 0 ,0) = 0
   bla.l = rtEZRequestA_ (&msg2$, &but2$, 0, 0 ,0)
Wend

msg$="Great, we'll continue then." : but$="Fine"
bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)

msg$="You can also put up a requester with"+l$+"three choices."+l$
msg$=msg$+"How do you like the demo so far ?" : but$="Great|So so|Rubbish"
ret.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)
If ret=0
   msg$="Too bad, I really hoped you"+l$+"would like it better." : but$="So what"
   bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)
EndIf
If ret=1
   msg$="I'm glad you like it so much." : but$="Fine"
   bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)
EndIf
If ret=2
   msg$="Maybe if you run the demo again"+l$+"you'll REALLY like it." : but$="Perhaps"
   bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)
EndIf

Dim RTags.TagItem(1)
RTags(0)\ti_Tag = #RTEZ_DefaultResponse, 4 : RTags(1)\ti_Tag = #TAG_DONE

msg$="The number of responses is not limited to three"+l$
msg$=msg$+"as you can see.  The gadgets are labeled with"+l$
msg$=msg$+"the return code from rtEZRequest()."+l$
msg$=msg$+"Pressing Return will choose 4, note that"+l$
msg$=msg$+"4's button text is printed in boldface." : but$="1|2|3|4|5|0"

ret.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,&RTags(0)\ti_Tag)

msg$="You picked "+Str$(ret) : but$="How true"
bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)

Dim RTags.TagItem(1)
RTags(0)\ti_Tag = #RT_Underscore, 95 : RTags(1)\ti_Tag = #TAG_DONE

msg$="New for Release 2.0 of ReqTools (V38) is"+l$
msg$=msg$+"the possibility to define characters in the"+l$
msg$=msg$+"buttons as keyboard shortcuts."+l$
msg$=msg$+"As you can see these characters are underlined."+l$
msg$=msg$+"Pressing shift while still holding down the key"+l$
msg$=msg$+"will cancel the shortcut."+l$+"Note that in other requesters a string gadget may"
msg$=msg$+l$+"Note that in other requesters a string gadget may"+l$
msg$=msg$+"be active.  To use the keyboard shortcuts there"+l$
msg$=msg$+"you have to keep the Right Amiga key pressed down."
but$="_Great|_Fantastic|_Swell|Oh _Boy"
bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,&RTags(0)\ti_Tag)

Dim RTags.TagItem(2) : RTags(0)\ti_Tag = #RT_Underscore, 95
RTags(1)\ti_Tag = #RT_IDCMPFlags, #IDCMP_DISKINSERTED : RTags(2)\ti_Tag = #TAG_DONE

msg$="It is also possible to pass extra IDCMP flags"+l$
msg$=msg$+"that will satisfy rtEZRequest(). This requester"+l$
msg$=msg$+"has had DISKINSERTED passed to it."+l$
msg$=msg$+"(Try inserting a disk or CD)." : but$="_Continue"
If rtEZRequestA_ (&msg$, &but$, 0, 0 ,&RTags(0)\ti_Tag)
   msg$="You inserted a disk." : but$="I did"
   bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)
Else
   msg$="You used the 'Continue' gadget." : but$="I did"
   bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)
EndIf

Dim RTags.TagItem(2) : RTags(0)\ti_Tag = #RT_Underscore, 95
RTags(1)\ti_Tag = #RT_ReqPos, #REQPOS_TOPLEFTSCR : RTags(2)\ti_Tag = #TAG_DONE

msg$="Finally, it is possible to specify the position"+l$
msg$=msg$+"of the requester."+l$+"E.g. at the top left of the screen, like this."+l$
msg$=msg$+"This works for all requesters, not just rtEZRequest()!" : but$="_Amazing"
bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,&RTags(0)\ti_Tag)

Dim RTags.TagItem(1) : but$="I'll do that"
RTags(0)\ti_Tag = #RT_ReqPos, #REQPOS_CENTERSCR : RTags(1)\ti_Tag = #TAG_DONE

msg$="Alternatively, you can center the"+l$+"requester on the screen."+l$
msg$=msg$+"Check out 'reqtools_include.bb2' for all the possibilities."
bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,&RTags(0)\ti_Tag)


; *** rtFileRequest ****************************************************

msg$="NUMBER 4:"+l$+"File requester"+l$+"function: rtFileRequestA_" : but$="Demonstrate"
bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)

filereq.l = rtAllocRequestA_(#RT_FILEREQ, 0)
If filereq
   tit$="Pick a File:" : wbuf$=workbuf$
   If rtFileRequestA_(filereq, &wbuf$, &tit$, 0)
      msg$="You picked the file:"+l$+Peek$(&wbuf$)+l$+"in directory:"+l$
      msg$=msg$+Peek$( Peek.l(filereq+#rtfi_Dir) ) : but$="Right"
      bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)
   Else
      msg$="You didn't pick a file." : but$="No"
      bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)
   EndIf

   msg$="The file requester has the ability"+l$+"to allow you to pick more than one"+l$
   msg$=msg$+"file (use Shift to extend-select)."+l$+"Note the extra gadgets you get."
   but$="Interesting" : bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)

   Dim RTags.TagItem(1) : tit$="Pick some Files:" : wbuf$=workbuf$
   RTags(0)\ti_Tag = #RTFI_Flags, #FREQ_MULTISELECT : RTags(1)\ti_Tag = #TAG_DONE

   flist.l = rtFileRequestA_(filereq, &wbuf$, &tit$, &RTags(0)\ti_Tag)
   If flist
      tempflist.l = flist : ret.l=1 : but$="Next|Quit": c.l=1
      While tempflist AND ret=1
         msg$="File "+Str$(c)+":- " + Peek$( Peek.l(tempflist+#rtfl_Name) )
         tempflist = Peek.l(tempflist) : c+1
         ret.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)
      Wend
      rtFreeFileList_ flist
   EndIf

   msg$="The file requester can be used"+l$+"as a directory requester as well."
   but$="Let's see that" : bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)

   Dim RTags.TagItem(1) : tit$="Pick a diretory:" : wbuf$=workbuf$
   RTags(0)\ti_Tag = #RTFI_Flags, #FREQ_NOFILES : RTags(1)\ti_Tag = #TAG_DONE

   If rtFileRequestA_(filereq, &wbuf$, &tit$, &RTags(0)\ti_Tag)
      msg$="You picked the directory:"+l$+Peek$( Peek.l(filereq+#rtfi_Dir) )
      but$="Right" : bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)
   Else
      msg$="You didn't pick a directory." : but$="No"
      bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)
   EndIf

   rtFreeRequest_ filereq
EndIf


; *** rtFontRequest ****************************************************

msg$="NUMBER 5:"+l$+"Font Requester"+l$+"function: rtFontRequestA_" : but$="Show"
bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)

fontreq.l = rtAllocRequestA_(#RT_FONTREQ, 0) : tit$="Pick a Font:" : #ta_YSize=4
If fontreq
   Poke.l (fontreq + #rtfo_Flags), (#FREQ_STYLE | #FREQ_COLORFONTS)
   If rtFontRequestA_(fontreq, &tit$, 0)
      fontname$ = Peek$(Peek.l(fontreq + #rtfo_Attr))
      fontsize.w = Peek.w(fontreq + #rtfo_Attr + #ta_YSize)
      msg$="You picked the font:"+l$+fontname$+l$+"with size:"+Str$(fontsize)
      but$="Right" : bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)
   Else
      msg$="You canceled."+l$+"Was there no font you liked ?" : but$="Nope"
      bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)
   EndIf
   rtFreeRequest_ fontreq
EndIf


; *** rtPaletteRequest ****************************************************

msg$="NUMBER 6:"+l$+"Palette Requester"+l$+"function: rtPaletteRequestA_"
but$="Proceed" : bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)

tit$="Change palette:" : col.l = rtPaletteRequestA_ (&tit$, 0, 0)
If col=-1
   msg$="You canceled."+l$+"No nice colors to be picked ?" : but$="Nah"
   bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)
Else
   msg$="You picked color number "+Str$(col) : but$="Sure did"
   bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)
EndIf


; *** [VolumeRequest] ****************************************************

msg$="NUMBER 7: (ReqTools 2.0)"+l$+"Volume Requester"+l$+"function: rtFileRequestA_"
but$="Show me" : bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)

Dim RTags.TagItem(1) : tit$="Pick a Volume:" : wbuf$=workbuf$
RTags(0)\ti_Tag = #RTFI_VolumeRequest, 0 : RTags(1)\ti_Tag = #TAG_DONE

filereq.l = rtAllocRequestA_(#RT_FILEREQ, 0)
If filereq
   If rtFileRequestA_(filereq, &wbuf$, &tit$, &RTags(0)\ti_Tag)
      msg$="You picked the Volume:"+l$+Peek$( Peek.l(filereq + #rtfi_Dir) )
      but$="Right" : bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)
   Else
      msg$="You didn't pick a volume." : but$="I did not"
      bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)
   EndIf
   rtFreeRequest_ filereq
End If


; *** rtScreenModeRequest ****************************************************

msg$="NUMBER 8: (ReqTools 2.0)"+l$+"Screen mode requester"+l$
msg$=msg$+"function: rtScreenModeRequestA_" : but$="Show me"
bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)

myflags.l=#SCREQ_DEPTHGAD|#SCREQ_SIZEGADS|#SCREQ_AUTOSCROLLGAD|#SCREQ_OVERSCANGAD
Dim RTags.TagItem(1) : tit$="Pick a screen mode:"
RTags(0)\ti_Tag = #RTSC_Flags, myflags : RTags(1)\ti_Tag = #TAG_DONE

scrmodereq.l = rtAllocRequestA_(#RT_SCREENMODEREQ, 0)
If scrmodereq
   If rtScreenModeRequestA_(scrmodereq, &tit$, &RTags(0)\ti_Tag)
      smodeid.l = Peek.l(scrmodereq + #rtsc_DisplayID)
      swidth.w =  Peek.w(scrmodereq + #rtsc_DisplayWidth)
      sheight.w = Peek.w(scrmodereq + #rtsc_DisplayHeight)
      sdepth.w =  Peek.w(scrmodereq + #rtsc_DisplayDepth)
      oscant.w =  Peek.w(scrmodereq + #rtsc_OverscanType)

      oversc$ = "no"
      If oscant=#OSCAN_MAX Then oversc$ = "maximal"
      If oscant=#OSCAN_STANDARD Then oversc$ = "standard"
      If oscant=#OSCAN_TEXT Then oversc$ = "text"
      If oscant=#OSCAN_VIDEO Then oversc$ = "video"

      If Peek.l(scrmodereq + #rtsc_AutoScroll)
         ascroll$ = "on"
      Else
         ascroll$ = "off"
      EndIf

      msg$="You picked this mode:"+l$+"ModeID: HEX="+Hex$(smodeid)+l$+"Size: "+Str$(swidth)
      msg$=msg$+" x "+Str$(sheight)+l$+"Depth: "+Str$(sdepth)+l$+"Overscan: "+oversc$+l$
      msg$=msg$+"AutoScroll: "+ascroll$ : but$="Right"
      bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)
   Else
      msg$="You didn't pick a screen mode." : but$="Nope"
      bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)
   EndIf
   rtFreeRequest_ scrmodereq
EndIf


; *** End of Demo ****************************************************

msg$="That's it!"+l$+"Hope you enjoyed the demo." : but$="Sure did"
bla.l = rtEZRequestA_ (&msg$, &but$, 0, 0 ,0)




