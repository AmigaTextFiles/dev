
; "eFFT_Test2.ab2" a C to Blitz adaptation by Lorence Lombardo.
; Based on Alexander Marx "eFFT_Test.c" example.

; FPU version with "timer.device".

; See the archive "Blitz_Timer.lha" on the aminet for additional information.


optimize 7

INCLUDE "easyFFT.include.bb2"
INCLUDE "timer.include.bb2"

*time1.EClockVal=0 : *time2.EClockVal=0 : myhl.l=0

If easyFFT_InitLib{2}=0 Then NPrint " Could not initialize v2 easyFFT lib...!!!" : End

If timer_Init{#UNIT_ECLOCK}
   *time1=AllocVec_(SizeOf.EClockVal, #MEMF_PUBLIC)
   *time2=AllocVec_(SizeOf.EClockVal, #MEMF_PUBLIC)
Else
   NPrint "Could not initialize timer...!!!" : easyFFT_FreeLib{} : End
EndIf

If *time1=0 OR *time2=0 Then NPrint "Could not initialize timer...!!!" : Goto exit

Macro startime
   !timer_ReadEClock {E_Freq.l, *time1}
End Macro

Macro stoptime
   !timer_ReadEClock {E_Freq.l, *time2}
End Macro


; The following function was taken from "unsign.ab2" from "SANE.lha"
; on the aminet.

Function.d unsL{long.l}
   If long<0 Then ret.d=256^4+long Else ret.d=long
   Function Return ret
End Function

Macro prtime
   thi.d = unsL{*time2\ev_hi} - unsL{*time1\ev_hi}
   tlo.d = unsL{*time2\ev_lo} - unsL{*time1\ev_lo}
   sec.d=(65536.0^2 * thi + tlo) / unsL{E_Freq}
   NPrint "STAT.: Operation took ",sec," seconds."
End Macro


l$=Chr$(10) : NPrint l$+"STAT.: Opened easyFFT.library V2.0."

!easyFFT_eFFTnew{myhl.l, 150}

If myhl=0 Then NPrint "ERROR: Failed to get handle!" : Goto exit

Dim smpldat.d(256), FFTamp.d(256), FFTpha.d(256), syndat.d(256)

NPrint "STAT.: Got handle from easyFFT.library. (150 samples)"
!easyFFT_eFFTgetdat{sampn.l, myhl, #eFFT_NUM}
NPrint "---> : Ok, library's set samples to ",Peek.l(sampn)

!easyFFT_eFFTsetdat{ret.l, myhl, #eFFT_NUM, 256}
If ret=0
   NPrint "ERROR: Failed to set samples to 256." : Goto exit
Else
   NPrint "ACT. : Set Samples to 256."
   !easyFFT_eFFTgetdat{sampn.l, myhl, #eFFT_NUM}
   NPrint "---> : Ok, library's set samples to ",Peek.l(sampn)
EndIf

NPrint "ACT. : Set sampling rate to 6400 S/s."
!easyFFT_eFFTsetdat{bla.l, myhl, #eFFT_SRATE, 6400}
!easyFFT_eFFTgetdat{sampn.l, myhl, #eFFT_SRATE}
NPrint "---> : Ok, library's set sampling rate to ",Peek.l(sampn)
!easyFFT_eFFTgetdat{sampn.l, myhl, #eFFT_RESOL}
NPrint "---> : Ok, library's set FFT resolution to ",Peek.d(sampn)

NPrint "ACT. : 'Sample' sin with 50Hz and 8 bit resolution."

!startime

For i.l=0 To 255
   smpldat(i)=Int(128.0*Sin(2*Pi*50*(1/6400.0)*i))
Next i

!stoptime
!prtime


!easyFFT_eFFTsetdat{bla.l, myhl, #eFFT_SIG, &smpldat(0)}
!easyFFT_eFFTsetdat{bla.l, myhl, #eFFT_DOPD, True}

NPrint "ACT. : Do spectrum analizing; with settings -> TRASH-FALSE, DOPD-TRUE)."

!startime

!easyFFT_eFFTcalc{myhl}

!stoptime
!prtime

!easyFFT_eFFTgetdat{sampn.l, myhl, #eFFT_ASP}
NPrint "---> :Printing now first 10 Entries."
NPrint "      Index Frequency Amplitude(= *128)"
NPrint "      ----- --------- -----------------"
For i=0 To 9
   NPrint "      ", i, "     "+LSet$(Str$(i*25),10), Int( Peek.d( sampn+(i*8) ) )
Next i

NPrint l$+"ACT. : Do spectrum analizing; with settings -> TRASH-FALSE, DOPD-FALSE)."
!easyFFT_eFFTsetdat{bla.l, myhl, #eFFT_DOPD, False}

!startime

!easyFFT_eFFTcalc{myhl}

!stoptime
!prtime

NPrint "ACT. : Do spectrum analizing; with settings -> TRASH-TRUE, DOPD-FALSE)."
!easyFFT_eFFTsetdat{bla.l, myhl, #eFFT_TRASH, True}
!easyFFT_eFFTsetdat{bla.l, myhl, #eFFT_DOPD, False}

!startime

!easyFFT_eFFTcalc{myhl}

!stoptime
!prtime

NPrint "ACT. : 'Sample' sin with 50Hz and 8 bit resolution. (again! because we trashed it!)"

!startime

For i=0 To 255
   smpldat(i)=Int(128.0*Sin(2*Pi*50*(1/6400.0)*i))
Next i

!stoptime
!prtime

!easyFFT_eFFTsetdat{bla.l, myhl, #eFFT_TRASH, False}
!easyFFT_eFFTsetdat{bla.l, myhl, #eFFT_DOPD, True}
!easyFFT_eFFTcalc{myhl}

!easyFFT_eFFTgetdat{sampn.l, myhl, #eFFT_ASP}
For i=0 To 255 : FFTamp(i)=Peek.d( sampn+(i*8) ) : Next i

!easyFFT_eFFTgetdat{sampn.l, myhl, #eFFT_PSP}
For i=0 To 255 : FFTpha(i)=Peek.d( sampn+(i*8) ) : Next i

NPrint "ACT. : Synthesize signal now."
!easyFFT_eFFTsetdat{bla.l, myhl, #eFFT_SYN, True}
!easyFFT_eFFTsetdat{bla.l, myhl, #eFFT_ASP, &FFTamp(0)}
!easyFFT_eFFTsetdat{bla.l, myhl, #eFFT_PSP, &FFTpha(0)}

!startime

!easyFFT_eFFTcalc{myhl}

!stoptime
!prtime

!easyFFT_eFFTgetdat{sampn.l, myhl, #eFFT_SYN}
For i=0 To 255 : syndat(i)=Peek.d( sampn+(i*8) ) : Next i
NPrint "       Orig.Dat.   Synth.Dat"+l$+"       ---------------------"
For i=0 To 10
   NPrint "       "+LSet$(Str$( smpldat(i) ),11), syndat(i)/128
Next i

exit:
If *time1 Then FreeVec_(*time1)
If *time2 Then FreeVec_(*time2)
If myhl<>0
   !easyFFT_eFFTfree{myhl}
   NPrint l$+"STAT.: Free'ed handle from easyFFT.library."
EndIf
easyFFT_FreeLib{}
NPrint l$+"STAT.: Closed easyFFT.library."+l$
timer_Free {}
End


