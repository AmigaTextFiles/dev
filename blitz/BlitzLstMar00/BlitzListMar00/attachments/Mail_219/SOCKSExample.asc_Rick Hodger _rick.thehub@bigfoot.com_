
socksServer$=Par$(1)
socksPort.w=Val(Par$(2))

server$=Par$(3)
port.w=Val(Par$(4))

XINCLUDE "BlitzLibs:TCPFuncs.bb"

NPrint ""
NPrint "SOCKS server on ",socksServer$,":",socksPort.w
NPrint "Final destination is ",server$,":",port.w
NPrint ""
Print "Trying to connect to SOCKS server..."

If ConnecTCP{sockServer$,socksPort.w}
  ;
  NPrint "SUCCESS!"
  Print "Sending version/method request (no authentication)..."
  SendTCP{Chr$(5)+Chr$(1)+Chr$(0)} : a$="" : count.l=0
  Repeat
    Delay_ 1
    count.l+1
    a$+ReadTCP{}
  Until Len(a$)=2 OR count>500
  If a$=Chr$(5)+Chr$(0) ; woohoo! got a 'no authentication required' response!
    NPrint "SUCCESS!"
    ;
    If Val(server$)>0 ; see if we have an IP address
      While Instr(server$,".") : server$=Replace$(server$,"."," ") : Wend
      InitArgParse server$
      a$=Chr$(5)+Chr$(1)+Chr$(0)+Chr$(1)
      a$+Chr$(Val(NextArg$))+Chr$(Val(NextArg$))+Chr$(Val(NextArg$))+Chr$(Val(NextArg$))
      a$+Chr$(Peek.b(&port.w))+Chr$(Peek.b(&port.w+1))
    Else
      a$=Chr$(5)+Chr$(1)+Chr$(0)+Chr$(3)+Chr$(Len(server$))+server$+Chr$(Peek.b(&port.w))+Chr$(Peek.b(&port.w+1))
    EndIf
    ;
    Print "Sending our connection request...."
    SendTCP{a$} : a$="" : count.l=0
    Repeat
      Delay_ 1
      count.l+1
      a$+ReadTCP{}
    Until a$<>""  OR count>500
    ;
    Select Val(Mid$(a$,2,1))
      Case 0 : NPrint "SUCCESS!"
      Case 1 : NPrint "SOCKS server failure!"
      Case 2 : NPrint "Connection not allowed!"
      Case 3 : NPrint "Network unreachable!"
      Case 4 : NPrint "Host unreachable!"
      Case 5 : NPrint "Connection refused!"
      Default : NPrint "Other error: ",Val(Mid$(a$,2,1))
    End Select
    ;
  Else
    Select Asc(Right$(a$,1))
      Case 1 : NPrint "Needs GSSAPI authentication!"
      Case 2 : NPrint "Needs Username/password authentication!"
      Case 3 : NPrint "Needs IANA (whatever the hell that is)!"
      Case 4 : NPrint "Got something we should never get!"
      Default : NPrint "No acceptable methods!"
    End Select
  EndIf
  ;
  CloseTCP{}
Else
  NPrint "Failed! Errno=",Errno_
EndIf

NPrint ""
End
