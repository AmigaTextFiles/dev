;-> AmySequencer by Stefano Maria Regattin
;i> 4 febbraio 1999
;m> 13 febbraio 1999
;m> 27,28,29,30,31 luglio 1999
;m> 1,3,6,7,8,11,12,13,16,21 agosto 1999
;m> 20,27,31 ottobre 1999
;m> 7,9,10,27,28 novembre 1999
;m> 4 dicembre 1999
;m> 14 maggio 2000
;m> 19,20,21,22,23,24,26,30,31 gennaio 2002
;m> 1,2,3,4,5,6,7,9,10,11,13,15,16,17 febbraio 2002
;---------------------------------------------------
;dati per i colori dello schermo
;data for screen colours
;------------------------
Data.b 0,0,0
Data.b 4,4,4
Data.b 8,8,8
Data.b 12,12,12
Data.b 12,0,0
Data.b 12,6,0
Data.b 12,12,0
Data.b 6,12,0
Data.b 0,12,0
Data.b 0,12,6
Data.b 0,12,12
Data.b 0,6,12
Data.b 0,0,12
Data.b 6,0,12
Data.b 12,0,12
Data.b 12,0,6

;------------------------------------------------------------------------------
;le seguenti costanti servono per ricavare il periodo di un canale audio AMIGA
; dalla frequenza di campionamento
;the following constants are used to obtain an AMIGA audio channel period from the
; sampling frequency; there is one for PAL AMIGAs and one for NTSC AMIGAs
;-------------------------------------------------------------------------

;PAL timing
#TemporizzazionePAL=3546895

;NTSC timing
#TemporizzazioneNTSC=3579545

;-------------------------------------------------------------------------
;questa costante indica il numero di riferimento della fonte di caratteri
; Topaz8
;this constant states the characters font Topaz8 referring number
;-----------------------------------------------------------------
#FonteDiCaratteriTopaz8=0

;-------------------------------------------------------------------------
;questa variabile imposta il periodo minimo per i canali audio; il limite
; per i vecchi AMIGA e' 123, ma io ho scelto un periodo minimo di 125, che
; corrisponde ad un La nella nona ottava
;this variable sets the audio channels minimum period for the audio channels;
; on the oldest AMIGA the lowerst usable period is 123, but I have choosen a
; minimum period of 125, that is the A note in the ninth octave
;---------------------------------------------------------------
#PeriodoMinimo=125

;--------------------------------------------------------------------
;questa costante indica il numero di riferimento della tavolozza dei
; colori
;this constant states the colours palette referring number
;----------------------------------------------------------
#TavolozzaDeiColori=0

;--------------------------------------------------
;le seguenti costanti indicano il tipo di finestra
;the following constants state the window's kind
;------------------------------------------------

;window sizing gadget
#GadgetRidimensionaFinestra=1

;draggable window
#FinestraMovibile=2

;window depth gadget
#GadgetNascondiFinestra=4

;close gadget
#GadgetChiudiFinestra=8

;sizing window right border (you must set gimme-zero-zero to use it)
#BordoDestraFinestraRidimensionabile=16

;sizing window bottom border (you must set gimme-zero-zero to use it)
#BordoBassoFinestraRidimensionabile=32

;backdrop window
#FinestraDiSfondo=256

;gimme-zero-zero window
#DammiZeroZero=1024

;borderless window
#FinestraSenzaBordi=2048

;window active
#FinestraAttivata=4096

;------------------------------------------------
;le seguenti costanti indicano il tipo di gadget
;the following constants state the gadgets' kind
;------------------------------------------------

;invert
#InvertiGadget=1

;related to window's right side
#PosizioneRelativaAllaDestraDellaFinestra=2

;related to window'w bottom side
#PosizioneRelativaAlBassoDellaFinestra=4

;size as window's inner width
#ComeLarghezzaInternaFinestra=8

;size as window's inner height
#ComeAltezzaInternaFinestra=16

;box select
#ScatolaDaSelezionare=32

;horiziontal movement
#MovimentoOrizzontale=64

;vertical movement
#MovimentoVerticale=128

;no border
#NessunBordo=256

;exclude each other
#SiEscludonoLUnLAltro=512

;attach to window's right side
#AttaccaADestraDellaFinestra=1024

;attach to window's left side
#AttaccaASinistraDellaFinestra=2048

;attach to window's top
#AttaccaAllaFinestraInAlto=4096

;attach to window's bottom
#AttaccaAllaFinestraInBasso=8192

;gimme-zero-zero border gadget
#GadgetConMemoriaBordo=16384

;--------------------------------------------------------
;le seguenti costanti indicano gli eventi delle finestre
;tht following constants state window events
;--------------------------------------------
;you have pressed a mouse button
#HaiPremutoUnTastoDelTopo=8

;you have moved the mouse
#HaiMossoIlTopo=16

;you have pressed a gadget
#HaiPremutoUnGadget=32

;you have released a gadget
#HaiRilasciatoUnGadget=64

;you have activated a menu
#HaiAttivatoIlMenu=256

;you have closed the window
#HaiChiusoLaFinestra=512

;you have pressed a key
#HaiPremutoUnTasto=1024

;-------------------------------------------------------
;le seguenti costanti indicano i tasti premuti del topo
;the following constants state the pressed mouse buttons
;--------------------------------------------------------

;left mouse button
#TastoSinistroPremutoDelTopo=1

;right mouse button
#TastoDestroPremutoDelTopo=2

;-----------------------------------------------------------------------------
;le seguenti costanti indicano il valore ascii del tasti Backspace, Esc e dei
; tasti freccia
;the following constants state the ascii value for the Backspace, Esc and
; arrow keys
;------------
#Backspace=8
#Escape=27
#CursorUp=28
#CursorDown=29
#CursorRight=30
#CursorLeft=31

;Backspace key
TastoBackspace$=Chr$(#Backspace)

;Esc key
TastoEsc$=Chr$(#Escape)

;Cursor up key
TastoCursoreSu$=Chr$(#CursorUp)

;Cursor down key
TastoCursoreGiu$=Chr$(#CursorDown)

;Cursor right key
TastoCursoreDestra$=Chr$(#CursorRight)

;Cursor left key
TastoCursoreSinistra$=Chr$(#CursorLeft)

;-----------------------------------------------------------
;le seguenti costanti indicano i bottoni del joypad premuti
;the following constants state the joypad pressed buttons
;---------------------------------------------------------
#_PlayB=1
#_ReverseB=2
#_ForwardB=4
#_GreenB=8
#_YellowB=16
#_RedB=32
#_BlueB=64

;-------------------------------------------------------------------------
;le seguenti costanti indicano la direzione scelta con il joystick/joypad
;the following constants state the choosen direction by joystick/joypad
;-----------------------------------------------------------------------
#JoyMovedUp=0
#JoyMovedRight=2
#JoyMovedDown=4
#JoyMovedLeft=6
#JoyNotMoved=8

;main window
#FinestraPrincipale=0

;main window's gadget list
#ListaGadgetPrincipale=0

;main window's menu list
#ListaMenuPrincipale=0

;sounds editor window
#FinestraEditoreSuoni=1

;sounds editor window's gadget list
#ListaGadgetEditoreSuoni=1

;sound editor window's menu list
#ListaMenuEditoreSuoni=1

;notes editor window
#FinestraEditoreNote=2

;notes editor window's gadget list
#ListaGadgetEditoreNote=2

;notes editor window's menu list
#ListaMenuEditoreNote=2

;note editor window's zonetable
#TabellaZoneEditoreNote=0

;window to change basic note length
#FinestraCambioNotaBase=3

;basic note length change window's gadget list
#ListaGadgetCambioNotaBase=3

;basic note change window's zonetable
#TabellaZoneCambioNotaBase=1

;------------------------------------------------------------------------------
;queste costanti indicano i cambiamenti che possono essere applicati alle note
;these constants states different changes that can be applied to notes
;----------------------------------------------------------------------

;select a note
#SelezionaUnaNota=0

;change the channel of the selected notes
#CambiaIlCanaleDelleNoteSelezionate=1

;put up the selected notes of a semitone
#AlzaDiUnSemitonoLeNoteSelezionate=2

;put down the selected notes of a semitone
#AbbassaDiUnSemitonoLeNoteSelezionate=3

;put up the selected notes of one octave
#AlzaDiUnOttavaLeNoteSelezionate=4

;put down the selected notes of one octave
#AbbassaDiUnOttavaLeNoteSelezionate=5

;move the selected notes forward
#SpostaAvantiNelTempoLeNoteSelezionate=6

;move the selected notes back
#SpostaIndietroNelTempoLeNoteSelezionate=7

;increase the selceted notes length
#AllungaLaDurataDelleNoteSelezionate=8

;decrease the selected notes length
#RiduciLaDurataDelleNoteSelezionate=9

;delete the selected notes
#EliminaLeNoteSelezionate=10

;select the noe with the same pitch and of the same channel
#SelezionaLeNoteConLaStessaAltezzaEDelloStessoCanale=11

;select all the notes
#SelezionaTutteLeNote=12

;deselect all the notes
#DeselezionaTutteLeNote=13

;toggle selection of all notes
#InvertiLaSelezioneDiTutteLeNote=14

;-------------------------------------------------------------------------
;le seguenti costanti indicano il modo di disegno delle note visualizzate
;the following constants state the displayed notes drawing mode
;---------------------------------------------------------------

;filled note only
#SoloNotaPiena=0

;unselected note
#NotaNonSelezionata=1

;selected note
#NotaSelezionata=2

;          font height, notes invisible area,   selected channel, note delta y,mouse pressed key,  visible area start y
DEFTYPE .b AltezzaFonte,AreaInvisibileDelleNote,CanaleSelezionato,DeltaYNota,  TastoPremutoDelTopo,YInizioAreaVisibile

;          note position y
DEFTYPE .b YPosizioneNota

;          editor window inner height,       editor window inner width
DEFTYPE .w AltezzaInternaFinestraEditoreNote,LarghezzaInternaFinestraEditoreNote

;          screen height, note delta y,screen height
DEFTYPE .w AltezzaSchermo,DeltaYNota,  LarghezzaSchermo

;          slider length, mouse x, mouse y, slider height
DEFTYPE .w XLunSlider,    XTopo,   YTopo,   YLunSlider

;          time invisible area,   note delta x,MIDI activated,  existing notes,selected notes, visible area start x
DEFTYPE .l AreaInvisibileDelTempo,DeltaXNota,  MIDIAttivato,    Note          ,NoteSelezionate,XInizioAreaVisibile

;          note position x
DEFTYPE .l XPosizioneNota

;-----------------------------------------------------------------------------
;questa variabile indica la velocita' di esecuzione delle note, indicata come
; battute al minuto
;this variable states the notes execution velocity, stated as bars for minute
;-----------------------------------------------------------------------------
BattuteAlMinuto.w=120

;----------------------------------------------------------------------------
;questa costante indica il valore per cui bisogna moltiplicare o dividere la
; frequenza della nota per ricavare rispettivamente la nota successiva o la
; nota precedente e corrisponde a 2^(1/12)
;this constant states the value witch it must moltiply or divide the note
; frequency to obtain respectiveli the following note or the previous note and
; corresponds to 2^(1/12)
;-------------------------
DistanzaSemitono.f=1.05946309

;--------------------------------------------------------------
;questa variabile, diventando Vera, vi fa uscire dal programma
;this variable, becaming True, let you quit the program
;-------------------------------------------------------
EsciDalProgramma.b=False

;------------------------------------------------------------------------------
;Questa variabile indica la larghezza di base delle note; puo' essere cambiata
; secondo i bisogni del momento
;This variable states the basic notes width; it can be changed depending of
; the needs of the moment
;-------------------------
LarghezzaNota.w=192

;-----------------------------------------------------
;questa stringa memorizza il carattere di nuova linea
;this string stores the new line character
;------------------------------------------
LF$=Chr$(10)

;------------------------------------------
;questa variabile indica la fine del brano
;this variable states music end
;-------------------------------
FineMotivo.l=LarghezzaNota*4

;------------------------------------------------------------------
;questa variabile indica se e' stata aperta la finestra principale
;this flag states if the main window is opened
;----------------------------------------------
FinestraPrincipaleAperta.b=False

;---------------------------------------------------------------
;questa variabile indica la scala di visualizzazione delle note
;this variable states the notes visualiziation scale
;----------------------------------------------------
ScalaNote.b=1

;-----------------------------------------------------------------------------
;questa variabile indica se la lista degli ultimi 5 progetti e' stata scritta
;this flag states if the last 5 loaded projects list has been written
;---------------------------------------------------------------------
StoriaDeiProgettiAperti.b=False

;------------------------------------------------------------------------
;La nota base, il quarto, corrisponde a 192 tick; questa costante indica
; quanti tick passano in un 50mo di secondo per un tempo di 120 battute al
; minuto
;The basic note, the quarter, correspond to 192 ticks; this constant signs how
; many tick takes place in a 50th of a second
;---------------------------------------------
TickPerCinquantesimoDiSecondo.f=30.72

;-----------------------------------------------------------------------
;questa variabile indica la posizione della voce Quit del menu' Project
;this variable marks the position of voice Quit in the Project menu
;-------------------------------------------------------------------
VoceMenuEsci.b=4

;-----------------------------------------------------------------------------
;questa struttura di variabili e' una tabella di riferimento per il periodo e
; la frequenza associati ad ognuna delle 128 note possibili
;this variables structure is a refferring table for the periode and frequency
; paired to every one of the 128 given notes
;--------------------------------------------
NEWTYPE .PeriFreq

;--------
;periode
;--------
 Periodo.l

;----------
;frequency
;----------
 Frequenza.f
End NEWTYPE
Dim PeriFreq.PeriFreq(127)

;--------------------------------------------------------------------------
;la seguente struttura di variabili serve per identificare il tipo di note
;the following variables structure is used to states the notes kind
;-------------------------------------------------------------------
NEWTYPE .TipoDiNota

;note italian name
 NomeItaliano$

;note english name
 NomeInglese$

;diesis flag
 Diesis.b

;note octave
 Ottava.b
End NEWTYPE
Dim TipoDiNota.TipoDiNota(127)

;-----------------------------------------------------------------
;la seguente struttura di variabili e' per la gestione delle note
;the following vars structure is for the notes management
;---------------------------------------------------------
NEWTYPE .Nota

;----------------------------------------------------------------------
;questa variabile indica l'altezza della nota; valore utile da 0 a 127
;this variable states note height; useful value from 0 to 127
;-------------------------------------------------------------
 Altezza.b

;-----------------------------------------------------------------------------
;questa variabile indica la posizione della nota nel tempo; valore utile da 0
; a 2 miliardi e rotti
;this variable states note position in time; useful value from 0 to over 2
; billions
;----------
 Inizio.l

;----------------------------------------------------------------------------
;questa variabile indica la durata della nota nel tempo; valore utile da 0 a
; 2 miliardi e rotti (192=nota di un quarto)
;this variable marks note duration; useful value from 0 to over 2 billions
;(192=a quarter note)
;---------------------
 Durata.l

;------------------------------------------------------------------------
;questa variabile indica l'intensita' del suono della nota, cioe' il suo
; volume
;this variable states the note intensity, that is its volume
;------------------------------------------------------------
 Velocita.b

;--------------------------------------------------------------------
;questa varibile indica il canale della nota; valore utile da 1 a 16
;this variable states note channel; useful value from 1 to 16
;-------------------------------------------------------------
 Canale.b

;--------------------------------------------------------
;questa variabile indica se la nota e' stata selezionata
;this variable marks if the note has been selected
;--------------------------------------------------
 Selezionata.b

;------------------------------------------------------------------------------
;questa variabile indica se la nota e' stata suonata: se vale zero la nota non
; e' stata ancora suonata; se vale uno il programma sta suonando la nota; se
; vale due, il programma ha smesso di suonare la nota
;this variable marks if the note has been played: if it is zero, the note is
; not played yet; if it is one, the note is playing; if it is two, the note
; has finished playing
;----------------------
 Suonata.b

;----------------------------------------------------------------------------
;questa variabile indica quale voce AMIGA e' stata usata per suonare la nota
;this variable marks witch AMIGA voice has been used to play the note
;---------------------------------------------------------------------
 VoceUsata.b

End NEWTYPE

;----------------------------------------------------------------
;la seguente struttura di variabili e' per la gestione dei suoni
;the following variables structure is for sounds management
;-----------------------------------------------------------
NEWTYPE .Suono

;---------------------------------------------------
;questa variabile indica il nome del suono caricato
;this variable states the name of the loaded sound
;--------------------------------------------------
 Nome$

;----------------------------------------------------------------------------
;questa variabile indica il canale associato al suono caricato; valore utile
; da 1 a 16
;this variable states the channel linked to the loaded sound; useful value
; from 1 to 16
;--------------
 Canale.b

End NEWTYPE
Dim Suono.Suono(31)

;------------------------------------------
;imposta il numero massimo di note usabili
;sets the maximun note handling
;-------------------------------
MassimoNote.w=120

;-----------------------------------------------------------------
;questa variabile indica se e' stato attivato un dispoditivo MIDI
;this variable states if a MIDI device has been activated
;---------------------------------------------------------
MIDIAttivato.l=0

;-----------------------------------------------------------------
;crea 11 variabili array lista con la struttura del NEWTYPE .Nota
;creates 11 list array variables with the NEWTYPE .Nota structure
;-----------------------------------------------------------------
Dim List Nota.Nota(MassimoNote-1)

;-----------------------------------------------------------------------
;queste 5 variabili conterranno i nomi degli ultimi 5 progetti caricati
;this 5 variables will keep names of last 5 projects loaded
;-----------------------------------------------------------
Dim ProgettoAperto$(4)

;--------------------------------------------------------------------------
;questa procedura imposta la fonte di caratteri Topaz8 per schermo, menu e
; requester, ma non per le finestre
;this procedure sets the characters font Topaz8 for screen, menus and
; requester, but not for windows
;--------------------------------
Statement CaricaLaFonteDiCaratteriTopaz8{}
SHARED AltezzaFonte
 AltezzaFonte=8:LoadFont #FonteDiCaratteriTopaz8,"topaz.font",AltezzaFonte
End Statement

Statement ImpostaIlParlatoDellAmiga{}
 SetVoice 130,250,0,1,64,22200
End Statement

Statement CaricaLeImmaginiDelleNote{}
 BitMap 0,126,16,1
 LoadBitMap 0,"Notes.2Cols"
 GetaShape 0,0,0,22,16
 GetaShape 1,23,0,12,16
 GetaShape 2,36,0,12,16
 GetaShape 3,49,0,17,16
 GetaShape 4,67,0,17,16
 GetaShape 5,85,0,17,16
 GetaShape 6,103,0,17,16
 GetaShape 7,121,0,5,16
 Free BitMap 0
End Statement

Statement AttivaMIDI{}
SHARED LF$
SHARED MIDIAttivato
 Use Window #FinestraPrincipale
 Repeat
  If MIDIAttivato=True Then CloseSerial 0
  WLocate 0,0:WColour 1,0:Print "Please type your MIDI device name>"
  DispositivoMIDI$=StripLead$(StripTrail$(Edit$("serial.device",35),Asc(" ")),Asc(" "))
  WLocate 0,0:Print SPACE$(80)
  If DispositivoMIDI$<>""
   MIDIAttivato=OpenSerial(DispositivoMIDI$,0,31250,16)
   If MIDIAttivato=0
    EZRequest "AmySequencer message","Unable to open MIDI"+LF$+"on "+DispositivoMIDI$,"OK"
   Else
    MIDIAttivato=True
   EndIf
  EndIf
 Until MIDIAttivato=True
 EZRequest "AmySequencer message","MIDI has been activated"+LF$+"on "+DispositivoMIDI$,"OK"
End Statement

;----------------------------------------------------------------------------
;questa procedura crea una tabella di riconoscimento delle note, memorizzata
; nell'array TipoDiNota()
;this procedure crates a referring note recognition table, stored in the
; TipoDiNota() array (that means Note Kind)
;-------------------------------------------
Statement ImpostaIlTipoDiNota{Nota.b}
SHARED TipoDiNota()

;--------------------------------------------
;questa variabile indica l'ottava della nota
;this variable state the note octave
;------------------------------------
 OttavaDellaNota.b=Nota/12

 TipoDiNota(Nota)\Ottava=OttavaDellaNota

;-----------------------------------------------------------------------
;questa variabile indica a quali delle 12 note dodici note fondamentali
; corrisponde la nota corrente
;this variable marks which of 12 fundamental notes corresponds the current
; note
;------
 NotaNellOttava.b=Nota MOD 12

 Select NotaNellOttava

;------------------
;la nota e' un Do?
;is the given note a C?
;-----------------------
 Case 0
  TipoDiNota(Nota)\NomeItaliano="Do"
  TipoDiNota(Nota)\NomeInglese="C"
  TipoDiNota(Nota)\Diesis=False

;-------------------------------
;la nota e' un Do diesis (Do#)?
;is the given note a C diesis (C#)?
;-----------------------------------
 Case 1
  TipoDiNota(Nota)\NomeItaliano="Do#"
  TipoDiNota(Nota)\NomeInglese="C#"
  TipoDiNota(Nota)\Diesis=True

;------------------
;la nota e' un Re?
;is the given note a D?
;-----------------------
 Case 2
  TipoDiNota(Nota)\NomeItaliano="Re"
  TipoDiNota(Nota)\NomeInglese="D"
  TipoDiNota(Nota)\Diesis=False

;-------------------------------
;la nota e' un Re diesis (Re#)?
;is the given note a D diesis (D#)?
;-----------------------------------
 Case 3
  TipoDiNota(Nota)\NomeItaliano="Re#"
  TipoDiNota(Nota)\NomeInglese="D#"
  TipoDiNota(Nota)\Diesis=True

;------------------
;la nota e' un Mi?
;is the given note a E?
;-----------------------
 Case 4
  TipoDiNota(Nota)\NomeItaliano="Mi"
  TipoDiNota(Nota)\NomeInglese="E"
  TipoDiNota(Nota)\Diesis=False

;------------------
;la nota e' un Fa?
;is the given note a F?
;-----------------------
 Case 5
  TipoDiNota(Nota)\NomeItaliano="Fa"
  TipoDiNota(Nota)\NomeInglese="F"
  TipoDiNota(Nota)\Diesis=False

;-------------------------------
;la nota e' un Fa diesis (Fa#)?
;is the given note a F diesis (F#)?
;-----------------------------------
 Case 6
  TipoDiNota(Nota)\NomeItaliano="Fa#"
  TipoDiNota(Nota)\NomeInglese="F#"
  TipoDiNota(Nota)\Diesis=True

;-------------------
;la nota e' un Sol?
;is the given note a G?
;-----------------------
 Case 7
  TipoDiNota(Nota)\NomeItaliano="Sol"
  TipoDiNota(Nota)\NomeInglese="G"
  TipoDiNota(Nota)\Diesis=False

;---------------------------------
;la nota e' un Sol diesis (Sol#)?
;is the given note a G diesis (G#)?
;-----------------------------------
 Case 8
  TipoDiNota(Nota)\NomeItaliano="Sol#"
  TipoDiNota(Nota)\NomeInglese="G#"
  TipoDiNota(Nota)\Diesis=True

;------------------
;la nota e' un La?
;is the given note an A?
;------------------------
 Case 9
  TipoDiNota(Nota)\NomeItaliano="La"
  TipoDiNota(Nota)\NomeInglese="A"
  TipoDiNota(Nota)\Diesis=False

;-------------------------------
;la nota e' un La diesis (La#)?
;is the given note an A diesis (A#)?
;------------------------------------
 Case 10
  TipoDiNota(Nota)\NomeItaliano="La#"
  TipoDiNota(Nota)\NomeInglese="A#"
  TipoDiNota(Nota)\Diesis=True

;------------------
;la nota e' un Si?
;is the given note a B?
;-----------------------
 Case 11
  TipoDiNota(Nota)\NomeItaliano="Si"
  TipoDiNota(Nota)\NomeInglese="B"
  TipoDiNota(Nota)\Diesis=False

 End Select
End Statement

Statement CalcolaLaFrequenzaEdIlPeriodoDelleNote{}
SHARED PeriFreq()
SHARED DistanzaSemitono
;--------------------------------------------------------------------------------
;per ottenere la frequenza di tutte le 128 note, si parte dal La della 2a ottava
; (la prima e' l'ottava zero), la cui frequenza e' nota e la si moltiplica o
; divide per la distanza minima tra due note che e' il semitono; per ottenere il
; periodo si divide una delle costanti di temporizzazione, a seconda che il
; il proprio AMIGA usi uno schermo PAL o NTSC, per la frequenza della nota; queste
; costanti sono riportate sul MANUALE DI RIFERIMENTO PER L'HARDWARE AMIGA.
;to obtain all 128 notes it starts from the A from the 3rd octave (the 1st is the
; zero octave) that has a known frequency, and it multiply or divide this for the
; minimum distance between two notes that is the semitone; to obtain the period it
; divides one of the timing constants, depending on your AMIGA uses a PAL or NTSC
; screen, for the note frequency; these constants are reported the AMIGA HARDWARE
; REFERENCE MANUAL
;------------------
PeriFreq(45)\Frequenza=440
If NTSC=True
 PeriFreq(45)\Periodo=#TemporizzazioneNTSC/PeriFreq(45)\Frequenza
Else
 PeriFreq(45)\Periodo=#TemporizzazionePAL/PeriFreq(45)\Frequenza
EndIf
ImpostaIlTipoDiNota{45}

For Nota.w=46 To 127
 PeriFreq(Nota)\Frequenza=PeriFreq(Nota-1)\Frequenza*DistanzaSemitono
 If NTSC=True
  PeriFreq(Nota)\Periodo=#TemporizzazioneNTSC/PeriFreq(Nota)\Frequenza
 Else
  PeriFreq(Nota)\Periodo=#TemporizzazionePAL/PeriFreq(Nota)\Frequenza
 EndIf
 ImpostaIlTipoDiNota{Nota}
Next Nota

For Nota=44 To 0 Step -1
 PeriFreq(Nota)\Frequenza=PeriFreq(Nota+1)\Frequenza/DistanzaSemitono
 If NTSC=True
  PeriFreq(Nota)\Periodo=#TemporizzazioneNTSC/PeriFreq(Nota)\Frequenza
 Else
  PeriFreq(Nota)\Periodo=#TemporizzazionePAL/PeriFreq(Nota)\Frequenza
 EndIf
 ImpostaIlTipoDiNota{Nota}
Next Nota
End Statement

;-----------------------------------------------------------------------------
;la seguente procedura converte un valore temporale da tic in battute, quarti
; e tic; la variabile OrigineZero serve per la differente visualizzazione
; della durata delle note che parte da zero
;the following procedure converts a time value from tcksin bars, quarters and
; ticks; the OrigineZero variable is used for the different note length
; displaying that starts from zero
;----------------------------------
Statement ConvertiIlTempoInBattute{Tempo.l,OrigineZero.b}
 If OrigineZero=True
  Battute.w=Tempo/(192*4)
  Quarti=((Tempo/192) MOD 4)
 Else
  Battute=Tempo/(192*4)+1
  Quarti=((Tempo/192) MOD 4)+1
 EndIf
 Print Battute,".",Quarti
 Tic.w=Tempo MOD 192
 If Tic>0 Then Print ".",Tic
End Statement

;-------------------------------------------------
;questa procedura imposta le barre di scorrimento
;this procedure sets sliders
;----------------------------
Statement ImpostaSlider{Slider.l,XPos.w,YPos.w,Orientamento$,Parametri.l,ListaGadgetSlider.l}
SHARED XLunSlider
SHARED YLunSlider
 Orientamento$=UCase$(Orientamento$)
 If Orientamento$="HORIZONTAL" Then Parametri+#MovimentoOrizzontale
 If Orientamento$="VERTICAL" Then Parametri+#MovimentoVerticale
 PropGadget ListaGadgetSlider,XPos,YPos,Parametri,Slider,XLunSlider,YLunSlider
End Statement

;--------------------------------------------------
;questa procedura gestisce le barre di scorrimento
;this procedure manage the sliders
;----------------------------------
Statement MostraSlider{Slider.l,ValoreDato.l,ValoreMassimo.l,Orientamento$,ListaGadgetSlider.l,FinestraSlider.l}
 Orientamento$=UCase$(Orientamento$)
 If Orientamento$="HORIZONTAL"
  SetHProp ListaGadgetSlider,Slider,ValoreDato/ValoreMassimo,1/ValoreMassimo
 EndIf
 If Orientamento$="VERTICAL"
  SetVProp ListaGadgetSlider,Slider,ValoreDato/ValoreMassimo,1/ValoreMassimo
 EndIf
 Redraw FinestraSlider,Slider
End Statement

;--------------------------------------------------
;questa procedura gestisce le barre di scorrimento
;this procedure manage the sliders
;----------------------------------
Function .l ValoreSlider{Slider.l,ValoreMassimo.l,LunghezzaSlider.l,Orientamento$,ListaGadgetSlider.l}
 Orientamento$=UCase$(Orientamento$)
 If Orientamento$="HORIZONTAL"
  ValoreSlider.f=HPropPot(ListaGadgetSlider,Slider)*ValoreMassimo
  ValoreIndicatoDalTopo.w=EMouseX*ValoreMassimo/LunghezzaSlider
 EndIf
 If Orientamento$="VERTICAL"
  ValoreSlider=VPropPot(ListaGadgetSlider,Slider)*ValoreMassimo
  ValoreIndicatoDalTopo=EMouseY*ValoreMassimo/LunghezzaSlider
 EndIf
 ValoreIndicatoDalloSlider.l=Int(ValoreSlider)
 If ValoreIndicatoDalTopo>ValoreIndicatoDalloSlider Then ValoreIndicatoDalloSlider+1
 If ValoreIndicatoDalTopo<ValoreIndicatoDalloSlider Then ValoreIndicatoDalloSlider-1
 ValoreIndicatoDalloSlider=Min(Max(ValoreIndicatoDalloSlider,0),ValoreMassimo)
 Function Return ValoreIndicatoDalloSlider
End Function

;--------------------------------------------------------------
;questa procedura cancella il file storico dei progetti aperti
;this procedure clears the loaded projects history file
;-------------------------------------------------------
Statement CancellaLaStoriaDeiProgettiAperti{}
SHARED ProgettoAperto$()
SHARED StoriaDeiProgettiAperti
SHARED VoceMenuEsci
 KillFile "ProjectsHistory"
 VoceMenuEsci=4
 StoriaDeiProgettiAperti=False
 For ProgettoAperto.b=0 To 4
  ProgettoAperto$(ProgettoAperto)=""
 Next ProgettoAperto
End Statement

;------------------------------------------------------------
;questa procedura carica il file storico dei progetti aperti
;this procedure loads the loaded projects history file
;------------------------------------------------------
Statement CaricaLaStoriaDeiProgettiAperti{}
SHARED ProgettoAperto$()
SHARED StoriaDeiProgettiAperti
SHARED VoceMenuEsci
 StoriaDeiProgettiAperti=ReadFile(0,"ProjectsHistory")
 If StoriaDeiProgettiAperti=True
  FileInput 0
  ProgettoAperto=0
  Repeat
   Dato$=Inkey$
   If Dato$=Chr$(10)
    ProgettoAperto+1
    VoceMenuEsci+1
   Else
    ProgettoAperto$(ProgettoAperto)+Dato$
   EndIf
  Until Dato$=""
  CloseFile 0
  PopInput
 EndIf
End Statement

;------------------------------------
;questa procedura imposta lo schermo
;this procedure sets the screen
;-------------------------------
Statement ImpostaLoSchermo{}
SHARED AltezzaSchermo
SHARED LarghezzaSchermo
SHARED ProgettoAperto$()

;--------------------------------------
;imposta la fonte di caratteri globale
;sets the global characters font
;--------------------------------
 Use IntuiFont #FonteDiCaratteriTopaz8

;------------------------------------
;inizializza la tavolozza dei colori
;initializes colours palette
;----------------------------
 InitPalette #TavolozzaDeiColori,16

;-------------------------------
;carica i colori per lo schermo
;loads colours for screen
;-------------------------
 For Colore.b=0 To 15
  Read Rosso.b
  Read Verde.b
  Read Blu.b
  PalRGB #TavolozzaDeiColori,Colore,Rosso,Verde,Blu
 Next Colore

;----------------------------------------------------------------
;imposta le penne dello schermo con i migliori colori (ROM 2.0+)
;sets screen pens with best colours (ROM 2.0+)
;----------------------------------------------
 ScreenPens 0,3,1,2,3,2,8

;------------------------------------------------
;apre lo schermo in alta risoluzione a 16 colori
;opens high resolution screen with 16 colours
;---------------------------------------------
 Screen 0,4+8,"AmySequencer by Stefano Maria Regattin"

;-----------------------------
;usa la tavolozza dei colori
;use colours palette
;--------------------
 Use Palette #TavolozzaDeiColori

;-------------------------------------
;memorizza la larghezza dello schermo
;records the screen width
;-------------------------
 LarghezzaSchermo=ScreenWidth

;----------------------------------
;memorizza l'altezza dello schermo
;records the screen height
;--------------------------
 If NTSC=True Then AltezzaSchermo=200 Else AltezzaSchermo=256

End Statement

;-------------------------------------------------------------------------------
;Questa procedura imposta la finestra principale invisibile e reimposta il menu
; principale
;this procedure sets the main invisible window and resets the main menu
;-----------------------------------------------------------------------
Statement ImpostaLaFinestraPrincipale{}
SHARED AltezzaFonte
SHARED AltezzaSchermo
SHARED BattuteAlMinuto
SHARED FinestraPrincipaleAperta
SHARED LarghezzaInternaFinestraPrincipale
SHARED LarghezzaSchermo
SHARED ProgettoAperto$()
SHARED StoriaDeiProgettiAperti
SHARED TickPerCinquantesimoDiSecondo
SHARED VoceMenuEsci
SHARED XLunSlider
SHARED YLunSlider

;-----------------------------------------------
;se la finestra era gia' stato aperto la chiude
;if window was opened it close it
;---------------------------------
 If FinestraPrincipaleAperta=True
  BeepScreen 0
  Free GadgetList #ListaGadgetPrincipale
  Free MenuList #ListaMenuPrincipale
  CloseWindow #FinestraPrincipale
 EndIf

;-------------------------
;imposta il menu' Project
;sets Project menu
;------------------
 MenuTitle #ListaMenuPrincipale,0,"Project"
 MenuItem #ListaMenuPrincipale,0,0,0,"New"
 MenuItem #ListaMenuPrincipale,0,0,1,"Load"
 MenuItem #ListaMenuPrincipale,0,0,2,"Save"
 MenuItem #ListaMenuPrincipale,0,0,3,"Save as"
 If StoriaDeiProgettiAperti=True
  For ProgettoAperto.b=4 To VoceMenuEsci-1
   MenuItem 0,0,0,ProgettoAperto,ProgettoAperto$(ProgettoAperto-4)
  Next ProgettoAperto
 EndIf
 MenuItem 0,0,0,VoceMenuEsci,"Quit"

;-------------------------
;imposta il menu' Windows
;sets Windows menu
;------------------
 MenuTitle #ListaMenuPrincipale,1,"Windows"
 MenuItem #ListaMenuPrincipale,0,1,0,"Sounds Editor"
 MenuItem #ListaMenuPrincipale,0,1,1,"Notes Editor"

;-----------------------
;imposta il menu' Prefs
;sets Prefs menu
;----------------
 MenuTitle #ListaMenuPrincipale,2,"Prefs"
 MenuItem #ListaMenuPrincipale,0,2,0,"Set MIDI device"
 MenuItem #ListaMenuPrincipale,0,2,1,"Clear projects history"

;--------------------------------------
;usa il menu' nella finestra che segue
;uses menu in following windows
;-------------------------------
 Use MenuList #ListaMenuPrincipale

;----------------------------------------------------------------------
;imposta la barra di scorrimento che regola la velocita' di esecuzione
;sets the slider that tunes the execution velocity
;_-------------------------------------------------
 XLunSlider=640
 YLunSlider=8
 ImpostaSlider{1,0,AltezzaSchermo-(AltezzaFonte+3)-8,"HORIZONTAL",0,#ListaGadgetPrincipale}

;------------------------------------------------------------------------------
;apre la finestra principale solo per il menu' come invisibile e ci attacca la
; lista dei gadget principale
;opens main window for menu usage only as invisible and it attaches the main
; gadget list
;-------------
 XPosF.w=0:YPosF.w=AltezzaFonte+3:XLunF.w=LarghezzaSchermo:YLunF.w=AltezzaSchermo-(AltezzaFonte+3)
 TipoFinestra.l=#FinestraDiSfondo+#FinestraSenzaBordi+#FinestraAttivata
 Window #FinestraPrincipale,XPosF,YPosF,XLunF,YLunF,TipoFinestra,"",-1,-1,#ListaGadgetPrincipale

;-------------------------------
;attacca il menu' alla finestra
;attaches menu to window
;------------------------
 SetMenu #ListaMenuPrincipale

;-----------------------------------------------------------------------
;imposta il colore del testo nei menu' a quello piu' chiaro disponibile
;sets text menu colour to the brightest available
;-------------------------------------------------
 MenuColour 3

;----------------------------------------------
;imposta la fonte di caratteri per la finestra
;sets the font for the window
;-----------------------------
 WindowFont #FonteDiCaratteriTopaz8

;------------------------------------------------------------------------
;iposta la barra di scorrimento della velocita' di esecuzione delle note
;sets the notes execution velocity slider
;-----------------------------------------
 XLunSlider=640
 MostraSlider{1,BattuteAlMinuto-10,300-9,"HORIZONTAL",#ListaGadgetPrincipale,#FinestraPrincipale}

;-------------------------------------------------------------------
;mostra la velocita' di esecuzione delle note in battute per minuto
;shows the execution velocity in bar per minute
;-----------------------------------------------
 WLocate 0,AltezzaSchermo-(AltezzaFonte+3)-16:WColour 1,0
 Print "BPM>",BattuteAlMinuto," Ticks/50th>",TickPerCinquantesimoDiSecondo

;-------------------------------------------------------------------------
;questa variabile conserva la larghezza interna della finestra principale
;this variable stores the inner witdh of the main window
;--------------------------------------------------------
 LarghezzaInternaFinestraPrincipale=InnerWidth

 EZOutput #FinestraPrincipale

 FinestraPrincipaleAperta=True
End Statement

;--------------------------------------------------------------------------
;questa procedura per ora legge solo il nome di un file e o inserisce nel
; menu principale; dovrebbe caricare un progetto di AmySequencer
;this procedure till now only reads a file name and put it into the main menu
; it should load an AmySequencer project
;----------------------------------------
Statement CaricaUnProgetto{}
SHARED ProgettoAperto$()
SHARED StoriaDeiProgettiAperti
SHARED VoceMenuEsci
 MaxLen P$=160
 MaxLen F$=64
 ProgettoAperto$=FileRequest$("Load AmySequencer project",P$,F$)
 If ProgettoAperto$<>""
  For ProgettoAperto.b=0 To 4
   If ProgettoAperto$=ProgettoAperto$(ProgettoAperto) Then Statement Return
  Next ProgettoAperto
  For ProgettoAperto=3 To 0 Step -1
   ProgettoAperto$(ProgettoAperto+1)=ProgettoAperto$(ProgettoAperto)
  Next ProgettoAperto
  ProgettoAperto$(0)=ProgettoAperto$
  StoriaDeiProgettiAperti=WriteFile(0,"ProjectsHistory")
  If StoriaDeiProgettiAperti=True
   FileOutput 0
   For ProgettoAperto=0 To 4
    If ProgettoAperto$(ProgettoAperto)<>""
     NPrint ProgettoAperto$(ProgettoAperto)
    EndIf
   Next ProgettoAperto
   CloseFile 0
   PopOutput
   VoceMenuEsci+1
  EndIf
 EndIf
End Statement

;-----------------------------------------------------------
;questa procedura imposta i suoni da usare con il programma
;this procedure sets sounds to use with program
;-----------------------------------------------
Statement EditoreSuoni{}
SHARED AltezzaFonte
SHARED AltezzaSchermo
SHARED LarghezzaSchermo
SHARED LF$
SHARED PercorsoSuoni$
SHARED Suono()
SHARED XLunSlider
SHARED YLunSlider
 MenuTitle #ListaMenuEditoreSuoni,0,"Sounds"
 MenuItem #ListaMenuEditoreSuoni,0,0,0,"Load"
 MenuItem #ListaMenuEditoreSuoni,0,0,1,"Discard"
 Use MenuList #ListaMenuEditoreSuoni

 XPosF.w=0:YPosF.w=AltezzaFonte+3:XLunF.w=LarghezzaSchermo:YLunF.w=AltezzaSchermo-(AltezzaFonte+3)
 TipoFinestra.l=#GadgetChiudiFinestra+#DammiZeroZero+#FinestraAttivata
 Window #FinestraEditoreSuoni,XPosF,YPosF,XLunF,YLunF,TipoFinestra,"Sounds Editor",-1,-1
 LarghezzaInternaFinestraEditoreSuoni.w=InnerWidth
 AltezzaInternaFinestraEditoreSuoni.w=InnerHeight
 CloseWindow #FinestraEditoreSuoni

;-------------------------------------------------------------------------------
;imposta la barra di scorrimento che serve per visualizzare i suoni disponibili
;sets the slider that is used to show the available sounds
;_---------------------------------------------------------
 XLunSlider=16
 YLunSlider=AltezzaInternaFinestraEditoreSuoni
 ImpostaSlider{1,LarghezzaInternaFinestraEditoreSuoni-16,0,"VERTICAL",0,#ListaGadgetEditoreSuoni}

 Window #FinestraEditoreSuoni,XPosF,YPosF,XLunF,YLunF,TipoFinestra,"Sounds Editor",-1,-1,#ListaGadgetEditoreSuoni
 SetMenu #ListaMenuEditoreSuoni

;-----------------------------------------------------------------------
;imposta il colore del testo nei menu' a quello piu' chiaro disponibile
;sets text menu colour to the brightest available
;-------------------------------------------------
 MenuColour 3

;----------------------------------------------
;imposta la fonte di caratteri per la finestra
;sets the font for the window
;-----------------------------
 WindowFont #FonteDiCaratteriTopaz8

;----------------------------------
;sounds window's displaying  start
;----------------------------------
 InizioSuoni.b=0

;------------------------
;maximum strings length
;------------------------
 LunghezzaMassimaStringhe.b=(LarghezzaInternaFinestraEditoreSuoni-16)/8

;-----------------------------
;stated by mouse sound number
;-----------------------------
  SuonoIndicatoDalTopo.b=-1

 Repeat
  WCls

  If SuoniCaricati.b>31 Then Enable #ListaGadgetEditoreSuoni,1 Else Disable #ListaGadgetEditoreSuoni,1
  Redraw #FinestraEditoreSuoni,1
;-------------------------------------------------------------------------
;imposta la barra di scorrimento della velocita' di esecuzione delle note
;sets the notes execution velocity slider
;-----------------------------------------
  YLunSlider=AltezzaInternaFinestraEditoreSuoni
  MostraSlider{1,InizioSuoni,2,"VERTICAL",#ListaGadgetEditoreSuoni,#FinestraEditoreSuoni}

  WLocate 0,0

;----------------------
;loaded sounds counter
;----------------------
  SuoniCaricati=0

  For Suono.b=0 To 30
   AssegnamentoSuono$=Suono(InizioSuoni+Suono)\Nome
   If AssegnamentoSuono$<>""
    CanaleSuono.b=Suono(InizioSuoni+Suono)\Canale
    If CanaleSuono>0 Then WColour CanaleSuono-1,16-CanaleSuono Else WColour 1,0
    Format "00"
    If CanaleSuono>0 Then CanaleSuono$=" channel "+Str$(CanaleSuono) Else CanaleSuono$=" no channel"
    Format""
    AssegnamentoSuono$+CanaleSuono$
    AssegnamentoSuono$=Right$(AssegnamentoSuono$,LunghezzaMassimaStringhe)
    AssegnamentoSuono$+SPACE$(LunghezzaMassimaStringhe-Len(AssegnamentoSuono$))
    NPrint AssegnamentoSuono$
    SuoniCaricati+1
   EndIf
  Next Suono
  EventoFinestraEditoreSuoni.l=WaitEvent
  Select EventoFinestraEditoreSuoni
  Case #HaiPremutoUnTastoDelTopo
   TastoCliccatoDelTopo.b=MButtons:YTopo=WMouseY
   If TastoCliccatoDelTopo=1
    SuonoIndicatoDalTopo=YTopo/8+InizioSuoni
    If SuoniCaricati>0 AND SuonoIndicatoDalTopo<SuoniCaricati
     CanaleSuono.b=Suono(SuonoIndicatoDalTopo)\Canale
     CanaleSuono+1:If CanaleSuono=17 Then CanaleSuono=0
     Suono(SuonoIndicatoDalTopo)\Canale=CanaleSuono
    EndIf
    FlushEvents #HaiPremutoUnTastoDelTopo
   EndIf
  Case #HaiPremutoUnGadget
   If SuoniCaricati>31
    InizioSuoni=ValoreSlider{1,2,AltezzaInternaFinestraEditoreSuoni,"VERTICAL",#ListaGadgetEditoreSuoni}
   EndIf
  Case #HaiRilasciatoUnGadget
   If SuoniCaricati>31
    InizioSuoni=ValoreSlider{1,2,AltezzaInternaFinestraEditoreSuoni,"VERTICAL",#ListaGadgetEditoreSuoni}
   EndIf
  Case #HaiAttivatoIlMenu
   If MenuHit=0 AND ItemHit=0
    If SuoniCaricati<32
     MaxLen PercorsoSuoni$=160
     MaxLen NomeSuono$=64
     NomeSuono$=FileRequest$("Load a sound...",PercorsoSuoni$,NomeSuono$)
     If NomeSuono$<>""
      StessoSuono.b=False
      If SuoniCaricati>0
       For SuonoPrecedente=0 To SuoniCaricati-1
        If NomeSuono$=Suono(SuonoPrecedente)\Nome Then StessoSuono=True
       Next SuonoPrecedente
      EndIf
      If StessoSuono=False
       Suono=SuoniCaricati
       LoadSound Suono,NomeSuono$
       Suono(Suono)\Nome=NomeSuono$
       Sound Suono,3
      EndIf
     EndIf
    EndIf
   EndIf
   If MenuHit=0 AND ItemHit=1
    If SuoniCaricati>0 AND SuonoIndicatoDalTopo<SuoniCaricati AND SuonoIndicatoDalTopo>-1
     Suono=SuonoIndicatoDalTopo
     Free Sound Suono
     Suono(Suono)\Nome=""
     While Suono+1<SuoniCaricati
      Suono(Suono)\Nome=Suono(Suono+1)\Nome
      Suono(Suono)\Canale=Suono(Suono+1)\Canale
      Suono+1
     Wend
     Suono(Suono)\Nome=""
     Suono(Suono)\Canale=0
     SuonoIndicatoDalTopo=-1
    EndIf
   EndIf
  Case #HaiChiusoLaFinestra
   SuoniAssegnatiAlloStessoCanale.b=False
   For SuonoUno.b=0 To 31
    For SuonoDue.b=0 To 31
     If SuonoUno<>SuonoDue
      If Suono(SuonoUno)\Canale>0 AND Suono(SuonoUno)\Canale=Suono(SuonoDue)\Canale
       SuoniAssegnatiAlloStessoCanale=True
      EndIf
     EndIf
    Next SuonoDue
   Next SuonoUno
   If SuoniAssegnatiAlloStessoCanale=True
    Messaggio$="You have two or more sounds"+LF$+"assigned to the same channel;"+LF$+"please adjust all better!"
    EZRequest "AmySequencer message",Messaggio$,"OK"
   Else
    EsciDallEditoreSuoni.b=True
   EndIf
  Case #HaiPremutoUnTasto
   TastoPremuto$=LCase$(Inkey$)
   Select TastoPremuto$
   Case TastoEsc$
    EsciDallEditoreSuoni=True
   Case TastoCursoreSu$
   Case TastoCursoreGiu$
   End Select
  End Select
 Until EsciDallEditoreSuoni=True
 CloseWindow #FinestraEditoreSuoni
 Use Window #FinestraPrincipale
End Statement

;------------------------------------------------------
;questa procedura cancella a video la nota selezionata
;this procedure clears on screen the selected note
;--------------------------------------------------
Statement CancellaNota{Nota.b,InizioNota.l,FineNota.l}
SHARED AltezzaInternaFinestraEditoreNote
SHARED LarghezzaInternaFinestraEditoreNote
SHARED ScalaNote
SHARED TipoDiNota()
SHARED YInizioAreaVisibile
 NotaVisualizzata.w=(127-Nota-YInizioAreaVisibile)*8
 If NotaVisualizzata>=0 AND NotaVisualizzata+7<=AltezzaInternaFinestraEditoreNote-8-8
  I.w=InizioNota/ScalaNote
  F.w=FineNota/ScalaNote
  If I>=0 OR I<LarghezzaInternaFinestraEditoreNote OR F>=0 OR F<LarghezzaInternaFinestraEditoreNote
   NotaVisibile.b=True
  Else
   NotaVisibile=False
  EndIf
  If NotaVisibile=True
   X0Nota=Min(Max(I,0),LarghezzaInternaFinestraEditoreNote-1)
   Y0Nota=Min(Max(NotaVisualizzata,0),AltezzaInternaFinestraEditoreNote-1-8-8)
   X1Nota=Min(Max(F,0),LarghezzaInternaFinestraEditoreNote-1)
   Y1Nota=Min(Max(NotaVisualizzata+7,0),AltezzaInternaFinestraEditoreNote-1-8-8)
   If TipoDiNota(Nota)\Diesis=True
    WBox 16+X0Nota,Y0Nota,16+X1Nota,Y1Nota,0
   Else
    WBox 16+X0Nota,Y0Nota,16+X1Nota,Y1Nota,3
   EndIf
  EndIf
 EndIf
End Statement

;----------------------------------
;questa procedura disegna una nota
;this procedure draws a note
;----------------------------
Statement DisegnaNota{Nota.b,InizioNota.l,FineNota.l,CanaleNota.b,ModoDisegno.b}
SHARED AltezzaInternaFinestraEditoreNote
SHARED LarghezzaInternaFinestraEditoreNote
SHARED ScalaNote
SHARED XInizioAreaVisibile
SHARED YInizioAreaVisibile
 NotaVisualizzata.w=(127-Nota-YInizioAreaVisibile)*8
 If NotaVisualizzata>=0 AND NotaVisualizzata+7<=AltezzaInternaFinestraEditoreNote-8-8
  I.w=InizioNota/ScalaNote-XInizioAreaVisibile
  F.w=FineNota/ScalaNote-XInizioAreaVisibile
  If I>=0 AND I<LarghezzaInternaFinestraEditoreNote
   InizioNotaVisibile.b=True
  Else
   InizioNotaVisibile=False
  EndIf
  If F>=0 AND F<LarghezzaInternaFinestraEditoreNote
   FineNotaVisibile.b=True
  Else
   FineNotaVisibile=False
  EndIf
  If I>=0 OR I<LarghezzaInternaFinestraEditoreNote OR F>=0 OR F<LarghezzaInternaFinestraEditoreNote
   NotaVisibile.b=True
  Else
   NotaVisibile=False
  EndIf
  If NotaVisibile=True
   X0Nota=Min(Max(I,0),LarghezzaInternaFinestraEditoreNote-1)
   Y0Nota=Min(Max(NotaVisualizzata,0),AltezzaInternaFinestraEditoreNote-1-8-8)
   X1Nota=Min(Max(F,0),LarghezzaInternaFinestraEditoreNote-1)
   Y1Nota=Min(Max(NotaVisualizzata+7,0),AltezzaInternaFinestraEditoreNote-1-8-8)
   WBox 16+X0Nota,Y0Nota,16+X1Nota,Y1Nota,16-CanaleNota
   If ModoDisegno=#NotaSelezionata
    Wline 16+X0Nota,NotaVisualizzata,16+X1Nota,NotaVisualizzata+7,CanaleNota-1
    Wline 16+X0Nota,NotaVisualizzata+7,16+X1Nota,NotaVisualizzata,CanaleNota-1
   EndIf
   If ModoDisegno=#NotaNonSelezionata
    Wline 16+X0Nota+1,NotaVisualizzata+1,16+X1Nota-1,NotaVisualizzata+1,CanaleNota-1
    Wline 16+X0Nota+1,NotaVisualizzata+6,16+X1Nota-1,NotaVisualizzata+6,CanaleNota-1
   EndIf
  EndIf
  If ModoDisegno=#NotaNonSelezionata
   If InizioNotaVisibile=True
    Wline 16+I+1,NotaVisualizzata+1,16+I+1,NotaVisualizzata+6,CanaleNota-1
   EndIf
   If FineNotaVisibile=True
    Wline 16+F-1,NotaVisualizzata+1,16+F-1,NotaVisualizzata+6,CanaleNota-1
   EndIf
  EndIf
 EndIf
End Statement

;-----------------------------------------------------------
;questa procedura disegna le note di sfondo con i loro nomi
;this procedure draws the notes background with note names
;----------------------------------------------------------
Statement MostraLeNoteDiSfondoConILoroNomi{}
SHARED AreaInvisibileDelleNote
SHARED LarghezzaInternaFinestraEditoreNote
SHARED TipoDiNota()
SHARED YInizioAreaVisibile
 For Tasto.b=0 To 127-AreaInvisibileDelleNote
  Nota.b=127-Tasto-YInizioAreaVisibile
  Y0Nota.w=Tasto*8:Y1Nota.w=Y0Nota+7
  If TipoDiNota(Nota)\Diesis=True
   WBox 16,Y0Nota,LarghezzaInternaFinestraEditoreNote,Y1Nota,0
   WColour 3,0
  Else
   WBox 16,Y0Nota,LarghezzaInternaFinestraEditoreNote,Y1Nota,3
   WColour 0,3
  EndIf
  WLocate 16,Y0Nota
  Print "Note ",Nota,">"
  Print TipoDiNota(Nota)\NomeInglese
  Print TipoDiNota(Nota)\Ottava
  Print "="
  Print TipoDiNota(Nota)\NomeItaliano
  Print TipoDiNota(Nota)\Ottava
 Next Tasto
End Statement

;-----------------------------------------------
;questa procedura mostra i numeri delle battute
;this procedure shows the bar numbers
;-------------------------------------
Statement MostraLeBattute{}
SHARED AreaInvisibileDelleNote
SHARED AreaInvisibileDelTempo
SHARED LarghezzaInternaFinestraEditoreNote
SHARED ScalaNote
SHARED TipoDiNota()
SHARED XInizioAreaVisibile
SHARED YInizioAreaVisibile
 Nota.b=127-YInizioAreaVisibile-(127-AreaInvisibileDelleNote)
 If TipoDiNota(Nota)\Diesis=True Then WColour 3,0 Else WColour 0,3
 FineTempoVisibile.l=XInizioAreaVisibile+LarghezzaInternaFinestraEditoreNote*ScalaNote
 For TempoVisibile=XInizioAreaVisibile To FineTempoVisibile Step 2*ScalaNote
  If TempoVisibile MOD (192*Max(ScalaNote,4)/4)=0
   XTempo.w=TempoVisibile/ScalaNote-XInizioAreaVisibile
   If XTempo>8*14
    WLocate XTempo,(127-AreaInvisibileDelleNote)*8:ConvertiIlTempoInBattute{TempoVisibile,False}
   EndIf
  EndIf
 Next TempoVisibile
End Statement

;-----------------------------------------------------
;questa procedura mostra gli slider dell'editore note
;this procedure shows the notes editor' sliders
;-----------------------------------------------
Statement MostraSliderEditoreNote{}
SHARED AreaInvisibileDelleNote
SHARED AreaInvisibileDelTempo
SHARED FineMotivo
SHARED LarghezzaInternaFinestraEditoreNote
SHARED ScalaNote
SHARED XInizioAreaVisibile
SHARED YInizioAreaVisibile
 MostraSlider{1,YInizioAreaVisibile,AreaInvisibileDelleNote,"VERTICAL",#ListaGadgetEditoreNote,#FinestraEditoreNote}
 AreaInvisibileDelTempo.l=FineMotivo/ScalaNote-(LarghezzaInternaFinestraEditoreNote-16)
 AreaInvisibileDelTempo=Max(AreaInvisibileDelTempo,0)
 If AreaInvisibileDelTempo=0
  Disable #ListaGadgetEditoreNote,2
  XInizioAreaVisibile=0
 Else
  Enable #ListaGadgetEditoreNote,2
  MostraSlider{2,XInizioAreaVisibile,AreaInvisibileDelTempo,"HORIZONTAL",#ListaGadgetEditoreNote,#FinestraEditoreNote}
 EndIf
 Redraw #FinestraEditoreNote,2
End Statement

;--------------------------------
;questa procedura mostra le note
;this procedure shows notes
;---------------------------
Statement MostraLeNote{}
SHARED Nota()
 ResetList Nota()
 While NextItem(Nota())=True
  If Nota()\Selezionata=True
   DisegnaNota{Nota()\Altezza,Nota()\Inizio,Nota()\Inizio+Nota()\Durata-1,Nota()\Canale,#NotaSelezionata}
  Else
   DisegnaNota{Nota()\Altezza,Nota()\Inizio,Nota()\Inizio+Nota()\Durata-1,Nota()\Canale,#NotaNonSelezionata}
  EndIf
 Wend
End Statement

;---------------------------------------------------------------
;questa procedura mostra la scala di visualizzazione delle note
;this procedure shows the note scale of notes displaying
;--------------------------------------------------------
Statement MostraLaScalaDelleNote{}
SHARED AltezzaInternaFinestraEditoreNote
SHARED ScalaNote
; For Scatola.b=0 To 4
;  Y0Scatola.w=AltezzaInternaFinestraEditoreNote-8-8
;  WBox Scatola,Y0Scatola+Scatola,15-Scatola,Y0Scatola+7-Scatola,15-Scatola
; Next Scatola
 Select ScalaNote
 Case 1
  Ingrandimento.b=1
 Case 2
  Ingrandimento=2
 Case 4
  Ingrandimento=3
 Case 8
  Ingrandimento=4
 Case 16
  Ingrandimento=5
 Case 32
  Ingrandimento=6
 End Select
 WLocate 0,AltezzaInternaFinestraEditoreNote-8-8
 WColour 1,0:Print "Z",Ingrandimento
End Statement

;---------------------------------------------------
;questa procedura permette di cambiare la nota base
;this procedure permits to change the basic note
;------------------------------------------------
Statement CambiaLaNotaDiBase{}
SHARED AltezzaFonte
SHARED LarghezzaNota
SHARED TastoCursoreDestra$
SHARED TastoCursoreSinistra$
SHARED TastoEsc$
SHARED XLunSlider
SHARED YLunSlider
 TipoFinestra.l=#FinestraMovibile+#GadgetNascondiFinestra+#GadgetChiudiFinestra+#DammiZeroZero+#FinestraAttivata
 YPosF.w=(AltezzaFonte+3)*2
 YLunF.w=AltezzaFonte+3+16+8+8+2
 Window #FinestraCambioNotaBase,4,YPosF,4+24*8+2,YLunF,TipoFinestra,"Basic note",-1,-1
 LarghezzaInternaFinestraCambioNotaBase.w=InnerWidth
 AltezzaInternaFinestraCambioNotaBase.w=InnerHeight
 CloseWindow #FinestraCambioNotaBase
 XLunSlider=LarghezzaInternaFinestraCambioNotaBase
 YLunSlider=8
 ImpostaSlider{1,0,AltezzaInternaFinestraCambioNotaBase-8,"HORIZONTAL",0,#ListaGadgetCambioNotaBase}
 Window #FinestraCambioNotaBase,4,YPosF,4+24*8+2,YLunF,TipoFinestra,"Basic note",-1,-1,#ListaGadgetCambioNotaBase
 WindowFont #FonteDiCaratteriTopaz8
 Zona.b=NewZoneTable(#TabellaZoneCambioNotaBase,8)
 If Zona=True Then UseZoneTable #TabellaZoneCambioNotaBase Else Statement Return
 For Zona=0 To 7
  WBlit Zona,24*Zona,0:SetZone Zona,24*Zona,0,24*Zona+23,15
 Next
 Repeat
  WLocate 0,AltezzaInternaFinestraCambioNotaBase-8-8
  If LarghezzaNota MOD 6=0 Then WColour 2,1 Else WColour 1,0
  Format "0000":Print LarghezzaNota:Format ""
  MostraSlider{1,LarghezzaNota/6,192*4/6,"HORIZONTAL",#ListaGadgetCambioNotaBase,#FinestraCambioNotaBase}
  EventoFinestraCambioNotaBase.l=WaitEvent
  Select EventoFinestraCambioNotaBase
  Case #HaiPremutoUnTastoDelTopo
   XTopo.w=WMouseX:YTopo.w=WMouseY:TastoCliccatoDelTopo.b=MButtons
   Select TastoCliccatoDelTopo
   Case 1
    Zona=Zone(XTopo,YTopo)
    Select Zona
    Case 0
     LarghezzaNota=192*4:NotaPuntata.w=0
     Speak "You have choosen a whole note."
    Case 1
     LarghezzaNota=192*2:NotaPuntata=0
     Speak "You have choosen a half note."
    Case 2
     LarghezzaNota=192:NotaPuntata=0
     Speak "You have choosen a quarter note."
    Case 3
     LarghezzaNota=192/2:NotaPuntata=0
     Speak "You have choosen an eighth note."
    Case 4
     LarghezzaNota=192/4:NotaPuntata=0
     Speak "You have choosen a sixteenth note."
    Case 5
     LarghezzaNota=192/8:NotaPuntata=0
     Speak "You have choosen a thirty second note."
    Case 6
     LarghezzaNota=192/16:NotaPuntata=0
     Speak "You have choosen a fourty sixth note."
    Case 7
     If NotaPuntata=0
      NotaPuntata=LarghezzaNota/2
      LarghezzaNota+NotaPuntata
      Speak "You have a dotted note."
     Else
      LarghezzaNota/3*2
      NotaPuntata=0
      Speak "You have no more a dotted note."
     EndIf
    End Select
   Case 5
   End Select
  Case #HaiPremutoUnGadget
   LarghezzaNota=ValoreSlider{1,192*4/6,LarghezzaInternaFinestraCambioNotaBase,"HORIZONTAL",#ListaGadgetCambioNotaBase}*6
  Case #HaiRilasciatoUnGadget
   LarghezzaNota=ValoreSlider{1,192*4/6,LarghezzaInternaFinestraCambioNotaBase,"HORIZONTAL",#ListaGadgetCambioNotaBase}*6
  Case #HaiChiusoLaFinestra
   EsciDalCambioNotaBase=True
  Case #HaiPremutoUnTasto
   TastoPremuto$=LCase$(Inkey$)
   Select TastoPremuto$
   Case TastoEsc$
    EsciDalCambioNotaBase=True
   Case TastoCursoreDestra$
    If LargghezzaNota+1<192*6 Then LarghezzaNota+2
   Case TastoCursoreSinistra$
    If LarghezzaNota>7 Then LarghezzaNota-2
   End Select
  End Select
 Until EsciDalCambioNotaBase=True

;***********************************************************
;la seguente linea se attivata da' errore dove non dovrebbe
;the following line if avtivated gives an error where it should not do
;**********************************************************************
;FreeZoneTable #TabellaZoneCambioNotaBase

 CloseWindow #FinestraCambioNotaBase
 Use Window #FinestraEditoreNote

;***************************************************
;il seguente comando sembra non avere alcun effetto
;the following command seems have no effect
;*******************************************
 UseZoneTable #TabellaZoneEditoreNote

End Statement

;---------------------------------------------------------------------------
;questa procedura permette di spostare le nota selezionate cliccando con il
; mouse su una di esse
;this procedure permits to move the selected notes clicking on one of these
;---------------------------------------------------------------------------
Function .b MuoviLeNoteSelezionateConIlTopo{XPosizioneNota.l,YPosizioneNota.b}
SHARED LarghezzaNota
SHARED Nota()
SHARED ScalaNote
SHARED XInizioAreaVisibile
SHARED XPosizioneNota
SHARED YInizioAreaVisibile
SHARED YPosizioneNota
 NotaSpostata.b=False
 Repeat
  NotaSpostataConIlTopo.b=False
  NuovaXTopo.w=WMouseX:NuovaYTopo.w=WMouseY:TastoPremutoDelTopo.b=Joyb(0)
  XNuovaPosizioneNota.l=(XInizioAreaVisibile+(NuovaXTopo-16)*ScalaNote)/LarghezzaNota*LarghezzaNota
  YNuovaPosizioneNota.b=127-YInizioAreaVisibile-NuovaYTopo/8
  If XNuovaPosizioneNota<>XPosizioneNota
;   CancellaNota{YPosizioneNota,XPosizioneNota,XPosizioneNota+Nota()\Durata-1}
;   DisegnaNota{YNuovaPosizioneNota,XNuovaPosizioneNota,XNuovaPosizioneNota+Nota()\Durata-1,Nota()\Canale,#NotaSelezionata}
   DeltaXNota.l=XNuovaPosizioneNota-XPosizioneNota
   XPosizioneNota=XNuovaPosizioneNota
   NotaSpostataConIlTopo=True:NotaSpostata=True
  Else
   DeltaXNota=0
  EndIf
  If YNuovaPosizioneNota<>YPosizioneNota
;   CancellaNota{YPosizioneNota,XPosizioneNota,XPosizioneNota+Nota()\Durata-1}
;   DisegnaNota{YNuovaPosizioneNota,XNuovaPosizioneNota,XNuovaPosizioneNota+Nota()\Durata-1,Nota()\Canale,#NotaSelezionata}
   DeltaYNota.b=YNuovaPosizioneNota-YPosizioneNota
   YPosizioneNota=YNuovaPosizioneNota
   NotaSpostataConIlTopo=True:NotaSpostata=True
  Else
   DeltaYNota=0
  EndIf
  If NotaSpostataConIlTopo=True
   ResetList Nota()
   While NextItem(Nota())=True
    If Nota()\Selezionata=True
     If Nota()\Altezza+DeltaYNota>=0 AND Nota()\Altezza+DeltaYNota<=127 Then Nota()\Altezza+DeltaYNota
     If Nota()\Inizio+DeltaXNota>=0 Then Nota()\Inizio+DeltaXNota
    EndIf
   Wend
   MostraLeNoteDiSfondoConILoroNomi{}
   MostraLeBattute{}
   MostraLeNote{}
   MostraSliderEditoreNote{}
  EndIf
 Until TastoPremutoDelTopo<>#TastoSinistroPremutoDelTopo
 Function Return NotaSpostata
End Function

;----------------------------------
;questa procedura modifica le note
;this procedure modifies the notes
;----------------------------------
Statement Selezione{Modo.b}
SHARED AltezzaInternaFinestraEditoreNote
SHARED CanaleSelezionato
SHARED FineMotivo
SHARED LarghezzaInternaFinestraEditoreNote
SHARED LarghezzaNota
SHARED MIDIAttivato
SHARED Nota()
SHARED Note
SHARED NoteSelezionate
SHARED PeriFreq()
SHARED ScalaNote
SHARED Suono()
SHARED TipoDiNota()
SHARED XInizioAreaVisibile
SHARED XPosizioneNota
SHARED XTopo
SHARED YInizioAreaVisibile
SHARED YPosizioneNota
SHARED YTopo
 ResetList Nota()
 StessaNota.b=%0000
 XTopo=Min(Max(XTopo,16),LarghezzaInternaFinestraEditoreNote-1)
 YTopo=Min(Max(YTopo,0),AltezzaInternaFinestraEditoreNote-1-8-8)
 While NextItem(Nota())=True
  StessaNota=%0000
  Nota.b=Nota()\Altezza:InizioNota.l=Nota()\Inizio:DurataNota.l=Nota()\Durata
  FineNota.l=InizioNota+DurataNota-1:VelocitaNota.b=Nota()\Velocita:CanaleNota.b=Nota()\Canale
  NotaSelezionata.b=Nota()\Selezionata
  If Modo=#SelezionaUnaNota
   XPosizioneNota=(XInizioAreaVisibile+(XTopo-16)*ScalaNote)/LarghezzaNota*LarghezzaNota
   YPosizioneNota=127-YInizioAreaVisibile-YTopo/8
   If XPosizioneNota>=InizioNota Then StessaNota OR %0001
   If XPosizioneNota<=FineNota Then StessaNota OR %0010
   If YPosizioneNota=Nota Then StessaNota OR %0100
   If CanaleSelezionato=CanaleNota Then StessaNota OR %1000
   If StessaNota=%1111
    If NotaSelezionata=True
     PushItem Nota()
     NotaSelezionataSpostata.b=MuoviLeNoteSelezionateConIlTopo{XPosizioneNota,YPosizioneNota}
     PopItem Nota():Nota=Nota()\Altezza:VelocitaNota=Nota()\Velocita:CanaleNota=Nota()\Canale
     If NotaSelezionataSpostata=False
      NoteSelezionate-1
      Nota()\Selezionata=False
     EndIf
    Else
     NotaPiuLunga.l=DurataNota
     NoteSelezionate+1
     Nota()\Selezionata=True
    EndIf
    For Suono.b=0 To 31
     If Suono(Suono)\Canale=CanaleNota Pop For:Goto FineCicloForUno
    Next Suono
FineCicloForUno
    If Suono<32
     If Suono(Suono)\Canale>0
      Periodo.l=PeriFreq(Nota)\Periodo
      If Periodo>=#PeriodoMinimo AND Periodo<65536
       SetPeriod Suono,Periodo:Sound Suono,3
      EndIf
     EndIf
    Else
     If MIDIAttivato=True
      ByteDiStatoMIDI.b=$90+CanaleNota-1
      WriteSerial 0,ByteDiStatoMIDI
      WriteSerial 0,Nota
      WriteSerial 0,VelocitaNota
      PushItem Nota()
     EndIf
    EndIf
    Pop While:Goto FineCicloWhile
   EndIf
  Else
   If NotaSelezionata=True
    Select Modo
    Case #CambiaIlCanaleDelleNoteSelezionate
     Nota()\Canale=CanaleSelezionato
    Case #AlzaDiUnSemitonoLeNoteSelezionate
     If Nota<127 Then Nota()\Altezza+1
    Case #AbbassaDiUnSemitonoLeNoteSelezionate
    If Nota>0 Then Nota()\Altezza-1
    Case #AlzaDiUnOttavaLeNoteSelezionate
     If Nota+12<=127 Then Nota()\Altezza+12
    Case #AbbassaDiUnOttavaLeNoteSelezionate
    If Nota-12>=0 Then Nota()\Altezza-12
    Case #SpostaAvantiNelTempoLeNoteSelezionate
     Nota()\Inizio+LarghezzaNota
     FineMotivo=Max(FineNota,FineMotivo)
    Case #SpostaIndietroNelTempoLeNoteSelezionate
     Nota()\Inizio-LarghezzaNota:If Nota()\Inizio<0 Then Nota()\Inizio=0
    Case #AllungaLaDurataDelleNoteSelezionate
     If DurataNota=6 Then Nota()\Durata=LarghezzaNota Else Nota()\Durata+LarghezzaNota
     FineMotivo=Max(InizioNota+Nota()\Durata,FineMotivo)
     NotaPiuLunga=Max(NotaPiuLunga,Nota()\Durata)
    Case #RiduciLaDurataDelleNoteSelezionate
     Nota()\Durata-LarghezzaNota:If Nota()\Durata<6 Then Nota()\Durata=6
     NotaPiuLunga=Max(NotaPiuLunga,Nota()\Durata)
    Case #EliminaLeNoteSelezionate
     If Note>0
      KillItem Nota()
      Note-1:NoteSelezionate-1
     EndIf
    Case #DeselezionaTutteLeNote
     NoteSelezionate-1
     Nota()\Selezionata=False
    Case #InvertiLaSelezioneDiTutteLeNote
     NoteSelezionate-1
     Nota()\Selezionata=False
    End Select
   Else
    Select Modo
    Case #SelezionaTutteLeNote
     NoteSelezionate+1
     Nota()\Selezionata=True
    Case #InvertiLaSelezioneDiTutteLeNote
     NoteSelezionate+1
     Nota()\Selezionata=True
    End Select
   EndIf
  EndIf
 Wend
FineCicloWhile
 If Modo=#SelezionaUnaNota AND StessaNota<%1111 AND CanaleSelezionato>0
  LastItem Nota()
  If AddItem(Nota())=True
   Nota()\Altezza=127-YInizioAreaVisibile-YTopo/8
   Nota()\Inizio=(XInizioAreaVisibile+(XTopo-16)*ScalaNote)/LarghezzaNota*LarghezzaNota
   Nota()\Durata=LarghezzaNota
   Nota()\Velocita=64
   Nota()\Canale=CanaleSelezionato
   Nota()\Selezionata=False
   Note+1
   For Suono.b=0 To 31
    If Suono(Suono)\Canale=CanaleSelezionato Then Pop For:Goto FineCicloForDue
   Next Suono
FineCicloForDue
   If Suono<32
    If Suono(Suono)\Canale>0
     Periodo=PeriFreq(Nota()\Altezza)\Periodo
     If Periodo>=#PeriodoMinimo AND Periodo<65536
      SetPeriod Suono,Periodo:Sound Suono,3
     EndIf
    EndIf
   Else
    If MIDIAttivato=True
     ByteDiStatoMIDI=$90+CanaleSelezionato-1
     WriteSerial 0,ByteDiStatoMIDI
     WriteSerial 0,Nota()\Altezza
     WriteSerial 0,Nota()\Velocita
     PushItem Nota()
    EndIf
   EndIf
   FineMotivo=Max(Nota()\Inizio+Nota()\Durata,FineMotivo)
  EndIf
 EndIf
 If Modo=#SelezionaLeNoteConLaStessaAltezzaEDelloStessoCanale
  ResetList Nota()
  Nota=127-YInizioAreaVisibile-YTopo/8
  While NextItem(Nota())=True
   If Nota()\Altezza=Nota AND Nota()\Canale=CanaleSelezionato
    NoteSelezionate+1
    Nota()\Selezionata=True
   EndIf
  Wend
 EndIf
 If NotaPiuLunga>0
  XMessaggio.w=16*16+8*3+8*4+8*6
  YMessaggio.w=AltezzaInternaFinestraEditoreNote-8
  WLocate XMessaggio,YMessaggio
  WColour 2,1
  Print SPACE$(80-XMessaggio/8)
  WLocate XMessaggio,YMessaggio
  Print "Len>":ConvertiIlTempoInBattute{NotaPiuLunga,True}
 EndIf
End Statement

;----------------------------------------------
;questa procedura si occupa di suonare le note
;this procedure plays notes
;---------------------------
Statement SuonaDallEditoreNote{}
SHARED AltezzaInternaFinestraEditoreNote
SHARED FineMotivo
SHARED MIDIAttivato
SHARED Nota()
SHARED PeriFreq()
SHARED ScalaNote
SHARED Suono()
SHARED TickPerCinquantesimoDiSecondo
 ResetTimer
 Tempo.l=0
 Voce.b=0
 VoceSpenta.b=%1111
 Repeat
  EventoFinestraEsecutoreNote=Event
  If EventoFinestraEsecutoreNote=#HaiChiusoLaFinestra Then Pop Repeat:Statement Return
  ResetList Nota()
  While NextItem(Nota())
   Nota.b=Nota()\Altezza:InizioNota.l=Nota()\Inizio:FineNota.l=InizioNota+Nota()\Durata-1
   VelocitaNota.b=Nota()\Velocita:CanaleNota=Nota()\Canale:NotaSelezionata.b=Nota()\Selezionata:NotaSuonata.b=Nota()\Suonata
   For Suono.b=0 To 31
    If Suono(Suono)\Canale=CanaleNota Then Pop For:Goto FineCicloForTre
   Next Suono
FineCicloForTre
   If Tempo>=InizioNota AND NotaSuonata=0
    If Suono<32
     Periodo.l=PeriFreq(Nota)\Periodo
     If Periodo>=#PeriodoMinimo AND Periodo<65536
      If VoceSpenta>0
       If VoceSpenta BitTst 0=True Then Voce=1:VoceSpenta BitClr 0:Goto SuonaCampione
       If VoceSpenta BitTst 1=True Then Voce=2:VoceSpenta BitClr 1:Goto SuonaCampione
       If VoceSpenta BitTst 2=True Then Voce=4:VoceSpenta BitClr 2:Goto SuonaCampione
       If VoceSpenta BitTst 3=True Then Voce=8:VoceSPenta BitClr 3
SuonaCampione
       SetPeriod Suono,Periodo:Sound Suono,Voce
       Frequenza.w=PeriFreq(Nota)\Frequenza
       Nota()\VoceUsata=Voce
      EndIf
     EndIf
    Else
     If MIDIAttivato=True
      ByteDiStatoMIDI.b=$90+CanaleNota-1
      WriteSerial 0,ByteDiStatoMIDI
      WriteSerial 0,Nota
      WriteSerial 0,VelocitaNota
      Messaggio$="Note>"+Str$(Nota)+""
     EndIf
    EndIf
    DisegnaNota{Nota,InizioNota,FineNota,CanaleNota,#SoloNotaPiena}
    XMessaggio.w=16*16+8*3+8*4+8*6
    YMessaggio.w=AltezzaInternaFinestraEditoreNote-8
    Messaggio$="Note>"+Str$(Nota)+" chan>"+Str$(CanaleNota)+" vel>"+Str$(VelocitaNota)
    Messaggio$+SPACE$(80-XMessaggio/8-Len(Messaggio$))
    WLocate XMessaggio,YMessaggio
    WColour CanaleNota-1,16-CanaleNota
    Print Messaggio$
    Nota()\Suonata=1
   EndIf
   NotaSuonata=Nota()\Suonata:VoceUsata.b=Nota()\VoceUsata
   If Tempo>FineNota AND NotaSuonata=1
    If Suono<32
     Quiet VoceUsata
     VoceSpenta OR VoceUsata
    Else
     If MIDIAttivato=True
      ByteDiStatoMIDI=$80+CanaleNota-1
      WriteSerial 0,ByteDiStatoMIDI
      WriteSerial 0,Nota
      WriteSerial 0,VelocitaNota
     EndIf
    EndIf
    Nota()\Suonata=2
    If NotaSelezionata=True
     DisegnaNota{Nota,InizioNota,FineNota,CanaleNota,#NotaSelezionata}
    Else
     DisegnaNota{Nota,InizioNota,FineNota,CanaleNota,#NotaNonSelezionata}
    EndIf
   EndIf
  Wend
  Tempo=Timer*TickPerCinquantesimoDiSecondo
 Until Tempo>=FineMotivo
 ResetList Nota()
 While NextItem(Nota())
  Nota()\Suonata=0
 Wend
 Quiet %1111
End Statement

Statement EditoreNote{}
SHARED AltezzaFonte
SHARED AltezzaInternaFinestraEditoreNote
SHARED AltezzaSchermo
SHARED AreaInvisibileDelleNote
SHARED AreaInvisibileDelTempo
SHARED CanaleSelezionato
SHARED FineMotivo
SHARED LarghezzaInternaFinestraEditoreNote
SHARED LarghezzaNota
SHARED LarghezzaSchermo
SHARED MIDIAttivato
SHARED Nota()
SHARED Note
SHARED NoteSelezionate
SHARED PeriFreq()
SHARED ScalaNote
SHARED TastoBackspace$
SHARED TastoCursoreDestra$
SHARED TastoCursoreGiu$
SHARED TastoCursoreSinistra$
SHARED TastoCursoreSu$
SHARED TastoEsc$
SHARED TipoDiNota()
SHARED XInizioAreaVisibile
SHARED XLunSlider
SHARED XTopo
SHARED YInizioAreaVisibile
SHARED YLunSlider
SHARED YTopo
 MenuTitle #ListaMenuEditoreNote,0,"Controls"
 MenuItem #ListaMenuEditoreNote,0,0,0,"Play"

 MenuTitle #ListaMenuEditoreNote,1,"Settings"
 MenuItem #ListaMenuEditoreNote,0,1,0,"Basic note"

 Use MenuList #ListaMenuEditoreNote

 XPosF.w=0:YPosF.w=AltezzaFonte+3:XLunF.w=LarghezzaSchermo:YLunF.w=AltezzaSchermo-(AltezzaFonte+3)
 TipoFinestra.l=#GadgetNascondiFinestra+#GadgetChiudiFinestra+#DammiZeroZero+#FinestraAttivata
 Window #FinestraEditoreNote,XPosF,YPosF,XLunF,YLunF,TipoFinestra,"Notes Editor",-1,-1
 LarghezzaInternaFinestraEditoreNote=InnerWidth
 AltezzaInternaFinestraEditoreNote=InnerHeight
 CloseWindow #FinestraEditoreNote

 AreaInvisibileDelleNote.b=128-(AltezzaInternaFinestraEditoreNote-8-8)/8

 XLunSlider=16
 YLunSlider=AltezzaInternaFinestraEditoreNote-8-8
 ImpostaSlider{1,0,0,"VERTICAL",0,#ListaGadgetEditoreNote}

 XLunSlider=LarghezzaInternaFinestraEditoreNote-16
 YLunSlider=8
 ImpostaSlider{2,16,AltezzaInternaFinestraEditoreNote-8-8,"HORIZONTAL",0,#ListaGadgetEditoreNote}

 Window #FinestraEditoreNote,XPosF,YPosF,XLunF,YLunF,TipoFinestra,"Notes Editor",-1,-1,#ListaGadgetEditoreNote
 SetMenu #ListaMenuEditoreNote

;-----------------------------------------------------------------------
;imposta il colore del testo nei menu' a quello piu' chiaro disponibile
;sets text menu colour to the brightest available
;-------------------------------------------------
 MenuColour 3

;----------------------------------------------
;imposta la fonte di caratteri per la finestra
;sets the font for the window
;-----------------------------
 WindowFont #FonteDiCaratteriTopaz8

 SetZone 0,0,0,LarghezzaInternaFinestraEditoreNote-1,AltezzaInternaFinestraEditoreNote-1-8-8
 Format "00"
 For Canale.b=1 To 16
  X0Canale=(Canale-1)*16:Y0Canale=AltezzaInternaFinestraEditoreNote-8
  WLocate X0Canale,Y0Canale
  If Canale=CanaleSelezionato Then WColour Canale-1,16-Canale Else WColour 1+Canale MOD 2,Canale MOD 2
  Print Canale
  SetZone Canale,X0Canale,Y0Canale,X0Canale+15,Y0Canale+7
 Next Canale
 Format ""
 WLocate X0Canale+16,Y0Canale:WColour 2,1:Print "All"
 SetZone 17,X0Canale+16,Y0Canale,X0Canale+16+23,Y0Canale+7
 WLocate X0Canale+16+8*3,Y0Canale:WColour 1,0:Print "None"
 SetZone 18,X0Canale+16+8*3,Y0Canale,X0Canale+16+8*3+8*4-1,Y0Canale+7
 WLocate X0Canale+16+8*3+8*4,Y0Canale:WColour 2,1:Print "Toggle"
 SetZone 19,X0Canale+16+8*3+8*4,Y0Canale,X0Canale+16+8*3+8*4+8*6-1,Y0Canale+7
 MostraLaScalaDelleNote{}
 SetZone 20,0,AltezzaInternaFinestraEditoreNote-8-8,15,AltezzaInternaFinestraEditoreNote-8-8+7
 Repeat
  If TastoCliccatoDeltopo.b<>5
   MostraLeNoteDiSfondoConILoroNomi{}
   MostraLeBattute{}
   MostraLeNote{}
   MostraSliderEditoreNote{}
  EndIf
  If NotaSelezionata.b=True
   PopItem Nota()
   If MIDIAttivato=True
    ByteDiStatoMIDI=$80+Nota()\Canale-1
    WriteSerial 0,ByteDiStatoMIDI
    WriteSerial 0,Nota()\Altezza
    WriteSerial 0,Nota()\Velocita
   EndIf
   NotaSelezionata=False
  EndIf
  WTitle Str$(NoteSelezionate)+" note selected of "+Str$(Note)
  EventoFinestraEditoreNote.l=WaitEvent
  Select EventoFinestraEditoreNote
  Case #HaiPremutoUnTastoDelTopo
   TastoCliccatoDelTopo=MButtons:TastoPremutoDelTopo=Joyb(0)
   XTopo=WMouseX:YTopo=WMouseY:ZonaTopo.b=Zone(XTopo,YTopo)
   Select TastoCliccatoDelTopo
   Case 1
    ResetTimer
    Select ZonaTopo
    Case 0
     Selezione{#SelezionaUnaNota}
    Case 1
     Format "00"
     If CanaleSelezionato>0
      WLocate (CanaleSelezionato-1)*16,AltezzaInternaFinestraEditoreNote-8
      WColour 1+CanaleSelezionato MOD 2,CanaleSelezionato MOD 2:Print Str$(CanaleSelezionato)
     EndIf
     CanaleSelezionato=1:Selezione{#CambiaIlCanaleDelleNoteSelezionate}
     WLocate (CanaleSelezionato-1)*16,AltezzaInternaFinestraEditoreNote-8
     WColour CanaleSelezionato-1,16-CanaleSelezionato:Print Str$(CanaleSelezionato)
     Format ""
    Case 2
     Format "00"
     If CanaleSelezionato>0
      WLocate (CanaleSelezionato-1)*16,AltezzaInternaFinestraEditoreNote-8
      WColour 1+CanaleSelezionato MOD 2,CanaleSelezionato MOD 2:Print Str$(CanaleSelezionato)
     EndIf
     CanaleSelezionato=2:Selezione{#CambiaIlCanaleDelleNoteSelezionate}
     WLocate (CanaleSelezionato-1)*16,AltezzaInternaFinestraEditoreNote-8
     WColour CanaleSelezionato-1,16-CanaleSelezionato:Print Str$(CanaleSelezionato)
     Format ""
    Case 3
     Format "00"
     If CanaleSelezionato>0
      WLocate (CanaleSelezionato-1)*16,AltezzaInternaFinestraEditoreNote-8
      WColour 1+CanaleSelezionato MOD 2,CanaleSelezionato MOD 2:Print Str$(CanaleSelezionato)
     EndIf
     CanaleSelezionato=3:Selezione{#CambiaIlCanaleDelleNoteSelezionate}
     WLocate (CanaleSelezionato-1)*16,AltezzaInternaFinestraEditoreNote-8
     WColour CanaleSelezionato-1,16-CanaleSelezionato:Print Str$(CanaleSelezionato)
     Format ""
    Case 4
     Format "00"
     If CanaleSelezionato>0
      WLocate (CanaleSelezionato-1)*16,AltezzaInternaFinestraEditoreNote-8
      WColour 1+CanaleSelezionato MOD 2,CanaleSelezionato MOD 2:Print Str$(CanaleSelezionato)
     EndIf
     CanaleSelezionato=4:Selezione{#CambiaIlCanaleDelleNoteSelezionate}
     WLocate (CanaleSelezionato-1)*16,AltezzaInternaFinestraEditoreNote-8
     WColour CanaleSelezionato-1,16-CanaleSelezionato:Print Str$(CanaleSelezionato)
     Format ""
    Case 5
     Format "00"
     If CanaleSelezionato>0
      WLocate (CanaleSelezionato-1)*16,AltezzaInternaFinestraEditoreNote-8
      WColour 1+CanaleSelezionato MOD 2,CanaleSelezionato MOD 2:Print Str$(CanaleSelezionato)
     EndIf
     CanaleSelezionato=5:Selezione{#CambiaIlCanaleDelleNoteSelezionate}
     WLocate (CanaleSelezionato-1)*16,AltezzaInternaFinestraEditoreNote-8
     WColour CanaleSelezionato-1,16-CanaleSelezionato:Print Str$(CanaleSelezionato)
     Format ""
    Case 6
     Format "00"
     If CanaleSelezionato>0
      WLocate (CanaleSelezionato-1)*16,AltezzaInternaFinestraEditoreNote-8
      WColour 1+CanaleSelezionato MOD 2,CanaleSelezionato MOD 2:Print Str$(CanaleSelezionato)
     EndIf
     CanaleSelezionato=6:Selezione{#CambiaIlCanaleDelleNoteSelezionate}
     WLocate (CanaleSelezionato-1)*16,AltezzaInternaFinestraEditoreNote-8
     WColour CanaleSelezionato-1,16-CanaleSelezionato:Print Str$(CanaleSelezionato)
     Format ""
    Case 7
     Format "00"
     If CanaleSelezionato>0
      WLocate (CanaleSelezionato-1)*16,AltezzaInternaFinestraEditoreNote-8
      WColour 1+CanaleSelezionato MOD 2,CanaleSelezionato MOD 2:Print Str$(CanaleSelezionato)
     EndIf
     CanaleSelezionato=7:Selezione{#CambiaIlCanaleDelleNoteSelezionate}
     WLocate (CanaleSelezionato-1)*16,AltezzaInternaFinestraEditoreNote-8
     WColour CanaleSelezionato-1,16-CanaleSelezionato:Print Str$(CanaleSelezionato)
     Format ""
    Case 8
     Format "00"
     If CanaleSelezionato>0
      WLocate (CanaleSelezionato-1)*16,AltezzaInternaFinestraEditoreNote-8
      WColour 1+CanaleSelezionato MOD 2,CanaleSelezionato MOD 2:Print Str$(CanaleSelezionato)
     EndIf
     CanaleSelezionato=8:Selezione{#CambiaIlCanaleDelleNoteSelezionate}
     WLocate (CanaleSelezionato-1)*16,AltezzaInternaFinestraEditoreNote-8
     WColour CanaleSelezionato-1,16-CanaleSelezionato:Print Str$(CanaleSelezionato)
     Format ""
    Case 9
     Format "00"
     If CanaleSelezionato>0
      WLocate (CanaleSelezionato-1)*16,AltezzaInternaFinestraEditoreNote-8
      WColour 1+CanaleSelezionato MOD 2,CanaleSelezionato MOD 2:Print Str$(CanaleSelezionato)
     EndIf
     CanaleSelezionato=9:Selezione{#CambiaIlCanaleDelleNoteSelezionate}
     WLocate (CanaleSelezionato-1)*16,AltezzaInternaFinestraEditoreNote-8
     WColour CanaleSelezionato-1,16-CanaleSelezionato:Print Str$(CanaleSelezionato)
     Format ""
    Case 10
     Format "00"
     If CanaleSelezionato>0
      WLocate (CanaleSelezionato-1)*16,AltezzaInternaFinestraEditoreNote-8
      WColour 1+CanaleSelezionato MOD 2,CanaleSelezionato MOD 2:Print Str$(CanaleSelezionato)
     EndIf
     CanaleSelezionato=10:Selezione{#CambiaIlCanaleDelleNoteSelezionate}
     WLocate (CanaleSelezionato-1)*16,AltezzaInternaFinestraEditoreNote-8
     WColour CanaleSelezionato-1,16-CanaleSelezionato:Print Str$(CanaleSelezionato)
     Format ""
    Case 11
     Format "00"
     If CanaleSelezionato>0
      WLocate (CanaleSelezionato-1)*16,AltezzaInternaFinestraEditoreNote-8
      WColour 1+CanaleSelezionato MOD 2,CanaleSelezionato MOD 2:Print Str$(CanaleSelezionato)
     EndIf
     CanaleSelezionato=11:Selezione{#CambiaIlCanaleDelleNoteSelezionate}
     WLocate (CanaleSelezionato-1)*16,AltezzaInternaFinestraEditoreNote-8
     WColour CanaleSelezionato-1,16-CanaleSelezionato:Print Str$(CanaleSelezionato)
     Format ""
    Case 12
     Format "00"
     If CanaleSelezionato>0
      WLocate (CanaleSelezionato-1)*16,AltezzaInternaFinestraEditoreNote-8
      WColour 1+CanaleSelezionato MOD 2,CanaleSelezionato MOD 2:Print Str$(CanaleSelezionato)
     EndIf
     CanaleSelezionato=12:Selezione{#CambiaIlCanaleDelleNoteSelezionate}
     WLocate (CanaleSelezionato-1)*16,AltezzaInternaFinestraEditoreNote-8
     WColour CanaleSelezionato-1,16-CanaleSelezionato:Print Str$(CanaleSelezionato)
     Format ""
    Case 13
     Format "00"
     If CanaleSelezionato>0
      WLocate (CanaleSelezionato-1)*16,AltezzaInternaFinestraEditoreNote-8
      WColour 1,0:Print Str$(CanaleSelezionato)
     EndIf
     CanaleSelezionato=13:Selezione{#CambiaIlCanaleDelleNoteSelezionate}
     WLocate (CanaleSelezionato-1)*16,AltezzaInternaFinestraEditoreNote-8
     WColour CanaleSelezionato-1,16-CanaleSelezionato:Print Str$(CanaleSelezionato)
     Format ""
    Case 14
     Format "00"
     If CanaleSelezionato>0
      WLocate (CanaleSelezionato-1)*16,AltezzaInternaFinestraEditoreNote-8
      WColour 1+CanaleSelezionato MOD 2,CanaleSelezionato MOD 2:Print Str$(CanaleSelezionato)
     EndIf
     CanaleSelezionato=14:Selezione{#CambiaIlCanaleDelleNoteSelezionate}
     WLocate (CanaleSelezionato-1)*16,AltezzaInternaFinestraEditoreNote-8
     WColour CanaleSelezionato-1,16-CanaleSelezionato:Print Str$(CanaleSelezionato)
     Format ""
    Case 15
     Format "00"
     If CanaleSelezionato>0
      WLocate (CanaleSelezionato-1)*16,AltezzaInternaFinestraEditoreNote-8
      WColour 1+CanaleSelezionato MOD 2,CanaleSelezionato MOD 2:Print Str$(CanaleSelezionato)
     EndIf
     CanaleSelezionato=15:Selezione{#CambiaIlCanaleDelleNoteSelezionate}
     WLocate (CanaleSelezionato-1)*16,AltezzaInternaFinestraEditoreNote-8
     WColour CanaleSelezionato-1,16-CanaleSelezionato:Print Str$(CanaleSelezionato)
     Format ""
    Case 16
     Format "00"
     If CanaleSelezionato>0
      WLocate (CanaleSelezionato-1)*16,AltezzaInternaFinestraEditoreNote-8
      WColour 1+CanaleSelezionato MOD 2,CanaleSelezionato MOD 2:Print Str$(CanaleSelezionato)
     EndIf
     CanaleSelezionato=16:Selezione{#CambiaIlCanaleDelleNoteSelezionate}
     WLocate (CanaleSelezionato-1)*16,AltezzaInternaFinestraEditoreNote-8
     WColour CanaleSelezionato-1,16-CanaleSelezionato:Print Str$(CanaleSelezionato)
     Format ""
    Case 17
     Selezione{#SelezionaTutteLeNote}
     NoteSelezionate=Note
    Case 18
     Selezione{#DeselezionaTutteLeNote}
     NoteSelezionate=0
    Case 19
     Selezione{#InvertiLaSelezioneDiTutteLeNote}
    Case 20
     ScalaNote+ScalaNote
     If ScalaNote=64 Then ScalaNote=1
     AreaInvisibileDelTempo=(FineMotivo-LarghezzaInternaFinestraEditoreNote-16)/ScalaNote
     MostraLaScalaDelleNote{}
    End Select
   Case 5
    TempoClic.l=Timer
    If TempoClic>35
     Selezione{#SelezionaLeNoteConLaStessaAltezzaEDelloStessoCanale}
    EndIf
   End Select
  Case #HaiPremutoUnGadget
   Select GadgetHit
   Case 1
    LunghezzaSlider=AltezzaInternaFinestraEditoreNote-8-8
    YInizioAreaVisibile=ValoreSlider{1,AreaInvisibileDelleNote,LunghezzaSlider,"VERTICAL",#ListaGadgetEditoreNote}
   Case 2
    LunghezzaSlider=LarghezzaInternaFinestraEditoreNote-16
    XInizioAreaVisibile=ValoreSlider{2,AreaInvisibileDelTempo,LunghezzaSlider,"HORIZONTAL",#ListaGadgetEditoreNote}
   End Select
  Case #HaiRilasciatoUnGadget
   Select GadgetHit
   Case 1
    LunghezzaSlider=AltezzaInternaFinestraEditoreNote-8-8
    YInizioAreaVisibile=ValoreSlider{1,AreaInvisibileDelleNote,LunghezzaSlider,"VERTICAL",#ListaGadgetEditoreNote}
   Case 2
    LunghezzaSlider=LarghezzaInternaFinestraEditoreNote-16
    XInizioAreaVisibile=ValoreSlider{2,AreaInvisibileDelTempo,LunghezzaSlider,"HORIZONTAL",#ListaGadgetEditoreNote}
   End Select
  Case #HaiAttivatoIlMenu
   If MenuHit=0 AND ItemHit=0 Then SuonaDallEditoreNote{}
   If MenuHit=1 AND ItemHit=0 Then CambiaLaNotaDiBase{}
  Case #HaiChiusoLaFinestra
   EsciDallEditoreNote.b=True
  Case #HaiPremutoUnTasto
   TastoPremuto$=LCase$(Inkey$)
   If TastoPremuto$=TastoEsc$ Then EsciDallEditoreNote=True
   TastiShiftPremuti.b=(Qualifier-$8000) AND %11
   If TastiShiftPremuti>0
    Select TastoPremuto$
    Case TastoBackspace$
     Selezione{#EliminaLeNoteSelezionate}
    Case TastoCursoreSu$
     Selezione{#AlzaDiUnOttavaLeNoteSelezionate}
    Case TastoCursoreGiu$
     Selezione{#AbbassaDiUnOttavaLeNoteSelezionate}
    Case TastoCursoreDestra$
     Selezione{#AllungaLaDurataDelleNoteSelezionate}
    Case TastoCursoreSinistra$
     Selezione{#RiduciLaDurataDelleNoteSelezionate}
    End Select
   Else
    Select TastoPremuto$
    Case TastoBackspace$
     If LastItem(Nota())=True
      If Nota()\Selezionata=True Then NoteSelezionate-1
      KillItem Nota():Note-1
     EndIf
    Case TastoCursoreSu$
     Selezione{#AlzaDiUnSemitonoLeNoteSelezionate}
    Case TastoCursoreGiu$
     Selezione{#AbbassaDiUnSemitonoLeNoteSelezionate}
    Case TastoCursoreDestra$
     Selezione{#SpostaAvantiNelTempoLeNoteSelezionate}
    Case TastoCursoreSinistra$
     Selezione{#SpostaIndietroNelTempoLeNoteSelezionate}
    End Select
   EndIf
  End Select
 Until EsciDallEditoreNote=True
 CloseWindow #FinestraEditoreNote
 Use Window #FinestraPrincipale
End Statement

;----------------------------------------------------
;questo programma puo' essere lanciato dal Workbench
n;this program can be launched from Workbench
;_-------------------------------------------
WBStartup

CaricaLaFonteDiCaratteriTopaz8{}

ImpostaIlParlatoDellAmiga{}

CaricaLeImmaginiDelleNote{}

ImpostaLoSchermo{}

CaricaLaStoriaDeiProgettiAperti{}

ImpostaLaFinestraPrincipale{}

CalcolaLaFrequenzaEdIlPeriodoDelleNote{}

AttivaMIDI{}

;-----------------
;clclo principale
;main loop
;----------
Repeat
;--------------------------------------------------------------
;questa variabile cattura gli eventi della finestra principale
;this variable catch main window events
;---------------------------------------
 EventoFinestraPrincipale.l=WaitEvent
 Select EventoFinestraPrincipale
;---------------------------
;se avete premuto un gadget
;if you have pressed a gadget
;-----------------------------
 Case #HaiPremutoUnGadget
  WLocate 0,AltezzaSchermo-11-16:WColour 1,0:Print SPACE$(80)
;------------------------------
;se avete rilasciato un gadget
;if you have released a gadget
;------------------------------
 Case #HaiRilasciatoUnGadget
  LunghezzaSlider=640
  BattuteAlMinuto=10+ValoreSlider{1,300-9,LunghezzaSlider,"HORIZONTAL",#ListaGadgetPrincipale}
  TickPerCinquantesimoDiSecondo=BattuteAlMinuto*192*4/3000
  WLocate 0,AltezzaSchermo-11-16:WColour 1,0:Print "BPM>",BattuteAlMinuto," Ticks/50th>",TickPerCinquantesimoDiSecondo
;--------------------------------------
;se avete attivato il menu' principale
;if you have activated main menu
;--------------------------------
 Case #HaiAttivatoIlMenu
  If MenuHit=0 AND ItemHit=1
   CaricaUnProgetto{}
   ImpostaLaFinestraPrincipale{}
  EndIf

  If MenuHit=1 AND ItemHit=0
   EditoreSuoni{}
  EndIf

  If MenuHit=1 AND ItemHit=1
   EditoreNote{}
  EndIf

  If MenuHit=2 AND ItemHit=0
   AttivaMIDI{}
  EndIf

  If MenuHit=2 AND ItemHit=1
   If Exists("ProjectsHistory")
    CancellaLaStoriaDeiProgettiAperti{}
    ImpostaLaFinestraPrincipale{}
   EndIf
  EndIf

;-------------------------------------------------------------------
;se avete scelto la voce Quit dal menu' Project attiva la variabile
; EsciDalProgramma
;if you have selected voice Quit from Project menu activates EsciDalProgramma
; flag
;------
  If MenuHit=0 AND ItemHit=VoceMenuEsci Then EsciDalProgramma=True
;--------------------------
;se avete premuto un tasto
;if you have pressed a key
;--------------------------
 Case #HaiPremutoUnTasto
;--------------------------------------------
;questa variabile memorizza il tasto premuto
;this variable store key pressed
;--------------------------------
  TastoPremuto$=Inkey$
;-------------------------------------------------------------------
;se avete premuto il tasto Esc attiva la variabile EsciDalProgramma
;if you have typed Esc key activates EsciDalProgramma flag
;----------------------------------------------------------
  If TastoPremuto$=TastoEsc$ Then EsciDalProgramma=True
 End Select
;----------------------------------------------------------------
;ripete il ciclo se la variabile EsciDalProgramma e' disattivata
;repeats loop if EsciDalProgramma flag is deactivated
;-----------------------------------------------------
Until EsciDalProgramma=True
;----------------------
;fine del programma
;program end
;------------
End
