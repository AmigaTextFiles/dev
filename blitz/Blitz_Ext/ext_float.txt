
; Here are some revised extended float functions for Blitz based on
; sane2float64{} from "SANE_FPU.ab2" from the archive "SANE.lha" from the
; Aminet.

; The original sane2float{} works quite well with FPU especially if a double
; is used. But for NoFPU there is room for improvement.


; This is an improved sane2float{} for NoFPU and should not be used with an
; FPU compile.
 
Function.f sane2float {sane$}
   sbin$ = Bin$(Peek.l(&sane$)) + Bin$(Peek.l(&sane$+4)) + Right$(Bin$(Peek.l(&sane$+6)),16)
   sign$ = Left$(sbin$,1)
   exponent.l = Vallong( "%"+Mid$(sbin$, 2, 15) ) - 15360  ; 16383 - 1023 = 15360
   exponent$ = Right$(Bin$(exponent),11) : fraction_bin$ = Mid$(sbin$, 18, 52)
   val1.l=Vallong("%"+sign$+exponent$+Left$(fraction_bin$,20))
   val2.l=Vallong("%"+Right$(fraction_bin$,32))
   ieee.l=IEEEDPTieee_ ( val1 , val2 ) : ffp.l=SPFieee_(ieee)
   Function Return Peek.f(&ffp)
End Function


; returns a 64bit IEEE string from an 80bit extended float string
; This would be used when even more precision is required by the
; NoFPU author in conjuction with a package such as "Blitz_Dub.lha"
; on the aminet. Although this could be used by FPU users the orignal
; sane2float{} and sane2float64{} should be enough & equivalent.

Function.s sane2float_S {sane$}
   sbin$ = Bin$(Peek.l(&sane$)) + Bin$(Peek.l(&sane$+4)) + Right$(Bin$(Peek.l(&sane$+6)),16)
   sign$ = Left$(sbin$,1)
   exponent.l = Vallong( "%"+Mid$(sbin$, 2, 15) ) - 15360  ; 16383 - 1023 = 15360
   exponent$ = Right$(Bin$(exponent),11) : fraction_bin$ = Mid$(sbin$, 18, 52)
   val1.l=Vallong("%"+sign$+exponent$+Left$(fraction_bin$,20))
   val2.l=Vallong("%"+Right$(fraction_bin$,32))
   Function Return Mkl$(val1)+Mkl$(val2)
End Function


; This function can be used with FPU and NoFPU compiles, and is quite often
; more than adequate for reading only the integer frequencies of AIFF files.

Function.l sane2long {sane$}
   sbin$ = Bin$(Peek.l(&sane$)) + Bin$(Peek.l(&sane$+4)) + Right$(Bin$(Peek.l(&sane$+6)),16)
   sign$ = Left$(sbin$,1)
   exponent.l = Vallong( "%"+Mid$(sbin$, 2, 15) ) - 15360  ; 16383 - 1023 = 15360
   exponent$ = Right$(Bin$(exponent),11) : fraction_bin$ = Mid$(sbin$, 18, 52)
   val1.l=Vallong("%"+sign$+exponent$+Left$(fraction_bin$,20))
   val2.l=Vallong("%"+Right$(fraction_bin$,32))
   Function Return IEEEDPFix_(val1, val2)
End Function


; Classic Blitz users can replace "Vallong" with an alternative such as
; "ValLM{}" or "VaLong{}" .

