.OpenScreen
WBenchToFront_
pic$="BB2.title"
  ;------- Open a screen & show the title pic ----------------
  ; the screen is opened at the back of the display
  ; when it is ready it is popped to the front
  ; so you don't see it being made up


;=============== ENABLE THIS PART TO SEE TITLE PIC ==============
  If Display=0
    Gosub getScreenFont
  EndIf

  If graphic$<>"ECS"
    ILBMInfo pic$
    pwidth.w=ILBMWidth
    pheight.w=ILBMHeight
    pdepth.b=ILBMDepth
    BitMap 0,pwidth,pheight,pdepth
    LoadBitMap 0,pic$,1
    Dim Ptags.TagItem(7)
    Ptags(0)\ti_Tag=#SA_DisplayID,$8000|$4
    Ptags(1)\ti_Tag=#SA_Behind,True
    Ptags(2)\ti_Tag=#SA_Depth,pdepth
    Ptags(3)\ti_Tag=#SA_Width,pwidth
    Ptags(4)\ti_Tag=#SA_Height,pheight
    Ptags(5)\ti_Tag=#SA_Pens,?DriPens        ;List of 13 Dripens
    Ptags(6)\ti_Tag=#TAG_DONE

    ScreenTags 1,"",&Ptags(0)

    Use Palette 1                          ;use it's colours
    Free Palette 1                         ;free the palette
    Window 2,0,0,pwidth,pheight,$1000|$800,"",0,0

    BitMaptoWindow 0,2                     ;put the pic in the window
    Free BitMap 0
    Use Window 2
    AbsMouse pwidth,pheight                ;hide the pointer
    ResetTimer
    ShowScreen 1                           ;OK, show it now
    Repeat
    Until Ticks=>180
  EndIf


;==================================================================

  ;------- now make the "Program's" screen --------------------
  ; again, we make it up in back and pop to front when ready
.OpenScreen2
  If Display=0
  newsetup.b=1
  AbsMouse 320,200
  Gosub Prefs                            ;you're first time, eh?
  savemode.b=0
EndIf

  Gosub getScreenFont
  name$="BeatBox2"
  scrname$="BeatBox2"
  *fn=Addr IntuiFont (0)

  Dim SCRtags.TagItem(16)
  SCRtags(0)\ti_Tag=#SA_DisplayID,Display
  SCRtags(1)\ti_Tag=#SA_Overscan,Oscan
  SCRtags(2)\ti_Tag=#SA_Depth,Dpth
  SCRtags(3)\ti_Tag=#SA_Width,Width
  SCRtags(4)\ti_Tag=#SA_Height,Height
  SCRtags(5)\ti_Tag=#SA_AutoScroll,1         ;autoscroll is on!
  SCRtags(6)\ti_Tag=#SA_Pens,?DriPens        ;List of 13 Dripens
  SCRtags(7)\ti_Tag=#SA_Type,#PUBLICSCREEN
  SCRtags(8)\ti_Tag=#SA_PubSig,0
  SCRtags(9)\ti_Tag=#SA_PubTask,FindTask_(0)
  SCRtags(10)\ti_Tag=#SA_Behind,1
  SCRtags(11)\ti_Tag=#SA_PubName,&scrname$
  SCRtags(12)\ti_Tag=#SA_Title,&name$
  SCRtags(13)\ti_Tag=#SA_ShowTitle,0
  SCRtags(14)\ti_Tag=#SA_Font,*fn
  SCRtags(15)\ti_Tag=#TAG_DONE

 ;ScreenTags 0,"BeatBox2",&SCRtags(0)

  *MyScreen=OpenScreenTagList_(0,&SCRtags(0))

  If *MyScreen
    PubScreenStatus_ *MyScreen,0  ; PUBLICSCREEN !
    FindScreen 0,"BeatBox2"       ; so Blitz can use the screen
  Else
    WbToScreen 0
    Window 0,0,0,1,1,$1000,"",0,0
    CatchDosErrs
    Request gm$,"Fatal Program Error!|Unable to Open Screen!|Program aborted","Damn!"
    End
  EndIf

  Use Palette 0
