
; Show system preferences information

; By James L Boyd - jamesboyd@all-hail.freeserve.co.uk

; create executable, run it as (eg.) :

; CLI> GetPrefs >Ram:PrefsInfo.txt

; The information is apparently not 100% reliable,
; but the includes say to see the SetPrefs_() autodocs
; for what you can't rely on. Everything checked out
; fine with my Printer and PrinterGfx program (Sys:Prefs/)
; settings though :)

DEFTYPE.Preferences *ReturnedPrefs, PrefsBuffer

*ReturnedPrefs=GetPrefs_(&PrefsBuffer,SizeOf.Preferences)

NPrint ""
NPrint "System Preferences (selected entries) :"
NPrint "---------------------------------------"
NPrint ""

.DefFont
NPrint "System default font height  : ",*ReturnedPrefs\FontHeight

NPrint ""

.PrinterPort

Print "Printer port                : "

Select *ReturnedPrefs\PrinterPort
  Case #PARALLEL_PRINTER
    NPrint "Parallel"
  Case #SERIAL_PRINTER
    NPrint "Serial"
  Default
    NPrint "Error"
End Select

NPrint ""

.BaudRate

Print "Baud rate                   : "

Select *ReturnedPrefs\BaudRate
  Case #BAUD_110
    NPrint "110 bytes/second"
  Case #BAUD_300
    NPrint "300 bytes/second"
  Case #BAUD_1200
    NPrint "1200 bytes/second"
  Case #BAUD_2400
    NPrint "2400 bytes/second"
  Case #BAUD_4800
    NPrint "4800 bytes/second"
  Case #BAUD_9600
    NPrint "9600 bytes/second"
  Case #BAUD_19200
    NPrint "19200 bytes/second"
  Case #BAUD_MIDI
    NPrint "Default MIDI setting (?!)"
  Default
    NPrint "Error!"
End Select

NPrint ""

.TimingRates

Print "Key repeat speed            : "
NPrint *ReturnedPrefs\KeyRptSpeed\tv_secs,".",1000000/*ReturnedPrefs\KeyRptSpeed\tv_micro," seconds"

Print "Key repeat delay            : "
NPrint *ReturnedPrefs\KeyRptDelay\tv_secs,".",1000000/*ReturnedPrefs\KeyRptDelay\tv_micro," seconds"

Print "Double click timeout        : "
NPrint *ReturnedPrefs\DoubleClick\tv_secs,".",1000000/*ReturnedPrefs\DoubleClick\tv_micro," seconds"

NPrint ""

.Pointer

NPrint "Pointer data location       : ",&*ReturnedPrefs\PointerMatrix
NPrint "------> size                : ",# POINTERSIZE," bytes"
NPrint "------> X hotspot           : ",*ReturnedPrefs\XOffset
NPrint "------> Y hotspot           : ",*ReturnedPrefs\YOffset

; Not sure how to deal with this :

; NPrint "------> colour 0            : ",*ReturnedPrefs\color17
; NPrint "------> colour 1            : ",*ReturnedPrefs\color18
; NPrint "------> colour 2            : ",*ReturnedPrefs\color19

NPrint "------> ticks (sensitivity) : ",*ReturnedPrefs\PointerTicks

NPrint ""

.Printer

Print "Printer type                : "

Select *ReturnedPrefs\PrinterType

  Case #CUSTOM_NAME
    NPrint "Custom"
  Case #ALPHA_P_101
    NPrint "Alpha P-101"
  Case #BROTHER_15XL
    NPrint "Brother 15XL"
  Case #CBM_MPS1000
    NPrint "Commodore MPS1000"
  Case #DIAB_630
    NPrint "DIAB 630"
  Case #DIAB_ADV_D25
    NPrint "DIAB ADV D25"
  Case #DIAB_C_150
    NPrint "DIAB C150"
  Case #EPSON
    NPrint "Epson"
  Case #EPSON_JX_80
    NPrint "Epson JX80"
  Case #OKIMATE_20
    NPrint "Okimate 20"
  Case #QUME_LP_20
    NPrint "Qume LP20"
  Case #HP_LASERJET
    NPrint "Hewlett-Packard LaserJet"
  Case #HP_LASERJET_PLUS
    NPrint "Hewlett-Packard LaserJet+"
  Default
    NPrint "Not in ye olde Commodore listing!"
End Select

NPrint "Printer filename            : ",Peek$(&*ReturnedPrefs\PrinterFilename)

Print "Printer pitch               : "

Select *ReturnedPrefs\PrintPitch
  Case #PICA
    NPrint "Pica"
  Case #ELITE
    NPrint "Elite"
  Case #FINE
    NPrint "Fine"
  Default
    NPrint "Error!"
End Select

Print "Printer quality             : "

Select *ReturnedPrefs\PrintQuality
  Case #DRAFT
    NPrint "Draft"
  Case #LETTER
    NPrint "Letter"
  Default
    NPrint "Error!"
End Select

Print "Printer LPI                 : "

Select *ReturnedPrefs\PrintSpacing
  Case #SIX_LPI
    NPrint "6 lines per inch"
  Case #EIGHT_LPI
    NPrint "8 lines per inch"
  Default
    NPrint "Error!"
End Select

NPrint "Left margin (characters)    : ",*ReturnedPrefs\PrintLeftMargin
NPrint "Right margin (characters)   : ",*ReturnedPrefs\PrintRightMargin

Print "Positive/negative printing  : "

Select *ReturnedPrefs\PrintImage
  Case #IMAGE_POSITIVE
    NPrint "Positive"
  Case #IMAGE_NEGATIVE
    NPrint "Negative"
  Default
    NPrint "Error!"
End Select

Print "Print aspect                : "

Select *ReturnedPrefs\PrintAspect
  Case #ASPECT_HORIZ
    NPrint "Horizontal"
  Case #ASPECT_VERT
    NPrint "Vertical"
  Default
    NPrint "Error!"
End Select

Print "Print shade                 : "

Select *ReturnedPrefs\PrintShade
  Case #SHADE_BW
    NPrint "Black and white"
  Case #SHADE_GREYSCALE
    NPrint "Greyscale"
  Case #SHADE_COLOR
    NPrint "Colour"
  Default
    NPrint "Error!"
End Select

NPrint "Print threshold             : ",*ReturnedPrefs\PrintThreshold

Print "Paper size                  : "

Select *ReturnedPrefs\PaperSize
  Case #US_LETTER
    NPrint "US Letter"
  Case #US_LEGAL
    NPrint "US Legal"
  Case #N_TRACTOR
    NPrint "Narrow tractor feed"
  Case #W_TRACTOR
    NPrint "Wide tractor feed"
  Case #CUSTOM
    NPrint "Custom size"
  Case #EURO_A0
    NPrint "Euro A0 : 841 x 1189 mm"
  Case #EURO_A1
    NPrint "Euro A1 : 594 x 841 mm"
  Case #EURO_A2
    NPrint "Euro A2 : 420 x 594 mm"
  Case #EURO_A3
    NPrint "Euro A3 : 297 x 420 mm"
  Case #EURO_A4
    NPrint "Euro A4 : 210 x 297 mm"
  Case #EURO_A5
    NPrint "Euro A5 : 148 x 210 mm"
  Case #EURO_A6
    NPrint "Euro A6 : 105 x 148 mm"
  Case #EURO_A7
    NPrint "Euro A7 : 74 x 105 mm"
  Case #EURO_A8
    NPrint "Euro A8 : 52 x 74 mm"
  Default
    NPrint "Error!"
End Select

NPrint "Paper length (lines)        : ",*ReturnedPrefs\PaperLength

Print "Paper type                  : "

Select *ReturnedPrefs\PaperType
  Case #FANFOLD
    NPrint "Fanfold (continuous)"
  Case #SINGLE
    NPrint "Single sheet"
  Default
    NPrint "Error!"
End Select

NPrint ""
NPrint "And this is where I gave up - see Blitz2:Developers/AmigaIncludes/Intuition/Preferences.bb2 !"
NPrint ""
NPrint "You should be able to use SetPrefs_() to change this stuff to suit..."
NPrint ""

End
