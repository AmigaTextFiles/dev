
;original code BY LEE PACKHAM
;modified by Curt Esser
;last modified Jan 1 1999
;needs amigalibs.res

;gets ALL the arguments from a WB run program
;or the pointer to the argument string if started from the CLI

;========================================================

; ***************** IMPORTANT!!!!! *********************

;NOTES:  Do NOT use "WBstartup" in your code
;        if you use this function!!!!!!!!!!!!!!!!!!!!!

;        This function will NOT work from inside Blitz!!!!
;        (because the startup message never arrives)
;        you must create an executable and run it to test

;  Click program's icon first
;  then shift-click the arguments

;  if an argument returns "" as the name, it means that
;  argument was a directory or a device...

;this will also work if run as the default tool
;of a project icon

;there is one bug!  If this program is compiled and saved
;in the "path", for example in C:
;and called by a project icon with only the program name
;as the default tool, and not the program's full path -
;the path returned for this program will incorrectly
;be reported as the same as the project's path...

;========================================================
NEWTYPE .Arg      ;structure to store arguments:
Name.s            ;argument (file) name
Dir.s             ;argument path
End NEWTYPE



;--------------------------------------------------------

*myproc.Process= FindTask_(O)      ;find ourselves  :)



If *myproc\pr_CLI=0   ;OK, program was run from WB

  cli.b=0

  WaitPort_( *myproc\pr_MsgPort)   ;wait for WB's message

  *wbmsg.WBStartup=GetMsg_(*myproc\pr_MsgPort)

  numargs.l = *wbmsg\sm_NumArgs    ;the number of arguments given
                                   ;in the case of a WB start, each argument
                                   ;will be strings containing
                                   ;the path of each argument and
                                   ;it's filename (if the argument was a file)
                                   ;the filename will be blank if the argument
                                   ;was a volume or directory
  Dim Args.Arg(numargs)

  *first.WBArg= *wbmsg\sm_ArgList

  MaxLen templock$ = 255

  numargs-1                        ;number of real arguments
                                   ;first arg is our own name
                                   ;and the path we ran from

  For i = 0 To numargs
    *firstloc= *first\wa_Name
    NameFromLock_ *first\wa_Lock,&templock$,255
    tempdir$ = Peek$(&templock$)
    If Right$(tempdir$,1)<>":"
      tempdir$+"/"
    EndIf
    tempname$ = Peek$(*firstloc)
    Args(i)\Name = tempname$
    Args(i)\Dir = tempdir$
    *first + SizeOf .WBArg         ;set pointer to next entry
  Next i

  ReplyMsg_(*wbmsg)                ;OK, we MUST reply to WB!!!!

  Repeat
    *wbmsg.WBStartup=GetMsg_(*myproc\pr_MsgPort)
  Until *wbmsg=0

Else

  cli.b=1                          ;our program was run from a CLI
  arg$=Peek.s(*myproc\pr_Arguments)
EndIf


;======================================================================
;========== EXAMPLE ===================================================

WBenchToFront_
WbToScreen 0

Window 0,70,10,500,180,$400|$1000|$8,"Wb Start",1,0


If cli=0
  NPrint " Started from WB..."
  NPrint " Program : ",Args(0)\Name
  NPrint " Run from: ",Args(0)\Dir
  NPrint " "
  NPrint " -----------------------------"
  NPrint " ",numargs," arguments recieved:"

  If numargs>0

    NPrint " "
    For i = 1 To numargs
      VWait 100
      NPrint "#",i
      WPrintScroll
      NPrint " Name : ",Args(i)\Name
      WPrintScroll
      NPrint " Dir  : ",Args(i)\Dir
      WPrintScroll
      NPrint " "
      WPrintScroll
    Next
  EndIf
Else
  NPrint " We were run from CLI"
  NPrint " With this command:"
  NPrint arg$
EndIf
NPrint " "
WPrintScroll
NPrint " Press close gadget to end"
WPrintScroll
Repeat
  ev.l=WaitEvent
  Until ev=$200 AND EventWindow=0
End

