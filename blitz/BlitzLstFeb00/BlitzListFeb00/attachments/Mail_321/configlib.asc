
; Looks more complicated than it is: delete the comments to
; see how little there really is ;)

; It's easier than writing & parsing a prefs file yourself,
; cos it handles all the updating of data in the file - no
; deleting data and rewriting it, etc...

; Basically, you decide on sections for your data.
; In this example, there's one section, "SCREENSTUFF",
; dealing with fictional screenmode data (mode & depth).

; Decide on a section name, eg SCREENSTUFF for
; screenmode and depth:

  smsection$  =  "SCREENSTUFF"  ; screenmode section of prefs

; Decide on the items in that section, eg SCREENMODE & DEPTH:

  smitem$     =  "SCREENMODE"   ; screenmode item
  dpitem$     =  "DEPTH"        ; depth item

; And the data that's going into each item:

  smdata$     =  "Hi-Res Laced" ; screenmode data
  dpdata.w    =  8              ; depth data

; Finally, a config filename:

  configfile$="ram:test.config" ; config file

; Open the library:

*config.Library=OpenLibrary_ ("config.library",2)

If *config                      ; library opened OK

  DEFTYPE.b success             ; used to get the value returned

; WriteConfig returns 1 for success, 0 for fail, so if we just
; add 'em up, we can tell how many worked. If not all of them
; worked, our config file's a failure anyway :)

; Write screenmode data:

  success=WriteConfig_ (&configfile$,&smsection$,&smitem$,&smdata$) ; put screenmode data in screenmode item in SM section

; Write depth data:

  success+WriteConfigNumber_ (&configfile$,&smsection$,&dpitem$,dpdata)

; (NOTE: the docs say that if you need to write a float (eg. 123.45),
;  you should just put it in a string and use WriteConfig)...

  If success=2                  ; both worked

    smdata$=""                  ; reset for demo...
    smitembuff$=SPACE$(255)     ; item read buffer
    smdef$="DATA NOT FOUND"     ; returned if item doesn't exist

    dpdata=0                    ; reset for demo...
    dpdef.b=-1                  ; returned if item doesn't exist

;   No need to reset success here - WriteConfig
;   will set it to 0 or 1 anyway:

    success=ReadConfig_(&configfile$,&smsection$,&smitem$,&smitembuff$,255,&smdef$)
    dpdata =ReadConfigNumber_(&configfile$,&smsection$,&dpitem$,def)

; NOTE: ReadConfigNumber is assumed never to fail. Unlike all
; the other functions, it doesn't return success or failure; it
; returns the number found in the item being read. If there's
; text in that item, it returns 0.

    If success=1
      NPrint "Screenmode: "+smitembuff$ ; returns full buffer (ie 255 characters if that's what you set!)
      NPrint "Depth: "+Str$(dpdata)
    Else NPrint "","Failed!"
    EndIf

  Else NPrint "Couldn't write all config data!"
  EndIf

  CloseLibrary_ *config

Else NPrint "Couldn't open config.library v2+!"
EndIf

MouseWait
End
