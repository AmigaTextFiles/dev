
; Dynamic WBstartup ][ routine

; (C) 2014  Lorence Lombardo

; Date commenced:-  3-Mar-2014

; This should work fine under the AB2 environment from here:-
; http://www.amiforce.de/amiblitz/ab_downloads.php

; Also tested under AB3.

; NB: testing has been limited.

; Requires "LotanArgs.obj"

; replace "PeekS" with "Peeks$" if you dont have it.

; This routine works OK providing the icon priority is
; not raised beyond zero, otherwise ocasional crashes
; may occure when started from WB, which seems to be a
; "wbmessage" issue.


wb.l=wbmessage

Dim List arg$(0)

; The following function has been taken from "cli_args_d.ab2" from here:-
; http://aminet.net/package/dev/basic/cli_args

Function.l NumArgs{}
   SHARED arg$()
   c.l=0: qt.b=0: qt$=Chr$(34): b$=" ": sc.l=0: l$=Chr$(10): arg_ptr.l=GetArgStr_
   Repeat
      a$=PeekS(arg_ptr+sc,1): sc+1
      If a$=qt$
         If qt=0 Then qt=1: c=c+1: AddLast arg$() Else qt=0: b$=" "
      Else
         If b$=" " AND a$<>" " AND a$<>l$ AND qt=0 Then c=c+1: AddLast arg$()
         b$=a$: If a$=" " AND qt=0 Then a$=""
         If a$<>l$ AND c>0 Then arg$()=arg$()+a$
      EndIf
   Until a$=l$
   ResetList arg$(): Function Return c
End Function


Function.l NumArgsWB{}
   SHARED arg$() : numargs.l=WBArgs-1
   For x.l=1 To numargs
      AddLast arg$() : arg$()=wArg(x)
   Next x
   ResetList arg$(): Function Return numargs
End Function


;=======================================================================================


If wb=0 Then numargs.l=NumArgs{} Else numargs.l=NumArgsWB{}

msg$="WB = "+Str$(wb)+Chr$(10)+"Args = "+Str$(numargs)

ret.b=RTEZRequest(" WB2/CLI",msg$,"_ok")

For i.l=1 To numargs
   NextItem arg$() : msg$=Str$(i)+" - "+arg$()
   ret.b=RTEZRequest(" WB2/CLI",msg$,"_Next|_Quit")
   If ret=0 Then i=numargs
Next i


End



