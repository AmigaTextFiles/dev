
; Dynamic WBstartup routine

; (C) 2011-2014  Lorence Lombardo

; Date commenced:-  15-Jan-2011
; Last modified:-    5-Mar-2014

; This should work fine under the AB2 environment from here:-
; http://www.amiforce.de/amiblitz/ab_downloads.php

; Also tested under AB3.

; NB: testing has been limited.

; Possibly ReplyMsg_ should be sandwiched between Forbid_ & Permit_ ?

; replace "PeekS" with "Peeks$" if you dont have it.

; Unfortunately ocasioanlly crashes when started from WB.
; This perhaps in part needs to be reimplemented as an ".obj" ?


Dim List arg$(0)

Function.l WBstart{}
   SHARED arg$(), numargs.l
   *proc.Process=FindTask_(0) : wb.l=0 : numargs=0
   If *proc\pr_CLI=0   ; <-- OK, program was run from WB
      WaitPort_( *proc\pr_MsgPort)
      *wbmsg.WBStartup=GetMsg_(*proc\pr_MsgPort) : wb_sz.l=SizeOf.WBArg
      numargs=*wbmsg\sm_NumArgs-1 : *arg.WBArg=*wbmsg\sm_ArgList+wb_sz
      str.l=AllocMem_(4096, #MEMF_PUBLIC)
      If str
         For x.l=1 To numargs
            NameFromLock_ *arg\wa_Lock, str, 4096
            dir$=Peek$(str) : If Right$(dir$,1)<>":" Then dir$=dir$+"/"
            AddLast arg$() : arg$()=dir$+Peek$(*arg\wa_Name)
            *arg+wb_sz
         Next x
         FreeMem_ str, 4096
      EndIf
      ReplyMsg_(*wbmsg)
      ResetList arg$() : wb=*wbmsg
   EndIf
   Function Return wb
End Function


; The following function has been taken from "cli_args_d.ab2" from here:-
; http://aminet.net/package/dev/basic/cli_args

Function.l NumArgs{}
   SHARED arg$()
   c.l=0: qt.b=0: qt$=Chr$(34): b$=" ": sc.l=0: l$=Chr$(10): arg_ptr.l=GetArgStr_
   Repeat
      a$=PeekS(arg_ptr+sc,1): sc+1
      If a$=qt$
         If qt=0 Then qt=1: c=c+1: AddLast arg$() Else qt=0: b$=" "
      Else
         If b$=" " AND a$<>" " AND a$<>l$ AND qt=0 Then c=c+1: AddLast arg$()
         b$=a$: If a$=" " AND qt=0 Then a$=""
         If a$<>l$ AND c>0 Then arg$()=arg$()+a$
      EndIf
   Until a$=l$
   ResetList arg$(): Function Return c
End Function


;=======================================================================================


wb.l=WBstart{}

If wb=0 Then numargs.l=NumArgs{}

msg$="WB = "+Str$(wb)+Chr$(10)+"Args = "+Str$(numargs)

ret.b=RTEZRequest(" WB/CLI",msg$,"_ok")

For i.l=1 To numargs
   NextItem arg$() : msg$=Str$(i)+" - "+arg$()
   ret.b=RTEZRequest(" WB/CLI",msg$,"_Next|_Quit")
   If ret=0 Then i=numargs
Next i


End



