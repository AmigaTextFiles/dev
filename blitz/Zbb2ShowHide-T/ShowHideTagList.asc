;** The ShowHide demo shows how to hide and show objects.
;Based on the example ShowHide.c by Stefan Stuntz
;Adapted in Blitz Basic 2 by Erwan Fouret
;Resorted by Benoit Socias

INCDIR "Blitz2:Include/"
INCLUDE mui.bb2

#SIGBREAKF_CTRL_C=(1 LSL 12)

DEFTYPE.b *app,*win,*cm1,*cm2,*cm3,*cm4,*cm5,*bt1,*bt2,*bt3,*bt4,*bt5;

;** Application begin
InitTagList 0,7
AddTags 0,#MUIA_Application_Title,Null("ShowHide")
AddTags 0,#MUIA_Application_Version,Null("$VER: ShowHide 19.5 (12.02.97)")
AddTags 0,#MUIA_Application_Copyright,Null(Chr$(167)+"1992/93, Stefan Stuntz")
AddTags 0,#MUIA_Application_Author,Null("Stefan Stuntz")
AddTags 0,#MUIA_Application_Description,Null("Show object hiding.")
AddTags 0,#MUIA_Application_Base,Null("SHOWHIDE")
  ;** Window begin
  InitTagList 1,3
  AddTags 1,#MUIA_Window_Title,Null("Show & Hide")
  AddTags 1,#MUIA_Window_ID, $53484844   ; "SHHD"
    ;** Horiz group begin
    InitTagList 2,2
    AddTags 2,#MUIA_Group_Horiz,MUI_TRUE
      ;** Frame begin
      InitTagList 3,3
      AddTags 3,#MUIA_Frame,#MUIV_Frame_Group
        ;** 5 CM Horiz begin
        InitTagList 4,7
        AddTags 4,#MUIA_Group_Horiz,MUI_TRUE
        AddTags 4,#MUIA_Weight,0
          *cm1=CheckMark(MUI_TRUE)
        AddTags 4,#Child,*cm1
          *cm2=CheckMark(MUI_TRUE)
        AddTags 4,#Child,*cm2
          *cm3=CheckMark(MUI_TRUE)
        AddTags 4,#Child,*cm3
          *cm4=CheckMark(MUI_TRUE)
        AddTags 4,#Child,*cm4
          *cm5=CheckMark(MUI_TRUE)
        AddTags 4,#Child,*cm5
        ;** 5 CM Horiz end
      AddTags 3,#Child,MUI_NewObjectA_("Group.mui",TagList(4))
        ;** 5 BT Horiz begin
        InitTagList 4,6
          *bt1=SimpleButton("Button 1")
        AddTags 4,#Child,*bt1
          *bt2=SimpleButton("Button 2")
        AddTags 4,#Child,*bt2
          *bt3=SimpleButton("Button 3")
        AddTags 4,#Child,*bt3
          *bt4=SimpleButton("Button 4")
        AddTags 4,#Child,*bt4
          *bt5=SimpleButton("Button 5")
        AddTags 4,#Child,*bt5
        AddTags 4,#Child,MUI_MakeObjectA_(#MUIO_VSpace,0)
        ;** 5 BT Horiz end
      AddTags 3,#Child,MUI_NewObjectA_("Group.mui",TagList(4))
      ;** Frame end
    AddTags 2,#Child,MUI_NewObjectA_("Group.mui",TagList(3))
    ;** Horiz group end
  AddTags 1,#WindowContents, MUI_NewObjectA_("Group.mui",TagList(2))
  *win=MUI_NewObjectA_("Window.mui",TagList(1))
  ;** Window end
AddTags 0,#SubWindow,*win
*app=MUI_NewObjectA_("Application.mui",TagList(0))
;** Application End

If *app=0
  End
EndIf

;** Install notification events...
InitTagList 0,8
AddTags 0,#MUIM_Notify,#MUIA_Window_CloseRequest,MUI_TRUE,*app
AddTags 0,2,#MUIM_Application_ReturnID,#MUIV_Application_ReturnID_Quit,0
DoMethodA *win,TagList(0)

DoMethod *cm1,#MUIM_Notify,#MUIA_Selected,#MUIV_EveryTime,*bt1,3,#MUIM_Set,#MUIA_ShowMe,#MUIV_TriggerValue
DoMethod *cm2,#MUIM_Notify,#MUIA_Selected,#MUIV_EveryTime,*bt2,3,#MUIM_Set,#MUIA_ShowMe,#MUIV_TriggerValue
DoMethod *cm3,#MUIM_Notify,#MUIA_Selected,#MUIV_EveryTime,*bt3,3,#MUIM_Set,#MUIA_ShowMe,#MUIV_TriggerValue
DoMethod *cm4,#MUIM_Notify,#MUIA_Selected,#MUIV_EveryTime,*bt4,3,#MUIM_Set,#MUIA_ShowMe,#MUIV_TriggerValue
DoMethod *cm5,#MUIM_Notify,#MUIA_Selected,#MUIV_EveryTime,*bt5,3,#MUIM_Set,#MUIA_ShowMe,#MUIV_TriggerValue
MUI_set *cm3,#MUIA_Selected,False

;** This is the ideal input loop for an object oriented MUI application.
;** Everything is encapsulated in classes, no return ids need to be used,
;** we just check if the program shall terminate.
;** Note that MUIM_Application_NewInput expects sigs to contain the result
;** from Wait() (or 0). This makes the input loop significantly faster.

MUI_set *win,#MUIA_Window_Open,MUI_TRUE
sigs.l = 0

While DoMethod(*app,#MUIM_Application_NewInput,&sigs) <> #MUIV_Application_ReturnID_Quit
  If (sigs)
    sigs = Wait_(sigs | #SIGBREAKF_CTRL_C)
    If (sigs AND #SIGBREAKF_CTRL_C)
      Pop While:Goto finish
    EndIf
  EndIf
Wend

finish:
MUI_set *win,#MUIA_Window_Open,False

;** Shut down...

MUI_DisposeObject_ *app

End

