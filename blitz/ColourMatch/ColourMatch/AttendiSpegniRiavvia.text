;-> AttendiSpegniRiavvia by Stefano Maria Regattin
;d> 10 settembre 2008 da MacBurgher
;m> 11,22 settembre 2008
;m> 1 novembre 2017
;-------------------


;--------------------------------------------------
;le seguenti costanti indicano il tipo di finestra
;the following constants state the window's kind
;------------------------------------------------

;window sizing button
#BottoneRidimensionaFinestra=1

;draggable window
#FinestraMovibile=2

;window depth button
#BottoneNascondiFinestra=4

;window close button
#BottoneChiudiFinestra=8

;sizing window right border (you must set gimme-zero-zero to use it)
#BordoDestraFinestraRidimensionabile=16

;sizing window bottom border (you must set gimme-zero-zero to use it)
#BordoBassoFinestraRidimensionabile=32

;backdrop window
#FinestraDiSfondo=256

;gimme-zero-zero window
#FinestraConMemoriaBottoni=1024

;borderless window
#FinestraSenzaBordi=2048

;window active
#FinestraAttivata=4096


;---------------------------------------
;questi sono gli eventi di una finestra
;these are the events of a window
;---------------------------------

;you have pressed a mouse button
#HaiPremutoUnTastoDelTopo=8

;you have moved the mouse
#HaiMossoIlTopo=16

;you have pressed a button
#HaiPremutoUnGadget=32

;you have released a button
#HaiRilasciatoUnGadget=64

;you have activated a menu
#HaiAttivatoUnMenu=256

;you have closed the window
#HaiChiusoLaFinestra=512

;you have pressed a key
#HaiPremutoUnTasto=1024

;you have inserted a diskette
#HaiInseritoUnDisco=32768

;you have removed a diskette
#HaiToltoUnDisco=65536

;you have activated the window
#HaiAttivatoLaFinestra=262144

;you have deactivated the window
#HaiDisattivatoLaFinestra=524288


;-----------------------------------------
;questi sono i valori validi per MenuItem
;this are the valid values for MenuItem
;---------------------------------------

;normal menu
#MenuNormale=0

;switch menu
#MenuAttivoONonAttivo=1

;one of the deactivated voices
#UnaDelleVociNonAttive=2

;the current switch menu is active
#MenuAttivo=3

;the current voice is active
#UnaDelleVociAttiva=4


;----------------------------------------------
;costanti ascii di alcuni tasti della tastiera
;some ascii keys of the keyboard
;--------------------------------

;esc key ascii value
#TastoFugaASCII=27

;cursor up key ascii value
#TastoCursoreSuASCII=28

;cursor down key ascii value
#TastoCursoreGiuASCII=29

;cursor right key ascii value
#TastoCursoreDestraASCII=30

;cursor left key ascii value
#TastoCursoreSinistraASCII=31

;space bar key ascii value
#TastoBarraDiSpazioASCII=32


;--------------------------
;valori dei tasti del topo
;mouse button values
;--------------------

;left mouse button value
#TastoSinistroDelTopoPremuto=1

;right mouse button value
#TastoDestroDelTopoPremuto=2


;---------------
;altre costanti
;other constants
;----------------

;workbench screen referring number
#SchermoWorkbench=0

;window referring number
#Finestra=0

;main window properties
#TipoFinestra=#FinestraConMemoriaBottoni+#FinestraAttivata

;graphic area referring number
#AreaGrafica=0

;change the colours
#CambiaIColori=1

;exit from the program
#EsciDalProgramma=2


;          black colour,orange colour,pale orange colour, green colour,pale green colour,blue colour,pale blue colour
DEFTYPE .w ColoreNero,  ColoreArancio,ColoreArancioChiaro,ColoreVerde, ColoreVerdeChiaro,ColoreBlu,  ColoreAzzurro

;          white colour,workbench colours
DEFTYPE .w ColoreBianco,ColoriWorkbench

;colours search mode (0-1-2)
ModoRicercaColori.b=0


;main window width
LarghezzaFinestra.w=314

;main window height
AltezzaFinestra.w=200

;main window title
TitoloFinestra$="Attendi-Spegni-Riavvia by Stefano Maria Regattin"

;workbench title
TitoloWorkbench$="Press SPACE to continue or Esc to quit..."

Function.w TrovaIlColore{Rosso.w,Verde.w,Blu.w,Modo.b}
SHARED ColoriWorkbench
 ColoreTrovato.w=256:DistanzaRosso.w=256:DistanzaVerde.w=256:DistanzaBlu.w=256
 For Colore.w=0 To ColoriWorkbench-1
  If CheckAGA=True
   NuovaDistanzaRosso.w=AGARed(Colore)
   NuovaDistanzaVerde.w=AGAGreen(Colore)
   NuovaDistanzaBlu.w=AGABlue(Colore)
  Else
   NuovaDistanzaRosso=Red(Colore)
   NuovaDistanzaVerde=Green(Colore)
   NuovaDistanzaBlu=Blue(Colore)
  EndIf
  NuovaDistanzaRosso=Rosso-NuovaDistanzaRosso
  NuovaDistanzaVerde=Verde-NuovaDistanzaVerde
  NuovaDistanzaBlu=Blu-NuovaDistanzaBlu
  If Modo=0
   If Abs(NuovaDistanzaRosso)<DistanzaRosso AND Abs(NuovaDistanzaVerde)<DistanzaVerde AND Abs(NuovaDistanzaBlu)<DistanzaBlu
    ColoreTrovato=Colore
    DistanzaRosso=Abs(NuovaDistanzaRosso)
    DistanzaVerde=Abs(NuovaDistanzaVerde)
    DistanzaBlu=Abs(NuovaDistanzaBlu)
   EndIf
  EndIf
  If Modo=1
   If Abs(NuovaDistanzaRosso)<31 AND Abs(NuovaDistanzaVerde)<31 AND Abs(NuovaDistanzaBlu)<31 Then ColoreTrovato=Colore
  EndIf
  If Modo=2
   If Abs(NuovaDistanzaRosso)<DistanzaRosso
    If Abs(NuovaDistanzaRosso)<Abs(NuovaDistanzaVerde) AND Abs(NuovaDistanzaRosso)<Abs(NuovaDistanzaBlu)
     ColoreTrovato=Colore
     DistanzaRosso=Abs(NuovaDistanzaRosso)
     DistanzaVerde=Abs(NuovaDistanzaVerde)
     DistanzaBlu=Abs(NuovaDistanzaBlu)
    EndIf
   EndIf
   If Abs(NuovaDistanzaVerde)<DistanzaVerde
    If Abs(NuovaDistanzaVerde)<Abs(NuovaDistanzaBlu) AND Abs(NuovaDistanzaVerde)<Abs(NuovaDistanzaRosso)
     ColoreTrovato=Colore
     DistanzaRosso=Abs(NuovaDistanzaRosso)
     DistanzaVerde=Abs(NuovaDistanzaVerde)
     DistanzaBlu=Abs(NuovaDistanzaBlu)
    EndIf
   EndIf
   If Abs(NuovaDistanzaBlu)<DistanzaBlu
    If Abs(NuovaDistanzaBlu)<Abs(NuovaDistanzaRosso) AND Abs(NuovaDistanzaBlu)<Abs(NuovaDistanzaVerde)
     ColoreTrovato=Colore
     DistanzaRosso=Abs(NuovaDistanzaRosso)
     DistanzaVerde=Abs(NuovaDistanzaVerde)
     DistanzaBlu=Abs(NuovaDistanzaBlu)
    EndIf
   EndIf
   If ColoreTrovato=256 AND Abs(NuovaDistanzaRosso)<31 AND Abs(NuovaDistanzaVerde)<31 AND Abs(NuovaDistanzaBlu)<31
    ColoreTrovato=Colore
   EndIf
  EndIf
 Next Colore
Function Return ColoreTrovato
End Function

Statement TrovaIColori{}
SHARED ColoreArancio
SHARED ColoreArancioChiaro
SHARED ColoreAzzurro
SHARED ColoreBianco
SHARED ColoreBlu
SHARED ColoreNero
SHARED ColoreVerde
SHARED ColoreVerdeChiaro
SHARED ModoRicercaColori
 ColoreNero=TrovaIlColore{0,5,0,ModoRicercaColori}
 ColoreArancio=TrovaIlColore{217,105,35,ModoRicercaColori}
 ColoreArancioChiaro=TrovaIlColore{248,185,89,ModoRicercaColori}
 ColoreVerde=TrovaIlColore{60,156,53,ModoRicercaColori}
 ColoreVerdeChiaro=TrovaIlColore{173,220,4,ModoRicercaColori}
 ColoreBlu=TrovaIlColore{1,51,153,ModoRicercaColori}
 ColoreAzzurro=TrovaIlColore{123,155,234,ModoRicercaColori}
 ColoreBianco=TrovaIlColore{227,234,247,ModoRicercaColori}
End Statement

;opens the main window
Statement ApriLaFinestra{}
SHARED AltezzaFinestra
SHARED LarghezzaFinestra
SHARED TitoloFinestra$
SHARED TitoloWorkbench$
 IndirizzoSchermo.l=Peek.l(Addr Screen(#SchermoWorkbench))
 LarghezzaSchermo.w=Peek.w(IndirizzoSchermo+12)
 AltezzaSchermo.w=Peek.w(IndirizzoSchermo+14)
 XPosFinestra.w=(LarghezzaSchermo-LarghezzaFinestra)/2
 YPosFinestra.w=(AltezzaSchermo-AltezzaFinestra)/2
 Window #Finestra,XPosFinestra,YPosFinestra,LarghezzaFinestra,AltezzaFinestra,#TipoFinestra,TitoloFinestra$,1,2
 LarghezzaFinestra+LarghezzaFinestra-InnerWidth
 AltezzaFinestra+AltezzaFinestra-InnerHeight
 CloseWindow #Finestra
 Window #Finestra,XPosFinestra,YPosFinestra,LarghezzaFinestra,AltezzaFinestra,#TipoFinestra,TitoloFinestra$,1,2
 WTitle TitoloFinestra$,TitoloWorkbench$
End Statement

Statement CreaLAreaGrafica{}
SHARED AltezzaFinestra
SHARED LarghezzaFinestra
 BitMap #AreaGrafica,LarghezzaFinestra,AltezzaFinestra,WBDepth
End Statement

Statement CaricaLImmagineNellAreaGrafica{}
 If Exists("AttendiSpegniRiavvia.IFF")>0
  Cls:LoadBitMap #AreaGrafica,"AttendiSpegniRiavvia.IFF"
 Else
  NPrint "Picture AttendiSpegniRiavvia.IFF"
  NPrint "not found!"
  NPrint "Click LEFT MOUSE BUTTON to quit!"
  MouseWait:Free BitMap #AreaGrafica:CloseWindow(#Finestra):End
 EndIf
End Statement

Statement CambiaIColoriDellImmagine{}
SHARED AltezzaFinestra
SHARED ColoreArancio
SHARED ColoreArancioChiaro
SHARED ColoreAzzurro
SHARED ColoreBianco
SHARED ColoreBlu
SHARED ColoreNero
SHARED ColoreVerde
SHARED ColoreVerdeChiaro
SHARED LarghezzaFinestra
 For YPunto.w=0 To AltezzaFinestra-1
  For XPunto.w=0 To LarghezzaFinestra-1
   Colore.w=Point(XPunto,YPunto)
   Select Colore
   Case 0
    Colore=ColoreNero
   Case 1
    Colore=ColoreArancio
   Case 2
    Colore=ColoreArancioChiaro
   Case 3
    Colore=ColoreVerde
   Case 4
    Colore=ColoreVerdeChiaro
   Case 5
    Colore=ColoreBlu
   Case 6
    Colore=ColoreAzzurro
   Case 7
    Colore=ColoreBianco
   End Select
   Plot XPunto,YPunto,Colore
  Next XPunto
 Next YPunto
End Statement

Statement MostraLImmagineNellaFinestra{}
 BitMaptoWindow #AreaGrafica,#Finestra
End Statement

;************************************************
;* fine delle procedure ed inizio del programma *
;******* procedures end and program start *******
;************************************************

WBStartup
WbToScreen #SchermoWorkbench

ColoriWorkbench=2^WBDepth
ApriLaFinestra{}
CreaLAreaGrafica{}

Repeat
 TrovaIColori{}
 WCls:WColour ColoreNero:WLocate 0,0:NPrint "Colour search mode>",ModoRicercaColori
 NPrint "Press LEFT MOUSE BUTTON or SPACE"
 NPrint " to change colours search"
 NPrint "Press RIGHT MOUSE BUTTON or ESC"
 NPrint " to end the program"
 CaricaLImmagineNellAreaGrafica{}
 CambiaIColoriDellImmagine{}
 MostraLImmagineNellaFinestra{}
 TastoPremutoValido.b=0
 Repeat
  VWait
  TastoPremuto.w=Asc(Inkey$)
  If TastoPremuto=#TastoFugaASCII Then TastoPremutoValido=#EsciDalProgramma
  If TastoPremuto=#TastoBarraDiSpazioASCII Then TastoPremutoValido=#CambiaIColori
  TastoDelTopoPremuto.b=Joyb(0)
  If TastoDelTopoPremuto=#TastoSinistroDelTopoPremuto Then TastoPremutoValido=#CambiaIColori
  If TastoDelTopoPremuto=#TastoDestroDelTopoPremuto Then TastoPremutoValido=#EsciDalProgramma
 Until TastoPremutoValido>0
 If TastoPremutoValido=#CambiaIColori Then ModoRicercaColori+1:If ModoRicercaColori=3 Then ModoRicercaColori=0
Until TastoPremutoValido=#EsciDalProgramma
CloseWindow #Finestra
Free BitMap #AreaGrafica
End
