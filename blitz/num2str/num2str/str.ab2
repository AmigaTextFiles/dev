
; Integer to string functions for Blitz

; (c) 2012  Lorence Lombardo.

; These functions can also be used with classic Blitz.


INCLUDE "ValL_inc.bb2"  ; <- here for testing purpose only
         ; ^- from "ValL.ab2" from the archive "SANE.lha"
         ;    from the Aminet.


; Like Str$() but only does integers
; Used Vallong to test this function

Function.s Str{num.l}
   v$="" : ngtv.b=0 : If num<0 Then ngtv=1 : num*-1
   ov.b=0 : If num<0 Then ov=1 : num+1 : num*-1
   Repeat
      ans.l=num/10 : rem.l=num-(ans*10)
      v$=Chr$(rem+48)+v$ : num=ans
   Until num=0
   If ngtv=1 Then v$="-"+v$ : If ov=1 Then Poke.b &v$+10,56
   Function Return v$
End Function


; Very much like Hex$() but not quite the same, this is signed
; Used ValLH{} to test this function

Function.s HexStr{num.l}
   v$="" : ngtv.b=0 : If num<0 Then ngtv=1 : num*-1
   ov.b=0 : If num<0 Then ov=1 : num+1 : num*-1
   Repeat
      ans.l=num/16 : rem.l=num-(ans*16)
      If rem>9
         If ov=1 Then rem=-7
         rem$=Chr$(rem+55)
      Else
         If ov=1 Then rem+1 : ov=0
         rem$=Chr$(rem+48)
      EndIf
      v$=rem$+v$ : num=ans
   Until num=0
   If ngtv=1 Then v$="-"+v$
   Function Return v$
End Function


; Very much like Bin$() but not quite the same, this is signed
; Used ValLB{} to test this function

Function.s BinsStr{num.l}
   v$="" : ngtv.b=0 : If num<0 Then ngtv=1 : num*-1
   ov.b=0 : If num<0 Then ov=1 : num+1 : num*-1
   Repeat
      ans.l=num/2 : rem.l=num-(ans*2) : If ov=1 Then rem=0
      If rem=1 Then v$="1"+v$ Else v$="0"+v$
      num=ans
   Until num=0
   If ov=1 Then v$="1"+v$
   If ngtv=1 Then v$="-"+v$
   Function Return v$
End Function


; Converts an integer value into an octal string
; Used ValLO{} to test this function

Function.s OctStr{num.l}
   v$="" : ngtv.b=0 : If num<0 Then ngtv=1 : num*-1
   ov.b=0 : If num<0 Then ov=1 : num+1 : num*-1
   Repeat
      ans.l=num/8 : rem.l=num-(ans*8) : If ov=1 Then rem=0
      v$=Chr$(rem+48)+v$ : num=ans
   Until num=0
   If ngtv=1 Then v$="-"+v$ : If ov=1 Then Poke.b &v$+1,50
   Function Return v$
End Function


; like Chr$, Mki$ and Mkl$ ; but not the same
; string size is dynamic rather than fixed (horses for coarses:)
; Used ValLCS{} to test this function since it is also dynamic
; To be more specific the string size is between 1 and 4.

Function.s ChrStr{num.l}
   v$="" : ngtv.b=0 : If num<0 Then ngtv=1 : num*-1
   Repeat
      ans.l=num/256 : rem.l=num-(ans*256)
      v$=Chr$(rem)+v$ : num=ans
   Until num=0
   If ngtv=1  ; <- here we obtain the complement & add 1 as per LU10 digital fundamentals BEC
      c.b=Len(v$) : v2$="" ;This is merely my interpretation of the BEC theory in Blitz code.
      For x.b=1 To c
         comp.w=255-Asc(Mid$(v$,x,1)) : v2$=v2$+Chr$(comp)
      Next x
      v$=v2$
      Repeat
         chrn.w=Asc(Mid$(v$,c,1))
         If chrn=255   ; <- if they were all 255 then it would b the complement of zero
            c-1 : Poke.b &v$+c, 0       ; & u cant have -ve zero...!!! ;)
         Else
            Poke.b &v$+c-1, chrn+1 : c=0
         EndIf
      Until c<1
      If Asc(Left$(v$,1))<128 Then v$=Chr$(255)+v$
   Else
      If Asc(Left$(v$,1))>127 Then v$=Chr$(0)+v$
   EndIf
   Function Return v$
End Function


;--------------------------------------------------------------------------


ov.l=-2147483648  ; <- largest -ve cant b made +ve coz its greater than the
                  ;    largest +ve by 1.

NPrint "" : NPrint " Str{} test" : NPrint ""
a$=Str{ov} : NPrint a$
a$=Str{-2250} : NPrint a$
a$=Str{68686868} : NPrint a$ : NPrint ""

NPrint "" : NPrint " HexStr{} test" : NPrint ""
a$=HexStr{ov} : NPrint a$
NPrint ValLH{a$}
a$=HexStr{-2250} : NPrint a$
NPrint ValLH{a$}
a$=HexStr{68686868} : NPrint a$
NPrint ValLH{a$} : NPrint ""

NPrint "" : NPrint " BinsStr{} test" : NPrint ""
a$=BinsStr{ov} : NPrint a$
NPrint ValLB{a$}
a$=BinsStr{-2250} : NPrint a$
NPrint ValLB{a$}
a$=BinsStr{68686868} : NPrint a$
NPrint ValLB{a$} : NPrint ""

NPrint "" : NPrint " OctStr{} test" : NPrint ""
a$=OctStr{ov} : NPrint a$
NPrint ValLO{a$}
a$=OctStr{-2250} : NPrint a$
NPrint ValLO{a$}
a$=OctStr{68686868} : NPrint a$
NPrint ValLO{a$} : NPrint ""

NPrint "" : NPrint " ChrStr{} test" : NPrint ""
a$=ChrStr{ov} : NPrint "Len:- ",Len(a$)
NPrint ValLCS{a$}
a$=ChrStr{-2250} : NPrint "Len:- ",Len(a$)
NPrint ValLCS{a$}
a$=ChrStr{68686868} : NPrint "Len:- ",Len(a$)
NPrint ValLCS{a$}
a$=ChrStr{128} : NPrint "Len:- ",Len(a$)
NPrint ValLCS{a$}
a$=ChrStr{127} : NPrint "Len:- ",Len(a$)
NPrint ValLCS{a$}
a$=ChrStr{-128} : NPrint "Len:- ",Len(a$)
NPrint ValLCS{a$}
a$=ChrStr{-129} : NPrint "Len:- ",Len(a$)
NPrint ValLCS{a$}
a$=ChrStr{40000} : NPrint "Len:- ",Len(a$)
NPrint ValLCS{a$} : NPrint ""

NPrint ov * -1 : NPrint ""


