
; ValF{}  NextGeneration  (C) 2012  Lorence Lombardo
; This is an alternative to the "Val" command with some advantages.

; ValF{} NG works with or without FPU. ValF{} NG works with new Blitz
; and Classic Blitz
; Similar to the original "Val" command now supporting hex, binary
; and octal fractions.

; With ValF{} NG the sign can be before or after the ID.

; Previous versions of ValF{} can be found in the archives "SANE.lha"
; and "num2str.lha" from the Aminet. Its possible previous versions
; were faster but speed was not the issue here.

; 26-Aug-2012 to 28-Aug-2012

; This ValF{} function was obtained from "ValF_NG.ab2" from the
; archive "ValF_NG.lha" from the Aminet.


CNIF #FPU=1
Function.d ValF {valf$}
   fract.d=0 : fl.d=0
CELSE
Function.f ValF {valf$}
   fract.f=0 : fl.f=0
CEND
   nmax.b=58 : c.w=0 : base.b=10 : sign.b=1
   valf$=Replace$(valf$,",","") : valf$=Replace$(valf$," ","")
   sign$=Left$(valf$,1) : If sign$="-" Then sign= -1
   If sign$="-" OR sign$="+" Then valf$=Mid$(valf$,2)
   ntype$=Left$(valf$,1)
   If ntype$<>"%" AND ntype$<>"$" AND ntype$<>"#" Then ntype$=""
   If ntype$<>"" Then valf$=Mid$(valf$,2)
   sign$=Left$(valf$,1) : If sign$="-" Then sign= -1
   If sign$="-" OR sign$="+" Then valf$=Mid$(valf$,2)
   xp.w=Instr(valf$,".") : intlen.w=Len(valf$)
   If ntype$="%" Then base=2 : nmax=50
   If ntype$="$" Then base=16 : valf$=UCase$(valf$)
   If ntype$="#" Then base=8 : nmax=56
   If xp>0 AND intlen>xp
      fract$=Mid$(valf$,xp+1) : fraclen.w=Len(fract$)
      Repeat
         v.b=Peek.b(&fract$+c) : c+1
         If v>47 AND v<nmax
            fract = (v-48) * (base^(c*-1)) + fract
         Else
            If base=16 AND v>64 AND v<71
               fract = (v-55) * (16^(c*-1)) + fract
            Else
               c = fraclen
            EndIf
         EndIf
      Until c=>fraclen
      intlen=xp-1 : valf$=Left$(valf$, intlen) : c=0
   EndIf
   If intlen>0
      Repeat
         v.b=Peek.b(&valf$+c) : c+1
         If v>47 AND v<nmax
            fl=fl*base+v-48
         Else
            If base=16 AND v>64 AND v<71
               fl=fl*16+v-55
            Else
               c=intlen
            EndIf
         EndIf
      Until c=>intlen
   EndIf
   Function Return (fl+fract)*sign
End Function


