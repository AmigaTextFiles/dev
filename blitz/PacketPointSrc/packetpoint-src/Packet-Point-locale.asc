;
;Packet-Point Version 0.160 BETA (c) 1997-2000 by David Scheibler
;This programme is ShareWare. Please register it! (30 DEM; 15 EUR; 15 USD)
;Now CardWare and OpenSource
;

WBStartup
NoCli

WbToScreen 0
WBenchToFront_

If ExecVersion < 36
  Request "Error","Sorry, but you need OS 2.0 or higher to|run Packet-Point!","Quit Packet-Point"
  Exit_(666)
  End
EndIf

;SetErr
;  LogFileOutput{locale$(254)}
;  EndIf
;  Request "Schwerer Programmfehler","Leider ist ein schwerer Programm aufgetreten|Das Programm muss beendet werden!","Schade"
;  End
;End SetErr

NEWTYPE .Mail
  mailflags.s
  mailnummer.l
  subject.s
  absender.s
  empfaenger.s
  reply.s
  replybid.s
  maildate.s
  mailtime.s
  maildatum.s
  nummer.s
  mailpath.s
  sent.s
  laenge.l
  newmail.l
  disku.s
  pgpmail.l
  emp.l
  zeilen.l
  comment.s
  mailfilter.l
End NEWTYPE

NEWTYPE .listview
  Flags.w
  Text.s
End NEWTYPE

NEWTYPE .screeninfo
  id.l
  width.l
  height.l
  tiefe.l
  overscan.l
  autoscroll.l
  bmapwidth.l
  bmapheight.l
End NEWTYPE

NEWTYPE .fontinfo
  name.s
  ysize.w
  style.b:flags.b
  pen1.b:pen2:drawmode:pad
End NEWTYPE

NEWTYPE .mailpath
  x.l:y
  name.s
End NEWTYPE

NEWTYPE.LocaleTags
  a.l:b:c:d:e:f:g
End NEWTYPE

DEFTYPE.Catalog *LocaleCatalog
DEFTYPE.LocaleTags LocTags
DEFTYPE.w

Dim List Liste.listview (9999)
Dim List Liste1.listview (9999)
Dim List Liste2.listview (9999)
Dim List Gadget00_list.listview (9999)
Dim List User.listview (9999)
Dim List Brett.listview (9999)
Dim      Mail.Mail (9999)
Dim      text$(9999)
Dim      text_info$(9999)
Dim      absender_1$(9999)
Dim      filterliste$(9999)
Dim      ppedit$(9999)
Dim      sig$(9999)
Dim      puffer$(9999)
Dim      cookie$(9999)
Dim      keyfile$(9999)
Dim      scrtags.TagItem(9)
Dim      einleitung$(9999)
Dim      marko$(10)
Dim      arg(30)
Dim      arg$(30)
Dim      path$(9999)
Dim      mailpath.mailpath(9999)
Dim      locale$(262)


Statement arexxcommand{command$}
  SHARED argnum
  SHARED arg$()
  argnum=0                                      ;RESET argnum
  wordpos = 0                                   ;internal wordposition counter
  For count= 0 To Len(command$)                 ;loop for each character
    If Mid$(command$,wordpos,1)=Chr$(32)        ;are we at end of word
      arg$(argnum)=Left$(command$,(wordpos-1))  ;yep then grab word into array
      command$=UnRight$(command$,wordpos)       ;and remove it from command$
      argnum+1                                  ;upto next bit in array
      wordpos=0                                 ;reset wordlength counter
    EndIf
    wordpos+1                                   ;not at end of word so inc wordlength
  Next count                                    ;and loop again
  arg$(argnum)=command$                         ;to get here must have processed all spaces!
End Statement

Statement mailboxpath{path$}
  SHARED pathnum
  SHARED path$()
  pathnum=0                                     ;RESET argnum
  wordpos = 0                                   ;internal wordposition counter
  For count= 0 To Len(path$)                    ;loop for each character
    If Mid$(path$,wordpos,1)="!"                ;are we at end of word
      path$(pathnum)=Left$(path$,(wordpos-1))   ;yep then grab word into array
      path$=UnRight$(path$,wordpos)             ;and remove it from command$
      pathnum+1                                 ;upto next bit in array
      wordpos=0                                 ;reset wordlength counter
    EndIf
    wordpos+1                                   ;not at end of word so inc wordlength
  Next count                                    ;and loop again
  path$(pathnum)=path$                          ;to get here must have processed all spaces!
End Statement

Function$ getenv{envfile$}
  If OpenFile(1,"ENV:"+envfile$)
    FileInput 1
    env$=Edit$(999)
    DefaultInput
    CloseFile 1
  EndIf
  Function Return env$
End Function

Function Empfaenger{empfaenger$}
  If OpenFile(1,"bretter.pp")
    FileInput 1
    While NOT Eof(1)
      brett$=Edit$(999)
      If brett$=empfaenger$
        Function Return 1
      EndIf
    Wend
    DefaultInput
    CloseFile 1
  EndIf
  Function Return 0
End Function

Statement OpenCatalog{Catalog$,Version}
  SHARED *LocaleCatalog,LocTags
  LocTags\a=#OC_BuiltInLanguage,&English$
  LocTags\c=#OC_Version,Version
  LocTags\e=#TAG_DONE,0
  *LocaleCatalog=OpenCatalogA_(0,&Catalog$,&LocTags)
End Statement

keynummer=-1

Statement CloseCatalog{}
  SHARED *LocaleCatalog
  CloseCatalog_ *LocaleCatalog
End Statement

Function$ GetLocaleString{StringID,def$}
  SHARED *LocaleCatalog
  s$=Peek$(GetCatalogStr_(*LocaleCatalog,StringID,&def$))
  Function Return s$
End Function

Statement LogFileOutput{text$}
  If OpenFile (2,"logfile.pp")
    FileSeek 2,Lof(2)
    FileOutput 2
    NPrint"",Date$(SystemDate)," ",Hours,":",Mins,":",Secs," - "+text$
    DefaultOutput
    CloseFile 2
  EndIf
End Statement

Statement BubbleSort{anzahl}
  SHARED Liste()
  For i=2 To anzahl
    For j=anzahl To i Step -1
      If Liste(j-1)\Text > Liste(j)\Text
        Exchange Liste(j-1)\Text,Liste(j)\Text
      EndIf
    Next
  Next
End Statement

Macro Header
  NPrint""
  NPrint locale$(0)+Mail(Auswahl)\absender
  NPrint locale$(1)+Mail(Auswahl)\empfaenger
  NPrint locale$(2)+Mail(Auswahl)\subject
  NPrint locale$(3)+Mail(Auswahl)\comment
  NPrint locale$(4)+" "+Str$(Mail(Auswahl)\zeilen)+" "+locale$(244)
  NPrint locale$(5)+Mail(Auswahl)\mailpath
  NPrint locale$(6)+Mail(Auswahl)\maildatum
  NPrint locale$(7)+Mail(Auswahl)\disku
  NPrint locale$(8)+Mail(Auswahl)\nummer
  NPrint locale$(11)+Mail(Auswahl)\reply
  NPrint locale$(9)+Mail(Auswahl)\sent
  NPrint locale$(10)+Mail(Auswahl)\mailflags
  NPrint""
  If Mail(Auswahl)\pgpmail=True
    NPrint locale$(54)
  EndIf
End Macro

Macro Nervfenster
  If keynummer=0
    ueber1$= "|"
    ueber2$= locale$(12)+ver$+locale$(13)
    ueber3$= locale$(14)
    ueber4$= locale$(15)
    ueber5$= locale$(16)
    ueber6$= locale$(17)
    ueber7$= locale$(18)
    ueber8$= locale$(19)
    Request locale$(20),ueber1$+ueber2$+ueber3$+ueber4$+ueber5$+ueber6$+ueber7$+ueber8$,locale$(21)
  EndIf
End Macro


;Lokalisierung

INCDIR "BLITZ2:Examples/Packet-Point/"
INCLUDE "localedat.bb2"

keynummer=-33

If ExecVersion > 37
  OpenCatalog{"Packet-Point.catalog",0}
  For a=0 To 262
    Read defaultstring$
    locale$(a)=GetLocaleString{a," "+defaultstring$}
  Next
  CloseCatalog{}
Else
  For a=0 To 262
    Read defaultstring$
    locale$(a)=defaultstring$
  Next
EndIf

;Standartkonfiguration:
username$="CA1LL"
homebbs$="MY1BBS"
prompt$="CA1LL >"
realname$="Computer User"
nickname$="Compi"
betreff$="irgendeinbetreff"
datei$="datei.txt"
user_id$="CA1LL"
keyring$="keyring.pgp"
mbxpasswort$=""
mailboxcall$="BB1S"
mbxpfad$="BB1S"
software=0
device$="serial.device"
unit$="0"
baud$="19200"
key_mitschicken=0
passwort_erfragen=0
editor_prefs$="ed %s"
pgp_path$="PGP:PGP"
plus_path$="c:7plus"
puffer_laden$="Puffer/Puffer_Laden"
puffer_speichern$="Puffer/Puffer_Speichern"
lifetime_prefs$="120"
passwort$=""
passwort_an=0
signaturtrenner$="-- "
signatur_ab=0
font$="topaz.font"
fontysize=8
wbstart=0
prefs$="prefs.pp"
keynummer=-66


INCDIR "Blitz2:Examples/Packet-Point/"
INCLUDE "prefs_load.bb2"

;Versionsnummer und andere Variable:

ver$="0.160 (20.03.1999)"
version$="$VER: Packet-Point 0.160 (20.03.99)"
y=1:x=0
laenge.l=0
*sc.screeninfo

;
; KEFILE oeffnen (DEVS:PP.key oder PROGDIR:PP.key)
;

keynummer=-1
If ReadFile(0,"devs:PP.key")
  MaxLen keynummer$=80:MaxLen regist_name$=80:MaxLen strasse$=80:MaxLen ort$=80:MaxLen keyfile$=80:MaxLen filenummer$=80
  Fields 0,keynummer$,regist_name$,strasse$,ort$,keyfile$,filenummer$
  Get 0,0
  CloseFile  0
  filenummer.l=Val(Cipher$(filenummer$))
  keynummer$=Cipher$(keynummer$)
  regist_name$=Cipher$(regist_name$)
  strasse$=Cipher$(strasse$)
  ort$=Cipher$(ort$)
  keyfile$=Cipher$(keyfile$)
  keynummer=Val(keynummer$)
Else
  If ReadFile(0,"PP.key")
    MaxLen keynummer$=80:MaxLen regist_name$=80:MaxLen strasse$=80:MaxLen ort$=80:MaxLen keyfile$=80:MaxLen filenummer$=80
    Fields 0,keynummer$,regist_name$,strasse$,ort$,keyfile$,filenummer$
    Get 0,0
    CloseFile  0
    filenummer.l=Val(Cipher$(filenummer$))
    keynummer$=Cipher$(keynummer$)
    regist_name$=Cipher$(regist_name$)
    strasse$=Cipher$(strasse$)
    ort$=Cipher$(ort$)
    keyfile$=Cipher$(keyfile$)
    keynummer=Val(keynummer$)
  Else
    keynummer = 0
  EndIf
EndIf


If keynummer > 0
  If keyfile$ <> "Packet-Point Key-File" OR keynummer < 0
    Request locale$(55),locale$(56),locale$(57)
    End
  EndIf
EndIf

If ReadFile(0,"quote.pp")
  FileInput 0
  While NOT Eof(0)
    quote+1
    quote1+1
    einleitung$(quote)=Edit$(999)
  Wend
  quote1+1
  quote=0
  CloseFile 0
EndIf

If ReadFile(0,"mapdata.pp")
  FileInput 0
  While NOT Eof(0)
    mapdata+1
    mailpath(mapdata)\name=Edit$(999)
    mailpath(mapdata)\x   =Edit (80)
    mailpath(mapdata)\y   =Edit (80)
  Wend
  DefaultInput
  CloseFile 0
EndIf

If Exists("pgpkey.pp")=0
  pgp_einbinden$=locale$(58)
  pgp=Request(locale$(59),pgp_einbinden$,locale$(60))
  If pgp <> 0
    cmd$=pgp_path$+" -kxa "+user_id$+" pgpkey.pp"+Chr$(10)+"wait 5"+Chr$(10)+"ENDCLI"
    cli$="con:0/0/0/0 "+locale$(61)
    If WriteFile(0,cli$)
     WBenchToFront_
     Execute_ &cmd$,Peek.l(Addr File(0)),0
     WBenchToBack_
     CloseFile 0
    EndIf
    NameFile "pgpkey.pp.asc","pgpkey.pp"
    DeleteFile_("pgpkey.pp.asc")
  EndIf
EndIf

b=0
c=0
If ReadFile(0,"filterliste.pp")
  FileInput 0
  While NOT Eof(0)
    b=b+1
    c=c+1
    filterliste$(b)=Edit$(999)
  Wend
  CloseFile 0
  DefaultOutput
  DefaultInput
EndIf
b=0
c=c+1

If ReadFile (0,"macros.pp")
  FileInput 0
  marko$(1)=Edit$(999)
  If marko$(1)="" Then marko$(1)=" "
  marko$(2)=Edit$(999)
  If marko$(2)="" Then marko$(2)=" "
  marko$(3)=Edit$(999)
  If marko$(3)="" Then marko$(3)=" "
  marko$(4)=Edit$(999)
  If marko$(4)="" Then marko$(4)=" "
  marko$(5)=Edit$(999)
  If marko$(5)="" Then marko$(5)=" "
  marko$(6)=Edit$(999)
  If marko$(6)="" Then marko$(6)=" "
  marko$(7)=Edit$(999)
  If marko$(7)="" Then marko$(7)=" "
  marko$(8)=Edit$(999)
  If marko$(8)="" Then marko$(8)=" "
  marko$(9)=Edit$(999)
  If marko$(9)="" Then marko$(9)=" "
  marko$(10)=Edit$(999)
  If marko$(10)="" Then marko$(10)=" "
  CloseFile 0
  DefaultInput
Else
  marko$(1)=" "
  marko$(2)=" "
  marko$(3)=" "
  marko$(4)=" "
  marko$(5)=" "
  marko$(6)=" "
  marko$(7)=" "
  marko$(8)=" "
  marko$(9)=" "
  marko$(10)=" "
EndIf

If ReadFile(1,"cookies.pp")
  FileInput 1
  cookie=0
  While NOT Eof(1)
    cookie+1
    cookie$(cookie)=Edit$(999)
  Wend
  DefaultInput
  CloseFile 1
EndIf

Gosub software

If AddFirst(Liste())
  Liste()\Text=" "+locale$(232)
EndIf

.main
arexxport.l=CreateMsgPort("PACKETPOINT")
While arexxport=0
  port+1:port$=Str$(port)
  port$="PACKETPOINT."+port$
  arexxport.l=CreateMsgPort(port$)
Wend

!Nervfenster

If Exists(prefs$)=0
  If Request(locale$(247),locale$(248),locale$(249))=1
    Gosub user_prefs
    Gosub mbx_prefs
    Gosub serial_prefs
    Gosub allgemeine_prefs
    INCLUDE "prefs_save.bb2"
  EndIf
EndIf

win:

;SCREEN OEFFEN

If wbstart=0
  If ExecVersion >37
    If screenprefs=True
      CloseScreen 0
      col.w=-1

      scrtags(0)\ti_Tag=#SA_Left,0
      scrtags(1)\ti_Tag=#SA_Top,0
      scrtags(2)\ti_Tag=#SA_Width,screendata1
      scrtags(3)\ti_Tag=#SA_Height,screendata2
      scrtags(4)\ti_Tag=#SA_Depth,screendata3
      scrtags(5)\ti_Tag=#SA_Overscan,screendata4
      scrtags(6)\ti_Tag=#SA_AutoScroll,screendata5
      scrtags(7)\ti_Tag=#SA_DisplayID,screendata6
      scrtags(8)\ti_Tag=#SA_Pens,&col
      scrtags(9)\ti_Tag=#TAG_END

      ScreenTags 0,"Packet-Point - (c) 1997-1999 by David Scheibler",&scrtags(0)
    Else
      *sc.screeninfo=ASLScreenRequest(31)
      CloseScreen 0
      If *sc
        col.w=-1
        scrtags(0)\ti_Tag=#SA_Left,0
        scrtags(1)\ti_Tag=#SA_Top,0
        scrtags(2)\ti_Tag=#SA_Width,*sc\width
        scrtags(3)\ti_Tag=#SA_Height,*sc\height
        scrtags(4)\ti_Tag=#SA_Depth,*sc\tiefe
        scrtags(5)\ti_Tag=#SA_Overscan,*sc\overscan
        scrtags(6)\ti_Tag=#SA_AutoScroll,*sc\autoscroll
        scrtags(7)\ti_Tag=#SA_DisplayID,*sc\id
        scrtags(8)\ti_Tag=#SA_Pens,&col
        If *sc\width >= 640 AND *sc\height >= 256
          ScreenTags 0,"Packet-Point - (c) 1997-1999 by David Scheibler",&scrtags(0)
        Else
          Request locale$(252),locale$(251),locale$(233)
          End
        EndIf
      Else
        Screen 0,0,0,640,256,4,$8000,"Packet-Point - (c) 1997-1999 by David Scheibler",1,2
      EndIf
    EndIf
  Else
    CloseScreen 0
    Screen 0,0,0,640,256,4,$8000,"Packet-Point - (c) 1997-1999 by David Scheibler",1,2
  EndIf
EndIf

  height=ScreenHeight
  width=ScreenWidth

  LoadFont 0,font$,fontysize
  Use IntuiFont 0


  MenuGap    3,1
  SubItemOff 10,0
  GTMenuTitle  0,0,locale$(22)
  GTMenuItem   0,#NM_ITEMDISABLED,0,0,LSet$(locale$(23),27),"N"
  GTMenuItem   0,00,0,1,LSet$(locale$(24),27),"L"
  GTMenuItem   0,00,0,2,locale$(25),"S"
  GTMenuItem   0,00,0,3,locale$(26),"K"
  GTMenuItem   0,00,0,4
  GTMenuItem   0,00,0,5,locale$(27),"H"
  GTMenuItem   0,00,0,6
  GTMenuItem   0,00,0,7,locale$(28),"?"
  GTMenuItem   0,00,0,8,locale$(29),"Q"
  GTMenuTitle  0,1,locale$(30)
  GTMenuItem   0,00,1,0,LSet$(locale$(31),33),"P"
  GTMenuItem   0,00,1,1,locale$(32),""
  GTMenuItem   0,00,1,2,locale$(33),"W"
  GTMenuItem   0,00,1,3,locale$(34),"D"
  GTMenuItem   0,00,1,4,locale$(35),"E"
  GTMenuItem   0,00,1,5
  GTMenuItem   0,00,1,6,locale$(36),"A"
  GTMenuItem   0,00,1,7,locale$(37),"F"
  GTMenuItem   0,00,1,8
  GTMenuItem   0,00,1,9,locale$(38),"W"
  GTMenuItem   0,00,1,10
  GTMenuItem   0,00,1,11,locale$(39),"0"
  GTMenuItem   0,00,1,12
  GTMenuItem   0,00,1,13,locale$(40),"I"
  GTMenuItem   0,00,1,14,locale$(41),"C"
  GTMenuItem   0,00,1,15,locale$(42)
  GTMenuTitle  0,2,locale$(43)
  GTMenuItem   0,00,2,0,LSet$(locale$(44),23),"1"
  GTMenuItem   0,00,2,1,locale$(45),"2"
  GTMenuItem   0,00,2,2,locale$(46),"3"
  GTMenuItem   0,00,2,3,locale$(47),"4"
  GTMenuItem   0,00,2,4,locale$(48),"5"
  GTMenuItem   0,00,2,5,locale$(49),"6"
  GTMenuItem   0,00,2,6,locale$(50),"7"
  GTMenuItem   0,00,2,7,locale$(51),"8"
  GTMenuItem   0,00,2,8,locale$(52),"9"
  GTMenuItem   0,00,2,9
  GTMenuItem   0,00,2,10,locale$(53),"O"
  GTMenuTitle  0,3,locale$(62)
  GTMenuItem   0,00,3,0,LSet$(locale$(63),20)
  GTMenuItem   0,00,3,1,locale$(64)
  GTMenuItem   0,00,3,2,locale$(65)
  GTMenuItem   0,00,3,3,locale$(66)
  GTMenuTitle  0,4,locale$(67)
  GTMenuItem   0,00,4,0,LSet$(locale$(68),29)
  GTMenuItem   0,00,4,1,locale$(69)
  GTMenuItem   0,00,4,2
  GTMenuItem   0,00,4,3,locale$(70),"U"
  GTMenuItem   0,00,4,4,locale$(71),"B"
  GTMenuItem   0,00,4,5
  GTMenuItem   0,00,4,6,locale$(72),""
  GTMenuItem   0,00,4,7,locale$(73),""
  GTMenuItem   0,00,4,8,locale$(74),""
  GTMenuItem   0,00,4,9,locale$(75),""
  GTMenuItem   0,40,4,10
  GTMenuItem   0,00,4,11,locale$(76),""
  If passwort_an=1
    GTMenuItem   0,#CHECKIT | #MENUTOGGLE | #CHECKED,4,12,locale$(77),""
  Else
    GTMenuItem   0,#CHECKIT | #MENUTOGGLE,4,12,locale$(77),""
  EndIf
  GTMenuTitle  0,5,locale$(78)
  GTMenuItem   0,00,5,0,locale$(79)
  GTMenuItem   0,00,5,1
  GTMenuItem   0,00,5,2,locale$(80),"R"
  GTMenuItem   0,00,5,3
  GTMenuItem   0,00,5,4,marko$(1)
  GTMenuItem   0,00,5,5,marko$(2)
  GTMenuItem   0,00,5,6,marko$(3)
  GTMenuItem   0,00,5,7,marko$(4)
  GTMenuItem   0,00,5,8,marko$(5)
  GTMenuItem   0,00,5,9,marko$(6)
  GTMenuItem   0,00,5,10,marko$(7)
  GTMenuItem   0,00,5,11,marko$(8)
  GTMenuItem   0,00,5,12,marko$(9)
  GTMenuItem   0,00,5,13,marko$(10)

  GTTags #GTLV_ShowSelected,0

  GTListView    0,0,0,0,615,188,"",$0001,Liste()
  GTButton      0,1,6,191,149,11,locale$(81),$0010
  GTButton      0,2,156,191,149,11,locale$(82),$0010
  GTButton      0,3,305,191,149,11,locale$(83),$0010
  GTButton      0,4,454,191,149,11,locale$(84),$0010
  GTButton      0,5,6,202,149,11,locale$(85),$0010
  GTButton      0,6,156,202,149,11,locale$(86),$0010
  GTButton      0,7,305,202,149,11,locale$(87),$0010
  GTButton      0,8,454,202,149,11,locale$(88),$0010

  Window        0,0,11,638,233,$0000000F|$1000,locale$(89)+" "+ver$+"",1,2
  WindowFont    0,0

  AddAppWindow  0

  AttachGTList  0,0

  GTBevelBox    0,5,200,612,30,$0001
  GTSetMenu 0

  Auswahl.w=0
  GTSetAttrs 0,0,#GTLV_Selected,0
  If passwort_an=1

    Window        14,3,192,637,64,$1002,locale$(90),1,2
    GTString      14,0,95,15,500,15,locale$(91),$0001,256,""
    AttachGTList  14,14

    CloseWin = False
    Repeat
      EventType.l = WaitEvent
      Select EventType
        Case #IDCMP_REFRESHWINDOW
          GT_BeginRefresh_ Peek.l (Addr Window(14))
          GT_EndRefresh_ Peek.l (Addr Window(14)),True
        Case $00000200
          CloseWin = False

        Case $00000040
          Select GadgetHit
            Case 0
              passwort_fragen$=GTGetString(14,0)
              CloseWin = True
          End Select
      End Select
    Until CloseWin
    CloseWin = False
    CloseWindow 14
    Free GTList 14
    If passwort$<>passwort_fragen$
      Request locale$(92),locale$(93),locale$(94)
      LogFileOutput{locale$(255)}
      End
    EndIf
  EndIf

.re
INCDIR "BLITZ2:Examples/Packet-Point/"
INCLUDE "re.bb2"

.load_puffer
If puffer_arexx=False
  MaxLen pa$=192
  MaxLen fi$=192
  puffer_laden_new$=ASLFileRequest$(locale$(95),pa$,fi$,"#?")
EndIf

If puffer_laden_new$<>"" OR puffer_arexx=True
  If puffer_arexx=False Then puffer_laden$=puffer_laden_new$
  GTChangeList 0,0
  ClearList Liste()

  Gosub software
  GTChangeList 0,0,Liste()
  If AddFirst(Liste())
    Liste()\Text=" "+locale$(232)
  EndIf
EndIf
Return

.software
If software=0
  INCDIR  "Blitz2:examples/Packet-Point/"
  INCLUDE "bcmread.bb2"
EndIf
If software=1
  INCDIR "Blitz2:examples/Packet-Point/"
  INCLUDE "fbbread.bb2"
EndIf
Return

.save_puffer
MaxLen pa$=192
MaxLen fi$=192
puffer_speichern$=ASLFileRequest$(locale$(96),pa$,fi$,"#?")

Return

.kill_puffer
MaxLen pa$=192
MaxLen fi$=192
puffer_killen$=ASLFileRequest$(locale$(97),pa$,fi$,"#?")
If puffer_killen$<>""
  puffer_killen=Request(locale$(98),locale$(99)+puffer_killen$+"|"+locale$(100),locale$(101))
  If puffer_killen
    KillFile puffer_killen$
    If UCase$(puffer_killen$)=UCase$(puffer_laden$) OR Instr(UCase$(puffer_laden$),UCase$(puffer_killen$))>0
      GTChangeList 0,0
      ClearList Liste()
      ResetList Liste()
      If AddFirst(Liste())
        Liste()\Text=" "+locale$(232)
      EndIf
      GTChangeList 0,0,Liste()
    EndIf
  EndIf
EndIf

Return

.help
WBenchToFront_
cmd$="AmigaGuide Packet-Point.guide"+Chr$(10)+"ENDCLI"
Execute_ &cmd$,Peek.l(Addr File(0)),0
WBenchToBack_

Return

.about
  ueber1$= "|"
  ueber2$= locale$(12)+ver$+locale$(13)
  ueber3$= locale$(14)
  ueber4$= locale$(15)
  ueber5$= locale$(16)
  ueber6$= locale$(17)

If keynummer=0
  ueber7$= locale$(18)
Else
  keynummer$=Str$(keynummer)
  ueber7$= locale$(102)+" "+keynummer$+" "+locale$(103)+" "+regist_name$+"| "+strasse$+"| "+ort$+"||"+locale$(104)+"|"
EndIf
  ueber8$= locale$(19)
  Request locale$(20),ueber1$+ueber2$+ueber3$+ueber4$+ueber5$+ueber6$+ueber7$+ueber8$,locale$(21)

Return

.drucken
  x=0
  PrtText""
  PrtText locale$(0)+Mail(Auswahl)\absender
  PrtText locale$(1)+Mail(Auswahl)\empfaenger
  PrtText locale$(2)+Mail(Auswahl)\subject
  PrtText locale$(3)+Mail(Auswahl)\comment
  PrtText locale$(4)+" "+Str$(Mail(Auswahl)\zeilen)+" "+locale$(244)
  PrtText locale$(5)+Mail(Auswahl)\mailpath
  PrtText locale$(6)+Mail(Auswahl)\maildatum
  PrtText locale$(7)+Mail(Auswahl)\disku
  PrtText locale$(8)+Mail(Auswahl)\nummer
  PrtText locale$(11)+Mail(Auswahl)\reply
  PrtText locale$(9)+Mail(Auswahl)\sent
  PrtText locale$(10)+Mail(Auswahl)\mailflags
  PrtText""
  If Mail(Auswahl)\pgpmail=True
    PrtText locale$(54)
  EndIf

  While x<y
    x=x+1
    PrtText text$(x)
  Wend
  x=0
  PrtText ""
  PrtCommand 6,0,0,0,0
  PrtText locale$(105)+ver$+locale$(106)
  PrtCommand 6,0,0,0,0
  PrtText locale$(107)
  PrtCommand 7,0,0,0,0
Return

.editieren
MaxLen pa$=192
MaxLen fi$=192
auslagern$=ASLFileRequest$(locale$(108),pa$,fi$,"#?")
If auslagern$="" Then Return

Gosub mail_inhalt
Gosub pgp

x=0
If WriteFile(0,auslagern$)
  FileOutput 0
  While x<y
    x=x+1
    NPrint text$(x)
  Wend
  CloseFile 0
  DefaultOutput
EndIf

Return

.save_absender
a=0
If OpenFile(0,"absender.pp")
  FileInput 0
  While NOT Eof(0)
    a=a+1
    absender_1$(a)=Edit$(999)
  Wend
  FileOutput 0
  FileSeek 0,0
  FileSeek 0,Lof(0)
    a=a+1
    NPrint Mail(Auswahl)\absender
  CloseFile 0
  DefaultOutput
  DefaultInput
EndIf
a=0

Return

.user_filtern
b=0
c=0
If OpenFile(0,"filterliste.pp")
  FileInput 0
  While NOT Eof(0)
    b=b+1
    c=c+1
    filterliste$(b)=Edit$(999)
  Wend
  FileOutput 0
  FileSeek 0,0
  FileSeek 0,Lof(0)
    NPrint Mail(Auswahl)\absender
  CloseFile 0
  DefaultOutput
  DefaultInput
EndIf
b=0
c=0
If OpenFile(0,"filterliste.pp")
  FileInput 0
  While NOT Eof(0)
    b=b+1
    c=c+1
    filterliste$(b)=Edit$(999)
  Wend
  CloseFile 0
  DefaultOutput
  DefaultInput
EndIf
b=0
c=c+1

Return

.load_prefs
MaxLen pa$=192
MaxLen fi$=192
prefs$=ASLFileRequest$(locale$(109),pa$,fi$,"#?")
If prefs$="" Then Return

INCLUDE "prefs_load.bb2"
Return

.save_prefs
MaxLen pa$=192
MaxLen fi$=192
prefs$=ASLFileRequest$(locale$(110),pa$,fi$,"#?")
If prefs$="" Then Return

INCLUDE "prefs_save.bb2"
Return

.user_prefs
Free GTList 3
CloseWindow 3
GTTags #GA_TabCycle,True

GTString 10,0,123,5,259,12,locale$(111),$01,256,realname$
GTString 10,1,123,21,259,12,locale$(112),$01,256,nickname$
GTString 10,2,123,37,259,12,locale$(113),$01,256,betreff$
GTString 10,3,123,54,233,12,locale$(114),$01,256,datei$
GTButton 10,4,360,54,20,12,locale$(115),$16
GTString 10,5,122,82,259,12,locale$(116),$01,256,user_id$
GTString 10,6,122,99,259,12,locale$(117),$01,256,keyring$
GTCheckBox 10,7,353,116,26,12,locale$(118),$01
GTCheckBox 10,8,353,131,26,12,locale$(119),$01

If key_mitschicken=1 Then GTToggle 10,7,On
If passwort_erfragen=1 Then GTToggle 10,8,On

Window 10,143,41,411,162,$0000000F,locale$(120),1,2
AttachGTList 10,10
GTBevelBox 10,8,89,381,68,1
GTBevelBox 10,6,13,385,69,1

Repeat
    ev.l = WaitEvent
    k$ = Inkey$
    Select ev
      Case #IDCMP_REFRESHWINDOW
        GT_BeginRefresh_ Peek.l (Addr Window(10))
        GT_EndRefresh_ Peek.l (Addr Window(10)),True

      Case #IDCMP_GADGETUP
        Select GadgetHit
            Case 0:realname$=GTGetString(10,0)
            Case 1:nickname$=GTGetString(10,1)
            Case 2:betreff$=GTGetString(10,2)
            Case 3:datei$=GTGetString(10,3)
            Case 4:
              MaxLen pa$=192
              MaxLen fi$=192
              datei$=ASLFileRequest$(locale$(147),pa$,fi$,"#?")
              GTSetString 10,3,datei$
            Case 5:user_id$=GTGetString(10,5)
            Case 6:keyring$=GTGetString(10,6)
            Case 7
              key_mitschicken+1
              If key_mitschicken=2 Then key_mitschicken=0
            Case 8
              passwort_erfragen+1
              If passwort_erfragen=2 Then passwort_erfragen=0
        End Select
    End Select
Until ev=#IDCMP_CLOSEWINDOW
DetachGTList 10
Free GTList 10
CloseWindow 10
Return

.mbx_prefs
Free GTList 3
CloseWindow 3
GTTags #GA_TabCycle,True

GTString 10,0,123,5,259,12,locale$(121),$01,256,mailboxcall$
GTString 10,1,123,21,259,12,locale$(122),$01,256,username$
GTString 10,2,123,37,259,12,locale$(123),$01,256,mbxpasswort$
GTString 10,3,123,54,259,12,locale$(124),$01,256,homebbs$
GTString 10,4,123,70,259,12,locale$(125),$01,256,prompt$
GTString 10,5,123,86,259,12,locale$(126),$01,256,mbxpfad$
GTCycle 10,6,111,116,198,12,locale$(127),$08,locale$(128)

Window 10,144,41,411,162,$0000000F,locale$(129),1,2
AttachGTList 10,10
GTBevelBox 10,12,121,375,36,1
GTBevelBox 10,10,13,380,103,1

Repeat
    ev.l = WaitEvent
    k$ = Inkey$
    Select ev
      Case #IDCMP_REFRESHWINDOW
        GT_BeginRefresh_ Peek.l (Addr Window(10))
        GT_EndRefresh_ Peek.l (Addr Window(10)),True

      Case #IDCMP_GADGETUP
        Select GadgetHit
            Case 0:mailboxcall$=GTGetString(10,0)
            Case 1:username$=GTGetString(10,1)
            Case 2:mbxpasswort$=GTGetString(10,2)
            Case 3:homebbs$=GTGetString(10,3)
            Case 4:prompt$=GTGetString(10,4)
            Case 5:mbxpfad$=GTGetString(10,5)
            Case 6:
              If ExecVersion < 39
                software=software+1
                If software=3 Then software=0
              Else
                software=GTGetAttrs(10,6,#GTCY_Active)
              EndIf
              If software=2 Then Gosub fehler
        End Select
    End Select
Until ev=#IDCMP_CLOSEWINDOW
DetachGTList 10
Free GTList 10
CloseWindow 10

Return

.serial_prefs
GTTags #GA_TabCycle,True

Free GTList 3
CloseWindow 3
GTString 10,0,123,5,259,12,locale$(130),$01,256,device$
GTString 10,1,123,21,259,12,locale$(131),$01,256,unit$
GTString 10,2,123,37,259,12,locale$(132),$01,256,baud$

Window 10,148,89,411,74,$0000000F,locale$(133),1,2
AttachGTList 10,10
GTBevelBox 10,6,12,385,59,1

Repeat
    ev.l = WaitEvent
    k$ = Inkey$
    Select ev
      Case #IDCMP_REFRESHWINDOW
        GT_BeginRefresh_ Peek.l (Addr Window(10))
        GT_EndRefresh_ Peek.l (Addr Window(10)),True

      Case #IDCMP_GADGETUP
        Select GadgetHit
            Case 0:device$=GTGetString(10,0)
            Case 1:unit$=GTGetString(10,1)
            Case 2:baud$=GTGetString(10,2)
        End Select
    End Select
Until ev=#IDCMP_CLOSEWINDOW
DetachGTList 10
Free GTList 10
CloseWindow 10

Return

.allgemeine_prefs
Free GTList 3
CloseWindow 3
GTTags #GA_TabCycle,True

GTString   12,0,123,5,237,12,locale$(134),$01,256,editor_prefs$
GTString   12,1,123,22,237,12,locale$(135),$01,256,puffer_speichern$
GTString   12,2,123,39,237,12,locale$(136),$01,256,puffer_laden$
GTString   12,3,123,56,237,12,locale$(137),$01,256,pgp_path$
GTString   12,4,123,73,237,12,locale$(138),$01,256,plus_path$
GTButton   12,5,66,122,87,12,locale$(139),$16
GTButton   12,6,153,122,87,12,locale$(140),$16
GTCheckBox 12,7,242,122,26,12,locale$(141),$02
GTButton   12,8,363,6,16,12,locale$(142),$16
GTButton   12,9,363,23,16,12,locale$(142),$16
GTButton   12,10,363,40,16,12,locale$(142),$16
GTButton   12,11,363,57,16,12,locale$(142),$16
GTButton   12,12,363,74,16,12,locale$(142),$16
GTString   12,13,123,90,237,12,locale$(143),$01,256,lifetime_prefs$
GTCheckBox 12,14,123,107,26,12,locale$(144),$01
GTString   12,15,285,107,75,12,locale$(145),$01,256,signaturtrenner$

If signatur_ab=1 Then GTToggle 12,14,On
If wbstart=1     Then GTToggle 12,7,On

Window 12,144,41,411,162,$0000000F,locale$(146),1,2
AttachGTList 12,12
GTBevelBox 12,07,132,380,15,1
GTBevelBox 12,07,13,380,118,1

Repeat
    ev.l = WaitEvent
    k$ = Inkey$
    Select ev
      Case #IDCMP_REFRESHWINDOW
        GT_BeginRefresh_ Peek.l (Addr Window(12))
        GT_EndRefresh_ Peek.l (Addr Window(12)),True

      Case #IDCMP_GADGETUP
        Select GadgetHit
            Case 0:editor_prefs$=GTGetString(12,0)
            Case 1:puffer_speichern$=GTGetString(12,1)
            Case 2:puffer_laden$=GTGetString(12,2)
            Case 3:pgp_path$=GTGetString(12,3)
            Case 4:plus_path$=GTGetString(12,4)
            Case 5
              *sc.screeninfo=ASLScreenRequest(31)

              If *sc
                col.w=-1
                scrtags(0)\ti_Tag=#SA_Left,0
                scrtags(1)\ti_Tag=#SA_Top,0
                scrtags(2)\ti_Tag=#SA_Width,*sc\width
                scrtags(3)\ti_Tag=#SA_Height,*sc\height
                scrtags(4)\ti_Tag=#SA_Depth,*sc\tiefe
                scrtags(5)\ti_Tag=#SA_Overscan,*sc\overscan
                scrtags(6)\ti_Tag=#SA_AutoScroll,*sc\autoscroll
                scrtags(7)\ti_Tag=#SA_DisplayID,*sc\id
                scrtags(8)\ti_Tag=#SA_Pens,&col
                If *sc\width >= 640 AND *sc\height >= 256
                  screendata1.l=*sc\width
                  screendata2.l=*sc\height
                  screendata3.l=*sc\tiefe
                  screendata4.l=*sc\overscan
                  screendata5.l=*sc\autoscroll
                  screendata6.l=*sc\id
                Else
                  Request locale$(252),locale$(251),locale$(21)
                EndIf
              EndIf

            Case 6
              *font.fontinfo=ASLFontRequest(31)
              font$=*font\name
              fontysize=*font\ysize
            Case 7
              wbstart+1
              If wbstart=2 Then wbstart=0
            Case 8
              MaxLen pa$=192
              MaxLen fi$=192
              editor_prefs$=ASLFileRequest$(locale$(134),pa$,fi$,"#?")
              GTSetString 12,0,editor_prefs$
            Case 9
              MaxLen pa$=192
              MaxLen fi$=192
              puffer_speichern$=ASLFileRequest$(locale$(147),pa$,fi$,"#?")
              GTSetString 12,1,puffer_speichern$
            Case 10
              MaxLen pa$=192
              MaxLen fi$=192
              puffer_laden$=ASLFileRequest$(locale$(147),pa$,fi$,"#?")
              GTSetString 12,2,puffer_laden$
            Case 11
              MaxLen pa$=192
              MaxLen fi$=192
              pgp_path$=ASLFileRequest$(locale$(147),pa$,fi$,"#?")
              GTSetString 12,3,pgp_path$
            Case 12
              MaxLen pa$=192
              MaxLen fi$=192
              plus_path$=ASLFileRequest$(locale$(147),pa$,fi$,"#?")
              GTSetString 12,4,plus_path$
            Case 13
              lifetime_prefs$=GTGetString(12,13)
            Case 14
              signatur_ab=GTStatus(12,14)
            Case 15
              signaturtrenner$=GTGetString(12,15)
        End Select
    End Select
Until ev=#IDCMP_CLOSEWINDOW
DetachGTList 12
Free GTList 12
CloseWindow 12

Return

.arexx_prefs
  GTTags #GA_TabCycle,True
  Window        3,97,11,428,228,$0000000F,locale$(148),1,2
  GTButton      3,0,375,8,16,12,locale$(150),$0010
  GTString      3,1,95,8,275,12,locale$(149)+"  1",$0001,256,marko$(1)
  GTButton      3,2,375,28,16,12,locale$(150),$0010
  GTString      3,3,95,28,275,12,locale$(149)+"  2",$0001,256,marko$(2)
  GTString      3,4,95,48,275,12,locale$(149)+"  3",$0001,256,marko$(3)
  GTButton      3,5,375,48,16,12,locale$(150),$0010
  GTString      3,6,95,68,275,12,locale$(149)+"  4",$0001,256,marko$(4)
  GTButton      3,7,375,68,16,12,locale$(150),$0010
  GTString      3,8,95,88,275,12,locale$(149)+"  5",$0001,256,marko$(5)
  GTButton      3,9,375,88,16,12,locale$(150),$0010
  GTString      3,10,95,108,275,12,locale$(149)+"  6",$0001,256,marko$(6)
  GTButton      3,11,375,108,16,12,locale$(150),$0010
  GTButton      3,12,375,128,16,12,locale$(150),$0010
  GTString      3,13,95,128,275,12,locale$(149)+"  7",$0001,256,marko$(7)
  GTString      3,14,95,148,275,12,locale$(149)+"  8",$0001,256,marko$(8)
  GTButton      3,15,375,148,16,12,locale$(150),$0010
  GTString      3,16,95,168,275,12,locale$(149)+"  9",$0001,256,marko$(9)
  GTButton      3,17,375,168,16,12,locale$(150),$0010
  GTButton      3,18,375,188,16,12,locale$(150),$0010
  GTString      3,19,95,188,275,12,locale$(149)+" 10",$0001,256,marko$(10)
  AttachGTList  3,3

  CloseWin = False
  Repeat
    EventType.l = WaitEvent
    Select EventType
      Case #IDCMP_REFRESHWINDOW
        GT_BeginRefresh_ Peek.l (Addr Window(3))
        GT_EndRefresh_ Peek.l (Addr Window(3)),True

      Case $00000200
        CloseWin = True

      Case $00000040
        Select GadgetHit
          Case 0
              MaxLen pa$=192
              MaxLen fi$=192
              marko$(1)=ASLFileRequest$(locale$(151),pa$,fi$,"#?")
              GTSetString 3,1,marko$(1)

          Case 2
              MaxLen pa$=192
              MaxLen fi$=192
              marko$(2)=ASLFileRequest$(locale$(151),pa$,fi$,"#?")
              GTSetString 3,3,marko$(2)

          Case 5
              MaxLen pa$=192
              MaxLen fi$=192
              marko$(3)=ASLFileRequest$(locale$(151),pa$,fi$,"#?")
              GTSetString 3,4,marko$(3)

          Case 7
              MaxLen pa$=192
              MaxLen fi$=192
              marko$(4)=ASLFileRequest$(locale$(151),pa$,fi$,"#?")
              GTSetString 3,6,marko$(4)

          Case 9
              MaxLen pa$=192
              MaxLen fi$=192
              marko$(5)=ASLFileRequest$(locale$(151),pa$,fi$,"#?")
              GTSetString 3,8,marko$(5)

          Case 11
              MaxLen pa$=192
              MaxLen fi$=192
              marko$(6)=ASLFileRequest$(locale$(151),pa$,fi$,"#?")
              GTSetString 3,10,marko$(6)

          Case 12
              MaxLen pa$=192
              MaxLen fi$=192
              marko$(7)=ASLFileRequest$(locale$(151),pa$,fi$,"#?")
              GTSetString 3,13,marko$(7)

          Case 15
              MaxLen pa$=192
              MaxLen fi$=192
              marko$(8)=ASLFileRequest$(locale$(151),pa$,fi$,"#?")
              GTSetString 3,14,marko$(8)

          Case 17
              MaxLen pa$=192
              MaxLen fi$=192
              marko$(9)=ASLFileRequest$(locale$(151),pa$,fi$,"#?")
              GTSetString 3,16,marko$(9)

          Case 18
              MaxLen pa$=192
              MaxLen fi$=192
              marko$(10)=ASLFileRequest$(locale$(151),pa$,fi$,"#?")
              GTSetString 3,19,marko$(10)
        End Select
    End Select
  Until CloseWin

  marko$(1)=GTGetString(3,1)
  marko$(2)=GTGetString(3,3)
  marko$(3)=GTGetString(3,4)
  marko$(4)=GTGetString(3,6)
  marko$(5)=GTGetString(3,8)
  marko$(6)=GTGetString(3,10)
  marko$(7)=GTGetString(3,13)
  marko$(8)=GTGetString(3,14)
  marko$(9)=GTGetString(3,16)
  marko$(10)=GTGetString(3,19)

  CloseWindow  3
  DetachGTList 3
  Free GTList  3

  CloseWin=False
  If WriteFile(0,"macros.pp")
    FileOutput 0
    NPrint marko$(1)
    NPrint marko$(2)
    NPrint marko$(3)
    NPrint marko$(4)
    NPrint marko$(5)
    NPrint marko$(6)
    NPrint marko$(7)
    NPrint marko$(8)
    NPrint marko$(9)
    NPrint marko$(10)
    CloseFile 0
    DefaultOutput
  EndIf

Return

.brettnews

If Instr(Liste(Auswahl)\Text,locale$(232)) > 0 Then Return
If Instr(Mail(Auswahl)\mailflags,"G")=0
  Mail(Auswahl)\mailflags+" G"
EndIf

Gosub mail_inhalt
Gosub pgp

.nachricht_lesen
 x=0:z=0
 Window        6,windowxpos,windowypos,640,245,$0000000F|$400,locale$(152),1,2
 Menus Off
  While NOT Joyb(0)=2 OR Event=#IDCMP_CLOSEWINDOW
    If x<y-1
     x=x+1
     z=z+1
     If z>WindowHeight/fontysize-2
       WaitEvent
       z=0
       WLocate 2,2
       WCls 0
     EndIf
     If Left$(text$(x),1)=">"
      WindowFont 0,2
      WJam 0
      WColour 1,0
     EndIf
     If Left$(text$(x),2)=">>"
      WindowFont 0,2
      WJam 0
      WColour 2,0
     EndIf
     If Left$(text$(x),3)=">>>"
      WindowFont 0,2
      WJam 0
      WColour 3,0
     EndIf
     If Left$(text$(x),1)<>">"
      WindowFont 0,0
      WJam 0
      WColour 1,0
     EndIf
     NPrint text$(x)
    EndIf

    Select Event
      Case #IDCMP_REFRESHWINDOW
        GT_BeginRefresh_ Peek.l (Addr Window(6))
        GT_EndRefresh_ Peek.l (Addr Window(6)),True
    End Select
  Wend
CloseWindow 6
x=0
z=0
Menus On
Return

.nachrichten_info
Window 3,60,11,524,160,$0000000F|$400,locale$(153),1,2
!Header

CloseWin = False
Repeat
  EventType.l = WaitEvent
  Select EventType
    Case $00000200
        CloseWin = True
  End Select
Until CloseWin = True
CloseWin=False
CloseWindow 3

Return

.pathkarte
Window 3,windowxpos,windowypos,638,244,$0000000F|$400,locale$(154),1,2

mailboxpath{Mail(Auswahl)\mailpath}
color=1
If Exists("mappic.pp")
  ILBMInfo "mappic.pp"
  BitMap 0,ILBMWidth,ILBMHeight,ILBMDepth
  LoadBitMap 0,"mappic.pp",1
  BitMaptoWindow 0,3
  SaveScreen 0,"t:screen.pp"
  ShowPalette 1
  pic=True
Else
  pic=False
EndIf

For mailp=1 To pathnum-1
  a=0
  Repeat
    a+1
  Until path$(mailp)=mailpath(a)\name OR a>mapdata
  If a<mapdata
    If mailp=1 Then corx=mailpath(a)\x:cory=mailpath(a)\y
    oldx=corx
    oldy=cory
    corx=mailpath(a)\x
    cory=mailpath(a)\y
    WLine oldx,oldy,corx,cory,color
    WLocate corx,cory
    NPrint mailpath(a)\name
    color=1
  Else
    color=2
  EndIf
Next

Repeat
  ev.l=WaitEvent
  If pic=True BitMaptoWindow 0,3
Until ev = #IDCMP_CLOSEWINDOW

If pic=True
  Free BitMap  0
  LoadPalette  0,"t:screen.pp"
  ShowPalette  0
  DeleteFile_  "T:screen.pp"
  Free Palette 0
EndIf

CloseWindow 3
Return

.search
  searchoptions$=locale$(155)

  Window 1,106,11,389,140,$0000000F,locale$(156),1,2
  GTString      1,1,24,6,314,12,locale$(157),$0008,256,searchstring$
  GTCycle       1,2,24,35,314,11,"",$0002,searchoptions$
  GTCheckBox    1,3,24,59,26,12,locale$(158),$0002
  GTButton      1,4,24,99,108,11,locale$(159),$0010
  GTButton      1,5,224,99,108,11,locale$(160),$0010
  AttachGTList  1,1
  GTBevelBox    1,9,13,353,117,$0001

  CloseWin = False
  CaseSense Off
  Repeat
    EventType.l = WaitEvent
    Select EventType
      Case #IDCMP_REFRESHWINDOW
        GT_BeginRefresh_ Peek.l (Addr Window(1))
        GT_EndRefresh_ Peek.l (Addr Window(1)),True

      Case #IDCMP_CLOSEWINDOW
        CloseWin = True

      Case #IDCMP_GADGETUP
        Select GadgetHit
          Case 1
            searchstring$=GTGetString(1,1)
            old_a=1
          Case 2
            If ExecVersion < 39
              searchoption+1
              If searchoption=2 Then searchoption=0
            Else
              searchoption=GTGetAttrs(1,2,#GTCY_Active)
            EndIf
          Case 3
            sense=GTStatus(1,3)
          Case 4
            If ExecVersion < 39
              searchoption+1
              If searchoption=2 Then searchoption=0
            Else
              searchoption=GTGetAttrs(1,2,#GTCY_Active)
            EndIf
            sense=GTStatus(1,3)
            If sense=1
              CaseSense On
            Else
              CaseSense Off
            EndIf

            Select searchoption
              Case 0
                For a=old_a To maxmails
                  If Instr(Mail(a)\subject,searchstring$)>0 Then GTSetAttrs 0,0,#GTLV_Selected,a:old_a=a+1:a=maxmails:
                Next
              Case 1
                For a=old_a To maxmails
                  If Instr(Mail(a)\nummer,searchstring$)>0 Then GTSetAttrs 0,0,#GTLV_Selected,a:old_a=a+1:a=maxmails:
                Next
              Case 2
                For a=old_a To maxmails
                  If Instr(Mail(a)\absender,searchstring$)>0 Then GTSetAttrs 0,0,#GTLV_Selected,a:old_a=a+1:a=maxmails:
                Next
              Case 3
                For a=old_a To maxmails
                  If Instr(Mail(a)\empfaenger,searchstring$)>0 Then GTSetAttrs 0,0,#GTLV_Selected,a:old_a=a+1:a=maxmails:
                Next
              Case 4
                For a=old_a To maxmails
                  If OpenFile(0,puffer_laden$)
                    FileInput 0
                    FileSeek 0,Mail(a)\laenge
                    y=0:x=0
                    Repeat
                      x+1
                      y+1
                      text$(x)=Edit$(999)
                    Until Loc(0)=Mail(a)\newmail OR Eof(0)
                    y+1:x=0
                    CloseFile 0
                    DefaultInput
                  EndIf

                  For x=0 To y
                    If Instr(text$(x),searchstring$)>0 Then GTSetAttrs 0,0,#GTLV_Selected,a:old_a=a+1:x=y:a=maxmails
                  Next
                Next
            End Select
          Case 5
            CloseWin=True
        End Select
    End Select
  Until CloseWin
  CaseSense On

  CloseWindow  1
  CloseScreen  1
  DetachGTList 1
  Free GTList  1
  CloseWin=False
Return

.entpacken
Request locale$(161),locale$(162),locale$(163)
MaxLen pa$=192
MaxLen fi$=192
plusfile$=ASLFileRequest$(locale$(164),pa$,fi$,"#?")
If plusfile$="" Then Return

plusfile$=Replace$(plusfile$,"Ram Disk:","Ram:")
plusfile$=plusfile$+".7pl"

Gosub mail_inhalt
Gosub pgp

If OpenFile(0,plusfile$)
  FileOutput 0
  x=0
  While x<y
    x=x+1
    NPrint text$(x)
  Wend
  x=0
  DefaultOutput
  CloseFile 0
EndIf

cmd$=plus_path$+" "+plusfile$+Chr$(10)+"WAIT 5"+Chr$(10)+"ENDCLI"
cli$="con:0/0/0/0 7plus entpacken"
If WriteFile(0,cli$)
  WBenchToFront_
  Execute_ &cmd$,Peek.l(Addr File(0)),0
  WBenchToBack_
  CloseFile 0
EndIf
DeleteFile_(plusfile$)
Return

.netcall
unit=Val(unit$)
If OpenFile(0,puffer_speichern$)
  g=0:h=0
  FileInput 0
  While NOT Eof(0)
    puffer$(g)=Edit$(999)
    g=g+1:h=h+1
  Wend
  h=h+1
  DefaultInput
  CloseFile 0
EndIf

If OpenSerial(device$,unit,Val(baud$),0)
  WriteSerial unit,Asc(Chr$(27))
  WriteSerial unit,Asc("S")
  WriteSerial unit,Asc(" ")
  WriteSerial unit,Asc("1")
  WriteSerial unit,Asc(Chr$(13))
  WriteSerial unit,Asc(Chr$(27))
  WriteSerial unit,Asc("I")
  WriteSerial unit,Asc(" ")
  For path=1 To Len(username$)
    WriteSerial unit,Asc(Mid$(username$,path,1))
  Next
  WriteSerial unit,Asc(Chr$(13))
  WriteSerial unit,Asc(Chr$(27))
  WriteSerial unit,Asc("C")
  WriteSerial unit,Asc(" ")
  For path=1 To Len(mbxpfad$)
    WriteSerial unit,Asc(Mid$(mbxpfad$,path,1))
  Next
  WriteSerial unit,Asc(Chr$(13))
  g=0

  While g<h
    For path=1 To Len(puffer$(g))
      WriteSerial unit,Asc(Mid$(puffer$(g),path,1))
    Next
    g=g+1
  Wend
  g=0:h=0
  WriteSerial unit,Asc(Chr$(13))

  If OpenFile (0,puffer_speichern$)
    puffer_senden=Lof(0)
    CloseFile 0
  EndIf
  DeleteFile_(puffer_speichern$)

  CloseSerial unit
EndIf

LogFileOutput{locale$(256)+" "+Str$(puffer_empfangen)+locale$(257)+" "+Str$(puffer_senden)+locale$(258)}
Return

.versand
INCDIR  "Blitz2:Examples/Packet-Point/"
INCLUDE "versand.bb2"

.fehler
Request locale$(207),locale$(208),locale$(209)
Return

.arexx
arexxcommand{UCase$(GetRexxCommand(arexxmsg,1))}
Select arg$(0)
  Case "PPQUIT"
    LogFileOutput{locale$(259)}
    CloseWindow 0:CloseScreen 0:DetachGTList 0:Free GTList 0:Free MenuList 0:End
  Case "LOADPUFFER"
    puffer_laden$=arg$(1)
    puffer_arexx=True
    Gosub load_puffer
    puffer_arexx=False
  Case "SAVEPUFFER"
    puffer_speichern$=arg$(1)
  Case "EXTRACTMAIL"
    auslagern$=arg$(1)
    header=False
    If arg$(2)<>"" AND arg$(2)<>"HEADER" Then Auswahl=Val(arg$(2))
    If UCase$(arg$(2))="HEADER" Then header=True
    If UCase$(arg$(3))="HEADER" Then header=True
    x=0

    Gosub mail_inhalt
    Gosub pgp

    If WriteFile(0,auslagern$)
      FileOutput 0
      If header=True
        header=False
        !Header
      EndIf
      While x<y
        x=x+1
        NPrint text$(x)
      Wend
      x=0
      CloseFile 0
      DefaultOutput
    EndIf
  Case "LOADPREFS"
    prefs$=arg$(1)
    INCLUDE "prefs_load.bb2"
  Case "SAVEPREFS"
    prefs$=arg$(1)
    INCLUDE "prefs_save.bb2"
  Case "EDITOR"
    editordatei$=arg$(1)
    editor$=Replace$(editor_prefs$,"%s",editordatei$)
    Execute_ editor$,0,0
  Case "PPHELP"
    Gosub help
  Case "NETCALL"
  Case "SAVEUSER"
    absender=False
    absendersave$=arg$(1)
    If absendersave$<>""
      absender=True
    EndIf
    absendersave$=" "+absendersave$
    a=0
    If OpenFile(0,"absender.pp")
      FileInput 0
      While NOT Eof(0)
        a=a+1
        absender_1$(a)=Edit$(999)
      Wend
      FileOutput 0
      FileSeek 0,0
      FileSeek 0,Lof(0)
        a=a+1
        If absender=True
          NPrint absendersave$
        Else
          NPrint Mail(Auswahl)\absender
        EndIf
      CloseFile 0
      DefaultOutput
      DefaultInput
    EndIf
    a=0
  Case "KILLUSER"
    userkill=False
    userkill$=arg$(1)
    If userkill$<>""
      userkill=True
    EndIf
    userkill$=" "+userkill$

    b=0
    c=0
    If OpenFile(0,"filterliste.pp")
      FileInput 0
      While NOT Eof(0)
        b=b+1
        c=c+1
        filterliste$(b)=Edit$(999)
      Wend
      FileOutput 0
      FileSeek 0,0
      FileSeek 0,Lof(0)
      If userkill=True
        NPrint userkill$
      Else
        NPrint Mail(Auswahl)\absender
      EndIf
      CloseFile 0
      DefaultOutput
      DefaultInput
    EndIf
    b=0
    c=0
    If OpenFile(0,"filterliste.pp")
      FileInput 0
      While NOT Eof(0)
        b=b+1
        c=c+1
        filterliste$(b)=Edit$(999)
      Wend
      CloseFile 0
      DefaultOutput
      DefaultInput
    EndIf
    b=0
    c=c+1
  Case "SENDMAIL"
    subjekt$=arg$(2)
    empfaenger$=arg$(1)
    dosdatei$=arg$(3)
    If arg$(4)="" OR arg$(4) <> "WAIT"
      If OpenFile (1,dosdatei$)
        g=0:h=0
        FileInput 1
        While NOT Eof(1)
          ppedit$(g)=Edit$(999)
          g=g+1:h=h+1
        Wend
        h=h+1
        DefaultInput
        CloseFile 1
      EndIf
      If OpenFile(0,"SIG.pp")
        s=0:t=0
        FileInput 0
        While NOT Eof(0)
          sig$(s)=Edit$(999)
          s=s+1:t=t+1
        Wend
        t=t+1
        DefaultInput
        CloseFile 0
      EndIf
      If OpenFile(0,puffer_speichern$)
        FileOutput 0
        FileSeek 0,Lof(0)
        NPrint "Send ",empfaenger$," #",lifetime_prefs$," ",subjekt$
        NPrint "DISKU-IN=no"
        NPrint "EMP=no"
        NPrint "DATE=",Date$(SystemDate)
        NPrint "TIME=",Hours,":",Mins,":",Secs,""
        NPrint""
        g=0
        While g<h
          NPrint ppedit$(g)
          g=g+1
        Wend
        s=0
        While s<t
          NPrint sig$(s)
          s=s+1
        Wend
        NPrint""
        NPrint locale$(196)+ver$+locale$(197)
        NPrint"nnnn"
        CloseFile 0
        DefaultOutput
      EndIf
      LogFileOutput{locale$(260)+" "+empfaenger$}
    Else
      Gosub versand
    EndIf
End Select
ReplyRexxMsg arexxmsg,0,0,"ALL MESSAGES OK!"
Return

.arexxmarko
msg.l=CreateRexxMsg(arexxport,"pprexx","PACKETPOINT")
If marko=False
  MaxLen pa$=192
  MaxLen fi$=192
  rexxmarko$=ASLFileRequest$(locale$(210),pa$,fi$,"#?.pprexx")
  rexxmarko$=Replace$(rexxmarko$,"Ram Disk:","Ram:")
EndIf
SendRexxCommand msg,rexxmarko$,#RXCOMM|RXFF_RESULT
marko=False
Return

.appevents
puffer_laden$=AppFile(1)
puffer_arexx=True
Gosub load_puffer
puffer_arexx=False
Return

.pgp
If Mail(Auswahl)\pgpmail=True
  x=0
  If WriteFile(0,"ram:mail.pgp")
    FileOutput 0
    While x<y
      x=x+1
      NPrint text$(x)
    Wend
    CloseFile 0
    DefaultOutput
  EndIf

  If passwort_erfragen=1 AND Exists("env:PGPPASS")=0

    Window        14,3,192,637,64,$0000000F,locale$(245),1,2
    GTString      14,0,135,15,460,15,locale$(246),$0001,256,""
    AttachGTList  14,14

    CloseWin = False
    Repeat
      EventType.l = WaitEvent
      Select EventType
        Case #IDCMP_REFRESHWINDOW
          GT_BeginRefresh_ Peek.l (Addr Window(14))
          GT_EndRefresh_ Peek.l (Addr Window(14)),True
        Case $00000200
          CloseWin = True
        Case $00000040
          Select GadgetHit
            Case 0
              CloseWin = True
              Execute_"setenv PGPPASS "+GTGetString(14,0),0,0
          End Select
      End Select
    Until CloseWin
    CloseWin = False
    CloseWindow 14
    DetachGTList 14
    Free GTList 14
  EndIf

  cmd$=pgp_path$+" ram:mail.pgp"+Chr$(10)+"ENDCLI"
  cli$="con:0/0/"+Str$(WBWidth)+"/"+Str$(WBHeight)+""
  If WriteFile(0,cli$)
    WBenchToFront_
    Execute_ &cmd$,Peek.l(Addr File(0)),0
    WBenchToBack_
    CloseFile 0
    DeleteFile_("ram:mail.pgp")
    x=0:y=0:
    If OpenFile(0,"ram:mail")
      FileInput 0
      While NOT Eof(0)
        x=x+1:y=y+1
        text$(x)=Edit$(999)
      Wend
      y=y+1
      CloseFile 0
      DefaultOutput
    EndIf
    DeleteFile_("ram:mail")
  EndIf
EndIf
Return

.mail_inhalt
If OpenFile(0,puffer_laden$)
  FileInput 0
  FileSeek 0,Mail(Auswahl)\laenge
  y=0:x=0
  Repeat
    x+1
    y+1
    text$(x)=Edit$(999)
  Until Loc(0)=Mail(Auswahl)\newmail OR Eof(0)
  y+1:x=0
  CloseFile 0
  DefaultInput
EndIf

Return
