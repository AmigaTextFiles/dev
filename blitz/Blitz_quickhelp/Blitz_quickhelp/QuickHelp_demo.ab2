
; "QuickHelp_demo.ab2" a C to Blitz adaptation by Lorence Lombardo.

; Based on Pawen Marciniak's "QuickHelp.demo.c" example.


WBStartup

INCLUDE "quickhelp.include.bb2"

If quickhelp_InitLib{37}=0 Then NPrint " Could not initialize quickhelp lib...!!!" : End

PubScreenName$="Workbench"
APP_TitleWindow$="QuickHelp lib Demo Window"
APP_TitleScreenWindow$="QuickHelp lib Demo Screen"

*VisualInfo.l = 0 : *scr.Screen = 0 : *APP_Window.Window=0

Width.w=320
Height.w=150


DEFTYPE .TextAttr *Font, ScreenFont
DEFTYPE .TextFont *APP_Font

FontX.w = 0 ; WORD
FontY.w = 0 ; WORD
DEFTYPE .w OffX, OffY ; UWORD

*scr = LockPubScreen_( &PubScreenName$ )
If *scr=0 Then Goto exit

*Font = &ScreenFont
*Font\ta_Name = *scr\RastPort\Font\tf_Message\mn_Node\ln_Name
*Font\ta_YSize = *scr\RastPort\Font\tf_YSize
FontY = *Font\ta_YSize
FontX = *scr\RastPort\Font\tf_XSize

OffX = *scr\WBorLeft
OffY = *scr\RastPort\TxHeight + *scr\WBorTop + 1

*APP_Font=OpenDiskFont_( &ScreenFont )
If *APP_Font=0 Then Goto exit

*VisualInfo.l = GetVisualInfoA_ ( *scr,0 )
If *VisualInfo=0 Then Goto exit

Dim mytags.TagItem(12)

flags.l = #WFLG_DRAGBAR | #WFLG_SIZEGADGET | #WFLG_DEPTHGADGET | #WFLG_CLOSEGADGET
flags = flags | #WFLG_SMART_REFRESH | #WFLG_HASZOOM

idcmp.l = #IDCMP_CLOSEWINDOW | #IDCMP_REFRESHWINDOW | #IDCMP_GADGETHELP
idcmp = idcmp | #IDCMP_CHANGEWINDOW | #IDCMP_INACTIVEWINDOW | #IDCMP_ACTIVEWINDOW

mytags(0)\ti_Tag  = #WA_Left,        20
mytags(1)\ti_Tag  = #WA_Top,         20
mytags(2)\ti_Tag  = #WA_Width,       Width
mytags(3)\ti_Tag  = #WA_Height,      Height
mytags(4)\ti_Tag  = #WA_Title,       &APP_TitleWindow$
mytags(5)\ti_Tag  = #WA_ScreenTitle, &APP_TitleScreenWindow$
mytags(6)\ti_Tag  = #WA_Flags,       flags
mytags(7)\ti_Tag  = #WA_IDCMP,       idcmp
mytags(8)\ti_Tag  = #WA_Gadgets,     False
mytags(9)\ti_Tag  = #WA_AutoAdjust,  True
mytags(10)\ti_Tag = #WA_PubScreen,   *scr
mytags(11)\ti_Tag = #TAG_DONE

*APP_Window=OpenWindowTagList_(0, &mytags(0)\ti_Tag)
If *APP_Window=0 Then Goto exit

GT_RefreshWindow_ *APP_Window, 0 : l$=Chr$(10) : class.l = 0

SetFont_ *APP_Window\RPort,*APP_Font
HelpControl_ *APP_Window, #HC_GADGETHELP


Repeat
   WaitPort_ ( *APP_Window\UserPort )
   *imsg.IntuiMessage = GT_GetIMsg_( *APP_Window\UserPort )

   If *imsg
      class = *imsg\Class : GT_ReplyIMsg_( *imsg )

      If class = #IDCMP_GADGETHELP

         *IObject.Gadget = *imsg\IAddress : !quickhelp_CloseHelp

         If *IObject<>0 AND *IObject<>*APP_Window

            sysgtype.l = *IObject\GadgetType AND $F0 : stat.l=0 : match.b=0

            If sysgtype=#GTYP_SIZING Then txt$="The famous resizable windows." : match=1

            If sysgtype=#GTYP_WDRAGGING
               txt$="- Move the top window border -"+l$+"---"+l$
               txt$=txt$+"press the left mouse button on the"+l$
               txt$=txt$+"top border and slide to the"+l$
               txt$=txt$+"location of your choice."+l$ :  match=1
            EndIf

            If sysgtype=#GTYP_WUPFRONT Then txt$="Window to front or back...!?!?" : match=1

            If sysgtype=#GTYP_WDOWNBACK
               txt$="This might change the location." : match=1
            EndIf

            If sysgtype=#GTYP_CLOSE Then txt$="Click here if you had enough." : match=1

            If match=0 Then txt$="Unknown Gadget"

            !quickhelp_OpenHelpA{stat, &txt$, *APP_Font, *APP_Window, 0}

            If stat=0 Then !quickhelp_GetQuickHelpString{ret.l} : PutStr_ Peek$(ret)+l$

         EndIf

      EndIf

      If class = #IDCMP_CHANGEWINDOW Then !quickhelp_CloseHelp

      If class = #IDCMP_REFRESHWINDOW
         GT_BeginRefresh_( *APP_Window ) : GT_EndRefresh_ *APP_Window, True
         !quickhelp_CloseHelp
      EndIf

   EndIf
Until class=#IDCMP_CLOSEWINDOW

!quickhelp_CloseHelp

exit:
If *VisualInfo Then FreeVisualInfo_ *VisualInfo
If *scr Then UnlockPubScreen_ 0,*scr
If *APP_Font Then CloseFont_ *APP_Font
If *APP_Window Then CloseWindow_ *APP_Window
quickhelp_FreeLib {} : End



