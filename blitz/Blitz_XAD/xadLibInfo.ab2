
; "xadLibInfo.ab2" for Blitz was based on "HB2 xadLibInfo.bas"
; which was based on "xadLibInfo.c" from the XAD package,
; implementing my own style into it. ;)

; Done by Lorence Lombardo in 2008  (lombi@iprimus.com.au)

; See the archives "XADDevBas.lha" and "xadmasterdev.lha" on the
; aminet for related interest.


INCLUDE "XAD_constants.bb2"

#lib_Version=20 : #lib_Revision=22 : e$=Chr$(27) : l$=Chr$(10)

Statement PrtFlags{ffl.l, cchar$}
   If ffl Then Print "," Else Print cchar$
End Statement

xadMasterBase.l = OpenLibrary_("xadmaster.library",0)
If xadMasterBase=0 Then End

xc.l = xadGetClientInfo_

Print l$+e$+"[4m"+"Clients of xadmaster.library "
Print Peek.w(xadMasterBase + #xmb_LibNode + #lib_Version) , "."
NPrint Peek.w(xadMasterBase + #xmb_LibNode + #lib_Revision)
NPrint l$+e$+"[0m"+"Name                      |  ID  | MV |  VER  | Flags"
NPrint "--------------------------+------+----+-------+------------------------------"

While xc

   char$ = "" : fl.l = Peek.l(xc+#xc_Flags)
   Print LSet$ ( Peek$( Peek.l(xc+#xc_ArchiverName) ) ,26 ) + "| "
   If Peek.l(xc+#xc_Identifier)
      Print LSet$ (Str$( Peek.l(xc+#xc_Identifier) ),4) + " | "
   Else
      Print "---- | "
   EndIf

   Print LSet$ (Str$(Peek.w(xc+#xc_MasterVersion)),2) + " |  "
   Print Peek.w (xc+#xc_ClientVersion),".",Peek.w (xc+#xc_ClientRevision)

   If Peek.w(xc+#xc_ClientVersion) < 100
      If Peek.w(xc+#xc_ClientRevision) >= 10
         Print " "
      Else
         Print "  "
      EndIf
   EndIf
   Print "| "

   If fl AND #XADCF_FILEARCHIVER
      fl = fl AND NOT #XADCF_FILEARCHIVER
      Print "FILE" : PrtFlags {fl,char$}
   EndIf

   If fl AND #XADCF_DISKARCHIVER
      fl = fl AND NOT #XADCF_DISKARCHIVER
      Print "DISK" : PrtFlags {fl,char$}
   EndIf

   If fl AND #XADCF_FILESYSTEM
      fl = fl AND NOT #XADCF_FILESYSTEM
      Print "FILESYS" : PrtFlags {fl,char$}
   EndIf

   If fl AND #XADCF_EXTERN
      fl = fl AND NOT #XADCF_EXTERN
      Print "EXTERN" : PrtFlags {fl,char$}
   EndIf

   If fl AND #XADCF_NOCHECKSIZE
      fl = fl AND NOT #XADCF_NOCHECKSIZE
      Print "NOCHECKSIZE" : PrtFlags {fl,char$}
   EndIf

   If fl AND #XADCF_DATACRUNCHER
      fl = fl AND NOT #XADCF_DATACRUNCHER
      Print "DATACRUNCHER" : PrtFlags {fl,char$}
   EndIf

   If fl AND #XADCF_EXECRUNCHER
      fl = fl AND NOT #XADCF_EXECRUNCHER
      Print "EXECRUNCHER" : PrtFlags {fl,char$}
   EndIf

   If fl AND #XADCF_ADDRESSCRUNCHER
      fl = fl AND NOT #XADCF_ADDRESSCRUNCHER
      Print "ADDRESSCRUNCHER" : PrtFlags {fl,char$}
   EndIf

   If fl AND #XADCF_LINKER
      fl = fl AND NOT #XADCF_LINKER
      Print "LINKER" : PrtFlags {fl,char$}
   EndIf

   flags.l=#XADCF_FREEFILEINFO | #XADCF_FREEDISKINFO | #XADCF_FREETEXTINFO
   flags=flags | #XADCF_FREESKIPINFO | #XADCF_FREETEXTINFOTEXT | #XADCF_FREESPECIALINFO
   flags=flags | #XADCF_FREEXADSTRINGS

   fl = fl AND flags

   If fl
      char$ = ")" : Print "FREE("

      If fl AND #XADCF_FREEFILEINFO
         fl = fl AND NOT #XADCF_FREEFILEINFO
         Print "FI" : PrtFlags {fl,char$}
      EndIf

      If fl AND #XADCF_FREEDISKINFO
         fl = fl AND NOT #XADCF_FREEDISKINFO
         Print "DI" : PrtFlags {fl,char$}
      EndIf

      If fl AND #XADCF_FREETEXTINFO
         fl = fl AND NOT #XADCF_FREETEXTINFO
         Print "TI" : PrtFlags {fl,char$}
      EndIf

      If fl AND #XADCF_FREESKIPINFO
         fl = fl AND NOT #XADCF_FREESKIPINFO
         Print "SI" : PrtFlags {fl,char$}
      EndIf

      If fl AND #XADCF_FREESPECIALINFO
         fl = fl AND NOT #XADCF_FREESPECIALINFO
         Print "SP" : PrtFlags {fl,char$}
      EndIf

      If fl AND #XADCF_FREEXADSTRINGS
         fl = fl AND NOT #XADCF_FREEXADSTRINGS
         Print "STR" : PrtFlags {fl,char$}
      EndIf

      If fl AND #XADCF_FREETEXTINFOTEXT
         fl = fl AND NOT #XADCF_FREETEXTINFOTEXT : Print "TEXT)"
      EndIf

   EndIf

   NPrint "" : xc = Peek.l(xc+#xc_Next)

Wend

NPrint "" : CloseLibrary_ xadMasterBase

