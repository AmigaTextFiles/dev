
; "SimpleUnpackDemo.ab2" for Blitz was based on "HB2 SimpleUnpackDemo.bas"
; from the HB2 XAD package, implementing my own style into it. ;)

; Done by Lorence Lombardo in 2008  (lombi@iprimus.com.au)

; See the archives "XADDevBas.lha" and "xadmasterdev.lha" on the
; aminet for related interest.

INCLUDE "XAD_constants.bb2"

#lib_Version=20 : #lib_Revision=22 : l$=Chr$(10)

np=NumPars : If np>0 Then f$=Par$(1)
If np=0 OR f$="?"
   NPrint l$+" Usage:-"+l$+l$+" SimpleUnpackDemo <archived_file>"+l$
   End
EndIf

masterstr.l = xadAllocObjectA_(#XADOBJ_ARCHIVEINFO,0)
If masterstr

   Dim XADtags.TagItem(2)
   XADtags(0)\ti_Tag = #XAD_INFILENAME, &f$ : XADtags(1)\ti_Tag = #TAG_DONE

   r1.l = xadGetInfoA_(masterstr, &XADtags(0)\ti_Tag)

   If NOT r1 AND NOT (Peek.l(masterstr+#xai_Client) AND #XADAIF_FILECORRUPT)

      nxt.l = Peek.l(masterstr+#xai_FileInfo)

      If nxt

         cli$ = Peek$( Peek.l( Peek.l(masterstr + #xai_Client) + #xc_ArchiverName ) )
         NPrint l$+" User client:- "+cli$+l$

         While nxt
            filename$ = Peek$( Peek.l(nxt + #xfi_FileName) )
            fentry.l  = Peek.l(nxt + #xfi_EntryNumber)

            tam1.l = Peek.l(nxt + #xfi_CrunchSize)
            tam2.l = Peek.l(nxt + #xfi_Size)

            Print " Extracting to RAM:"+RSet$(Str$(tam1),10)+" bytes: ",fentry
            NPrint " - "+filename$+" (",tam2," bytes)"

            XADtags(0)\ti_Tag = #XAD_ENTRYNUMBER, fentry : out$="RAM:"+filename$
            XADtags(1)\ti_Tag = #XAD_OUTFILENAME, &out$
            XADtags(2)\ti_Tag = #TAG_DONE

            r2.l = xadFileUnArcA_(masterstr, &XADtags(0)\ti_Tag)

            If r2 Then NPrint "Error (xadFileUnArcA: "+Peek$(xadGetErrorText_(r2))+")"

            nxt =  Peek.l(nxt+#xfi_Next)
         Wend

         NPrint ""

      Else
         msg$=l$+" File does not exist"+l$+"OR"+l$+" compression is not recognised"
         msg$=msg$+l$+"OR"+l$+" file is not compressed."+l$ : NPrint msg$
      EndIf
   EndIf

   If NOT r1 Then xadFreeInfo_ masterstr
   xadFreeObjectA_ masterstr, 0

EndIf


