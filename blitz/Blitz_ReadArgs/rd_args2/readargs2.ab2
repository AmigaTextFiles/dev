
; Conventional ReadArgs in Blitz by Lorence Lombardo

; Date commenced:-  24-Feb-2014

; Here I use all of the modifiers for testing purposes only,
; which would be an unlikely real life scenario.

; If I was going to use ReadArgs in a real life prog I would try
; to arrange the reduced modifier "arg$" template in a
; friendlier manner.


Dim dargs.l(8)  ; <- number of args or more

arg$="COMPULSORY/A,OPTIONAL,NUMBER/N,MULTI/M,SWITCH/S,TOGGLE/T,KEYWORD/K,ALL/F"

; arg$="COMPULSORY/A,OPTIONAL,NUMBER/N,MULTI/M"  ; <- easier for testing multi

rdargs.l=ReadArgs_(&arg$, &dargs(0), 0)

If rdargs

   NPrint " Compulsory = "+Peek$(dargs(0))

   If dargs(1) Then NPrint " Optional = "+Peek$(dargs(1))

   If dargs(2) Then NPrint " Number = ",Peek.l(dargs(2))

   If dargs(3)  ; <--------------------------------------- ther is at least 1 arg if true
      Print " Multi = " : addy.l=Peek.l(dargs(3)) : c.l=4
      Repeat
         Print Peek$(addy) : addy=Peek.l(dargs(3)+c) : c+4
         If addy Then Print ", "
      Until addy=0
      NPrint ""
   EndIf

   If dargs(4) Then NPrint " Switch = ", dargs(4)

   If dargs(5) Then NPrint " Toggle = ", dargs(5)  ; <- on/off keyword required

   If dargs(6) Then NPrint " Keyword = "+Peek$(dargs(6))

   If dargs(7) Then NPrint " All = "+Peek$(dargs(7))  ; <- all the arg line at this point

   FreeArgs_ rdargs

Else

   NPrint "Wrong args: "+arg$
   NPrint ""
   NPrint " eg. ArgTest 1a 2b 3 4c SWITCH TOGGLE ON KEYWORD tit bla ALL bla ggggggg"
   NPrint ""

EndIf

End



