
; "cath_eg.ab2" a C to Blitz adaptation by Lorence Lombardo.
; Based on Joerg van de Loo's "Example.c" example.

; Classic Blitz Version.

; eg. cath_eg.exe times.font 15 2 1


INCLUDE "ram:catharsis.include.bb2"

If catharsis_InitLib{1}=0 Then NPrint "Can't open catharsis lib...!!!" : End

DEFTYPE .NewWindow WinRec : tit$="Catharsis Example"

WinRec\LeftEdge    = 0
WinRec\TopEdge     = 0
WinRec\Width       = 640
WinRec\Height      = 240
WinRec\DetailPen   = -1
WinRec\BlockPen    = -1
WinRec\IDCMPFlags  = #IDCMP_CLOSEWINDOW | #IDCMP_INTUITICKS
WinRec\Flags       = #WFLG_DRAGBAR | #WFLG_DEPTHGADGET | #WFLG_CLOSEGADGET
WinRec\Flags       = WinRec\Flags | #WFLG_SMART_REFRESH | #WFLG_RMBTRAP
WinRec\FirstGadget = 0
WinRec\CheckMark   = 0
WinRec\Title       = &tit$
WinRec\Screen      = 0
WinRec\BitMap      = 0
WinRec\MinWidth    = 640
WinRec\MinHeight   = 240
WinRec\MaxWidth    = -1
WinRec\MaxHeight   = -1
WinRec\Type        = #WBENCHSCREEN


StrTxt1$ = "Going to create Catharsis-font, that takes its time..."
StrTxt2$ = "Done!  Click here." : Dim StrTxts$(4)

StrTxts$(0) = "A camel is an animal, but an animal is not necessarily a camel."
StrTxts$(1) = "0 1 2 3 4 5 6 7 8 9"
StrTxts$(2) = "% & ! ' e 0 . 1 $ ! x # p f"
StrTxts$(3) = "Catharsis - simple Amiga shared library"

*ib.IntuitionBase = *_CatharsisBase\cts_IntuitionBase

Function.l ReadPensCol{pen.l}
   SHARED *ib : Dim table.l(3) : DEFTYPE .l v, v1, help
   ms.b=0 ; <- when this is changed to a word the CLI text output is closer to the C eg.
   If ExecVersion>38
      GetRGB32_ *ib\ActiveScreen\ViewPort\ColorMap, pen, 1, &table(0)
      ms = table(0) LSR 24 : v = ms LSL 16
      ms = table(1) LSR 24 : v = v + (ms LSL 8)
      ms = table(2) LSR 24 : v = v + ms
   Else
      v = GetRGB4_( *ib\ActiveScreen\ViewPort\ColorMap, pen)
      help = ((v & $F00) LSR 8) : help = help + (help LSL 4)
      v1 = help LSL 16 : help = ((v & $F0) LSR 4)
      help = help + (help LSL 4) : v1 = v1 + (help LSL 8)
      help = v & $F : help = help + (help LSL 4) : v1=v1+help : v=v1
   EndIf
   Function Return v
End Function


Statement ClearRectangle{ *rp.RastPort, pen.l, x.l, y.l, width.l, height.l}
   SetAPen_  *rp, pen : SetDrMd_ *rp, #JAM2
   RectFill_ *rp, x, y, x + width - 1, y + height - 1 : SetDrMd_ *rp, #JAM1
End Statement


Function.s hlx{num.l, dig.b}
   Function Return "$"+Right$(Hex$(num),dig)
End Function


np.b=NumPars : #XOFS = 10 : Dim os.l(3)

If np>2
   bgpen.l = Val( Par$(3) ) : bg.l = ReadPensCol{ bgpen }
Else
   bgpen.l=0 : bg.l=ReadPensCol{0}
EndIf


If np>3
   fgpen.l = Val( Par$(4) ) : fg.l = ReadPensCol{ fgpen }
Else
   fgpen.l=0 : fg.l=ReadPensCol{0}
EndIf


!CatharsisColorValueA {cts.l, bg, fg, &os(0), &os(1), &os(2)}
Print "BG: "+hlx{bg,6}+", FG: "+hlx{fg,6}+", Catharsis-Text-Color: "+hlx{cts,6}
NPrint " or RGB32: "+hlx{os(0),8}+", "+hlx{os(1),8}+", "+hlx{os(2),8}


If np>1
   DEFTYPE .TextAttr TextFontAttr : stat.l=Val(Par$(2))
   DEFTYPE .TextFont *TextFont, *CatharsisFont : nm$=Par$(1)
   TextFontAttr\ta_Name = &nm$
   TextFontAttr\ta_YSize = stat
   TextFontAttr\ta_Style = 0
   TextFontAttr\ta_Flags = #FPF_ROMFONT
   *TextFont = OpenFont_(&TextFontAttr)
   If *TextFont=0 OR *TextFont\tf_YSize<>stat
      If *TextFont Then CloseFont_ *TextFont
      TextFontAttr\ta_Flags = #FPF_DISKFONT
      *TextFont = OpenDiskFont_( &TextFontAttr)
   EndIf
   If *TextFont
      *win.Window=OpenWindow_( &WinRec)
      If *win
         penreserved.l = -1
         If ExecVersion>38
            Dim catags.TagItem(2) : catags(0)\ti_Tag = #OBP_Precision
            catags(0)\ti_Data = #PRECISION_EXACT
            catags(1)\ti_Tag = #TAG_DONE
            pen.l=ObtainBestPenA_( *win\WScreen\ViewPort\ColorMap, os(0), os(1), os(2), &catags(0)\ti_Tag)
         Else
            pen.l=3
         EndIf
         If pen=-1
            pen=FindColor_( *win\WScreen\ViewPort\ColorMap, os(0), os(1), os(2), -1)
         Else
            penreserved = pen
         EndIf
         SetFont_ *win\RPort, *TextFont : SetAPen_ *win\RPort, 1
         SetDrMd_ *win\RPort, #JAM2 : slen.w = Len (StrTxt1$)
         width.l = *win\Width - *win\BorderLeft - *win\BorderRight - #XOFS
         While width < TextLength_( *win\RPort, &StrTxt1$, slen)
            slen-1
         Wend
         Move_ *win\RPort, #XOFS, *win\BorderTop + 8 + *TextFont\tf_Baseline
         Text_ *win\RPort, &StrTxt1$, slen
         !catharsis_OpenCatharsisFontA {*CatharsisFont, *TextFont}
         If *CatharsisFont
            slen = Len (StrTxt2$)
            While width < TextLength_( *win\RPort, &StrTxt2$, slen)
               slen-1
            Wend
            Move_ *win\RPort, #XOFS, *win\BorderTop + 8 + *TextFont\tf_Baseline + *TextFont\tf_YSize + 4
            Text_ *win\RPort, &StrTxt2$, slen : cycl.l=0 : #MAXTEXTS=4
            stat=-1 : imsgclass.l=0 : WaitPort_ *win\UserPort
            Repeat
               *imsg.IntuiMessage=GetMsg_(*win\UserPort)
               If *imsg
                  imsgclass = *imsg\Class : ReplyMsg_ *imsg
                  If imsgclass=#IDCMP_INTUITICKS Then cycl+1
                  If cycl=40
                     cycl=1 : stat+1 : If stat > (#MAXTEXTS-1) Then stat=0
                     slen = Len (StrTxts$(stat))
                     While width < TextLength_( *win\RPort, &StrTxts$(stat), slen)
                        slen-1
                     Wend
                     ClearRectangle{*win\RPort, bgpen, #XOFS, *win\BorderTop + 8, width, *TextFont\tf_YSize}
                     Move_ *win\RPort, #XOFS, *win\BorderTop + 8 + *TextFont\tf_Baseline
                     SetAPen_ *win\RPort, fgpen
                     Text_ *win\RPort, &StrTxts$(stat), slen

                     y.l=*win\BorderTop + 8 + *TextFont\tf_YSize + 4
                     ClearRectangle{*win\RPort, bgpen, #XOFS, y, width, *TextFont\tf_YSize}
                     Move_ *win\RPort, #XOFS, *win\BorderTop + 8 + *TextFont\tf_Baseline + *TextFont\tf_YSize + 4
                     SetAPen_ *win\RPort, fgpen
                     !CatharsisTextA{bla.l, *win\RPort, &StrTxts$(stat), slen, pen, *CatharsisFont}

                  EndIf
               Else
                  WaitPort_ *win\UserPort
               EndIf
            Until imsgclass=#IDCMP_CLOSEWINDOW
            !catharsis_CloseCatharsisFontA{*CatharsisFont}  ; ROM2.0 prob here
         Else
            NPrint "Cannot open/create font!"
         EndIf
         If penreserved <> -1
            ReleasePen_ *win\WScreen\ViewPort\ColorMap, penreserved
         EndIf
         CloseWindow_ *win
      Else
         NPrint "Cannot open a window of size 640x240 pixels!"
      EndIf
      CloseFont_ *TextFont
   EndIf
EndIf

catharsis_FreeLib {} : End


