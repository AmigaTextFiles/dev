
; "cli_args13.ab2" v1.0 (c) 2010 by Lorence Lombardo.

; This is the "ROM 1.3" version "cli_args.ab2".
; Because "ROM 1.3" does not have GetArgStr_ it was necessary to
; use a different method.

; This requires that you have "all.res" or "AmigaLibs.res" in your
; compiler options residents.

; Use this as an example to easily adapt "cli_args_3.ab2" to
; "ROM 1.3" also if you wish.

; Thanks to Ralph Babel for bringing the ".FileHandle" structure
; to my attention. ;)


maxargs.l=5000: Dim arg$(maxargs)

Function.l NumArgs{}
   SHARED arg$(), maxargs
   c.l=0: qt.b=0: qt$=Chr$(34): b$=" ": sc.l=0: l$=Chr$(10)
   *fh.FileHandle = Input_ * 4 : arg_ptr.l = *fh\fh_Buf * 4
   Repeat
      a$=Peeks$(arg_ptr+sc,1): sc+1
      If a$=qt$
         If qt=0 Then qt=1: c=c+1 Else qt=0: b$=" "
      Else
         If b$=" " AND a$<>" " AND a$<>l$ AND qt=0 Then c=c+1
         b$=a$: If a$=" " AND qt=0 Then a$=""
         If a$<>l$ AND c<=maxargs Then arg$(c)=arg$(c)+a$
      EndIf
   Until a$=l$ OR c>maxargs
   If c>maxargs Then c=maxargs
   Function Return c
End Function


numargs.l=NumArgs{}

NPrint numargs

For i.l=1 To numargs
   NPrint i," - "+arg$(i)
Next i

NPrint "bla"


