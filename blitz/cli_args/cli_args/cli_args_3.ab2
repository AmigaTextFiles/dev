
; "cli_args_3.ab2" v1.3 (c) 2007 by Lorence Lombardo.

; this routine gathers args without the use of an array or an individual
; arg counter so it is entirely up to you if you want to count the
; individual args or store them into an array.

l$=Chr$(10): arg_ptr.l=GetArgStr_: sc.l=0    ; enitre arg string position counter

Function.s GetArg{}
   SHARED sc, l$,  arg_ptr
   arg$="": arg.b=0: qt.b=0: qt$=Chr$(34): b$=" "
   Repeat
      a$=PeekS(arg_ptr+sc,1): sc+1
      If a$=qt$
         If qt=0
            qt=1: If arg=0 Then arg=1 Else If arg=1 Then arg=2: sc-1
         Else
            qt=0: arg=2
         EndIf
      Else
         If arg=1 AND qt=0 AND a$=" " Then arg=2
         If b$=" " AND a$<>" " AND a$<>l$ AND qt=0 AND arg=0 Then arg=1
         b$=a$: If a$=" " AND qt=0 Then a$=""
         If a$<>l$ AND arg=1 Then arg$=arg$+a$
         If a$=l$ Then sc-1: If arg$="" Then arg$=l$
      EndIf
   Until a$=l$ OR arg=2
   Function Return arg$
End Function



c.l=0     ; counter for individual args not part of the GetArg{} function
Repeat
   my_arg$=GetArg{}: If my_arg$<>l$ Then c+1: NPrint c," - "+my_arg$
Until my_arg$=l$   ;  EOL = end of line

NPrint "bla"

; an empty string (="") can be a valid paramater thats why a Chr$(10) is
; used to determine the end of arguments.

