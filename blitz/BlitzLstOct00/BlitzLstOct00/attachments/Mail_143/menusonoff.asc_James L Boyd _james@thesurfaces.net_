;allows use of right mouse button,while still giving access to menus...

WBStartup
NoCli

MenuTitle 0,0,"Stuff  "
MenuItem 0,0,0,0,"Load Project     ","L"
MenuItem 0,0,0,1,"Save Project     ","S"

MenuTitle 0,1,"Junk  "
MenuItem 0,0,1,0,"Cut          "
MenuItem 0,0,1,1,"Copy         "
MenuItem 0,0,1,2,"Paste        "

; demo menus ^

Screen 0,0,0,640,256,4,$8000,"Click RIGHT mouse in title bar and also below. Left mouse to exit...",1,0

AddIDCMP $10 ; NEEDED to read position constantly...

Window 0,0,0,640,256,$1900,"Menus On/Off Demo",1,2

SetMenu 0 ; attach menu list 0 to this window

Menus Off

Repeat

Select WaitEvent    ; SELECT 1 - match below! done to keep track of loops!
; wait for user input

Case $10 ; mouse moved

  If WMouseY <WTopOff+2 Then Menus On Else Menus Off
      ; checks position of mouse...WTopOff is the titlebar height

Case $100 ; menu accessed

  WLocate 0,2:Print"Menu Accessed - Clearing screen...":Delay_ 50:WCls

Case $8 ; mouse button hit

  Select MButtons   ; SELECT 2
  ; which one?

    Case $1 ; left
      End

    Case $2 ; right
      WLocate WMouseX,WMouseY-12:Print"X"

  End Select        ; SELECT 2

End Select          ; SELECT 1 - match above!

Forever
