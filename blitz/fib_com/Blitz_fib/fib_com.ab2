
Dim qtab.b(512), dtab.b(256,256), fibtab.b(15), exptab.b(15)


; FDC

For b.b=0 To 15 : Read fibtab(b) : Next b

For i.l=0 To 511 : qtab(i) = 0 : Next i

For i=1 To 15
   j.l = 256 + fibtab(i) : k.l = 256 + fibtab(i-1)
   If fibtab(i)<0
      While j>k : qtab(j) = i : j-1 : Wend
   Else
      While j<512 : qtab(j) = i : j+1 : Wend
   EndIf
Next i


; EDC

For b.b=0 To 15 : Read exptab(b) : Next b

For i.l=-128 To 127
   For j.l=0 To 255 : dtab(i+128,j) = 0 : Next j : j2.l = 0
   For j.l=1 To 8
      k = i + exptab(j2) : If k<-128 Then k = k + 256
      l.l = i + exptab(j)  : If l<-128 Then l = l + 256
      If k<l
         While k<=l
            dtab(128+i,128+k) = j2 : k+1
            dtab(128+i,128+l) = j : l-1
         Wend
      Else
         While k<=128
            dtab (128+i,128+k) = j2 : k+1
         Wend
         While l=>-129
            dtab (128+i,128+l) = j : l-1
         Wend
      EndIf
      l = i - exptab(j) : If l>127 Then l = l - 256

      k = i - exptab(j2) : If k>127 Then k = k - 256
      mj.l = (16-j) & $f : mj2.l = (16-j2) & $f
      If l<k
         While l<=k
            dtab(128+i,128+l) = mj : l+1
            dtab(128+i,128+k) = mj2 : k-1
         Wend
      Else
         While l<=128
            dtab(128+i,128+l) = mj : l+1
         Wend
         While k=>-129
            dtab (128+i,128+k) = mj2 : k-1
         Wend
      EndIf
      j2 = j

   Next j
Next i

If NumPars<3 Then End
inf$=Par$(1) : outf$=Par$(2) : mode$=UCase$(Par$(3)) : bla.w=0
If mode$<>"F" AND mode$<>"E" Then End

infile.l=Open_(&inf$, #MODE_OLDFILE)
If infile=0 Then NPrint "File not found...!!!" : End

outfile.l=Open_(&outf$, #MODE_NEWFILE)
If outfile=0 Then NPrint "Unable to create output...!!!" : Close_(infile) : End

buf_sz.l=4096
buf.l=AllocMem_(buf_sz, #MEMF_PUBLIC)
If buf=0 Then Close_(infile) : Close_(outfile) : End

Write_ outfile, &bla, 2  ; <- needed only for 8svx

c.b=0 : c0.b=0 : o.w=0 : n.b=0


Repeat
   rcount.l=Read_ (infile, buf, buf_sz) : bc.w=0
   If rcount>0
      If rcount<buf_sz
         If rcount AND 1 ; <- its odd, so simply make it even
            Poke.b(buf+rcount), 0 : rcount+1
         EndIf
      EndIf
      For i.l=0 To rcount-1
         c = Peek.b(buf+i) : delta.l = c - c0
         If mode$="F"
            n = qtab(256+delta) : c0 = c0 + fibtab(n)
         Else
            n = dtab(128+c0,128+c) : c0 = c0 + exptab(n)
         EndIf
         If i AND 1
            Poke.b(buf+bc), o + n : bc+1
         Else
            o = n LSL 4
         EndIf
      Next i
      Write_ outfile, buf, bc
   EndIf
Until rcount<1 OR CtrlC<>0


Close_(infile) : Close_(outfile)
FreeMem_ buf, buf_sz

End

FDC:
Data.b -34, -21, -13, -8, -5, -3, -2 , -1 ,0 , 1, 2, 3, 5, 8, 13, 21

EDC:
Data.b -128, -64, -32, -16, -8, -4, -2, -1, 0, 1, 2, 4, 8, 16, 32, 64



