
; Adaptive Macro's for Classic Blitz or greater

; (C) 2014  Lorence Lombardo


Macro args ; {[args]}
   ra$=ra$+Mkl$(`1)
   CSIF "`2">""
      !args{`2,`3,`4,`5,`6,`7,`8,`9,`a,`b,`c,`d,`e,`f,`g}
   CEND
End Macro


Macro MkTags ; {[Tags]}
   ra$="" : !args{`1,`2,`3,`4,`5,`6,`7,`8,`9,`a,`b,`c,`d,`e,`f,`g}
End Macro


Macro AdTags ; {[Tags]}
   !args{`1,`2,`3,`4,`5,`6,`7,`8,`9,`a,`b,`c,`d,`e,`f,`g}
End Macro


scnt.b=-1  ; <- string array counter
           ;    all sprintf/printf like macs must be set to this when done


Macro vprint ; {fmt$, [args]}
   CNIF `0>1
      !MkTags{`2,`3,`4,`5,`6,`7,`8,`9,`a,`b,`c,`d,`e,`f,`g}
      addy.l=&ra$
   CELSE
      addy.l=0
   CEND
   VPrintf_ `1 , addy : scnt=-1
End Macro


Macro vwrite ; {fmt$, [args]}
   CNIF `0>1
      !MkTags{`2,`3,`4,`5,`6,`7,`8,`9,`a,`b,`c,`d,`e,`f,`g}
      addy.l=&ra$
   CELSE
      addy.l=0
   CEND
   VFWritef_ Output_,`1 , addy : scnt=-1
End Macro


Function.s sprintf{fmt$, args.l}
   vspfPCP.l = $16c04e75  ; <- magic number obtained from hb2
   buff.l=AllocMem_(1024, #MEMF_PUBLIC) : buf$=""
   If buff
      RawDoFmt_ &fmt$, args, &vspfPCP, buff
      buf$=Peek$(buff) : FreeMem_ buff, 1024
   EndIf
   Function Return buf$
End Function


Statement prints{s$}
   Write_ Output_, &s$, Len(s$)
End Statement


Macro sprintf ; {buf$, fmt$, [args]}
   !MkTags{`3,`4,`5,`6,`7,`8,`9,`a,`b,`c,`d,`e,`f,`g}
   `1 = sprintf{`2, &ra$} : scnt=-1
End Macro


Macro vprintf ; {fmt$, [args]}
   CNIF `0>1
      !MkTags{`2,`3,`4,`5,`6,`7,`8,`9,`a,`b,`c,`d,`e,`f,`g}
      addy.l=&ra$
   CELSE
      addy.l=0
   CEND
   prints{sprintf{`1, addy}} : scnt=-1
End Macro


Function.s vsprintf{fmt$, args.l}
   buf$="" : sprcnt.b=-1
   Repeat
      outf$="t:sprout_"+Chr$(65+sprcnt)+".tmp"
      sprout.l=Open_(&outf$, #MODE_NEWFILE) : sprcnt+1
   Until sprout<>0 OR sprcnt>26
   If sprout
      VFWritef_ sprout, fmt$, args
      If ChangeMode_(#CHANGE_FH, sprout, #MODE_OLDFILE)
         sz.w=Seek_(sprout, 0 , #OFFSET_BEGINNING)
         buf$=String$(Chr$(0), sz) : Read_ sprout, &buf$, sz
      EndIf
      Close_(sprout) : DeleteFile_ &outf$
   EndIf
   Function Return buf$
End Function


Macro vsprintf ; {buf$, fmt$, [args]}
   !MkTags{`3,`4,`5,`6,`7,`8,`9,`a,`b,`c,`d,`e,`f,`g}
   `1 = vsprintf{`2, &ra$} : scnt=-1
End Macro


smax.b=15 : Dim stxt$(smax)

Function.l txt_addy{txt$}
   SHARED stxt$(), scnt, smax
   scnt+1 : If scnt>smax Then scnt=smax
   stxt$(scnt)=txt$ : Function Return &stxt$(scnt)
End Function


Macro s  ; {unquoted_text_string}
   txt_addy{"`1"}
End Macro


;=====================================================================================


a$="testing some text" : exec.l=ExecVersion


If exec>35

   !vprint {"%lctest1 %ld %lx %s %lc", 10, 30, 10, !s{fun test1}, 10}

   !vprint {"test2 Next character here:- "}

   !vprint {"%lc",10}

Else

   !vprintf {"%lctest1 Exec %ld detected. %lc",10,exec,10}

   !vprintf {"test2 Next character here:- "}

   !vprintf {"%lc",10}

EndIf


!sprintf {buf$, "test3 %ld %lx %s %lc", 35, 15, &a$, 10}  ; <- no %lu for rom 1.3

prints{buf$}


If exec>36

   !vprintf {"test4 %ld %lx %s %lu %lc", 69, 44, !s{fun test2}, -1, 10}

Else

   !vprintf {"test4 %ld %lx %s %ld %lc", 69, 44, !s{fun test2}, -1, 10}

EndIf


!vprintf {"test5 %s %s %s %s %s %lc", !s{wow},!s{wow2},!s{wow3},!s{wow4},!s{WOW5}, 10}

!vprintf {"test6 waiting for an EOL."}

!vprintf {"%lc%lc",10,10} : If exec<36 Then End


; %S,%Tx,%C,%Ox,%Xx,%Ix,%N,%Ux,%$  'x' = base 36, see wikipedia  ;)

; base 36:-  0-9 A-Z, 10 = 36 decimal

!vwrite {"bonus%I3 %TJ %N %U1 %O4 %C", 69, &a$, 32, -1, 333, 10}

!vwrite {"bonus %S %$%X3 %S %C", !s{wow}, 68, 62, !s{wow2}, 10}  ; <- "%$" ignores the item

!vwrite {"bonus %S %S %S %S %S %C", !s{wow}, !s{wow2}, !s{wow3}, !s{bow}, !s{wow4}, 10}

!vsprintf {buf$, "bonus spr %N %X2 %S %S %U1 %C", -85, 15, !s{txt 1}, !s{txt-2}, -7, 10}

prints{buf$}

!vwrite {"bonus the end"}

!vwrite{"%C%C",10,10} : End



