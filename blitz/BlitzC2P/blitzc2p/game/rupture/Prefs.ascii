
;Preferences

PrefDisplayID.l=$0      ; Default (unspecified, as it may possibly retarget if they have gfx card)
;PrefDisplayID.l=$40120000 ; Cybervision Lores 320x240 ?Hz
;PrefDisplayID.l=$21000 ; AGA Lores PAL 320x256 non-laced single-scan 50Hz
;PrefDisplayID.l=$A1000 ; AGA Lores DoublePAL 384x275 non-laced double-scan 48Hz
;PrefDisplayID.l=$89000 ; AGA Lores Super72 384x290 non-laced single-scan 71Hz
;PrefDisplayID.l=$11000 ; AGA Lores NTSC 320x200 non-laced single-scan 60Hz
;PrefDisplayID.l=$39000 ; AGA Lores Multiscan 320x240 double-scan 58Hz
;PrefDisplayID.l=$59000 ; AGA Lores HighGFX 512x250 non-laced single-scan 54Hz
;PrefDisplayID.l=$69000 ; AGA Lores Euro72 320x200 non-laced double-scan 69Hz
;PrefDisplayID.l=$91000 ; AGA Lores DoubleNTSC 384x227 non-laced double-scan 58Hz
;PrefDisplayID.l=$29004 ; AGA PAL 640x400 Hires laced single-scan 50Hz
;PrefDisplayID.l=$19004 ; AGA NTSC 640x400 Hires laced single-scan 60Hz
;PrefDisplayID.l=$39024 ; AGA Multiscan 640x400 Hires non-laced single-scan 58Hz
;PrefDisplayID.l=$69024 ; AGA Euro72 640x400 Hires non-laced single-scan 69Hz
;PrefDisplayID.l=$A9004 ; AGA DoublePAL 640x400 Hires non-laced double-scan 48Hz
;PrefDisplayID.l=$99004 ; AGA DoubleNTSC 640x400 Hires non-laced double-scan 58Hz
PrefDisplayWidth.w=320
PrefDisplayHeight.w=220
PrefBackdrop.b=True ; Bitmap background
PrefDisplayBuffering.b=2

If Joyb(0)=0 AND Joyb(1)=0 Then Goto PrefsSkip

NEWTYPE.SMode
  DID.l
  DWidth.l
  DHeight.l
  DDepth.w
  DType.w
End NEWTYPE

DEFTYPE.Hook hook ; The hook for ASL tag as &hook
funcret.l = 0     ; Global used by the hook stub to return value from Blitz function

Function.l bfunc{*h.Hook,modeID.l,*smr.ScreenModeRequester}
; THESE VALUES ARE HARDCODED FOR THE POSITIONS OF THE LOCAL VARIABLES
; IN THIS FUNCTION:
; variable at address (value in a4 - >12): Any variables defined after *smr
; variable at address (value in a4 -  12): *smr
; variable at address (value in a4 -   8): modeID
; variable at address (value in a4 -   4): *h
; variable at address (value in a4)      : Value to return from function?
  MOVE.l a0,-4(a4)
  MOVE.l a1,-8(a4)
  MOVE.l a2,-12(a4)
  ; Filter modeID's here
;  DEFTYPE.DisplayInfo DisInfoBuf
;  DEFTYPE.DimensionInfo DimInfoBuf
;  DEFTYPE.MonitorInfo MonInfoBuf
;  DEFTYPE.NameInfo NamInfoBuf
;  IDhandle.l=FindDisplayInfo_(modeID)
;  GetDisplayInfoData_ IDhandle,&DisInfoBuf,SizeOf.DisplayInfo,#DTAG_DISP,0
;  GetDisplayInfoData_ IDhandle,&DimInfoBuf,SizeOf.DimensionInfo,#DTAG_DIMS,0
;  GetDisplayInfoData_ IDhandle,&MonInfoBuf,SizeOf.MonitorInfo,#DTAG_MNTR,0
;  GetDisplayInfoData_ IDhandle,&NamInfoBuf,SizeOf.NameInfo,#DTAG_NAME,0
;  If DimInfoBuf\MaxDepth>8
;    Function Return False ; Current modeID is not valid
;  Else
    Function Return True ; Current modeID is valid
;  EndIf
End Function

Dim SMRtags.TagItem(17)
SMRtags(0)\ti_Tag=#ASLSM_InitialLeftEdge,160
SMRtags(1)\ti_Tag=#ASLSM_InitialTopEdge,0
SMRtags(2)\ti_Tag=#ASLSM_InitialWidth,300
SMRtags(3)\ti_Tag=#ASLSM_InitialHeight,600
SMRtags(4)\ti_Tag=#ASLSM_InitialDisplayID,$21000
SMRtags(5)\ti_Tag=#ASLSM_InitialDisplayDepth,8
SMRtags(6)\ti_Tag=#ASLSM_InitialDisplayWidth,320
SMRtags(7)\ti_Tag=#ASLSM_InitialDisplayHeight,200
SMRtags(8)\ti_Tag=#ASLSM_InitialOverscanType,1
SMRtags(9)\ti_Tag=#ASLSM_InitialInfoOpened,1
SMRtags(10)\ti_Tag=#ASLSM_InitialInfoLeftEdge,350
SMRtags(11)\ti_Tag=#ASLSM_InitialInfoTopEdge,50
SMRtags(12)\ti_Tag=#ASLSM_DoDepth,0
SMRtags(13)\ti_Tag=#ASLSM_DoOverscanType,1
SMRtags(14)\ti_Tag=#ASLSM_DoWidth,1
SMRtags(15)\ti_Tag=#ASLSM_DoHeight,1
SMRtags(17)\ti_Tag=#TAG_DONE,0

; Fill out hook structure
;hook\h_MinNode is reserved for system use
hook\h_Entry = ?hookfunc ; Hook stub address
;hook\h_SubEntry should be the address of the high level function
;hook\h_Data is for the user to use

SMRtags(16)\ti_Tag=#ASLSM_FilterFunc,&hook
*sreq.SMode=0
*sreq=AllocAslRequest_(2,&SMRtags(0)\ti_Tag)
ok.b=AslRequest_(*sreq,&SMRtags(0)\ti_Tag)

If ok<>0
  PrefDisplayID.l=*sreq\DID
  PrefDisplayWidth.w=*sreq\DWidth
  PrefDisplayHeight.w=*sreq\DHeight
EndIf
If (*sreq) Then FreeAslRequest_(*sreq)
Goto PrefsSkip

; The function that is called by the filter hook callback (hook stub)
hookfunc:   funcret=bfunc{0,0,0}  ; Call actual blitz function (dummy parameters)
            GetReg d0,funcret     ; Return result
            RTS
PrefsSkip

