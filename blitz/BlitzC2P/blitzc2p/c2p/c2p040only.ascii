WBStartup
NoCli

; 030/50 results:

; 320x200 @28.2fps PAL
; 320x256 @21.6fps PAL

; 040/25 results:

; 320x200 @49.65fps DoublePAL or 55.3fps PAL
; 320x256 @36.2fps DoublePAL or 42.5fps PAL
; 320x240 @39.5fps DoublePAL or 45.5fps PAL

; 060/50 results:

; 320x256 @50fps PAL
; 320x200 @66.1fps PAL

#c2pBPLX=320
#c2pBPLY=256
#c2pBPLSIZE=(#c2pBPLX*#c2pBPLY)/8

#scrwidth=#c2pBPLX
#scrheight=#c2pBPLY
#screensize=#scrwidth*#scrheight

; c2p1x1_8_c5_040

; 110% on 040-25

Statement c2p040onlyinit{A.l,B.l}

  ;A.l=d0=Width.w
  ;B.l=d1=Height.w

; d0.w  chunkyx [chunky-pixels]
; d1.w  chunkyy [chunky-pixels]
; d3.w  scroffsy [screen-pixels]

c2p1x1_8_c5_040_init
  LEA c2p_data(pc),a0
  ANDI.l  #$ffff,d0
  MULU.w  d0,d3
  LSR.l #3,d3
  MOVE.l  d3,c2p_scroffs-c2p_data(a0)
  MULU.w  d0,d1
  MOVE.l  d1,c2p_pixels-c2p_data(a0)
AsmExit
End Statement

Statement c2p040only{A.l,B.l}

  MOVE.l  d0,a0 ; Chunky
  MOVE.l  d1,a1 ; Planar

; a0  c2pscreen
; a1  bitplanes

c2p1x1_8_c5_040
  MOVEM.l a3-a6,-(a7)

  LEA c2p_data(pc),a2

  MOVE.l  c2p_pixels-c2p_data(a2),a3
  ADD.l a0,a3

  ADD.w #c2pBPLSIZE,a1
  ADD.l c2p_scroffs-c2p_data(a2),a1
  MOVE.l  a1,a2
  ADD.l #c2pBPLSIZE*4,a2

  MOVE.l  (a0)+,d0
  MOVE.l  (a0)+,d1
  MOVE.l  (a0)+,d2
  MOVE.l  (a0)+,d3
  MOVE.l  (a0)+,d4
  MOVE.l  (a0)+,d5

  MOVE.l  d4,d7     ; Swap 16x4, part 1
  MOVE.w  d0,d4
  SWAP  d4
  MOVE.w  d4,d0
  MOVE.w  d7,d4
  MOVE.l  d5,d7
  MOVE.w  d1,d5
  SWAP  d5
  MOVE.w  d5,d1
  MOVE.w  d7,d5

  MOVE.l  d4,d7     ; Swap 2x4, part 1
  LSR.l #2,d7
  EOR.l d0,d7
  AND.l #$33333333,d7
  EOR.l d7,d0
  LSL.l #2,d7
  EOR.l d7,d4
  MOVE.l  d5,d7
  LSR.l #2,d7
  EOR.l d1,d7
  AND.l #$33333333,d7
  EOR.l d7,d1
  LSL.l #2,d7
  EOR.l d7,d5

  MOVE.l  (a0)+,a5
  MOVE.l  (a0)+,a6

  EXG d4,a5
  EXG d5,a6

  MOVE.l  d4,d7     ; Swap 16x4, part 2
  MOVE.w  d2,d4
  SWAP  d4
  MOVE.w  d4,d2
  MOVE.w  d7,d4
  MOVE.l  d5,d7
  MOVE.w  d3,d5
  SWAP  d5
  MOVE.w  d5,d3
  MOVE.w  d7,d5

  MOVE.l  d4,d7     ; Swap 2x4, part 2
  LSR.l #2,d7
  EOR.l d2,d7
  AND.l #$33333333,d7
  EOR.l d7,d2
  LSL.l #2,d7
  EOR.l d7,d4
  MOVE.l  d5,d7
  LSR.l #2,d7
  EOR.l d3,d7
  AND.l #$33333333,d7
  EOR.l d7,d3
  LSL.l #2,d7
  EOR.l d7,d5

  MOVE.l  d1,d7     ; Swap 4x1
  LSR.l #4,d7
  EOR.l d0,d7
  AND.l #$0f0f0f0f,d7
  EOR.l d7,d0
  LSL.l #4,d7
  EOR.l d7,d1
  MOVE.l  d3,d7
  LSR.l #4,d7
  EOR.l d2,d7
  AND.l #$0f0f0f0f,d7
  EOR.l d7,d2
  LSL.l #4,d7
  EOR.l d7,d3

  BRA _start
_x
  MOVE.l  (a0)+,d0
  MOVE.l  (a0)+,d1
  MOVE.l  (a0)+,d2
  MOVE.l  (a0)+,d3
  MOVE.l  (a0)+,d4
  MOVE.l  (a0)+,d5

  MOVE.l  a6,-c2pBPLSIZE(a1)

  MOVE.l  d4,d7     ; Swap 16x4, part 1
  MOVE.w  d0,d4
  SWAP  d4
  MOVE.w  d4,d0
  MOVE.w  d7,d4
  MOVE.l  d5,d7
  MOVE.w  d1,d5
  SWAP  d5
  MOVE.w  d5,d1
  MOVE.w  d7,d5

  MOVE.l  d4,d7     ; Swap 2x4, part 1
  LSR.l #2,d7
  EOR.l d0,d7
  AND.l #$33333333,d7
  EOR.l d7,d0
  LSL.l #2,d7
  EOR.l d7,d4
  MOVE.l  d5,d7
  LSR.l #2,d7
  EOR.l d1,d7
  AND.l #$33333333,d7
  EOR.l d7,d1
  LSL.l #2,d7
  EOR.l d7,d5

  MOVE.l  (a0)+,d7
  MOVE.l  (a0)+,a6

  MOVE.l  a5,-c2pBPLSIZE(a2)

  MOVE.l  d7,a5
  EXG d4,a5
  EXG d5,a6

  MOVE.l  d4,d7     ; Swap 16x4, part 2
  MOVE.w  d2,d4
  SWAP  d4
  MOVE.w  d4,d2
  MOVE.w  d7,d4
  MOVE.l  d5,d7
  MOVE.w  d3,d5
  SWAP  d5
  MOVE.w  d5,d3
  MOVE.w  d7,d5

  MOVE.l  d4,d7     ; Swap 2x4, part 2
  MOVE.l  d6,(a2)+
  LSR.l #2,d7
  EOR.l d2,d7
  AND.l #$33333333,d7
  EOR.l d7,d2
  LSL.l #2,d7
  EOR.l d7,d4
  MOVE.l  d5,d7
  LSR.l #2,d7
  EOR.l d3,d7
  AND.l #$33333333,d7
  EOR.l d7,d3
  LSL.l #2,d7
  EOR.l d7,d5

  MOVE.l  d1,d7     ; Swap 4x1
  LSR.l #4,d7
  EOR.l d0,d7
  MOVE.l  a4,(a1)+
  AND.l #$0f0f0f0f,d7
  EOR.l d7,d0
  LSL.l #4,d7
  EOR.l d7,d1
  MOVE.l  d3,d7
  LSR.l #4,d7
  EOR.l d2,d7
  AND.l #$0f0f0f0f,d7
  EOR.l d7,d2
  LSL.l #4,d7
  EOR.l d7,d3
_start

  MOVE.l  d2,d7     ; Swap 8x2, part 1
  LSR.l #8,d7
  EOR.l d0,d7
  AND.l #$00ff00ff,d7
  EOR.l d7,d0
  LSL.l #8,d7
  EOR.l d7,d2
  MOVE.l  d2,d7
  LSR.l #1,d7     ; Swap 1x2, part 1
  EOR.l d0,d7
  AND.l #$55555555,d7
  EOR.l d7,d0
  MOVE.l  d0,c2pBPLSIZE*2(a2)
  ADD.l d7,d7
  EOR.l d7,d2
  MOVE.l  d3,d7     ; Swap 8x2, part 2
  LSR.l #8,d7
  EOR.l d1,d7
  AND.l #$00ff00ff,d7
  EOR.l d7,d1
  LSL.l #8,d7
  EOR.l d7,d3
  MOVE.l  d3,d7
  LSR.l #1,d7     ; Swap 1x2, part 2
  EOR.l d1,d7
  AND.l #$55555555,d7
  EOR.l d7,d1
  MOVE.l  d1,c2pBPLSIZE*2(a1)
  ADD.l d7,d7
  EOR.l d7,d3

  MOVE.l  d5,d7
  LSR.l #4,d7
  EOR.l d4,d7
  AND.l #$0f0f0f0f,d7
  EOR.l d7,d4
  LSL.l #4,d7
  EOR.l d7,d5

  EXG d4,a5
  EXG d5,a6

  MOVE.l  d5,d7
  LSR.l #4,d7
  EOR.l d4,d7
  AND.l #$0f0f0f0f,d7
  EOR.l d7,d4
  LSL.l #4,d7
  EOR.l d7,d5

  MOVE.l  a5,d0
  MOVE.l  a6,d1
  MOVE.l  d2,c2pBPLSIZE(a2)


  MOVE.l  d0,d7     ; Swap 8x2, part 3
  LSR.l #8,d7
  EOR.l d4,d7
  AND.l #$00ff00ff,d7
  EOR.l d7,d4
  LSL.l #8,d7
  EOR.l d7,d0
  MOVE.l  d0,d7
  LSR.l #1,d7     ; Swap 1x2, part 3
  EOR.l d4,d7
  AND.l #$55555555,d7
  EOR.l d7,d4
  ADD.l d7,d7
  EOR.l d7,d0
  MOVE.l  d1,d7     ; Swap 8x2, part 4
  LSR.l #8,d7
  MOVE.l  d3,c2pBPLSIZE(a1)
  EOR.l d5,d7
  AND.l #$00ff00ff,d7
  EOR.l d7,d5
  LSL.l #8,d7
  EOR.l d7,d1
  MOVE.l  d1,d7
  LSR.l #1,d7     ; Swap 1x2, part 4
  EOR.l d5,d7
  AND.l #$55555555,d7
  EOR.l d7,d5
  ADD.l d7,d7
  EOR.l d7,d1

  MOVE.l  d0,a5
  MOVE.l  d1,a6

  MOVE.l  d4,d6
  MOVE.l  d5,a4

  CMP.l a0,a3
  BNE _x

  MOVE.l  a6,-c2pBPLSIZE(a1)
  MOVE.l  a5,-c2pBPLSIZE(a2)
  MOVE.l  d6,(a2)+
  MOVE.l  a4,(a1)+

  MOVEM.l (a7)+,a3-a6
AsmExit

  Even4
c2p_data
c2p_scroffs: Dc.l 0
c2p_pixels: Dc.l 0
  Ds.l  16
End Statement




.blitzprogram
; Setup
InitBank 0,(#scrwidth*#scrheight)+1000,2|65536 ; Chipram planar buffer
CludgeBitMap 0,#scrwidth,#scrheight,8,Bank(0)
InitPalette 0,256
For c=0 To 255
  AGAPalRGB 0,c,Rnd(c),Rnd(c),Rnd(c)
Next c
AGAPalRGB 0,0,0,0,0
Screen 0,0,0,#scrwidth,#scrheight,8,0,"c2p test",0,0,0
Use Palette 0
VWait 50
baseaddress1.l=AllocMem(#scrwidth*#scrheight,$10000) ; Fastram chunky buffer

; Put something into the chunky buffer so we can see it working
GetReg a0,baseaddress1
MOVE.l  #0,d0
MOVE.l  #screensize-1,d1
cloop
  MOVE.b  d0,(a0)+
  ADDQ.l  #1,d0
  SUBQ.l  #1,d1
  TST.l   d1
  BLT     done
  BRA     cloop
done

; Do the c2p test
c2p040onlyinit{#scrwidth,#scrheight}
VWait 20
Forbid_
VWait
ResetTimer
For time=1 To 800
  c2p040only{baseaddress1,Bank(0)} ; Convert chunky to planar
Next time
t=Ticks
VWait 2 : Permit_
VWait 20
FindScreen 0
Window 0,0,11,640,100,0,"Test results for c2p",0,0
WindowOutput 0
NPrint "Routine performed @ ",50/(t/800),"fps - ",t," ticks"
NPrint " "
NPrint "Press mousebutton..."
Free Screen 0
MouseWait
Free Window 0
End

