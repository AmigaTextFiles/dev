;INCLUDE "blitz2:sourcecodes/includes/error.include.bb2"

#max_midiports = 16

NEWTYPE.midiport
is_open.w
msgport.l
msg.l
devicename.s
unit.w
stolen.w
cache.l
blength.l
devicebase.l
End NEWTYPE

Dim midiport.midiport(#max_midiports-1)

USEPATH midiport(mp)


.mp_close
Statement midiport_close {mp.w}
SHARED midiport()
  If \msg Then WaitIO_ \msg
  If \devicebase><False AND \stolen=False Then CloseDevice_ \msg:\devicebase=False
  If \msg     Then DeleteIORequest_ \msg : \msg = False
  If \msgport><False AND \stolen = False Then DeleteMsgPort_ \msgport
  If \cache>0 Then FreeMem_ \cache,\blength : \cache = 0:\blength = 0
\msgport = False
\is_open = False
\devicename = ""
\unit       = -2
\stolen     = False
End Statement


.mp_close_all
Statement midiport_close_all {}
SHARED midiport()
For mp.w = 0 To #max_midiports-1
  midiport_close{mp}
Next
End Statement


.mp_open
Function.w midiport_open {mp.w,devicename.s,unit.l}
SHARED midiport()
If \is_open Then midiport_close {mp}
\devicename = devicename.s
\unit       = unit
\cache      = 0
\blength    = 0

If \unit<0
Else
 For n.l=0 To 15
  If midiport(n)\devicename = \devicename AND midiport(n)\unit=\unit AND mp><n
    \msgport = \msgport
    \stolen = True
    \msg     = AllocMem_ (SizeOf.IOExtSer,0)
    If \msg
      CopyMem_ midiport(n)\msg,\msg,SizeOf.IOExtSer
      \is_open = True
      n = 15
    Else
      error {"Unable to create IORequest !"}
    End If
  End If
 Next
 If \is_open = False
  \msgport = CreateMsgPort_ ()
  \stolen = False
  If \msgport
    \msg = CreateIORequest_ (\msgport,SizeOf.IOExtSer)
    If \msg
      If OpenDevice_ (\devicename,\unit,\msg,0) = 0
        \devicebase = True
        *serialIO.IOExtSer = \msg
        *serialIO\IOSer\io_Command  = #SDCMD_SETPARAMS       ; erstmal Parameter setzen
        *serialIO\io_Baud           = 31250
        *serialIO\io_SerFlags       = 0 ;#SERB_RAD_BOOGIE
        If DoIO_ (\msg) = 0                              ; ... und dem device mitteilen
          \is_open   = True                                   ; Pointer merken
        Else
          error {"Device is not compatible with serial.device !"} ; Parameter setzen hat nicht geklappt !
        End If
      Else
        DeleteIORequest_ \msg : \msg = False  ; Weil sonst waitio_ !!!
        error {"Unable to open "+devicename.s+" unit "+Str$(unit)+" !"}
      End If
    Else
      error {"Unable to create IORequest !"}
    End If
  Else
    error {"Unable to create messageport !"}
  End If
 End If
End If
If \is_open = False Then midiport_close {mp}
Function Return \is_open
End Function


.mp_write
Statement midiport_write {mp.w,ptr.l,blength.l}     ; just sends the given data, memory is not copied
SHARED midiport()
If \is_open = True AND ptr><0
    WaitIO_ \msg
    *serialIO.IOExtSer          = \msg
    *serialIO\IOSer\io_Length   = blength
    *serialIO\IOSer\io_Data     = ptr
    *serialIO\IOSer\io_Command  = #CMD_WRITE
    SendIO_ \msg
Else
  error {"Tried to write illegal data to (closed) midiport !"}
End If
End Statement


.mp_write_c
Statement midiport_write_c {mp.w,ptr.l,blength.l}    ; copies the given data and sends it after
SHARED midiport()
If \is_open = True AND ptr><0
  WaitIO_ \msg
  If blength>\blength
    If \cache>0 Then FreeMem_ \cache,\blength
    \cache = AllocMem_ (blength.l,0)
    \blength=blength
  End If
  If \cache
    CopyMem_ ptr,\cache,blength
    *serialIO.IOExtSer          = \msg
    *serialIO\IOSer\io_Length   = blength
    *serialIO\IOSer\io_Data     = \cache
    *serialIO\IOSer\io_Command  = #CMD_WRITE
    SendIO_ \msg
 End If
Else
  error {"Tried to write illegal data to (closed) midiport !"}
End If
End Statement


.mp_read
Statement midiport_read {mp.w,ptr.l,blength.l}
SHARED midiport()
If \is_open = True AND ptr><0
  ;-)
Else
  error {"Tried to read illegal data to (closed) midiport !"}
End If
End Statement


