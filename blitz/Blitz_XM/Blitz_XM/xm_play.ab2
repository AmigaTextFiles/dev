
; "xm_play.ab2" a Blitz demonstration program by Lorence Lombardo
; for Michai Skorupka's "xmplayer.library".

; 16-Oct-2012


#R=1005 : l$=Chr$(10)

np.b=NumPars : If np>0 Then f$=Par$(1)
If np=0 OR f$="?"
   NPrint l$+" Usage:-"+l$+l$+" xm_play.exe <xm_file>"+l$
   End
EndIf

INCLUDE "xmplayer.include.bb2"

If xmplayer_InitLib{1}=0 Then NPrint "Can't open xmplayer lib...!!!" : End

infile.l=Open_(&f$, #R) : buf.l = 0 : buf$="Extended Module"
If infile=0 Then NPrint l$+" File not found...!!!"+l$ : Goto exit

ExamineFH_ infile, fib.FileInfoBlock : sz.l=fib\fib_Size
buf.l = AllocMem_(sz, #MEMF_PUBLIC)
If buf=0 Then NPrint l$+" Not enough RAM...!!!"+l$ : Close_ infile : Goto exit
Read_ infile, buf, sz : Close_ infile

If PeekS(buf, Len(buf$))<>buf$
   NPrint l$+" File does not appear to be an XM...!!!"+l$
   Goto exit
EndIf

DEFTYPE .XMPlayerInfo XMPlayerInfo : prname$="Test XM-Player"

XMPlayerInfo\XMPl_Cont = buf
XMPlayerInfo\XMPl_Mixtype = #XM_STEREO14
XMPlayerInfo\XMPl_Mixfreq = 22000
XMPlayerInfo\XMPl_Vboost = 2
XMPlayerInfo\XMPl_PrName = &prname$
XMPlayerInfo\XMPl_PrPri = 0

!xmplayer_XMPl_Init {xm_ret.l, &XMPlayerInfo}
If xm_ret=0 Then NPrint l$+" Failed to initialize...!!!"+l$ : Goto exit

!xmplayer_XMPl_Play {xm_ret.l}

Print l$+" Playing XM module... Press Ctrl-C to stop... "

While CtrlC=0 : Delay_ 5 : Wend : NPrint l$

!xmplayer_XMPl_StopPlay : !xmplayer_XMPl_DeInit

exit:
If buf Then FreeMem_ buf, sz
xmplayer_FreeLib{} : End


