Function.b COMPARE_STRINGS{str1.l,str2.l}
; Case insensitive compare.
;  two strings are equal if they are different lengths and the
;  only difference is trailing spaces.

.compare_strings
         MOVEA.l  d0,a0
         MOVEA.l  d1,a1

         MOVE.b   #96,d2
         MOVE.b   #123,d3
         MOVE.b   #$DF,d4
         MOVE.b   #$20,d5

'fetch:  MOVE.b   (a1)+,d1   ; get second string character
         BEQ      'test1     ; second string is ended
         MOVE.b   (a0)+,d0   ; get first string character
         BEQ      'test2     ; first string is ended

         CMP.b    d0,d1      ; Check if characters are equal
         BEQ      'fetch     ; ... if so, go get the next pair

         CMP.b    d2,d0
         BLE      'noup1
         CMP.b    d3,d0
         BGE      'noup1
         AND.b    d4,d0

'noup1:  CMP.b    d2,d1
         BLE      'noup2
         CMP.b    d3,d1
         BGE      'noup2
         AND.b    d4,d1

'noup2:  CMP.b    d0,d1       ; check if letters are same
         BEQ      'fetch      ; if they are, move onto next letter
         BRA      'exit       ; if not, exit

'test1:  MOVE.b   (a0)+,d0
         BEQ      'exit       ; second string is ended
         CMP.b    d0,d5       ; Trailing space?
         BEQ      'test1      ; yes, keep going
         BRA      'exit

'test2:  CMP.b    d1,d5       ; Trailing space?
         BNE      'exit       ; yes, keep going
         MOVE.b   (a1)+,d1
         BNE      'test2

'exit:   ANDI.w   #$00FF,d0   ; make sure that top byte of word is clear
         ANDI.w   #$00FF,d1   ; make sure that top byte of word is clear
         SUB.w    d1,d0       ; get return result
         AsmExit
End Function
