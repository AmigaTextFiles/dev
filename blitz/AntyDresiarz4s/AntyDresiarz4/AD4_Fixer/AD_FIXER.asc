
  WBStartup
  MaxLen PATH$=192

  x$="$VER: AntyDresiarz #4 Fixer v0.02 by Taski (7.07.98)" : x$=""



  TITLE$="AntyDresiarz #4 Fixer v0.02 by Taski."

  t$="Witamy w programie poprawiajacym dzialanie zinu AntyDresiarz #4!"+Chr$(10)
  t$=t$+"Program dokona kilku zmian w pliku DATA_FILE znajdujacym sie"+Chr$(10)
  t$=t$+"w katalogu z AntyDresiarzem oraz zapisze nowy plik AntyDresiarz.exe."+Chr$(10)+Chr$(10)
  t$=t$+"Jezeli zaakceptujesz dzialanie programu, zostaniesz poproszony o"+Chr$(10)
  t$=t$+"'wejscie' do katalogu z zainstalowanym AntyDresiarzem."+Chr$(10)
  t$=t$+"Nastepnie program automatycznie dokona potrzebnych zmian."+Chr$(10)+Chr$(10)
  t$=t$+"Pamietaj, aby przed poprawieniem AD#4 dysk twardy byl odbezpieczony!"

  d$="Ok!|Poniechaj"
  req=RTEZRequest(TITLE$,t$,d$)
  t$="" : d$=""

  DATA_FILE_POPRAWNIE=0
  EXE_FILE_POPRAWNIE=0
  PREFS_FILE_POPRAWNIE=0
  POPRAWNIE=0

  If req=1
    PATH$=RTEZPathRequest("Wejdz do katalogu z AntyDresiarzem#4")

    If Exists(PATH$) AND PATH$<>""
      If Exists(PATH$+"DATA_FILE")>0
        If Exists(PATH$+"DATA_FILE")=8120528

          DATA_WYNIK=OpenFile(1,PATH$+"DATA_FILE")
          If DATA_WYNIK=True
            FileOutput 1
            ; ----- Marek Pampuch - AntyStresiarz
            FileSeek 1,3197138
            WriteMem 1,?ANTYSTRESIARZ,26327
            ; ----- Muffin - Wstepniak
            FileSeek 1,8120528
            WriteMem 1,?WSTEPNIAK,3642
            ; ----- Link File
            FileSeek 1,8120514
            Print "8124170,3236" ;12
            FileSeek 1,8124170
            WriteMem 1,?LINK_FILE,3236
            ; ----- END
            CloseFile 1
            DATA_FILE_POPRAWNIE=1
          Else
            t$="Mam problem z otwarciem pliku:"+Chr$(10)
            t$+"'"+PATH$+"DATA_FILE'"+Chr$(10)
            x=RTEZRequest(TITLE$,t$,"Ok!")
            t$=""
          EndIf
        Else
          t$="Plik z danymi nie jest oryginalny!"+Chr$(10)
          t$+"'"+PATH$+"DATA_FILE'"+Chr$(10)
          t$+"Nastepnym razem podaj poprawna sciezke dostepu do magazynu!"
          x=RTEZRequest(TITLE$,t$,"Ok!")
          t$=""
        EndIf
      Else
        t$="Plik z danymi nie istnieje!"+Chr$(10)
        t$+"'"+PATH$+"DATA_FILE'"
        x=RTEZRequest(TITLE$,t$,"Ok!")
        t$=""
      EndIf

      If DATA_FILE_POPRAWNIE=1
        ; ---- ANTYDRESIARZ - Executable
        If Exists(PATH$+"AntyDresiarz.exe")
          DeleteFile_ PATH$+"AntyDresiarz.exe"
        EndIf
        EXE_WYNIK=BSave (PATH$+"AntyDresiarz.exe",?AD,287244)
        If EXE_WYNIK
          EXE_FILE_POPRAWNIE=1
        Else
          t$="Mam problemy z zapisem pliku:"+Chr$(10)
          t$+"'"+PATH$+"AntyDresiarz.exe'"+Chr$(10)
          x=RTEZRequest(TITLE$,t$,"Ok!")
          t$=""
        EndIf
      EndIf

      ; ---- PREFS FILE
      If Exists(PATH$+"PREFS.PRE")
        PREFS_FILE=OpenFile(2,PATH$+"PREFS.PRE")
        If PREFS_FILE
          FileOutput 2
          FileSeek 2,Lof(2)
          NPrint "COLOR=22,90,90,90"
          CloseFile 2
          PREFS_FILE_POPRAWNIE=1
        Else
          t$="Mam problemy z poprawa pliku:"+Chr$(10)
          t$+"'"+PATH$+"PREFS.PRE'"+Chr$(10)
          x=RTEZRequest(TITLE$,t$,"Ok!")
          t$=""
        EndIf
      Else
          t$="Mam problemy ze znalezieniem pliku:"+Chr$(10)
          t$+"'"+PATH$+"PREFS.PRE'"+Chr$(10)
          x=RTEZRequest(TITLE$,t$,"Ok!")
          t$=""
      EndIf

    Else
      t$="Wskazany przez Ciebie katalog:"+Chr$(10)
      t$+"'"+PATH$+"'"+Chr$(10)
      t$+"nie istnieje!"
      x=RTEZRequest(TITLE$,t$,"Ok!")
      t$=""
    EndIf

  EndIf


  If EXE_FILE_POPRAWNIE=1 AND DATA_FILE_POPRAWNIE=1 AND PREFS_FILE_POPRAWNIE=1
    t$="AntyDresiarz #4 zostal poprawnie naprawiony!"+Chr$(10)
    t$+"Przepraszamy za wynikle niedogodnosci oraz..."
    x=RTEZRequest(TITLE$,t$,"zyczymy milej lektury!")
    t$=""
  Else
    If req=1
      t$="AntyDresiarz #4 nie zostal poprawnie naprawiony!"+Chr$(10)
      x=RTEZRequest(TITLE$,t$,"Ok!")
      t$=""
    EndIf
  EndIf

End

ANTYSTRESIARZ:
IncBin "_AntyStresiarz.txt"
Even

WSTEPNIAK:
IncBin "_Wstepniak.txt"
Even

LINK_FILE:
IncBin "_LINK_FILE"
Even


AD:
IncBin "_Antydresiarz.exe"
Even


End
