NoCli

;Include the timer functions
XINCLUDE "INC_Timer.bb2"


;Init
If CreateTimer{}>0
   NPrint "Can't create timer!"
   End
EndIf

;Open a window for testing
FindScreen 0 : Window 1,10,10,300,100,15,"Test",1,2

NPrint "Timer set to 5 sec.."
SetTimer{5,0}

;Main wait loop
quit=False
While quit=False
   ;Wait for any window or port event
   Wait

   ;Check what we got

   ;Was it the timer?
   If CheckIfTimer{}=True
      a.l = SystemDate
      InnerCls
      WLocate 10,10
      NPrint "The timer! - ",Secs
      SetTimer{5,0} ;Set new timer
   EndIf


;* This is the usual window event handling,
;* exept that you have to empty the queue since WAIT
;* don't check if the queue is empty before it goes to sleep

   ;Was it a window event?
   winevent.l = Event

   While winevent.l <> 0  ;Important, get ALL queued events!

      Select winevent
         Case $200
            quit = True

         Case $2
            NPrint "Resized!"
         Case $40000
            NPrint "Window activated!"
         Case $80000
            NPrint "Window inactivated!"
      End Select

      ;Any more event?
      winevent.l = Event

   Wend

Wend

;Remove window
Free Window 1

;Remove timer
DeleteTimer{}

End

