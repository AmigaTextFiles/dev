;*****************************************************
;*                                                   *
;*  INC_Timer.bb2, Version 0.1 Beta                  *
;*                                                   *
;*  Functions for easy handling of the timer device  *
;*                                                   *
;*  Written by Hans Olsen, pt96hol@student.hk-r.se   *
;*                                                   *
;*****************************************************

;This function starts the timer.device
Function.l CreateTimer{}
SHARED hc_timer_wp.l, *hc_timer_ioreq.timerequest

   ;Clear any old data
   hc_timer_wp.l = 0
   *hc_timer_ioreq.timerequest = 0

   ;Create msg port for timer handling
   hc_timer_wp.l = CreateMsgPort("")
   If hc_timer_wp.l = 0
      Function Return 1 ;Can't open port!"
   EndIf

   ;Create a request for the timer.device
   *hc_timer_ioreq.timerequest = CreateIORequest_(hc_timer_wp.l,40)
   If *hc_timer_ioreq = 0
      DeleteMsgPort hc_timer_wp
      hc_timer_wp = 0
      Function Return 2 ;Can't create request!
   EndIf

   ;Init request
   *hc_timer_ioreq\tr_node\io_Command = 9  ;Add request

   ;Open timer device with our request
   r.l = OpenDevice_("timer.device",0,*hc_timer_ioreq,0)
   If r<>0
      DeleteIORequest_(*hc_timer_ioreq)
      DeleteMsgPort hc_timer_wp
      *hc_timer_ioreq = 0
      hc_timer_wp = 0
      Function Return 3 ;Can't open timer device!
   EndIf

Function Return 0; All system is go!
End Function

;This statement stops and frees the timer
Statement DeleteTimer{}
SHARED hc_timer_wp.l, *hc_timer_ioreq.timerequest

   ;Close the device and request if there is any open
   If *hc_timer_ioreq <> 0

      ;We must wait for any active requests
      If CheckIO_(*hc_timer_ioreq)=0
         AbortIO_(*hc_timer_ioreq)
         WaitIO_(*hc_timer_ioreq)
      EndIf

      CloseDevice_(*hc_timer_ioreq)
      DeleteIORequest_(*hc_timer_ioreq)
      ;Dispose the pointer
      *hc_timer_ioreq = 0
   EndIf

   ;Close the port if there is any
   If hc_timer_wp <> 0
      DeleteMsgPort hc_timer_wp
      ;Dispose the pointer
      hc_timer_wp = 0
   EndIf

End Statement

;This statement sets the timer
Statement SetTimer{sec.l,micro.l}
SHARED hc_timer_wp.l, *hc_timer_ioreq.timerequest

;Set the request to desired delay
*hc_timer_ioreq\tr_time\tv_secs = sec
*hc_timer_ioreq\tr_time\tv_micro = mirco
;Set the timer
SendIO_(*hc_timer_ioreq)

End Statement

;You MUST call this statement when you get the timer signal!
Statement ReplyTimer{}
SHARED hc_timer_wp.l

   ;Get all timer requests and stop them
   a.l = 1
   While a.l<>0
      a.l = GetMsg_(hc_timer_wp)
      If a.l<>0 Then AbortIO_(a.l)
   Wend

End Statement

;Wait for the timer and nothing else
Statement WaitForTimer{}
SHARED hc_timer_wp.l

   WaitPort_(hc_timer_wp)
   ReplyTimer{}

End Statement

;Use this function to check for the timer event when you use the WAIT command
Function CheckIfTimer{}
SHARED hc_timer_wp.l

   gotevent = False

   a.l = GetMsg_(hc_timer_wp)
   If a.l<>0
      gotevent = True
      While a.l<>0
         If a.l<>0 Then AbortIO_(a.l)
         a.l = GetMsg_(hc_timer_wp)
      Wend
   EndIf

   Function Return gotevent
End Function

