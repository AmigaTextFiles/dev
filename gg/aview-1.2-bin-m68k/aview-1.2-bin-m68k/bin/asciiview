/*
 * asciiview(amiga) [0.01] by megacz@usa.com in 2007
 * - 'asciiview' is a script that converts ILBM,JPEG,GIF,BMP 
 *   and PNG files TO PNM and then passes it to the 'aview',
 *   the reson for own Amiga version is that there is high 
 *   probability that the Linux version will not work for you
 *   due to lack of GeekGadgetes environment in your system.
 *
 *   template: asciiview [filename] [options]
*/

pre_:
OPTIONS FAILAT 99999999999
SIGNAL ON Halt
SIGNAL ON Break_C
PARSE ARG _dosline
_dosline=STRIP(_dosline,'B')
IF _dosline="" THEN DO
say " *** template: asciiview [filename] [options]"
EXIT
END
lf='0a'x

var_:
rseed_numb = COMPRESS(RIGHT(STORAGE(),6))
temp__file = "T:asciiview.temp."
stack_size = 100000
term__type = "amiga"

parse_:
_lastpos=LASTPOS('"',_dosline)
IF _lastpos>0 THEN DO
_filename=SUBSTR(_dosline,1,_lastpos)
_restline=SUBSTR(_dosline,_lastpos+1)
END;ELSE DO
_filename=WORD(_dosline,1)
_restline=SUBWORD(_dosline,2)
END
_filename_c=COMPRESS(_filename,'"')

recogn_:
_procfile=temp__file||rseed_numb
IF OPEN(__probe,_filename_c,'R')<=0 THEN DO
say " *** oops, unable to open '"_filename"'!"
EXIT
END
_data=READCH(__probe,32)
_close=CLOSE(__probe)
_stack=PRAGMA('S',stack_size)
SELECT
WHEN POS('ILBM',_data)>0 THEN ADDRESS COMMAND 'ilbmtopnm '_filename' >'_procfile
WHEN POS('JFIF',_data)>0 THEN ADDRESS COMMAND 'jpegtopnm '_filename' >'_procfile
WHEN POS('GIF',_data)>0 THEN ADDRESS COMMAND 'giftopnm '_filename' >'_procfile
WHEN POS('BM',_data)>0 THEN ADDRESS COMMAND 'bmptopnm '_filename' >'_procfile
WHEN POS('PNG',_data)>0 THEN ADDRESS COMMAND 'pngtopnm '_filename' >'_procfile
OTHERWISE
say " *** image file is not supported by the script!"
EXIT
END

aview_:
_clifile=temp__file||rseed_numb+1
IF OPEN(__aview,_clifile,'W')<=0 THEN DO
say " *** unable o build shell script/wrapper!"
EXIT
END
_write=WRITELN(__aview,'SET TERM 'term__type''lf'aview '_restline' '_procfile)
_close=CLOSE(__aview)
ADDRESS COMMAND 'execute '_clifile
_delete=DELETE(_procfile)
_delete=DELETE(_clifile)
EXIT

Halt:
Break_C:
EXIT
