
;**************************************
;*                                    *
;* Install Script for LibSnooper 0.9b *
;* Program and script by:             *
;* ScoTT/BlaBla                       *
;*                                    *
;* BLABLA - it means system friendly  *
;*                                    *
;**************************************

(if (= @language "polski")

   (
   (set destdirmsg "Podaj katalog, do którego program ma byê skopiowany (katalog LibSnooper bëdzie utworzony automatycznie)")
   (set docbool "Czy chcesz zainstalowaê dokumentacjë do programu (AmigaGuide)?")
   (set doccopy "Podaj katalog, do którego chcesz skopiowaê dokumentacjë")
   (set patchlib "LibSnooper wymaga patch.library. Czy mam jâ zainstalowaê?")
   (set patchcopy "Kopiujë patch.library")
   (set fdmessage "Podaj ôcieûkë dostëpu do plików .fd (zostanie ona zapisana w tooltype ikony)")
   (set yes "Tak, proszë")
   (set no "Nie")
   )
   
   (
   (set destdirmsg "Pick directory you want LibSnooper to be copied to (LibSnooper directory will be created automatically)")
   (set docbool "Do you want to install documentation (AmigaGuide)?")
   (set doccopy "Pick directory you want docs to be copied to")
   (set patchlib "LibSnooper requires patch.library. Shall I install it?")
   (set patchcopy "Copying patch.library")
   (set fdmessage "Pick directory where you have your .fd files stored (this will be saved as tooltype)")
   (set yes "Yes, please")
   (set no "No")
   )
)

(set destdir
   (tackon
      (askdir
      (prompt destdirmsg)
      (help @askdir-help)
      (default @default-dest)
      )
   @app-name
   )
)

(complete 10)

(copyfiles
   (source "")
   (choices
      "LibSnooper"
      "LibSnooper_STOP"
   )
   (infos)
   (dest destdir)
)

(complete 30)

(if (askbool
    (prompt docbool)
    (help @askbool-help)
    (choices
    yes
    no
    )
    )

   (
   (set destdoc
      (askdir
      (prompt doccopy)
      (help @askdir-help)
      (default destdir)
      )
   )
   (copyfiles
   (source "Docs/")
   (dest destdoc)
   (infos)
   (all)
   )
   )
)

(complete 50)

(user 2)
(if (askbool
    (prompt patchlib)
    (help @askbool-help)
    (choices
    yes
    no
    )
    )

   (
   (copylib
	(source "Libs/patch.library")
	(dest "LIBS:")
	(prompt patchcopy)
	(help @copylib-help)
	(confirm)
   )
   )
)

(complete 70)

(set fdpath
   (askdir
   (prompt fdmessage)
   (help @askdir-help)
   (default "fd:")
   )
)

(tooltype
   (dest (tackon destdir "LibSnooper"))
   (settooltype "FDPATH" fdpath)
)

(complete 90)

(working "And always remember:\nBLABLA - it means system and user friendly")

(run 
   (cat
   "SYS:Utilities/Multiview "
   '"'
   (tackon destdoc "LibSnooper.guide")
   '"'
   )
)

(complete 100)
(exit)
