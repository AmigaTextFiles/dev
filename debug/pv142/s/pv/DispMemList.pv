/* Utility for PowerVisor to show the memorylist
   © J.Tyberghein  6 May 1990  V1.1
						11 Jun 1990  V1.2
						19 Jul 1992  V2.0
*/

options results

parse arg start
'eval "'start'"'
if rc~=0 then
	do
		'print "Bad argument!\n"'
		exit
	end
start = result
'sync'

'hide'
'openpw graph 30 10 460 280'
if rc~=0 then
	do
		'unhide'
		'async'
		'print "Error opening window!\n"'
		exit
	end
pwin = result

'void *('pwin'+62)'
win = result
'assign _rp=*('win'+50)'
'assign _top=*('win'+55).b'

size = 512*1024
tend = start+size

'loadfd graphics fd:graphics_lib.fd'

'void SetAPen(_rp,1) Move(_rp,100,240+_top) Text(_rp,"Memory Map  V2.0",16)'
'void Move(_rp,100,250+_top) Text(_rp,"Type \"closepw graph\" to close window",36)'

'void Move(_rp,98,11+_top) Draw(_rp,421,11+_top) Draw(_rp,421,219+_top)'
'void Draw(_rp,98,219+_top) Draw(_rp,98,11+_top)'

do s=0 to 16 by 1
	'void Move(_rp,92,10+_top+('s'*13)) Draw(_rp,98,10+_top+('s'*13))'
	'void Move(_rp,26,13+_top+('s'*13)) Text(_rp,"\('start+(s*(size/16))',%08lx)",8)'
end

'clear 041424344'
"search "start size" 'ABCDABCD'"
res = result

'void SetAPen(_rp,3)'

if res==0 then
	do
		'unhide'
		'print "No free memory !\n"'
		'hide'
	end
else
	do forever
		st = res
		'assign rc='st+4
		'go "\20\11\02\80\ff\ff\ff\fe\20\40\0c\98\41\42\43\44\67\f8\22\88\4e\75"'
		'void rc'
		en = result
		if en>=tend then en=tend

		'void ((('st-start')/8)%320)+100'
		x1 = result
		'void ((('st-start')/8)/320)+13+_top'
		y1 = result
		'void ((('en-start')/8)%320)+100'
		x2 = result
		'void ((('en-start')/8)/320)+13+_top'
		y2 = result

		if y1==y2 then
			'void Move(_rp,'x1','y1') Draw(_rp,'x2','y1')'
		else
			do
				'void Move(_rp,'x1','y1') Draw(_rp,419,'y1') Move(_rp,300,'y2') Draw(_rp,'x2','y2')'
				if y2>(y1+1) then
					'void RectFill(_rp,100,'y1'+1,419,'y2'-1)'
			end

		if en==tend then leave
		"search "en tend-en" 'ABCDABCD'"
		res = result
		if res==0 then leave
	end

'remvar _rp _top'
'unhide'
'async'
