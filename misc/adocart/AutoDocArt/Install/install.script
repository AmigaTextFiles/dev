; $VER: AutoDocArt Install Script 1.03 (28.09.2002) by Vladimir Javorski
;----------------------------------------------------------------------------
(if (= @language "english")
  (
    (set #MSG_COPYRIGHTS "AutoDocArt ©2000 by United Arts.\n\nMUI ©1992-1997 by Stefan Stuntz.\nNList, NListview, NFloattext ©1996 by Gilles Masson.\nNListtree ©1999-2000 by Carsten Scholling.\nToolbar ©1997-2000 Benny Kjжr Nielsen.\nBetterBalance ©1998 Mark Schreiber.\n\nYou'll use this programm on your risk.\nWe don't guarantee anything.")
    (set #MSG_WELCOME ("Welcome to %s Installer!" @app-name))
    (set #MSG_ASKDIR ("Where would you like %s to be installed?\n(the drawer will be created)" @app-name))
    (set #MSG_INSTALL "Install")
    (set #MSG_SKIP "Skip")
    (set #MSG_COPY "Copy programm files...")
    (set #MSG_COPY_MCC "Copy MUI Custom Classes...")
    (set #MSG_INSTALL_MCC "Checking MUI Custom Classes.")
    (set #MSG_INSTALL_PREFS "Do You want to install\nthe original AutoDocArt prefs files?")
    (set #MSG_AUTODOCS_HELP "Assign \"AutoDocs:\" allows programm to scan autodocs from deferent directories. You may add multiassigns to this one. It may be need to rename the directories.")
    (set #MSG_PREFS_HELP "This files are MUI config. It describe interface as author sets. I don't recommend You to miss them.")
    (set #MSG_AUTODOCS "You'll may need \"AutoDocs:\" assign.\nIf you want to install one, copy your autodocs\nto one directory and point it.")
    (set #MSG_AUTODOCS_DIR "Choose your autodocs directory...")
    (set #MSG_AUTODOCS_MAKEDIR "Directary %s does not exist.\nCreate directory?")
    (set #MSG_STARTUP "Some commands will be added\nto your \"s:user-startup\" file:\n\nAssign AutoDocs: %s")
    (set #MSG_FINAL "Installation complete!\n\nThank You for using our product!\n\n%s was placed in drawer:\"\n%s\"\n\nCreate thanks to all Amiga peaple!\nThanks to BETA-testers! Thanks to MCC developers, H&P for the StormC and AmigaOS, author and YAM team, authors CED, GoldEd, PPaint, DOpus5, AmiTCP, Genesis, IBrowse and AIC<http://www.Amiga.Org.Ru> team.")
  )
)
(if (= @language "russian")
  (
    (set #MSG_COPYRIGHTS "AutoDocArt ©2000 by United Arts.\n\nMUI ©1992-1997 by Stefan Stuntz.\nNList, NListview, NFloattext ©1996 by Gilles Masson.\nNListtree ©1999-2000 by Carsten Scholling.\nToolbar ©1997-2000 Benny Kjжr Nielsen.\nBetterBalance ©1998 Mark Schreiber.\n\nВы используете эту программу на свой риск.\nМы ничего не гарантируем.")
    (set #MSG_WELCOME ("Добро пожаловать в инсталлятор %s!" @app-name))
    (set #MSG_ASKDIR ("Где Вы хотите разположить %s?\n(директория будет создана)" @app-name))
    (set #MSG_INSTALL "Инсталлировать")
    (set #MSG_SKIP "Пропустить")
    (set #MSG_COPY "Копирование файлов программы...")
    (set #MSG_COPY_MCC "Копирование MUI Custom Classes...")
    (set #MSG_INSTALL_MCC "Проверка классов MUI.")
    (set #MSG_INSTALL_PREFS "Установить оригинальные файлы конфигурации?")
    (set #MSG_AUTODOCS_HELP "Логическое устройство \"AutoDocs:\" позволяет объединить несколько директорий, содержащих файлы autodoc. Возможно, будет необходимо переименовать их для удобства.")
    (set #MSG_PREFS_HELP "Это файлы установок MUI: AutoDocArt.cfg и AutoDocArt.prefs. Они описывают интерфейс программы делая его таким, как у автора. Без этих файлов внешний вид программы будет непредсказуем и скорее всего пугающим.")
    (set #MSG_AUTODOCS "Вам может понадобиться логическое\nустройство \"AutoDocs:\".\nЕсли Вы хотите использывать его, скопируйте\nнужные Вам документы в одну директорию\nи укажите её.")
    (set #MSG_AUTODOCS_DIR "Выбирете директорию...")
    (set #MSG_AUTODOCS_MAKEDIR "Директория \"%s\" не существует.\nСоздать?")
    (set #MSG_STARTUP "В файл \"s:user-startup\" будет добавлено:\n\nAssign AutoDocs: %s")
    (set #MSG_FINAL "Инсталляция завершена!\n\nСпасибо Вам за то,\nчто используете нашу программу!\n\n%s помещён в каталог:\n\"%s\"\n\nПривет всем амижникам!\nСпасибо БЕТА-тестерам, разработчикам MCC, StormC, PPaint, авторам и разработчикам DOpus5, CED, GoldEd, YAM, AmiTCP, Genesis и IBrowse, а также команде AIC<http://www.Amiga.Org.Ru>.")
  )
)
(procedure P_InstallMCC mcc_name c
  (
    (copylib (prompt #MSG_COPY_MCC)
             (help @copyfiles-help)
             (source mcc_name)
             (dest "Libs:mui")
             (confirm 0)
    )
    (complete c)
  )
)
(procedure P_InstallFiles dir_name c
  (
    (copyfiles (prompt #MSG_COPY)
               (help @copyfiles-help)
               (source (cat "/" dir_name))
               (dest (tackon @default_dest dir_name))
;               (infos)
               (all)
    )
    (complete c)
  )
)
;----------------------------------------------------------------------------
(set installer_version (/ @installer_version 65536))
(if (>= installer_version 44)
  (effect 'center' 'horizontal' 0 255)
)
(complete 0)
(message #MSG_COPYRIGHTS)
(welcome #MSG_WELCOME)
(set dest_dir (askdir (prompt #MSG_ASKDIR)
                      (help @askdir-help)
                      (default @default-dest)
                      (newpath)
              )
)
(set @default_dest (tackon dest_dir @app-name))
(set muilibs_path "MUI:libs/")
;----------------------------------------------------------------------------
; Создание директорий
;----------------------------------------------------------------------------
(makedir @default_dest)
(makedir (tackon @default_dest "Catalogs"))
(makedir (tackon @default_dest "Data"))
(makedir (tackon @default_dest "Images"))
(makedir (tackon @default_dest "Docs"))
(complete 10)
;----------------------------------------------------------------------------
; Копирование основной программы
;----------------------------------------------------------------------------
(copyfiles (prompt #MSG_COPY)
           (help @copyfiles-help)
           (source "/")
           (dest @default_dest)
           (infos)
           (pattern '(autodocart~(#?.#?)|(#?.info)|(#?.Guide)|(#?.ReadMe))')
)
(complete 17)
;----------------------------------------------------------------------------
(P_InstallFiles "Data" 20)
(P_InstallFiles "Catalogs" 27)
(P_InstallFiles "Docs" 36)
(P_InstallFiles "Images" 50)
;----------------------------------------------------------------------------
; Проверка присутствия и версий MCC
;----------------------------------------------------------------------------
(working #MSG_INSTALL_MCC)
(P_InstallMCC "/libs/NList.mcc" 53)
(P_InstallMCC "/libs/NListview.mcc" 56)
(P_InstallMCC "/libs/NListviews.mcp" 59)
(P_InstallMCC "/libs/NFloattext.mcc" 62)
(P_InstallMCC "/libs/NListtree.mcc" 65)
(P_InstallMCC "/libs/NListtree.mcp" 68)
(P_InstallMCC "/libs/ToolBar.mcc" 71)
(P_InstallMCC "/libs/ToolBar.mcp" 74)
(P_InstallMCC "/libs/BetterBalance.mcc" 80)
;----------------------------------------------------------------------------
; Установка файлов конфигурации
;----------------------------------------------------------------------------
(if (askbool (prompt #MSG_INSTALL_PREFS)
             (help #MSG_PREFS_HELP)
             (default 1)
             (choices #MSG_INSTALL #MSG_SKIP)
    )
    (copyfiles (prompt #MSG_COPY)
               (help @copyfiles-help)
               (source "")
               (pattern "autodocart.#?")
               (dest "ENVARC:mui")
    )
)
(complete 90)
;----------------------------------------------------------------------------
; Проверка нахождения логического устройства AutoDocs:
;----------------------------------------------------------------------------
(if (NOT (exists "AUTODOCS:" (noreq)))
    (if (askbool (prompt #MSG_AUTODOCS)
                 (help #MSG_AUTODOCS_HELP)
                 (default 0)
                 (choices #MSG_INSTALL #MSG_SKIP)
        )
        (
          (set autodocs_dir
               (askdir (prompt #MSG_AUTODOCS_DIR)
                       (help #MSG_AUTODOCS_HELP)
                       (default "WORK:")
                       (newpath)
               )
          )
          (if (NOT (exists autodocs_dir (noreq)))
              (makedir autodocs_dir
                       (prompt (#MSG_AUTODOCS_MAKEDIR autodocs_dir))
                       (help @makedir-help)
                       (confirm 1)
              )
          )
          (makeassign "AutoDocs" autodocs_dir)
          (startup "AUTODOCART"
                   (prompt (#MSG_STARTUP autodocs_dir))
                   (help @startup-help)
                   (command ("Assign AutoDocs: %s" autodocs_dir))
          )
        )
    )
)
;----------------------------------------------------------------------------
(complete 100)
(exit (#MSG_FINAL @app-name (pathonly @default_dest)) (quiet))
