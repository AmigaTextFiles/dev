
;******************************************************************************
; $VER: 1.5 (Feb-6-1996)
; Changes, suggestions or problems: autech.research@tassie.net.au
; PPMS Installer by Ian Masters ©1995,1996
;******************************************************************************

;*** System Stuff ***
(set cpu (database "cpu"))
(set osver (/ (getversion "LIBS:version.library") 65536))
(set cpu (database "cpu"))
(if (> (exists ("sys:libs/68060.library")) 0) (set #cpu 68060) )

(set vernum1 (getversion "PPMS")) (set ver1 (/ vernum1 65536))
(set rev1 (- vernum1 (* ver1 65536) ) ) (set PPMSver (cat ver1 "." rev1))

(set keeper 1) (set firsttime 0)

;********************************************************************
; English Strings
;********************************************************************
(set #about (cat "\n\n\nWelcome to\n\nPPMS " PPMSver "\n\nCopyright © 1996 by\n\nIan Masters\n") )
(set #nomui (cat "\n\nPPMS uses Magic User Interface (MUI).\n\nThe installer has not found MUI: to be assigned, "
		 "therefore PPMS will not work correctly.  You will need to install "
		 "MUI before running PPMS."
	    )
)
(set #wherePPMS "Where do you wish to install PPMS. A directory called 'PPMS' will be created if one is not there. If a copy of PPMS is found it will be backed up.")
(set #need-os "\n\nPPMS needs version 39 or higher of the Amiga OS installed to work correctly.\n\n It looks like you have version ")
(set #need-os2 "\n\nYou can still install if you desire.")
(set #backuptoXXX "Back-UP to file named 'PPMS")
(set #copyover "Copy Over Old Version")
(set #PPMSfound "\nThe Installer has found a copy of PPMS already installed. What do you want to do?\n")
(set #nocpu "\nCPU Check: The installer has found you have a ")
(set #nocpu2 "\n\nPPMS requires a 68020 or better.\n\nYou can still install if you desire.")
(set #byebye "\nPPMS Install Complete!\n\n")

(set app-name (cat @app-name " 4.0 demo version"))

;********************************************************************
; Hello
;********************************************************************
(message #about)
(welcome) 

;********************************************************************
; OS check
;********************************************************************
(if (< osver 39) (message #need-os osver "." #need-os2) )

;********************************************************************
; CPU check
;********************************************************************
(if (< cpu 68020) (message #nocpu cpu "." #nocpu2) )

;********************************************************************
; MUI installed?
;********************************************************************
(set mui (getassign "MUI"))
(if (= mui "") (message #nomui) )

;********************************************************************
; Ask where to put PPMS & Make dir if needed
;********************************************************************
(set Package (getassign "PPMS"))

(set ppmsdir  (askdir (prompt #wherePPMS)
		       (help   #wherePPMS)
		       (default Package)
	       )  
)

(set isthere (exists(tackon ppmsdir "PPMS")) )
(if (= isthere 0)
  (
    (makedir (tackon ppmsdir "PPMS") )
    (copyfiles (source "/PPMS.info") (dest ppmsdir) (optional "nofail") )
    (set where (tackon ppmsdir "PPMS") )
  )
)
(if (= isthere 2)
    (set where (tackon ppmsdir "PPMS") )
)

(if (= isthere 1)
    (set where ppmsdir)
)

(set @default-dest where)

;********************************************************************
; Check for old & backup
;********************************************************************
(if (exists (tackon where "PPMS"))
  (
      (set vernum1 (getversion (tackon where "PPMS") ))
      (set ver1 (/ vernum1 65536)) (set rev1 (- vernum1 (* ver1 65536) ) )
      (set PPMS_old (cat ver1 "." rev1))

      (set keeper  (askchoice (choices  (cat #backuptoXXX PPMS_old "'")  #copyover)
		     (prompt #PPMSfound)
		     (help   #PPMSfound)
		     (default 0)
		   )
      )    


      (if (= keeper 0)
	 (copyfiles (source (tackon where "PPMS"))
		     (dest where) 
		     (infos)
		     (newname (cat "PPMS" PPMS_old))
	 )
      )          
  )
)

;********************************************************************
; Copy over correct PPMS and Icon
;********************************************************************

(copyfiles (source "PPMS") (dest where) (infos) (optional "nofail") )
(copyfiles (source "ppms.key") (dest where) (infos) (optional "nofail") )
(copyfiles (source "PPMS.guide") (dest where) (infos) (optional "nofail") )
(copyfiles (source "changes.txt") (dest where) (infos) (optional "nofail") )
(copyfiles (source "Last Project.info") (dest where) (optional "nofail") )
(copyfiles (source "Projects") (dest where) (infos) (optional "nofail") )
(copyfiles (source "Editors.prefs") (dest where) (optional "nofail") )
(copyfiles (all) (source "Converters") (dest (tackon where "Converters")) (infos) (optional "nofail") )
(copyfiles (all) (source "Examples") (dest (tackon where "Examples")) (infos) (optional "nofail") )

;********************************************************************
; Startup
;********************************************************************

(set addition (cat "Assign PPMS: " @default-dest "\n"))

(startup (
      "PPMS"
      (command addition)
      (prompt "Installer will modify your S:User-Startup file. " )
      (help "   Installer needs to make indicated modifications to "
	    "your user startup file to make sure that everything is "
	    "correctly set up to run PPMS.\n"
	    "   You should make modifications later by hand "
	    "if you skip this part.")
))

;********************************************************************
; End Bye 
;********************************************************************
(exit #byebye)
