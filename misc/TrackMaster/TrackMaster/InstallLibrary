.K ""
echo "This script will install lh.library, after checking to see if"
echo "you already have one.  If so, it will check the version numbers"
echo "of the installed one, and the one in this directory, and give"
echo "you the option of installing the one found here."
echo ""

ask "Do you wish to continue with the installation of lh.library? [y/n]"
if WARN
	if not EXISTS libs:lh.library
		echo "No existing library, installing libs:lh.library..."
		copy libs/lh.library libs:lh.library
	else
		echo "Examine the version numbers of each library."
		echo ""
		version libs:lh.library
		version libs/lh.library
		echo ""
		ask "Do you wish to overwrite libs:lh.library? [y/n]"
		if WARN
			echo "Updating libs:lh.library..."
			copy libs/lh.library libs:lh.library
		else
			echo "libs:lh.library unchanged..."
		endif
	endif
endif

echo "All Done.  Click close gadget to get rid of window."
endcli
