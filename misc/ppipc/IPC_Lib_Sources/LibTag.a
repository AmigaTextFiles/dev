;       LibTag.a -- romtag for (IPC) resident library  89:3:25

        include 'exec/types.i'
        include 'exec/resident.i'
        include 'exec/nodes.i'
        include 'exec/libraries.i'
        include 'exec/initializers.i'

*** No way as far as I can see to specify these from C:
*** (but the Init code can change them in the Library Node if it likes...)

LIBVERSION  equ 1
LIBREV      equ 0
LIBPRI      equ 0   *** (unless this is a very unusual library...!)


****************************************************************

        section text,code    ; romtag must be in first hunk

        xref  _libName
        xref  _libId
        xref  _libInitTable

    ;*** first executable location:
        move.l #20,d0   ;protection against running as a program
        rts

        xdef  _libTag
_libTag:
        dc.w    RTC_MATCHWORD
        dc.l    _libTag
        dc.l    endtag
        dc.b    RTF_AUTOINIT
        dc.b    LIBVERSION
        dc.b    NT_LIBRARY
        dc.b    LIBPRI
        dc.l    _libName
        dc.l    _libId
        dc.l    _libInitTable
endtag:

****************************************************************

        section libdata,data

        xdef _DataTable

_DataTable:             ; uses macros from initializers.i
        INITBYTE    LN_TYPE,NT_LIBRARY
        INITBYTE    LN_PRI,LIBPRI
        INITLONG    LN_NAME,_libName
        INITBYTE    LIB_FLAGS,LIBF_CHANGED!LIBF_SUMUSED
        INITWORD    LIB_VERSION,LIBVERSION
        INITWORD    LIB_REVISION,LIBREV
        INITLONG    LIB_IDSTRING,_libId
        dc.l        0



        xdef _SysBase

_SysBase:   dc.l    0   ;   C library functions will need this

****************************************************************

        end
