/*
** GoFetchRefs.edge - A script to interface FetchRefs with Edge
**
** By Jon Rocatis, Wednesday 11-Jan-95 16:06:55
**
** Extended by Anders Melchiorsen, 24-Feb-96 (hopefully still works!)
*/

OPTIONS RESULTS
OPTIONS FAILAT 20

position eow
cursor right 1
copy word back resultbuff
searchstr = result

function = searchstr

/* It doesn't matter whether we want a taglist, varargs og whatever function */
IF RIGHT(function, 7) = "TagList" THEN
    function = LEFT(function, LENGTH(function) - 7)
ELSE IF RIGHT(function, 4) = "Tags" THEN
    function = LEFT(function, LENGTH(function) - 4)
ELSE IF RIGHT(function, 1) = "A" THEN
    function = LEFT(function, LENGTH(function) - 1)

/* Define a temporary filename to put the reference into */
cutat = VERIFY(function, "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz1234567890_")
filename = 'T:FR_' || LEFT(function, MAX(0, cutat - 1))

ADDRESS 'FETCHREFS'
FR_GET FIND function || '(%|Tags|TagList|A)' TO filename FILEREF
gotoline = rc2

if rc == 0 then do
  ADDRESS 'EDGE'
  NEW

  if rc == 0 then do         /* if all is well then talk to it */
    address value result
    open filename
  end  

  /* Delete the temp autodoc file */
  ADDRESS COMMAND 'C:Delete >NIL:' filename
end  

