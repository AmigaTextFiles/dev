; Install VW
; For V1.012 of Virtual Worlds

(delopts "oknodelete" "force" "askuser")
(complete 0)
(set update 0)
(set HD (askchoice
   (prompt (cat "\n"
      " Welcome to the Virtual Worlds install utility. "
      " Please select whether you wish to install Virtual"
      " Worlds to a floppy disk or to a hard disk drive.\n"
   ))
   (default 0)
   (choices
      "Hard disk Installation"
      "Floppy disk Installation"
   )
   (help (cat "\n"
      " Virtual Worlds may be installed to either your hard"
      " disk (if you have one), or to a bootable floppy disk.\n"
   ))
))
(if (= HD 0) (
;-------------------- Hard disk install routine.
(if (exists "VW:" (noreq))
   (set update (askchoice
      (prompt (cat "\n"
         "           VW: assign detected!         \n"
         " It would appear that Virtual Worlds is \n"
         " already installed - do you wish to :-  \n"
      ))
      (default 1)
      (choices
         "Install new"
         "Update existing"
         "Remove"
      )
      (help (cat "\n"
         " The assign VW: has been detected.  If you wish to"
         " completely reinstall Virtual Worlds select Install"
         " new.  If you just wish to update a previous version"
         " then select Update existing.  If you wish to remove"
         " Virtual Worlds then select the Remove option.  This"
         " will attempt to remove all files originally"
         " installed by Virtual Worlds.  However, some files,"
         " such as the asl.library will not be removed as they"
         " may be used by other applications.\n"
      ))
   ))
)

(if (= update 2) (
   (if (exists "VW:" (noreq))
      (
         (run "delete >NIL: S:VW.Config quiet force")
         (run "delete >NIL: S:VWProject.info quiet force")
         (run "delete >NIL: VW: all quiet force")
         (run "assign >NIL: VW: remove")
         (startup
            "Virtual Worlds"
            (prompt "\n Do you wish to remove the 'VW:' assign?\n")
            (command ";(removed)")
            (help (cat "\n"
              " The user-startup contains an assign to VW: \n"
              " This script will remove this for you.      \n"
            ))
         )
      )
   )
   (exit (quiet))
))
(if (= update 1)
   (set target "VW:/")
;else
   (set target (askdir
      (prompt "\n Where do you want Virtual Worlds installed? \n")
      (help (cat "\n"
         " A new directory called Virtual Worlds will be made"
         " in the directory that you specify.\n"
      ))
      (default "Work:")
   ))
)
(set @default-dest target)
(complete 10)

(set ts (getdiskspace target))
(if (< ts 1000000)
   (if (<> (getdevice target) "RAM")
      (abort (cat "\n"
         "   Not enough free disk space!!!  \n"
         " Virtual Worlds requires at least \n"
         "    1 Meg of hard disk space.     \n"
      ))
   )
)

(complete 20)
(copyfiles
   (source "VW")
   (dest (tackon target "VirtualWorlds"))
   (infos)
   (all)
)

(complete 50)
(if (= update 0) (
   (run (cat "Assign VW: " (tackon target "VirtualWorlds")))
   (startup
      "Virtual Worlds"
      (prompt "\n Add 'VW:' assign to 's:user-startup' ? \n")
      (command ("ASSIGN >NIL: VW: %s " (tackon target "VirtualWorlds")))
      (help (cat "\n"
         " This script will add an entry to the user-startup file"
         " so that the assign VW: is made to the correct directory"
         " on startup.\n"
      ))
   )
))

(complete 60)
(if (= update 0) (
   (set version (askchoice
      (prompt "\n What version of asl.library? \n")
      (default 1)
      (choices
         "Workbench 1.3"
         "Workbench 2.0+"
         "Dont bother!"
      )
      (help (cat "\n"
         " The correct version of asl.library must be present in"
         " your LIBS: drawer for Virtual Worlds to run correctly."
         " This process will not overwrite a newer library (higher"
         " version number).\n"
      ))
   ))
   (if (= version 0)
      (copylib (source "ASL/asl13.library") (dest "libs:") (newname "asl.library"))
   )
   (if (= version 1)
      (copylib (source "ASL/asl204.library") (dest "libs:") (newname "asl.library"))
   )
))
(complete 70)
(if (= update 0) (
   (set font (askchoice
      (prompt "\n Do you wish to install the Times font? \n")
      (default 0)
      (choices
         "Yup - go ahead!"
         "No way!  Leave my fonts alone!"
      )
      (help (cat "\n"
         " Virtual Worlds requires the Times font in order to function"
         " correctly.  If you already have a Times font then you can"
         " decide not to install this version and test Virtual Worlds. "
         " If it appears that your font does not perform as required,"
         " then you may decide to reinstall Virtual Worlds.  In this"
         " case, you should select Install New, and then opt to install"
         " the Times font provided.  Your old font will be renamed to"
         " TimesOld, and the new font will be installed as Times.\n"
      ))
   ))
   (if (= font 0)
      ((if (exists "FONTS:Times.font")
         ((run "rename FONTS:Times.font FONTS:TimesOld.font")
         (run "rename FONTS:Times FONTS:TimesOld"))
      )
      (copyfiles
         (source "Fonts")
         (dest "FONTS:")
         (nogauge)
         (all)
      ))
   )
))
(complete 90)
(If (= update 0) (
    (set icontype (askchoice
        (prompt "\n Which icons do you want?\n")
        (default 0)
        (choices
            "Standard icons"
            "MagicWB icons"
        )
        (help (cat "\n"
            " There is an icon provided for those people who use"
            " MagicWB.  If you do not use this then Select the"
            " standard icons. \n"
            (cat "copy icons/VW_MWB.info " (tackon target "VirtualWorlds/VirtualWorlds.info >NIL:"))
            (cat "copy icons/VW_MWB.info " (tackon target "VirtualWorlds/VWRun.info >NIL:"))
        ))
    ))
    (If (= icontype 0)(
        (run (cat "copy icons/VW_NORM.info " (tackon target "VirtualWorlds/VirtualWorlds.info >NIL:")))
        )
        (
        (run (cat "copy icons/VW_MWB.info " (tackon target "VirtualWorlds/VirtualWorlds.info >NIL:")))
        (run (cat "copy icons/VW_MWB.info " (tackon target "VirtualWorlds/VWRun.info >NIL:")))
        (run (cat "copy icons/VWDOC_MWB.info " (tackon target "VirtualWorlds/VirtualSoftware.DOC.info >NIL:")))
        (run (cat "copy icons/VWDOC_MWB.info " (tackon target "VirtualWorlds/VW.Guide.info >NIL:")))
        )
    ))
)
(complete 95)
(if (= update 0) (
   (set icon (askchoice
      (prompt "\n Do you wish to have icons for saved World files?\n")
      (default 0)
      (choices
         "Ooooh!  Yes please!"
         "Errr.  No thanks."
      )
      (help (cat "\n"
         " If you want to have nice little icons for your files then"
         " select as such here.  This will copy the default icon to"
         " your S: directory.  If you wish to disable this feature"
         " then merely remove the icon from your S: drawer.\n"
      ))
   ))
   (if (= icon 0) (
      (If (= icontype 0) (
         (run "copy icons/VWProject_NORM.info S:VWProject.info >NIL:")
         )(
         (run "copy icons/VWProject_MWB.info S:VWProject.info >NIL:")
         ))
      )
   ))
)
(complete 100)
)
; -------------------------- Floppy disk install bit
(
   (complete 0)
   (set form (askchoice (
      (prompt (cat
         " For this install to work, there are a few conditions that MUST"
         " be met. "
         " The Virtual Worlds archive has been dearchived onto a"
         " floppy disk that is NOT called VW. "
         " You need to boot from your normal boot disk - ie a copy"
         " of your Workbench disk. "
         " You need a blank disk called VW to make into an auto-"
         "booting Virtual Worlds disk. "
         " This script can perform the third part for you, but you"
         " must see to the other parts yourself.  Select Abort Install"
         " if you need to reboot/find a disk."
      ))
      (default 0)
      (choices
         "Go on then - format me a disk as well."
         "Ive got a disk called VW here for you!"
      )
      (help (cat "\n"
         " You need a blank disk called VW for this install"
         " to work, and you must boot from a normal Workbench"
         " disk.  This script can create the disk for you"
         " but you'll have to reboot yourself!\n"
      )))
   ))
   (if (= form 0) (
      (run "copy sys:c/install RAM: >NIL:")
      (run "copy sys:system/format RAM: >NIL:")
      (run "copy sys:c/delete RAM: >NIL:")
      (run "copy sys:c/copy RAM: >NIL:")
      (message "\n Please insert a blank disk into DF0: \n")
      (working "\n Please wait -> Formatting disk in DF0: \n\n This will take approximately 1.5 minutes. \n")
      (run "ram:format DEVICE=DF0: NAME VW")
      (run "ram:install df0: >NIL:")
      (run "ram:delete RAM:installl >NIL:")
      (run "ram:delete RAM:format >NIL:")
   ))
   (complete 20)
   (message (cat "\n"
      " Please re-insert your system disk. \n"
   ))
   (
      (working "\n Please wait -> copying files to RAM: \n")
      (run "makedir RAM:c >NIL:")
      (complete 21)
      (run "makedir RAM:libs >NIL:")
      (complete 22)
      (run "makedir RAM:devs >NIL:")
      (complete 23)
      (run "makedir RAM:fonts >NIL:")
      (run "makedir RAM:fonts/topaz >NIL:")
      (complete 24)
      (run "makedir RAM:s >NIL:")
      (complete 25)
      (run "copy c:delete ram:c >NIL:")
      (complete 27)
      (run "copy c:assign ram:c >NIL:")
      (complete 29)
      (run "copy c:copy ram:c >NIL:")
      (complete 30)
      (run "copy c:endcli ram:c >NIL:")
      (complete 32)
      (run "copy c:execute ram:c >NIL:")
      (complete 34)
      (run "copy c:LoadWB ram:c >NIL:")
      (complete 36)
      (run "copy c:path ram:c >NIL:")
      (complete 38)
      (run "ram:copy FONTS:Topaz#? RAM:Fonts/Topaz >NIL:")
      (complete 42)
      (run "ram:copy FONTS:topaz.font ram:fonts >NIL:")
      (complete 43)
      (run "copy libs:amigaguide.library ram:libs >NIL:")
      (complete 44)
      (run "copy libs:diskfont.library ram:libs >nil:")
      (complete 45)
      (run "copy libs:icon.library ram:libs >nil:")
      (complete 46)
      (run "copy libs:info.library ram:libs >nil:")
      (complete 47)
      (run "copy libs:math#? ram:libs >nil:")
      (complete 49)
      (run "copy libs:version.library ram:libs >nil:")
      (complete 50)
   )
   (message (cat "\n"
      " Ok - Now insert the disk which contains the unarchived"
      " Virtual Worlds stuff.\n")
   )
   (set version (askchoice
      (prompt "\n What version of asl.library? \n")
      (default 1)
      (choices
         "Workbench 1.3"
         "Workbench 2.0+"
      )
      (help (cat "\n"
         " The correct version of asl.library must be present in"
         " your LIBS: drawer for Virtual Worlds to run correctly."
         " This process will not overwrite a newer library (higher"
         " version number).\n"
      ))
   ))
   (working "\n Please wait -> Copying VW files to RAM: \n")
   (if (= version 0)
      (run "ram:copy ASL/asl13.library ram:libs/asl.library")
   )
   (if (= version 1)
      (run "ram:copy ASL/asl204.library ram:libs/asl.library")
   )
   (complete 55)
   (copyfiles
      (source "FONTS/Times")
      (dest "RAM:fonts/Times")
      (all)
      (nogauge)
   )
   (complete 60)
   (run "ram:copy FONTS/times.font ram:fonts >NIL:")
   (complete 61)
   (copyfiles
      (source "VW")
      (dest "ram:VW")
      (all)
      (infos)
      (nogauge)
   )
   (complete 67)
   (run "ram:copy S/Startup-Sequence ram:s >NIL:")
   (complete 69)
   (run "ram:copy S/VWProject.info ram:s >NIL:")
   (complete 70)
;copy files from >copyfrom< to df1:
   (message "\n Please insert disk to install VW on. \n")
   (working "\n Please wait -> Copying files to disk. \n")
   (copyfiles
      (source "RAM:c")
      (dest "VW:c")
      (all)
      (nogauge)
   )
   (complete 75)
   (copyfiles
      (source "RAM:s")
      (dest "VW:s")
      (all)
      (infos)
      (nogauge)
   )
   (complete 80)
   (copyfiles 
      (source "RAM:libs")
      (dest "VW:libs")
      (all)
      (nogauge)
   )
   (complete 85)
   (copyfiles
      (source "RAM:fonts")
      (dest "VW:fonts")
      (all)
      (nogauge)
   )
   (complete 90)
   (copyfiles 
      (source "RAM:VW")
      (dest "VW:")
      (all)
      (infos)
      (nogauge)
   )
   (complete 100)
)
)
