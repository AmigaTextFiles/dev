
;***********
;*         *
;* STRINGS *
;*         *
;***********

(set #LANG_FOUND 0)

(if (= "deutsch" @language)
       (
	(set #MSG_WELCOME "Willkommen beim Installations-Skript für \"Offset\".")
	(set #MSG_WELCOME_WARN "\n\nSie benutzen eine alte Version (<42) des Installers. Das könnte unter Umständen Probleme bereiten!")

	(set #MSG_SELECTEXEPATH "In welches Verzeichnis soll das Programm  (\"Offset\") kopiert werden?")
	(set #MSG_SELECTSTRUCTPATH "In welches Verzeichnis soll die \"Struktur-Offset-Datei\" kopiert werden?")
	(set #MSG_SELECTDOCPATH "In welches Verzeichnis soll die Dokumentation (\"Offset.guide\") kopiert werden?")

	(set #MSG_COPY_EXE "Kopiere Offset")
	(set #MSG_COPY_STRUCT "Kopiere \"Struktur-Offset-Datei\"")
	(set #MSG_COPY_DOC "Kopiere Dokumentation")
	(set #MSG_COMMENTPATH "Speichere Pfad der \"Struktur-Offset-Datei\" im Comment von Offset")

	(set #MSG_ICONPOS "Korrigiere Icon Position von \"%s\"")

	(set #MSG_NOHELP "Keine Hilfe verfügbar. Sorry. Ich bin einfach zu faul :-( Lesen Sie das \"Offset.guide\"!")

	(set #LANG_FOUND 1)
       )
)

(if (= "italiano" @language)
       (
	(set #MSG_WELCOME "Benvenuto nello script di installazione per \"Offset\".")
	(set #MSG_WELCOME_WARN "\n\nLei possiede una versione vecchia (<42) dell'Installer. Questo potrebbe eventualmente causare dei problemi!")

	(set #MSG_SELECTEXEPATH "In che directory devo copiare il programma (\"Offset\")?")
	(set #MSG_SELECTSTRUCTPATH "In che directory devo copiare il \"Structure-Offset-File\"?")
	(set #MSG_SELECTDOCPATH "In che directory devo copiare la documentazione (\"Offset.guide\")?")

	(set #MSG_COPY_EXE "Copio Offset")
	(set #MSG_COPY_STRUCT "Copio \"Structure-Offset-File\"")
	(set #MSG_COPY_DOC "Copio documentazione")
	(set #MSG_COMMENTPATH "Salvo il path del \"Structure-Offset-File\" nel Comment di Offset")
		
	(set #MSG_ICONPOS "Corrego posizione dell'icona di \"%s\"")

	(set #MSG_NOHELP "Nessun aiuto disponibile. Sorry. Sono troppo pigro :-( Leggi il \"Offset.guide\"!")

        (set #LANG_FOUND 1)
       )
)

(if (= 0 #LANG_FOUND)
       (
	(set #MSG_WELCOME "Welcome to the installation script for \"Offset\".")
	(set #MSG_WELCOME_WARN "\n\nYou are using an old version (<42) of the Installer. This might eventually cause problems!!")

	(set #MSG_SELECTEXEPATH "Where do you want the executable (\"Offset\") to be copied?")
	(set #MSG_SELECTSTRUCTPATH "Where do you want the \"Structure-Offset-File\" to be copied?")
	(set #MSG_SELECTDOCPATH "Where do you want the documentation (\"Offset.guide\") to be copied?")

	(set #MSG_COPY_EXE "Copying Offset")
	(set #MSG_COPY_STRUCT "Copying \"Structure-Offset-File\"")
	(set #MSG_COPY_DOC "Copying documentation")
	(set #MSG_COMMENTPATH "Saving path of the \"Structure-Offset-File\" in the comment of Offset")
	
	(set #MSG_ICONPOS "Correcting icon position of \"%s\"")

	(set #MSG_NOHELP "No help available. Sorry. I'm just too lazy :-( Have a look at \"Offset.guide\"!")

       )
)

;==========================================================================

;********
;*      *
;* MAIN *
;*      *
;********

(set #OSVERSION (/ (getversion) 65536))

(if (< (/ @installer-version 65536) 42)
	(welcome #MSG_WELCOME #MSG_WELCOME_WARN)
	(welcome "\n" #MSG_WELCOME)
)

;******************
;*                *
;* SELECT EXE DIR *
;*                *
;******************

(set #exedest
	(Expandpath
		(askdir (prompt #MSG_SELECTEXEPATH)
			(default "SYS:C")
			(help #MSG_NOHELP)
		)
	)
)

;*********************
;*                   *
;* SELECT STRUCT DIR *
;*                   *
;*********************

(set #structdest
	(Expandpath
		(askdir (prompt #MSG_SELECTSTRUCTPATH)
			(default "S:")
			(help #MSG_NOHELP)
		)
	)
)

;******************
;*                *
;* SELECT DOC DIR *
;*                *
;******************

(set #docdest
	(Expandpath
		(askdir (prompt #MSG_SELECTDOCPATH)
			(default "HELP:")
			(help #MSG_NOHELP)
		)
	)
)

;**************
;*            *
;* COPY FILES *
;*            *
;**************

(copyfiles (prompt #MSG_COPY_EXE)
	   (source "")
	   (choices "Offset" "Offset.info")
	   (dest #exedest)
	   (confirm)
	   (help #MSG_NOHELP)
)

(copyfiles (prompt #MSG_COPY_STRUCT)
	   (source "Structure.Offsets")
	   (dest #structdest)
	   (confirm)
	   (help #MSG_NOHELP)
)

(copyfiles (prompt #MSG_COPY_DOC)
	   (source "")
	   (choices "Offset.guide" "Offset.guide.info")
	   (dest #docdest)
	   (confirm)
	   (help #MSG_NOHELP)
)


;** Comment von Offset auf Pfad von struct-dest setzen

(run  ("c/setcomment \"%s\" \"%s\"" (tackon #exedest "Offset") (tackon #structdest "Structure.Offsets"))
      (prompt #MSG_COMMENTPATH)
      (help #MSG_NOHELP)
      (confirm)
)

;** IconPosition

(if (exists (tackon #exedest "Offset.info"))
	(run	("c/noiconpos %s" (tackon #exedest "Offset"))
		(prompt (#MSG_ICONPOS "Offset"))
		(help #MSG_NOHELP)
		(confirm)
	)
)

(if (exists (tackon #docdest "Offset.guide.info"))
	(run	("c/noiconpos %s" (tackon #docdest "Offset.guide"))
		(prompt (#MSG_ICONPOS "Offset.guide"))
		(help #MSG_NOHELP)
		(confirm)
	)
)

;***********
;*         *
;* THE END *
;*         *
;***********

(set @default-dest #exedest)
