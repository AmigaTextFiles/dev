.key dest,all/s,dirs/s,files/s,wide/k,high/k,pat/k
.bra {
.ket }


; set text for pattern match in list command
set pat ""
if not {pat$1} eq 1
set pat pat={pat}
endif

; build a temp file defining the gui

; define the window (delay the window open) and a list browser in it
echo >t:thelist.gui  "*"GUIList  {dest$Current Dir} *" m nowindow defg q a cs*nlistbrowser ro lbl *"Name|Size|Date|Time|Protection|Key|Path*" arrows minw {wide$600} minh {high$200} st"

; the lformat creates a list brwser node for each file.
list >>t:thelist.gui {dest} {all} {dirs} {files} $pat lformat "browsernode gt *"%n|%l|%d|%t|%a|%k|%s%s*""

; some disabled menus to display a little info
echo >>t:thelist.gui "menu gt *"Project  |About|$!List wrapper with|$!  GUI by AWNPipe:" noline
echo >>t:thelist.gui "|Author  |$!William H.M. Parker|$! <bill@amitrix.com>*""

; end the gui definition
echo >>t:thelist.gui "open"

;sort list by column 6 (path)
echo >>t:thelist.gui "id 1 sort 6"

;open the window
echo >>t:thelist.gui "id 0 s 64"

;turn off modify
echo >>t:thelist.gui "m"


; send the file to the pipe
copy t:thelist.gui awnpipe:/xc

; cleanup
delete t:thelist.gui >nil:
unset pat

;The gui only displays information all gadgets are read only. It goes away
;when the user closes its window

