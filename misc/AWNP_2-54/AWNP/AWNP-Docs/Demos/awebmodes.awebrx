/*
	$VER: AWeb Modes 1.2 (1.10.99) by Gabriele Favrin (favrin@tin.it)

	Change various AWeb modes from a ClassAct/Reaction GUI
	Thanks to my Friend Bill Parker for his great AWNPipe!
*/

options results
options failat 999

if ~Show('L', 'rexxsupport.library') then do
	call addlib('rexxsupport.library', 0, -30, 0)
end

if Show('P', 'AWEBMODES') then do
	address AWEBMODES 'front'
	exit
end

if Exists('env:aweb3/') then
	envname='aweb3/amwindow'
else
	envname='awebSE/amwindow'

eol='0A'x; spooftext.0=''; spooftext.1='Mozilla/3.01'
'Get VERSION VAR ver'
fullv=(Pos('SE', ver) == 0)
small2=''
savewindow=''
call initPipe()
call getenv()
call init()
call manageGUI()

exit 0

init:
	'Get SCREEN' VAR screen

	'GetCFG HTMLMODE VAR' html
	'GetCFG FRAMES VAR' frames
	'GetCFG NOMINALFRAME VAR' nframe
	'GetCFG JAVASCRIPT VAR' js
	'GetCFG JSERRORS VAR' jserrors
	'GetCFG ANONYMOUS VAR' anon
	'GetCFG IGNOREMIME VAR' mime
	'GetCFG RFCCOOKIES VAR' rfc
	'GetCFG NAVIGATION VAR' nav
	'GetCFG SHOWBUTTONS VAR' buttons

	if fullv == 0 then do
		jsbanners=0
		spoof=0
		gui_jsbanners='disable 1'
		gui_spoof='disable 1'
		spoofflag=0
	end
	else do
		'GetCFG SUPPRESSBANNERS VAR' jsbanners
		'GetCFG SPOOFAS VAR' spoof
		gui_jsbanners='disable' 1-js
		gui_spoof=''
		spoofflag=(spoof ~= '')
	end

	'PLUGIN awebgif.awebplugin ANIM'
	if rc == 5 then do
		anim=0
		nogif=1
	end
	else do
		anim=result
		nogif=0
	end
	if small2 ~='' then small=small2
	if savewindow ~='' then window=savewindow
	gui='"AWeb Modes 1.2 - press <HELP> key for help" dg db 'window' cg ps "'screen'" so a q m sk'eol
	if small == 1 then do
		gui=gui||'menu gt "Project|Window|$@SSnapshot|$@UUnsnapshot|$@M^&Small|About|$!AWeb Modes 1.2|Author|$!Written by Gabriele Favrin|$!using AWNPIPE: gui|!|@QQuit"'eol
		gui=gui||'layout so si b 0'eol'layout v so si gt "Document"'eol
		gui=gui||'radiobutton rl "Strict |Tolerant |Compatible " s' html||eol'space minh 0'eol
		gui=gui||'checkbox gt "_Frames" s' frames||eol'checkbox gt "_Nominal height" s' nframe 'disable' 1-frames||eol'checkbox gt "_GIF animation" s' anim 'disable' nogif||eol'space minh 0'eol
		gui=gui||'checkbox gt "_JavaScript" s' js||eol'checkbox gt "JS _Errors" s' jserrors 'disable' 1-js||eol'checkbox gt "_Banner windows" s' jsbanners gui_jsbanners||eol'pg'eol
		gui=gui||'layout b 0 v'eol'layout v so si gt "Network"'eol
		gui=gui||'checkbox gt "_Anonymous" s' anon||eol'label gt "(HTTP referrer)" noms'eol'checkbox gt "S_poofing" s' spoofflag gui_spoof||eol'label gt "(as Netscape 3.x)"'eol
		gui=gui||'checkbox gt "Ignore _MIME TYPE" s' mime||eol'checkbox gt "RFC 210_9 cookies" s' rfc||eol'pg'eol'space nimh 0'eol
		gui=gui||'layout v so si gt "GUI"'eol'checkbox gt "Na_vigation" s' nav||eol'checkbox gt "_User buttons" s' buttons||eol'pg'eol'pg'eol'pg'eol
	end
	else do
		gui=gui||'menu gt "Project|Window|$@SSnapshot|$@UUnsnapshot|$@M^%Small|About|$!AWeb Modes 1.2|Author|$!Written by Gabriele Favrin|$!using AWNPIPE: gui|!|@QQuit"'eol
		gui=gui||'layout so si b 0'eol'layout v so si gt "Document related options"'eol
		gui=gui||'radiobutton rl "Strict (HTML 2.0/3.2/4.0)|Tolerant (also 3.0/MSIE/NS HTML)|Compatible (badly written HTML)" s' html||eol'space minh 0'eol
		gui=gui||'checkbox gt "Use _Frames" s' frames||eol'checkbox gt "_Nominal (fixed) frames height" s' nframe 'disable' 1-frames||eol'checkbox gt "_GIF animations playback" s' anim 'disable' nogif||eol'space minh 0'eol
		gui=gui||'checkbox gt "_JavaScript usage" s' js||eol'checkbox gt "Show JavaScript _errors" s' jserrors 'disable' 1-js||eol'checkbox gt "Suppress JS _banner windows" s' jsbanners gui_jsbanners||eol'pg'eol
		gui=gui||'layout b 0 v'eol'layout v so si gt "Network related options"'eol
		gui=gui||'checkbox gt "_Anonymous browsing" s' anon||eol'label gt "(HTTP referrer header strip)" noms'eol'checkbox gt "S_poofing" s' spoofflag gui_spoof||eol'label gt "(HTTP/JS spoof as Netscape 3.x)"'eol
		gui=gui||'checkbox gt "Ignore _MIME TYPE server header" s' mime||eol'checkbox gt "RFC 210_9 compliant cookies" s' rfc||eol'pg'eol'space nimh 0'eol
		gui=gui||'layout v so si gt "GUI related options"'eol'checkbox gt "Show Na_vigation gadgets" s' nav||eol'checkbox gt "Show _User buttons" s' buttons||eol'pg'eol'pg'eol'pg'eol
	end
	gui=gui||'ARexx gt "AWEBMODES|front|quit|close"'eol'open'
	return


initPipe:
	if Showlist('H', 'AWNPIPE') then res=open(gui_fp, 'awnpipe:awebmodes/-2010/xc', 'W')
	if res ~= 1 then do
		'request NOWAIT "Error" "To use this program you need AWNPipe:*N        (Vers 2.10 or newer)." "_OK"'
		exit 0
	end
	return


manageGUI:
	call WriteLN(gui_fp, gui)
	if small == 1 then call WriteLN(gui_fp,'id 1 tar 3 s 1')

	do while ~(EOF(gui_fp))
		call WriteLN(gui_fp, 'continue')
		gad=ReadLN(gui_fp)
		select
			when left(gad, 5) == 'arexx' then do
				parse VAR gad in1 in2 in3 .
				rxcmd=readch(gui_fp,in3)
				if in2>1 then call WriteLN(gui_fp,"rc 10")
				if in2 = 0 then do
					call WriteLN(gui_fp,'rc 0 result "AWebModes window brought to front"')
					tofront=1
				end
				if in2 = 1 then do
					call WriteLN(gui_fp,'rc 0 result "AWebModes exiting"')
					call WriteLN(gui_fp, 'close')
					call Close(gui_fp)
					exit 0
				end
				if tofront = 1 then do
					call WriteLN(gui_fp,'id 0 s 67')
					tofront=0
				end
			end
			when left(gad, 4) == 'menu' then do
				select
					when gad == 'menu 0 0 0 0' then call setenvarc()
					when gad == 'menu 0 0 1 0' then call unsetenvarc()
					when left(gad, 10) == 'menu 0 0 2' then do
						parse VAR gad . . . . small2 .
						call WriteLN(gui_fp,'id 0 read')
						windowr=ReadLN(gui_fp)
						parse VAR windowr wl wt ww wh .
						if (datatype(wt,N) &datatype(wl,N) ) then do
							savewindow='top' wt 'left' wl
						end
						call Close(gui_fp)
						call initPipe()
						call getenv()
						call init()
						call WriteLN(gui_fp, gui)
					end
					when gad == 'menu 0 4 31 0' then do
						call WriteLN(gui_fp, 'close')
						call Close(gui_fp)
						exit 0
					end
					otherwise nop
				end
			end
			when left(gad, 6) == 'gadget' then do
				parse VAR gad . gid value .
				select
					when gid == 4 then 'SetCFG HTMLMODE' value
					when gid == 5 then do
						call WriteLN(gui_fp, 'id 6 disable' 1-value 'ref 6')
						'SetCFG FRAMES' value
					end
					when gid == 6 then 'SetCFG NOMINALFRAME' value
					when gid == 7 then do
						if value == 1 then
							'PLUGIN awebgif.awebplugin STARTANIM'
						else
							'PLUGIN awebgif.awebplugin STOPANIM'
					end
					when gid == 8 then do
						call WriteLN(gui_fp, 'id 9 disable' 1-value 'ref 9')
						if fullv == 1 then call WriteLN(gui_fp, 'id 10 disable' 1-value 'ref 10')
						'SetCFG JAVASCRIPT' value
					end
					when gid == 9 then 'SetCFG JSERRORS' value
					when gid == 10 then 'SetCFG SUPPRESSBANNERS' value
					when gid == 13 then 'SetCFG ANONYMOUS' value
					when gid == 14 then 'SetCFG SPOOFAS "'spooftext.value'"'
					when gid == 15 then 'SetCFG IGNOREMIME' value
					when gid == 16 then 'SetCFG RFCCOOKIES' value
					when gid == 18 then do
						'SetCFG NAVIGATION' value
						call Delay(75)
						call WriteLN(gui_fp, 'id 0 s 3')
						if show('P','AWEBTOOLBAR') then address AWEBTOOLBAR 'front'
					end
					when gid == 19 then do
						'SetCFG SHOWBUTTONS' value
						call Delay(75)
						call WriteLN(gui_fp, 'id 0 s 3')
						if show('P','AWEBTOOLBAR') then address AWEBTOOLBAR 'front'
					end
				end
			end
			when left(gad, 3) == 'key' then do
				parse VAR gad . key .
				select
					when key == 161 then do
						call WriteLN(gui_fp, 'id 4 s 0 ref 4')
						'SetCFG HTMLMODE' 0
					end
					when key == 148 then do
						call WriteLN(gui_fp, 'id 4 s 1 ref 4')
						'SetCFG HTMLMODE' 1
					end
					when key == 179 then do
						call WriteLN(gui_fp, 'id 4 s 2 ref 4')
						'SetCFG HTMLMODE' 2
					end
					when key == 223 then 'OPEN "file:///AWebPath:Plugins/AWebModes_doc.html"'
					otherwise nop
				end
			end
			otherwise nop
		end
	end
	call Close(gui_fp)
	return


getenv:
	if Open(env, 'env:'envname, 'R') then do
		windows=ReadLN(env)
		small=ReadLN(env)
		parse var windows wl wt ww wh .
		window= 'top' wt 'left' wl
		call Close(env)
	end
	else
		window='cs'
	return


setenv:
	call WriteLN(gui_fp,'id 0 read')
	windowr=ReadLN(gui_fp)
	parse VAR windowr wl wt .
	if (datatype(wt,N) & datatype(wl,N) ) then do
		if Open(env, 'env:'envname, 'W') then do
			call WriteLN(env, windowr)
			call WriteLN(env, small)
			call Close(env)
		end
	end
	return


setenvarc:
	call setenv()
	address command 'C:Copy >NIL: QUIET "env:'envname'" "envarc:'envname'"'
	return


unsetenvarc:
	call setenv()
	if Exists('envarc:'envname) then delete('envarc:'envname)
	if Exists('env:'envname) then delete('env:'envname)
	return
