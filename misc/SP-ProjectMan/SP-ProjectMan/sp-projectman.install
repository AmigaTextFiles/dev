; $VER: SP-ProjectMan.install 1.00 (30.12.97)
; Install script for SP-ProjectMan 1.00

(complete 0)
(set ProgAssign "SP-ProjectMan")
(set ProgramAssign (cat ProgAssign ":"))
(set ProgramDir "SP-ProjectMan")
(set ProgramName "SP-ProjectMan")
(set EnvVarName "SP-ProjectMan")
(set MUIEnvDir "ENVARC:mui")

(set @language (getenv "language"))

(complete 1)
(if (= @language "français")
 (
  (set DestDirPrompt (cat "Spécifiez le répertoire où installer " ProgramName ".\nLe répertoire \"" ProgramDir "\" sera créé si nécessaire."))
  (set DestDirHelp (cat "Tous les fichiers de l'application seront copiés dans le répertoire .../" ProgramDir " ainsi créé."
			" Si le répertoire existe déja, les fichiers seront remplacés en fonction de leur numéro de version."))
  (set CopyProgramPrompt (cat "Copie du fichier \"" ProgramName "\":"))
  (set CopyProgramHelp "Il s'agit du programme exécutable.")
  (set DestDirFileMess "Le nom de répertoire spécifié est déja utilisé par un fichier.")
  (set SameVersionMess "Cette version (%ld.%ld) du programme a déjà été installée. Faut-il quand même le réinstaller?")
  (set SameVersionHelp (cat "En principe, il est inutile de réinstaller la même version du programme. "
			    "Toutefois, il se peut que suite à une manipulation erronée ou quelque autre circonstance malencontreuse,"
			    " l'un ou l'autre des fichiers ait été corrompu. Dans ce cas, une réinstallation peut etre utile."))
  (set DataDirPrompt "Spécifiez le répertoire où installer les fichiers de travail.\nLe répertoire sera créé au besoin.")
  (set DataDirHelp (cat "Ce répertoire regroupe les fichiers contenant des données modifiables par le programme. "
  			"Celà permet d'installer le programme dans un répertoire qui pourra par la suite être protégé contre l'écriture."))
 )
 (
  (set DestDirPrompt (cat "Specify the directory where to install " ProgramName ".\nThe \"" ProgramDir "\" directory will be created if needed."))
  (set DestDirHelp (cat "All the application files will be copied in the created .../" ProgramDir " directory."
			" If the directory already exists, the file will be replaced depending of their version number."))
  (set CopyProgramPrompt (cat "Copy file \"" ProgramName "\":"))
  (set CopyProgramHelp "This is the executable program file.")
  (set DestDirFileMess "The specified directory name is already used by a file.")
  (set SameVersionMess "The same version (%ld.%ld) of the program is already installed. Reinstall it anyway?")
  (set SameVersionHelp (cat "By principle, there is no need to reinstall the same version of the program."
  			    " However, it's possible that following some wrong operation or any unfortunate circumstance,"
  			    " one or another file was corrupted. In such a case, a reinstallation may be useful."))
  (set DataDirPrompt "Specify the directory where to install the work files.\nThe directory will be created if needed.")
  (set DataDirHelp (cat "This directory stores the files modifiable by the program."
  			"This makes possible to install the program in a directory that could be write protected."))
 )
)
(complete 2)

(if (exists ProgramAssign (noreq))
 (set @default-dest (pathonly (expandpath ProgramAssign)))
)

(set DestDir (askdir (prompt DestDirPrompt) (help DestDirHelp) (default @default-dest) (disk)))

(set @default-dest (tackon DestDir ProgramDir))

(if (= 1 (exists @default-dest (noreq)))
 (
  (abort DestDirFileMess)
 )
)
(complete 3)

(if (= 0 (exists @default-dest (noreq)))
 (makedir @default-dest (infos))
)

(complete 10)
(set FontDir     (tackon @default-dest "Fonts"))
(set ClassesDir  (tackon @default-dest "Classes"))
(set CatalogsDir (tackon @default-dest "Catalogs"))
(set RexxDir     (tackon @default-dest "Rexx"))
(set DataDir     (tackon @default-dest ""))
(set DocsDir     (tackon @default-dest "Docs"))

(set VersionDest   (getversion (tackon @default-dest ProgramName)))
(set VersionSource (getversion ProgramName))
(set Version  (/ VersionSource 65536))
(set Revision (- VersionSource (* Version 65536)))

(if (= VersionDest VersionSource)
 (
  (set InstallProg (askbool (prompt (SameVersionMess Version Revision)) (help SameVersionHelp)))
 )
 (set InstallProg 1)
)

(complete 20)
(if (= 1 InstallProg)
 (
(complete 21)
  (if (= VersionDest VersionSource)
   (
(complete 22)
    (copyfiles (source ProgramName) (dest @default-dest) (infos) (optional "force"))
(complete 23)
   )
   (
(complete 24)
    (copylib (source ProgramName) (dest @default-dest) (infos) (optional "askuser") (confirm) (prompt CopyProgramPrompt) (help CopyProgramHelp))
(complete 25)
   )
  )
(complete 40)
  (set VersionDest   (getversion (tackon @default-dest ProgramName)))
(complete 45)
  (if (= VersionDest VersionSource)
   (
(complete 50)
    (copyfiles (source "Fonts") (dest FontDir) (all) (infos) (fonts) (optional "force"))
(complete 60)
    (copyfiles (source "Classes") (dest ClassesDir) (all) (infos) (optional "force"))
(complete 70)
    (copyfiles (source "Catalogs") (dest CatalogsDir) (all) (infos) (optional "force"))
(complete 75)
    (copyfiles (source "Rexx") (dest RexxDir) (all) (infos) (optional "force"))
(complete 80)
    (copyfiles (source "Docs") (dest DocsDir) (all) (infos) (optional "force"))
(complete 85)
    (copyfiles (source "Env") (dest MUIEnvDir) (all) (infos) (optional "force"))
(complete 90)
    (if (NOT (exists (tackon "ENV:" EnvVarName)))
     (
(complete 90)
      (set DataDir (askdir (prompt DataDirPrompt) (help DataDirHelp) (default DataDir) (newpath)))
(complete 91)
      (if (= 0 (exists DataDir (noreq)))
       (
(complete 92)
        (makedir DataDir (infos))
(complete 93)
       )
      )
      (textfile (dest (tackon "ENV:" EnvVarName)) (append DataDir))
(complete 94)
      (textfile (dest (tackon "ENVARC:" EnvVarName)) (append DataDir))
(complete 95)
     )
    )
   )
  )
  (if (= @language "français")
   (
    (set StartupPrompt (cat "L'installation va maintenant inclure les assignations suivantes dans le fichier s:user-startup:\n\n\"assign "
                             ProgramAssign " " @default-dest "\"\n\n\"assign REXX: " RexxDir " add\""))
    (set StartupHelp (cat "Ces assignations permettent de retrouver automatiquement les fichiers associée à l'application \"" ProgramName
                          "\""))
   )
   (
    (set StartupPrompt (cat "The installer will now include the following assigns in the s:user-startup file:\n\n\"assign "
                             ProgramAssign " " @default-dest "\"\n\n\"assign REXX: " RexxDir " add\""))
    (set StartupHelp (cat "This assign enables automatic retrieval of the files assocaited to the \"" ProgramName "\ application."))
   )
  )
  (makeassign ProgAssign @default-dest)
  (makeassign "Rexx" RexxDir)
  (startup ProgramName (prompt StartupPrompt) (help StartupHelp)
   (command (cat "assign " ProgramAssign " " @default-dest "\n"))
   (command (cat "assign Rexx: " RexxDir " add"))
  )
 )
)
(complete 100)
