
(*##############*)
 MODULE TstFuncs;      (* 103095 *)
(*##############*)

(*

Created "by hand".

*)

FROM   SYSTEM IMPORT ADDRESS, ADR;

IMPORT EasyRexx, InOut, LongInOut, TagsUtils;
IMPORT AmigaDOS, Libraries, RexxErrors, Tasks, Utility;

CONST helloworld = "Hello World! :)";
      errormessage = "Error: Because you asked for it ;)";

CONST ArexxCLEAR      = 1;
      ArexxGETVAR     = 2;
      ArexxHELP       = 3;
      ArexxOPEN       = 4;
      ArexxQUIT       = 5;
      ArexxROW        = 6;
      ArexxSAVE       = 7;
      ArexxTEXT       = 8;
      ArexxRX         = 9;
      ArexxCAUSEERROR = 10;
      ArexxENDTABLE   = 11;

VAR   commandTable   :ARRAY[1..ArexxENDTABLE] OF EasyRexx.ARexxCommandTable;

TYPE  ARexxRetValues = RECORD
                         retvalue    :LONGINT;
                         retstring,
                         error       :EasyRexx.CharPointer;
                       END;

VAR   myreturn :ARexxRetValues;

TYPE  String0Pointer = POINTER TO ARRAY[0..133] OF CHAR;
      funcstype = PROCEDURE (EasyRexx.ARexxContext):INTEGER;

(*-------------------------------------------------------*)
 PROCEDURE arexxfuncCLEAR(c:EasyRexx.ARexxContext):INTEGER;
(*-------------------------------------------------------*)
BEGIN

InOut.WriteString("CLEAR");
IF EasyRexx.Arg(c, 0) # NIL THEN
   InOut.WriteString(" FORCE=on");
END;
RETURN RexxErrors.RCOk;

END arexxfuncCLEAR;

(*---------------------------------------------------------*)
 PROCEDURE arexxfuncGETVAR(c:EasyRexx.ARexxContext):INTEGER;
(*---------------------------------------------------------*)
BEGIN

InOut.WriteString("GETVAR");
IF EasyRexx.Arg(c, 0) # NIL THEN
   InOut.WriteString(" HELLOWORLD"); InOut.WriteLn;
   InOut.WriteString(" string returned");
   myreturn.retstring := ADR(helloworld);
END;
RETURN RexxErrors.RCOk;

END arexxfuncGETVAR;

(*-------------------------------------------------------*)
 PROCEDURE arexxfuncHELP(c:EasyRexx.ARexxContext):INTEGER;
(*-------------------------------------------------------*)

VAR  sp :String0Pointer;

BEGIN

InOut.WriteString("HELP");
IF EasyRexx.Arg(c, 0) # NIL THEN
   InOut.WriteString(" AMIGAGUIDE");
END;
IF EasyRexx.Arg(c, 1) # NIL THEN
   sp := String0Pointer(EasyRexx.ArgString(c,1));
   InOut.WriteString(" TOPIC="); InOut.WriteString(sp^);
END;
RETURN RexxErrors.RCOk;

END arexxfuncHELP;

(*-------------------------------------------------------*)
 PROCEDURE arexxfuncOPEN(c:EasyRexx.ARexxContext):INTEGER;
(*-------------------------------------------------------*)

VAR  sp :String0Pointer;

BEGIN

InOut.WriteString("OPEN");
IF EasyRexx.Arg(c, 1) # NIL THEN
   InOut.WriteString(" TEXT");
ELSE
   InOut.WriteString(" PROJECT");  (* defaults to PROJECT *)
END;
IF EasyRexx.Arg(c, 2) # NIL THEN
   sp := String0Pointer(EasyRexx.ArgString(c,2));
   InOut.WriteString(" '"); InOut.WriteString(sp^); InOut.Write("'");
END;
RETURN RexxErrors.RCOk;

END arexxfuncOPEN;

(*-------------------------------------------------------*)
 PROCEDURE arexxfuncQUIT(c:EasyRexx.ARexxContext):INTEGER;
(*-------------------------------------------------------*)
BEGIN

InOut.WriteString("QUIT");
RETURN -1;

END arexxfuncQUIT;

(*------------------------------------------------------*)
 PROCEDURE arexxfuncROW(c:EasyRexx.ARexxContext):INTEGER;
(*------------------------------------------------------*)

BEGIN

InOut.WriteString("ROW");
IF EasyRexx.Arg(c, 0) # NIL THEN
   InOut.Write(' '); LongInOut.WriteLongInt(EasyRexx.ArgNumber(c,0), 0);
END;

RETURN RexxErrors.RCOk;

END arexxfuncROW;

(*-------------------------------------------------------*)
 PROCEDURE arexxfuncSAVE(c:EasyRexx.ARexxContext):INTEGER;
(*-------------------------------------------------------*)

VAR  sp :String0Pointer;

BEGIN

InOut.WriteString("SAVE");
IF EasyRexx.Arg(c, 0) # NIL THEN
   InOut.WriteString(" AS");
END;
IF EasyRexx.Arg(c, 1) # NIL THEN
   sp := String0Pointer(EasyRexx.ArgString(c,1));
   InOut.WriteString(" '"); InOut.WriteString(sp^); InOut.Write("'");
END;

RETURN RexxErrors.RCOk;

END arexxfuncSAVE;

(*-------------------------------------------------------*)
 PROCEDURE arexxfuncTEXT(c:EasyRexx.ARexxContext):INTEGER;
(*-------------------------------------------------------*)

VAR  sp :String0Pointer;

BEGIN

InOut.WriteString("TEXT");
IF EasyRexx.Arg(c, 0) # NIL THEN
   sp := String0Pointer(EasyRexx.ArgString(c,0));
   InOut.WriteString(" '"); InOut.WriteString(sp^); InOut.Write("'");
END;

RETURN RexxErrors.RCOk;

END arexxfuncTEXT;

(*-----------------------------------------------------*)
 PROCEDURE arexxfuncRX(c:EasyRexx.ARexxContext):INTEGER;
(*-----------------------------------------------------*)

VAR  sp       :String0Pointer;
     taglist  :ARRAY[0..4] OF Utility.TagItem;
     liRes    :LONGINT;
     pn       :ARRAY[0..81] OF CHAR;

BEGIN

sp := String0Pointer(EasyRexx.ArgString(c,0));
InOut.WriteString( "RX '"); InOut.WriteString(sp^); InOut.Write("'");
                InOut.WriteLn;
InOut.WriteString(" Sending command asynchronously: '"); InOut.WriteString(sp^);
                EasyRexx.Portname(c, pn);
                InOut.WriteString("' to the '"); InOut.WriteString(pn); InOut.WriteString("' port");
                InOut.WriteLn;

TagsUtils.AsgTags3(taglist, EasyRexx.ERPort, ADDRESS(EasyRexx.Port(c)),
                            EasyRexx.ERContext, ADDRESS(c),
                            EasyRexx.ERAsynch, ORD(TRUE));

liRes := EasyRexx.SendARexxCommandA(EasyRexx.ArgString(c,0), ADR(taglist));

RETURN RexxErrors.RCOk;

END arexxfuncRX;

(*-------------------------------------------------------------*)
 PROCEDURE arexxfuncCAUSEERROR(c:EasyRexx.ARexxContext):INTEGER;
(*-------------------------------------------------------------*)
BEGIN

InOut.WriteString( "CAUSEERROR");
myreturn.error := ADR(errormessage);

RETURN RexxErrors.RCError;

END arexxfuncCAUSEERROR;

(*-------------------------------------------------------*)
 PROCEDURE myHandleARexx(c:EasyRexx.ARexxContext):BOOLEAN;
(*-------------------------------------------------------*)

VAR  done    :INTEGER;
     i       :INTEGER;
     ctp     :EasyRexx.ARexxCommandTablePtr;
     func    :funcstype;
     idfound :BOOLEAN;
     taglist :ARRAY[0..4] OF Utility.TagItem;

BEGIN

done := 0;

myreturn.retvalue  := RexxErrors.RCOk;
myreturn.retstring := NIL;
myreturn.error     := NIL;

IF CHAR(EasyRexx.GetARexxMsg(c)) # 0C THEN

   i := 0;

   InOut.WriteString("Received: ");

   idfound := FALSE;
   ctp := EasyRexx.Table(c, i);
   WHILE NOT idfound AND (ctp^.command # NIL) DO

      IF ctp^.id = EasyRexx.Id(c) THEN

         IF ctp^.userdata # (NIL) THEN
            func := funcstype(ctp^.userdata);
            myreturn.retvalue := LONGINT(func(c));
            done := myreturn.retvalue;
         END;
         idfound := TRUE;

      ELSE
         INC(i);
         ctp := EasyRexx.Table(c, i);
      END;

   END;

   IF myreturn.retvalue = -1D THEN
      TagsUtils.AsgTag(taglist, EasyRexx.ERReturnCode, RexxErrors.RCOk);
   ELSE
      TagsUtils.AsgTag(taglist, EasyRexx.ERReturnCode, myreturn.retvalue);
   END;
   IF myreturn.error # NIL THEN
      TagsUtils.CatTag(taglist, EasyRexx.ERErrorMessage, ADDRESS(myreturn.error));
   END;
   IF myreturn.retstring # NIL THEN
      TagsUtils.CatTag(taglist, EasyRexx.ERResultString, ADDRESS(myreturn.retstring));
   END;

   EasyRexx.ReplyARexxMsgA(c, ADR(taglist));

   InOut.WriteLn;
END;

RETURN done = -1;  (* -1 is QUIT *)

END myHandleARexx;

(*----------------------*)
 PROCEDURE InitCmdTable;
(*----------------------*)

BEGIN

WITH commandTable[ArexxCLEAR] DO         id := ArexxCLEAR;
   command := ADR("CLEAR");
   cmdtemplate := ADR("FORCE/S");
   userdata := ADR(arexxfuncCLEAR); END;
WITH  commandTable[ArexxGETVAR] DO        id := ArexxGETVAR;
   command := ADR("GETVAR");
   cmdtemplate := ADR("HELLOWORLD/S");
   userdata := ADR(arexxfuncGETVAR); END;
WITH commandTable[ArexxHELP] DO          id := ArexxHELP;
   command := ADR("HELP");
   cmdtemplate := ADR("AMIGAGUIDE/S,TOPIC/F");
   userdata := ADR(arexxfuncHELP); END;
WITH commandTable[ArexxOPEN] DO          id := ArexxOPEN;
   command := ADR("OPEN");
   cmdtemplate := ADR("PROJECT/S,TEXT/S,NAME/F");
   userdata := ADR(arexxfuncOPEN); END;
WITH commandTable[ArexxQUIT] DO          id := ArexxQUIT;
   command := ADR("QUIT");
   cmdtemplate := ADR("");
   userdata := ADR(arexxfuncQUIT); END;
WITH commandTable[ArexxROW] DO           id := ArexxROW;
   command := ADR("ROW");
   cmdtemplate := ADR("NUMBER/A/N");
   userdata := ADR(arexxfuncROW); END;
WITH commandTable[ArexxSAVE] DO          id := ArexxSAVE;
   command := ADR("SAVE");
   cmdtemplate := ADR("AS/S,NAME/F");
   userdata := ADR(arexxfuncSAVE); END;
WITH commandTable[ArexxTEXT] DO          id := ArexxTEXT;
   command := ADR("TEXT");
   cmdtemplate := ADR("TEXT/A/F");
   userdata := ADR(arexxfuncTEXT); END;
WITH commandTable[ArexxRX] DO            id := ArexxRX;
   command := ADR("RX");
   cmdtemplate := ADR("COMMAND/A/F");
   userdata := ADR(arexxfuncRX); END;
WITH commandTable[ArexxCAUSEERROR] DO    id := ArexxCAUSEERROR;
   command := ADR("CAUSEERROR");
   cmdtemplate := ADR("");
   userdata := ADR(arexxfuncCAUSEERROR);
END;

EasyRexx.TableEnd(commandTable[ArexxENDTABLE]);

END InitCmdTable;

VAR   done    :BOOLEAN;
      context :EasyRexx.ARexxContext;
      taglist :ARRAY[0..5] OF Utility.TagItem;
      signals :Tasks.SignalSet;

(*-----------*)
 BEGIN (*main*)
(*-----------*)

InitCmdTable;

done := FALSE;
context := EasyRexx.NULLARexxContext;

EasyRexx.EasyRexxBase := Libraries.OpenLibrary(ADR(EasyRexx.EasyRexxName), 2(*EasyRexx.EasyRexxVersion*));

IF (EasyRexx.EasyRexxBase # NIL) THEN

   TagsUtils.AsgTags5(taglist, EasyRexx.ERCommandTable, ADR(commandTable),
                               EasyRexx.ERAuthor,       ADR("Ketil Hunn"),
                               EasyRexx.ERCopyright,    ADR("© 1995 Ketil Hunn"),
                               EasyRexx.ERVersion,      ADR("3.0"),
                               EasyRexx.ERPortname,     ADR("EASYREXX_TEST"));

   context := EasyRexx.AllocARexxContextA(ADR(taglist));

ELSE

   InOut.WriteString("easyrexx.library not found"); InOut.WriteLn;

END;

IF context # EasyRexx.NULLARexxContext THEN

   InOut.WriteString("Welcome to a small EasyRexx demonstration"); InOut.WriteLn;
   InOut.WriteString("-----------------------------------------"); InOut.WriteLn;
   InOut.WriteString("Open another shell and start the small"); InOut.WriteLn;
   InOut.WriteString("AREXX script: rx test"); InOut.WriteLn;
   InOut.WriteString("or doubleclick on the test.rexx icon."); InOut.WriteLn;

   WHILE NOT (done AND EasyRexx.ERSafeToQuit(context)) DO
      signals := Tasks.Wait(EasyRexx.ERSignals(context) + Tasks.SignalSet{AmigaDOS.SigBreakCtrlC});
      IF AmigaDOS.SigBreakCtrlC IN signals THEN
         done := TRUE;
      END;
      EasyRexx.ERSetSignals(context, signals);

      IF (signals * EasyRexx.ERSignal(context) # Tasks.SignalSet{}) THEN
         done := myHandleARexx(context);
      END;
   END;

END;

IF EasyRexx.EasyRexxBase # NIL THEN
   EasyRexx.FreeARexxContext(context);
   Libraries.CloseLibrary(EasyRexx.EasyRexxBase^);
END;

END TstFuncs.


