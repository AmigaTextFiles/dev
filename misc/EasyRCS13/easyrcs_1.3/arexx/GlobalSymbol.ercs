/*  GlobalSymbol.ercs
 *    Yann Muller © 1996
 *    Version 1.0   ---  03/09/96
 */

OPTIONS RESULTS
OPTIONS FAILAT 11
ADDRESS EASYRCS

'askstring "Global symbol" Symbol'

if (RC == 0) then
do
    symb = RESULT

    'query showstate'
    show_old = RESULT

    'show in'
    'select top'
    DO while RC == 0
        /* symbol for each file */
        'query filename'
        file = RESULT
        address command 'rcs:rcs -N'symb':' file
        'select next'
        END

    'show out'
    'select top'
    DO while RC == 0
        /* symbol for each file */
        'query filename'
        file = RESULT
        address command 'rcs:rcsdiff -q' file '>NIL:'
        if (RC == 0) then do
            /* file must be checked in */
            'select next'
            if (RC == 0) then do
                /* this is not the last file */
                'query filename'
                file = RESULT
                'select prev'
                'checkin open'
                address command 'rcs:rcs -N'symb':' file
                'select FILE' file
                end
            else do
                /* this is the last file */
                'checkin open'
                address command 'rcs:rcs -N'symb':' file
                'select end'
                'select next'
                end
            end
        else do
            address command 'rcs:rcs -N'symb':' file
            'select next'
            end
        END

    'show' show_old
    end
