; Script d'installation pour EasyRCS
; Yann Muller

; ###### Intro ######
(message "\n\nEasyRCS"
         "\n\nAn Intuition interface for the Amiga port\nof Revision Control System (HGWRCS)\n\n"
         "\nEasyRCS : © Yann Muller\n\nHWGRCS by Heinz Wrobel" )

; ###### Destination ######
(set ProgDest "RCS:/")
(if (exists "SC:" (noreq))
    (set ProgDest "SC:c")
)

(set ProgDestDir
    (askdir
        (prompt "\nWhere do you want to copy the program? No directory will be created!\n")
        (help "You must indicate a place to copy the program!"
              "If you have SASC a good place would be \"SC:C\", otherwise the HWGRCS directory may be a good choice. Or whereever you want indeed!!!")
        (default ProgDest)
    )
)

(copyfiles
    (source "EasyRCS")
    (dest   ProgDestDir)
    (infos)
)

; ####### Project icon ######
(set IconDest ProgDestDir)
(if (exists "SC:" (noreq))
    (set IconDest "SC:starter_project")
)

(set IconDestDir
    (askdir
        (prompt "\nWhere do you want to copy the project icon?\n")
        (help "You must indicate a place to copy the project icon!"
              "If you have SASC a good place would be \"SC:starter_project\", otherwise the same directory as the program. Or whereever you want indeed!!!")
        (default IconDest)
    )
)

(set IconName "EasyRCS")
(if ( = ProgDestDir IconDestDir )
    (set IconName "Easy_RCS")
)

(copyfiles
    (source  "Easy_RCS.info")
    (dest    IconDestDir)
    (newname (cat IconName ".info"))
)

(tooltype
    (dest (tackon IconDestDir IconName))
    (setdefaulttool (tackon ProgDestDir "EasyRCS"))
    (noposition)
)

; ###### Locale ######
(set Language
    (askchoice
        (prompt  "\nSelect the language catalog you wish to install.\nEnglish is buit-in.\n")
        (help    "With the support of the locale.library EasyRCS is able to support the language of your choice (as long as the corresponding catalog as been created!).")
        (choices "English" "Français" )
        (default 0)
    )
)

(if ( > Language 0 )
(    (set ch
        (askchoice
            (prompt "\nWhere should the catalog(s) be installed?\n")
            (help "The catalogs for the localisation can be copied to the program directory or to the 'locale' directory of your system disk. It is your choice.")
            (choices "Locale:" "Program directory")
            (default 0)
        )
    )
    (debug "dest=" ch)

    (set CatDest "Locale:")
    (if ( = ch 1 )
        (set CatDest ProgDestDir)
        (if (NOT (exists (tackon ProgDestDir "Catalogs") (noreq) ))
            (makedir (tackon ProgDestDir "Catalogs"))
        )
    )

    (if ( = Language 1)
        (copyfiles
            (source "Catalogs/Français/EasyRCS.catalog")
            (dest   (tackon CatDest "Catalogs/Français") )
        )
    )
))

; ###### textField ######
(copylib
    (source "Support/TextField.gadget")
    (dest   "SYS:Classes/Gadgets")
)

; ###### EasyRexx.library ######
(copylib
    (prompt "The 'EasyRexx.library' is needed if you want to make use of the ARexx capabilities of the program.")
    (source "Support/easyrexx.library")
    (dest   "Libs:")
)

; ###### Documentation ######
(set DefDocDest ProgDestDir)
(if (exists "Docs:" (noreq))
    (set ProgDest "Docs:")
)

(set DocDestDir
    (askdir
        (prompt "\nWhere do you want to copy the manual?\n")
        (help "Specify a destination directory for the AmigaGuide manual.")
        (default DefDocDest)
    )
)

(copyfiles
    (source  "Docs/EasyRCS_E.guide")
    (dest    DocDestDir)
    (newname ("EasyRCS.guide"))
    (infos)
)


; ###### ARexx scripts ######
(set RexxDestDir
    (askdir
        (prompt "\nWhere do you want to copy the ARexx scripts?\n")
        (help "Specify the place to copy the ARexx scripts to. Rexx: is a good place.")
        (default "Rexx:")
    )
)

(copyfiles
    (source "ARexx")
    (dest   RexxDestDir)
    (all)
)

(set @default-dest "")

