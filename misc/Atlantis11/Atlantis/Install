; $VER: Atlantis Installer Script 1.0 (22.04.97)
;
; Installer script for Atlantis V1.0
;
; © 1997 Staffan Palmroos
;
(if (= @language "svenska")
  (
    (set #WelcomeStr
      (cat "Välkommen till Atlantis Installation.\n\n"
           "Jag hoppas att du får mycket nytta av Atlantis och att du registrerar Atlantis snart."))

    (set #DestinationStr "I vilken katalog skall jag skapa katalogen Atlantis?")
    (set #DestinationHelp
      (cat "Välj den befintliga katalog där en ny katalog med namnet Atlantis "
           "skall skapas. Den logiska enheten 'Atlantis:' kommer att peka på denna katalog.\n\n"))
    (set #WhichProcStr "Vilken version vill du installera?")
    (set #WhichProcHelp
      (cat "Atlantis finns i en 68020 - version som är specialkompilerad just för denna version."
           "Denna version är snabbare, men kräver naturligtvis att du har en 68020 processor"
           "eller bättre.\n\n"))
    (set #68000Str "68000-versionen")
    (set #68020Str "68020-versionen")
    (set #WhatLangStr "Vilket/Vilka språk vill du installera? Det inbyggda språket är engelska.")
    (set #WhatLangHelp
      (cat "Atlantis finns för närvarande bara på svenska och engelska, så denna fråga är kanske lite "
           "fånig. Dock hoppas jag att det kommer fler språk i framtiden, och då kommer denna fråga "
           "att innehålla fler möjliga svar.\n\n"))
    (set #IconStr
      (cat "Om ikonerna:\n\n"
           "Ikonerna i detta arkiv är ritade för MagicWB med Roman Patzners 16-färgers palett. "
           "Om du inte använder MagicWB kommer de med största sannolikhet att se hemska ut, "
           "och du kanske ritar om dem. Om du gör det, varför inte skicka mig de nya ikonerna "
           "så att jag kan inkludera dem i nästa version av Atlantis"))
    (set #DocDirStr "Var vill du ha dokumentationen?")
    (set #DocDirHelp
      (cat "Somliga väljer att samla alla guide-filer i en och samma katalog. Om du är en av dem "
           "kan du nu ange var du vill ha Atlantis.guide. Observera dock att om du väljer att ha "
           "manualen någon annanstans kanske inte Atlantis hitta den när man trycker på HELP-"
           "tangenten.\n\n"))
    (set #MCCInstallStr "Installerar Icon.mcc")
    (set #MCCInstallHelp
      (cat "Atlantis behöver den externa klassen Icon.mcc. Icon.mcc är © Russell Leighton."
           "Se dokumentationen för Icon.mcc i katalogen Docs för mer information.\n\n"))
    (set #ModifyStartupStr "Jag måste lägga in några rader i S:User-Startup")
    (set #ModifyStartupHelp
      (cat "Följande rader läggs till i din S:User-Startup:\n\n"
           ";BEGIN Atlantis\n"
           "Assign Atlantis: %s\n"
           ";END Atlantis\n\n"))
  )
  (
    (set #WelcomeStr
      (cat "Welcome to the Atlantis Installation.\n\n"
           "I hope that Atlantis will give you what you want and that you register Atlantis soon."))
    (set #DestinationStr "In which drawer shall I create the Atlantis drawer?")
    (set #DestinationHelp
      (cat "Pick a drawer where the program drawer shall be created. An assign named 'Atlantis:'"
           "will be pointed to this drawer.\n\n"))
    (set #WhichProcStr "Which version do you want to install?")
    (set #WhichProcHelp
      (cat "Included in the archive is a special 68020 version of Atlantis. This version is"
           "faster, but does of course require that you have a 68020 processor or better.\n\n"))
    (set #68000Str "The 68000 version")
    (set #68020Str "The 68020 version")
    (set #WhatLangStr "Which language(s) do you want to install? The built-in language is English.")
    (set #WhatLangHelp
      (cat "Currently Atlantis is only available in Swedish and English, so this question seems quite "
           "redundant. In the future though I hope there will be more languages to choose from.\n\n"))
    (set #IconStr
      (cat "About the Icons:\n\n"
           "The icons in this package is drawn for MagicWB using Roman Patzners 16 color "
           "palette. If you are using a different desktop they will most likely look "
           "terrible. In that case you will probably want to redraw them for your desktop. "
           "If you do, why not send them to me so I can include them in the next version "
           "of Atlantis?"))
    (set #DocDirStr "Where do you want the documentation?")
    (set #DocDirHelp
      (cat "Some people collect all guides at the same place. If you are one of them, "
           "select the directory where the guide belongs. Please note that if you do "
           "put it somewhere else Atlantis might not find it when you press the HELP "
           "key in the program.\n\n"))
    (set #MCCInstallStr "Installing Icon.mcc")
    (set #MCCInstallHelp
      (cat "Atlantis needs the MUI custom class Icon.mcc. Icon.mcc is © Russell Leighton. See "
           "the Icon.mcc docs in the Docs directory for more information.\n\n"))
    (set #ModifyStartupStr "I have to add a few lines to S:User-Startup")
    (set #ModifyStartupHelp
      (cat "The following lines is added to your S:User-Startup:\n\n"
           ";BEGIN Atlantis\n"
           "Assign Atlantis: %s\n"
           ";END Atlantis\n\n"))
  )
)

(welcome #WelcomeStr)

(complete 0)

;
; NORMAL: Fixa en katalog
;
(if (> @user-level 0)
  (set #AtlantisHome
    (askdir
      (help #DestinationHelp @askdir-help)
      (prompt #DestinationStr)
      (default "Work:Graphics")
    )
  )
  (set #AtlantisHome "Work:Graphics")
)

(set #AtlantisHome (tackon #AtlantisHome 'Atlantis'))
(set @default-dest #AtlantisHome)
;
; Skapa en hemkatalog
;
(makedir #AtlantisHome (infos))

;
; Ta reda på vilken processor som används
;
(set #FastProcessor
  (not
    (patmatch "68000|68010"
      (database "cpu")
    )
  )
)

;
; EXPERT: Vilken processor?
;
(complete 1)
(if (> @user-level 1)
  (set #WantProcVer
    (askchoice
      (prompt #WhichProcStr)
      (help #WhichProcHelp @askchoice-help)
      (choices #68000Str #68020Str)
    )
  )
  (set #WantProcVer #FastProcessor)
)

;
; NORMAL: Vilket/vilka språk?
;
(complete 2)
(if (> @user-level 0)
  (set #Langs
    (askoptions
      (prompt #WhatLangStr)
      (help #WhatLangHelp @askoptions-help)
      (choices "Svenska")
      (default 1)
    )
  )
  (set #Langs -1) ; Alla språk
)

;
; EXPERT: Dokumentationen vart?
;
(complete 3)
(if (> @user-level 1)
  (set #DocDir
    (askdir
      (help #DocDirHelp @askdir-help)
      (prompt #DocDirStr)
      (default #AtlantisHome)
    )
  )
  (set #DocDir #AtlantisHome)
)

;
; Om ikonerna...
;
(complete 4)
(message #IconStr (all))

;
; Kopierar Binärerna
;
(complete 10)
(copyfiles
  (prompt "")
  (help @copyfiles-help)
  (source
    (if (= 1 #WantProcVer)
        'Atlantis.020'
        'Atlantis'))
  (dest #AtlantisHome)
  (newname "Atlantis")
  (infos)
)

;
; Kopiera catalogs
;
(complete 20)
(if (> #Langs 0)
  (
    (makedir (tackon #AtlantisHome "Catalogs"))
    (if (in #Langs 0)
      (copyfiles
        (prompt "")
        (help "")
        (source "Catalogs/svenska")
        (dest (tackon #AtlantisHome "Catalogs/svenska"))
        (all)
      )
    )
  )
)

;
; Kopiera dokumentationen
;
(complete 30)
(copyfiles
  (prompt "")
  (help @copyfiles-help)
  (source "Atlantis.guide")
  (dest #DocDir)
  (infos)
)

;
; Kopiera 'Docs'
;
(complete 40)
(copyfiles
  (prompt "")
  (help @copyfiles-help)
  (source 'Docs')
  (dest (tackon #AtlantisHome "Docs"))
  (all)
  (infos)
)

;
; Kopiera 'Examples'
;
(complete 50)
(copyfiles
  (prompt "")
  (help @copyfiles-help)
  (source 'Examples')
  (dest (tackon #AtlantisHome "Examples"))
  (all)
  (infos)
)

;
; Kopiera 'Graphics'
;
(complete 60)
(copyfiles
  (prompt "")
  (help @copyfiles-help)
  (source 'Graphics')
  (dest (tackon #AtlantisHome "Graphics"))
  (all)
  (infos)
)

;
; Kopiera 'Rexx'
;
(complete 70)
(copyfiles
  (prompt "")
  (help @copyfiles-help)
  (source 'Rexx')
  (dest (tackon #AtlantisHome "Rexx"))
  (all)
  (infos)
)

;
; Lägg in Icon.mcc
;
(complete 80)
(copylib
  (prompt #MCCInstallStr)
  (help #MCCInstallHelp @copylib-help)
  (confirm)
  (source "MUI/Icon.mcc")
  (dest "Libs:MUI")
)

;
; Lägg in assign i s:user-startup
;
(complete 90)
(set #ModifyStartupHelp (#ModifyStartupHelp #AtlantisHome))
(startup "Atlantis"
  (prompt #ModifyStartupStr)
  (help #ModifyStartupHelp @startup-help)
  (command "Assign Atlantis: " #AtlantisHome)
)

(makeassign 'Atlantis' #AtlantisHome)

(complete 100)



