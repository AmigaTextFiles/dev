*************
* Deck "DirDeck"
*************

*************
* Card(s) in deck.
*  Card "DirUte"
*************
* 1 Card(s), 1 were printed.
*************

*************
* Natural order of Cards
*  Card "DirUte"
*************

*************
* There are no Global routines in this deck.
*************

*************
* Card "DirUte"
	Routine "ShowSys"
		WorkWithDocument "System"
		Clear DOCUMENT 
		If ARG1 = "P"
			InsertDeviceList PHYSICAL 
		ElseIf ARG1 = "A"
			InsertDeviceList ASSIGNS 
		ElseIf ARG1 = "L"
			InsertDeviceList LOGICAL 
		EndIf
		MoveCursorTo ENDOF DOCUMENT 
		Delete CHARACTER ,-1
		SortDocument
		MoveCursorTo STARTOF DOCUMENT 
	EndScript
	Routine "CurrentSide"
		SetPen ARG1
		DrawRectangle 5,22,243,161
		DrawRectangle 6,23,241,159
		DrawRectangle 7,24,239,157
		SetPen ARG2
		DrawRectangle 391,22,243,161
		DrawRectangle 392,23,241,159
		DrawRectangle 393,24,239,157
	EndScript
	AfterAttachment
		Let LeftDir = "SYS:"
		Let Current = "L"
		Let sys = 0
		SetPen 2
		PrintText "PHYSICAL",290,84
		Do "ShowSys","P"
		Do "ShowDir",LeftDir
		Do "CurrentSide",1,0
	EndScript
	Routine "ShowDir"
		SetCurrentDirectory ARG1
		IfError
			SetWindowTitle "Error accessing Directory..."
			Delay 0,1,0
			SetWindowTitle "Directory Utility..."
			ExitScript
		EndIf
		MakeDocument "Work1"
		MakeDocument "Work2"
		WorkWithDocument "Work1"
		InsertDirectoryList DIRECTORIESONLY 
		SortDocument
		MoveCursorTo STARTOF DOCUMENT 
		If TrimString(TheLine) = ""
			Delete LINE 
		EndIf
		Let L = LinesInDocument
		If SizeOfDocument > 0
			Let x = 0
			Loop
				Let x = x + 1
				PositionOnLine x
				MoveCursorTo STARTOF LINE 
				Type "[Dir] "
			Until x = L
			MoveCursorTo ENDOF DOCUMENT 
			NewLine
		EndIf
		WorkWithDocument "Work2"
		InsertDirectoryList FILESONLY 
		SortDocument
		MoveCursorTo STARTOF DOCUMENT 
		If TrimString(TheLine) = ""
			Delete LINE 
		EndIf
		WorkWithDocument "Work1"
		InsertDocument "Work2"
		Flush "Work2"
		If Current = "L"
			WorkWithDocument "Left"
			Let LeftDir = TheCurrentDirectory
			If UpperCase(GetWord(LeftDir,1,":")) = "RAM DISK"
				Let LeftDir = RemoveChars(LeftDir,4,5)
			EndIf
			SetText "LeftDir",LeftDir
		ElseIf Current = "R"
			WorkWithDocument "Right"
			Let RightDir = TheCurrentDirectory
			If UpperCase(GetWord(RightDir,1,":")) = "RAM DISK"
				Let RightDir = RemoveChars(RightDir,4,5)
			EndIf
			SetText "RightDir",RightDir
		EndIf
		Clear DOCUMENT 
		InsertDocument "Work1"
		MoveCursorTo STARTOF DOCUMENT 
		Flush "Work1"
	EndScript
	Window "UserWindow"
		Definition
			Origin 0,0
			Size 640,256
			Title "Directory Utility..."
			NumberOfColors 4
			DefaultColors 0,1,0
			WindowObjects CLOSEBUTTON DEPTHBUTTONS DRAGBAR 
			WindowFlags ACTIVATE TOFRONT WORKBENCH 
			VisualEffects NONE ,WAIT 
		EndScript
		OnCloseButton
			Quit
		EndScript
	EndObject
	List "LeftSide"
		Definition
			Origin 14,27
			Size 228,152
			Font "topaz",8 ; FontName, PointSize
			Style PLAIN ,2,3
			TextColors 1,0,JAM2  ; PenA, PenB, DrawMode
			Border DOUBLEBEVEL ,2,1 ; BorderStyle, MainPen, ExtraPen
			ListDocument "Left" ; where the text comes from
		EndScript
		OnClick
			WorkWithDocument "Left"
			Do "CurrentSide",1,0
			Let Current = "L"
		EndScript
		OnDoubleClick
			WorkWithDocument "Left"
			Do "CurrentSide",1,0
			Let Current = "L"
			If GetChars(TheLine,1,5) = "[Dir]"
				Let Dir = GetWord(TheLine,2)
				SetCurrentDirectory LeftDir||Dir
				Let LeftDir = TheCurrentDirectory
				Do "ShowDir",LeftDir
			EndIf
		EndScript
	EndObject
	TextField "LeftDir"
		Definition
			Origin 7,13
			Size 239,8
			Justification LEFT 
			MaxFieldLength 32
			InitialText ""
			Border DOUBLEBEVEL ,2,1 ; BorderStyle, MainPen, ExtraPen
		EndScript
	EndObject
	TextField "RightDir"
		Definition
			Origin 393,13
			Size 239,8
			Justification LEFT 
			MaxFieldLength 32
			InitialText ""
			Border DOUBLEBEVEL ,2,1 ; BorderStyle, MainPen, ExtraPen
		EndScript
	EndObject
	List "SystemDoc"
		Definition
			Origin 253,13
			Size 132,64
			Font "topaz",8 ; FontName, PointSize
			Style PLAIN ,2,3
			TextColors 1,0,JAM2  ; PenA, PenB, DrawMode
			Border DOUBLEBEVEL ,2,1 ; BorderStyle, MainPen, ExtraPen
			ListDocument "System" ; where the text comes from
		EndScript
		OnRelease
			WorkWithDocument "System"
			Let Dir = TrimString(TheLine)||":"
			If UpperCase(Dir) = "RAM DISK:"
				Let Dir = "RAM:"
			EndIf
			Do "ShowDir",Dir
		EndScript
	EndObject
	AreaButton "Sys"
		Definition
			Origin 254,80
			Size 130,15
			Border ROLLO ,2,1 ; BorderStyle, MainPen, ExtraPen
			Highlight COMPLEMENT 
			ButtonFlags NONE 
		EndScript
		OnRelease
			SetDrawMode JAM2 
			SetPen 2,0
			If sys = 0
				Let sys = 1
				PrintText "ASSIGNS ",290,84
				Do "ShowSys","A"
			ElseIf sys = 1
				Let sys = 2
				PrintText "LOGICAL ",290,84
				Do "ShowSys","L"
			ElseIf sys = 2
				Let sys = 0
				PrintText "PHYSICAL",290,84
				Do "ShowSys","P"
			EndIf
		EndScript
	EndObject
	List "RightSide"
		Definition
			Origin 399,27
			Size 228,152
			Font "topaz",8 ; FontName, PointSize
			Style PLAIN ,2,3
			TextColors 1,0,JAM2  ; PenA, PenB, DrawMode
			Border DOUBLEBEVEL ,2,1 ; BorderStyle, MainPen, ExtraPen
			ListDocument "Right" ; where the text comes from
		EndScript
		OnClick
			WorkWithDocument "Right"
			Do "CurrentSide",0,1
			Let Current = "R"
		EndScript
		OnDoubleClick
			WorkWithDocument "Right"
			Do "CurrentSide",0,1
			Let Current = "R"
			If GetChars(TheLine,1,5) = "[Dir]"
				Let Dir = GetWord(TheLine,2)
				SetCurrentDirectory RightDir||Dir
				Let RightDir = TheCurrentDirectory
				Do "ShowDir",RightDir
			EndIf
		EndScript
	EndObject
	TextButton "Parent"
		Definition
			Origin 270,99
			Font "topaz",8 ; FontName, PointSize
			Style PLAIN ,2,3
			TextColors 1,0,JAM1  ; PenA, PenB, DrawMode
			Text "   Parent   "
			Border SHADOW ,2,1 ; BorderStyle, MainPen, ExtraPen
			Highlight COMPLEMENT 
			ButtonFlags NONE 
		EndScript
		OnRelease
			If Current = "L"
				Do "ShowDir",ParentOf(LeftDir)
			ElseIf Current = "R"
				Do "ShowDir",ParentOf(RightDir)
			EndIf
		EndScript
	EndObject
	TextButton "Copy"
		Definition
			Origin 270,119
			Font "topaz",8 ; FontName, PointSize
			Style PLAIN ,2,3
			TextColors 1,0,JAM1  ; PenA, PenB, DrawMode
			Text "    Copy    "
			Border SHADOW ,2,1 ; BorderStyle, MainPen, ExtraPen
			Highlight COMPLEMENT 
			ButtonFlags NONE 
		EndScript
		OnRelease
			If Current = "L"
				WorkWithDocument "Left"
				Let ToCopy = TrimString(TheLine)
				If GetChars(ToCopy,1,5) = "[Dir]"
					Let ToCopy = RemoveChars(ToCopy,1,6)
					Dos "c:copy"|||LeftDir||ToCopy|||RightDir||ToCopy|||"ALL"
				Else
					Dos "c:copy"|||LeftDir||ToCopy|||RightDir
				EndIf
				Let Current = "R"
				Do "ShowDir",RightDir
				Let Current = "L"
				SetCurrentDirectory LeftDir
			ElseIf Current = "R"
				WorkWithDocument "Right"
				Let ToCopy = TrimString(TheLine)
				If GetChars(ToCopy,1,5) = "[Dir]"
					Let ToCopy = RemoveChars(ToCopy,1,6)
					Dos "c:copy"|||RightDir||ToCopy|||LeftDir||ToCopy|||"ALL"
				Else
					Dos "c:copy"|||RightDir||ToCopy|||LeftDir
				EndIf
				Let Current = "L"
				Do "ShowDir",LeftDir
				Let Current = "R"
				SetCurrentDirectory RightDir
			EndIf
		EndScript
	EndObject
	TextButton "Delete"
		Definition
			Origin 270,139
			Font "topaz",8 ; FontName, PointSize
			Style PLAIN ,2,3
			TextColors 1,0,JAM1  ; PenA, PenB, DrawMode
			Text "   Delete   "
			Border SHADOW ,2,1 ; BorderStyle, MainPen, ExtraPen
			Highlight COMPLEMENT 
			ButtonFlags NONE 
		EndScript
		OnRelease
			If Current = "L"
				WorkWithDocument "Left"
				Let ToDelete = TrimString(TheLine)
				If GetChars(ToDelete,1,5) = "[Dir]"
					Let ToDelete = RemoveChars(ToDelete,1,6)
					Dos "c:delete"|||LeftDir||ToDelete|||"ALL"
				Else
					Dos "c:delete"|||LeftDir||ToDelete
				EndIf
				Do "ShowDir",LeftDir
			ElseIf Current = "R"
				WorkWithDocument "Right"
				Let ToDelete = TrimString(TheLine)
				If GetChars(ToDelete,1,5) = "[Dir]"
					Let ToDelete = RemoveChars(ToDelete,1,6)
					Dos "c:delete"|||RightDir||ToDelete|||"ALL"
				Else
					Dos "c:delete"|||RightDir||ToDelete
				EndIf
				Do "ShowDir",RightDir
			EndIf
		EndScript
	EndObject
* End of Card "DirUte"
*************

