;
; $VER: CyberAVI installation 1.2 (28.9.97)
;
; Copyright (c) 1996 Thore Böckelmann
;

;****************************************************************************
;
; English texts
;
(if (not (= @language "deutsch"))
  (
    (set #bad-kick "You must have AmigaOS 3.0 (V39) or newer to be able to use CyberAVI.")
    (set #bad-cpu "You must have at least an MC68020 to use CyberAVI.")
    (set #selectdir "Please select where to install CyberAVI\nA directory 'CyberAVI' will be created automatically")
    (set #selectlibsdir "Please select where to install necessary libraries")
    (set #makedircyberavi "Creating directory 'CyberAVI'")
    (set #copymainprg "Copying CyberAVI...")
    (set #copydocs "Copying documentation...")
    (set #copylibs "Copying asyncio.library...")
    (set #installgui "\n\nDo you want to install the Argue interface for CyberAVI?\n(You need to have Argue installed!)")
    (set #installguihelp "You will have to unpack Argue14.lha and install it to use this graphical user interface.\n\nArgue is ©1996 by Thorsten Stocksmeier")
    (set #copyargue "Copying interface for CyberAVI...")
    (set #selectcmaps "Please choose the color presets you wish to install")
    (set #copycmaps "Copying color presets...")
  )
)

;****************************************************************************
;
; Deutsche Texte
;
(if (= @language "deutsch")
  (
    (set #bad-kick "Sie müssen AmigaOS 3.0 (V39) oder höher installieren, um CyberAVI zu benutzen.")
    (set #bad-cpu "Sie brauchen mindestens einen MC68020, um CyberAVI zu benutzen.")
    (set #selectdir "Wohin soll CyberAVI installiert werden?\nEin Verzeichnis 'CyberAVI' wird automatisch erstellt")
    (set #selectlibsdir "Wohin sollten die benötigten Bibliotheken installiert werden?")
    (set #makedircyberavi "Erstelle Verzeichnis 'CyberAVI'")
    (set #copymainprg "Kopiere CyberAVI...")
    (set #copydocs "Kopiere Anleitung...")
    (set #copylibs "Kopiere asyncio.library...")
    (set #installgui "\n\nWollen Sie die Argue-Oberfläche für CyberAVI installieren?\n(Sie müssen dafür Argue installiert haben!)")
    (set #installguihelp "Sie müssen Argue14.lha entpacken und installieren um die Oberfläche benutzen zu können.\n\nArgue ist ©1996 by Thorsten Stocksmeier")
    (set #copyargue "Kopiere Oberfläche für CyberAVI...")
    (set #selectcmaps "Bitte wählen Sie die Farbvorlagen, die Sie installieren wollen")
    (set #copycmaps "Kopiere Farbvorlagen...")
  )
)

;****************************************************************************
;
; Setup
;

(complete 0)

(if (< (/ (getversion) 65536) 39)
  (abort #bad-kick)
)

(if (patmatch "68000|68010" (database "cpu"))
  (abort #bad-cpu)
)

(set @default-dest "SYS:")
(set @libs-dest "LIBS:")

;****************************************************************************
;
; Welcome
;

(welcome)

(complete 10)

;****************************************************************************
;
; Setup directory
;

(set #destdir
  (askdir
    (prompt #selectdir)
    (help @askdir-help)
    (default @default-dest)
    (newpath)
  )
)

(set #libsdir
  (askdir
    (prompt #selectlibsdir)
    (help @askdir-help)
    (default @libs-dest)
  )
)

(set @default-dest (tackon #destdir "CyberAVI"))
(set @libs-dest #libsdir)

(complete 20)

(makedir @default-dest
  (prompt #makedircyberavi)
  (help @makedir-help)
  (infos)
)

(complete 40)

;****************************************************************************
;
; Copy main program
;

(copyfiles
  (prompt #copymainprg)
  (help @copyfiles-help)
  (source "CyberAVI")
  (dest @default-dest)
  (infos)
  (confirm)
)

(complete 60)

;****************************************************************************
;
; Copy documentation
;

(copyfiles
  (prompt #copydocs)
  (help @copyfiles-help)
  (source "")
  (pattern "#?.(guide|readme)")
  (dest @default-dest)
  (infos)
  (confirm)
)

(complete 80)

;****************************************************************************
;
; Install libraries
;

(copylib
  (help @copylib-help)
  (prompt #copylibs)
  (source "asyncio.library")
  (dest @libs-dest)
  (confirm)
)

;****************************************************************************
;
; Install GUI
;

(if (askbool
      (prompt #installgui)
      (help #installguihelp)
    )
  (

    (copyfiles
      (prompt #copyargue)
      (help @copyfiles-help)
      (source "CyberAVI GUI")
      (dest @default-dest)
      (infos)
      (confirm)
    )

    (tooltype
      (dest (tackon @default-dest "CyberAVI GUI"))
      (settooltype "COMMAND" (tackon @default-dest "CyberAVI"))
    )
  )
)

(copyfiles
  (prompt #selectcmaps)
  (help @copyfiles-help)
  (source "")
  (pattern "#?.pal")
  (dest @default-dest)
  (infos)
  (confirm)
)

;****************************************************************************
;
; Exit
;

(complete 100)
(exit)

