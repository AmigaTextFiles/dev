;
; $VER: CyberQT installation 1.2 (28.9.97)
;
; Copyright (c) 1996 Thore Böckelmann
;

;****************************************************************************
;
; English texts
;
(if (not (= @language "deutsch"))
  (
    (set #bad-kick "You must have AmigaOS 3.0 (V39) or newer to be able to use CyberQT.")
    (set #bad-cpu "You must have at least an MC68020 to use CyberQT.")
    (set #selectdir "Please select where to install CyberQT\nA directory 'CyberQT' will be created automatically")
    (set #selectlibsdir "Please select where to install necessary libraries")
    (set #makedircyberqt "Creating directory 'CyberQT'")
    (set #copymainprg "Copying CyberQT...")
    (set #copydocs "Copying documentation...")
    (set #copyasynciolib "Copying asyncio.library...")
    (set #copygarbagelib "Copying garbagecollector.library...")
    (set #installgui "\n\nDo you want to install the Argue interface for CyberQT?\n(You need to have Argue installed!)")
    (set #installguihelp "You will have to unpack Argue14.lha and install it to use this graphical user interface.\n\nArgue is ©1996 by Thorsten Stocksmeier")
    (set #copyargue "Copying interface for CyberQT...")
    (set #selectcmaps "Please choose the color presets you wish to install")
    (set #copycmaps "Copying color presets...")
  )
)

;****************************************************************************
;
; Deutsche Texte
;
(if (= @language "deutsch")
  (
    (set #bad-kick "Sie müssen AmigaOS 3.0 (V39) oder höher installieren, um CyberQT zu benutzen.")
    (set #bad-cpu "Sie brauchen mindestens einen MC68020, um CyberQT zu benutzen.")
    (set #selectdir "Wohin soll CyberQT installiert werden?\nEin Verzeichnis 'CyberQT' wird automatisch erstellt")
    (set #selectlibsdir "Wohin sollten die benötigten Bibliotheken installiert werden?")
    (set #makedircyberqt "Erstelle Verzeichnis 'CyberQT'")
    (set #copymainprg "Kopiere CyberQT...")
    (set #copydocs "Kopiere Anleitung...")
    (set #copyasynciolib "Kopiere asyncio.library...")
    (set #copygarbagelib "Kopiere garbagecollector.library...")
    (set #installgui "\n\nWollen Sie die Argue-Oberfläche für CyberQT installieren?\n(Sie müssen dafür Argue installiert haben!)")
    (set #installguihelp "Sie müssen Argue14.lha entpacken und installieren um die Oberfläche benutzen zu können.\n\nArgue ist ©1996 by Thorsten Stocksmeier")
    (set #copyargue "Kopiere Oberfläche für CyberQT...")
    (set #selectcmaps "Bitte wählen Sie die Farbvorlagen, die Sie installieren wollen")
    (set #copycmaps "Kopiere Farbvorlagen...")
  )
)

;****************************************************************************
;
; Setup
;

(complete 0)

(if (< (/ (getversion) 65536) 39)
  (abort #bad-kick)
)

(if (patmatch "68000|68010" (database "cpu"))
  (abort #bad-cpu)
)

(set @default-dest "SYS:")
(set @libs-dest "LIBS:")

;****************************************************************************
;
; Welcome
;

(welcome)

(complete 10)

;****************************************************************************
;
; Setup directory
;

(set #destdir
  (askdir
    (prompt #selectdir)
    (help @askdir-help)
    (default @default-dest)
    (newpath)
  )
)

(set #libsdir
  (askdir
    (prompt #selectlibsdir)
    (help @askdir-help)
    (default @libs-dest)
  )
)

(set @default-dest (tackon #destdir "CyberQT"))
(set @libs-dest #libsdir)

(complete 20)

(makedir @default-dest
  (prompt #makedircyberqt)
  (help @makedir-help)
  (infos)
)

(complete 40)

;****************************************************************************
;
; Copy main program
;

(copyfiles
  (prompt #copymainprg)
  (help @copyfiles-help)
  (source "CyberQT")
  (dest @default-dest)
  (infos)
  (confirm)
)

(complete 60)

;****************************************************************************
;
; Copy documentation
;

(copyfiles
  (prompt #copydocs)
  (help @copyfiles-help)
  (source "")
  (pattern "#?.(guide|readme)")
  (dest @default-dest)
  (infos)
  (confirm)
)

(complete 80)

;****************************************************************************
;
; Install libraries
;

(copylib
  (help @copylib-help)
  (prompt #copyasynciolib)
  (source "asyncio.library")
  (dest @libs-dest)
  (confirm)
)

(copylib
  (help @copylib-help)
  (prompt #copygarbagelib)
  (source "garbagecollector.library")
  (dest @libs-dest)
  (confirm)
)

;****************************************************************************
;
; Install GUI
;

(if (askbool
      (prompt #installgui)
      (help #installguihelp)
    )
  (

    (copyfiles
      (prompt #copyargue)
      (help @copyfiles-help)
      (source "CyberQT GUI")
      (dest @default-dest)
      (infos)
      (confirm)
    )

    (tooltype
      (dest (tackon @default-dest "CyberQT GUI"))
      (settooltype "COMMAND" (tackon @default-dest "CyberQT"))
    )
  )
)

(copyfiles
  (prompt #selectcmaps)
  (help @copyfiles-help)
  (source "")
  (pattern "#?.pal")
  (dest @default-dest)
  (infos)
  (confirm)
)

;****************************************************************************
;
; Exit
;

(complete 100)
(exit)

