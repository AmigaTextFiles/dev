/* collect-log */

PARSE ARG ProjektPfad Dateien

IF ~SHOW( "library", "rexxarplib.library") THEN
  ADDLIB("rexxarplib.library",0,-30,0)

Repository = GETENV( "CVSROOT")


i = POS( "/", ProjektPfad)
IF i = 0 THEN
  Projekt = SUBSTR( ProjektPfad, 2)
ELSE
  Projekt = SUBSTR( ProjektPfad, 2, i-2)

ProjektLog = Repository||"/CVSlogfiles/"||Projekt||".log"


SAY( "Maintaining log file for project '"||Projekt||"'")
IF ~EXISTS( ProjektLog) THEN
  DO
    SAY( "Create new log file '"||ProjektLog||"'")
    OPEN( LogFile, ProjektLog, "Write")
    WRITELN( LogFile, "")
    WRITELN( LogFile, "CVS log file for project: "||Projekt)
    WRITELN( LogFile, "")
    CLOSE( LogFile)
  END


DateTime = "Date: "||DATE()||", "||TIME()

OPEN( LogFile, ProjektLog, "Append")

WRITELN( LogFile, "")
WRITELN( LogFile, "=====================================================================")
WRITELN( LogFile, "")
WRITELN( LogFile, DateTime)

DO WHILE ~EOF( STDIN)
  Line = READLN( STDIN)
  WRITELN( LogFile, Line)
END

WRITELN( LogFile, "")
CLOSE( LogFile)
