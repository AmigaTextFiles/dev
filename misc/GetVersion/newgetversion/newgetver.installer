; *******************************************************
; *******************************************************
;
; newgetversion ()
;
; (C) 1997 by Andreas R. Kleinert (Andreas_Kleinert@t-online.de)
; Freeware. Feel free to add it to your own installer scripts!
;
; INPUT:  #NGVER_NAME
;         (file to check)
; OUTPUT: #NGVER              #NGREV
;         (version string)    (revision string)
;
; Installer procedure to replace the (buggy)
; internal getversion() function by calling
; "C:Version" and parsing the resulting
; version information.
;
; V1.1: - calling C:Version now with "FILE" parameter
;
; V1.0: - first release
;
; *******************************************************
;
(procedure newgetversion

 (set #NGVER "")
 (set #NGREV "")

 (run "C:Version "#NGVER_NAME" >ENV:ngver_verstring FILE")
 (set #ngver_verstring (getenv "ENV:ngver_verstring"))
 (delete "ENV:ngver_verstring")

 (set #ngver_t1 #ngver_verstring)
 (until (= (strlen #ngver_t1) 0)
  (
   (set #ngver_cmp (substr #ngver_t1 0 1))
   (if (= #ngver_cmp " ")
    (
     (set #ngver_t1 (substr #ngver_t1 1))
     (set #ngver_t2 #ngver_t1)
     (set #ngver_cmp (substr #ngver_t2 0 1))
     (until (= #ngver_cmp ".")
      (
       (set #NGVER (cat #NGVER #ngver_cmp))
       (set #ngver_t2  (substr #ngver_t2 1))
       (set #ngver_cmp (substr #ngver_t2 0 1))
      )
     )
     (set #NGREV (substr #ngver_t2 1))
    )
   )
   (set #ngver_t1 (substr #ngver_t1 1))
  )
 )
)
; *******************************************************
; *******************************************************



; *** Example: How to use newgetversion() to check the
;              version of a given file

(set #NGVER_NAME "C:Dir")
(newgetversion)

(textfile (dest "RAM:Dir_Ver") (append #NGVER))
(textfile (dest "RAM:Dir_Rev") (append #NGREV))
