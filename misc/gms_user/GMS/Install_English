; $VER: GMS V2.1 Installer 27.2.98
; (Users archive)
; Written by Michael Praschl

(complete 0)

;*** Strings festlegen ***

(set #copy-gms-help
  (cat "GMS will now be installed. All files will be copied to the directory "
       "you selected, including libraries and fonts, so it's very easy to uninstall GMS!  "
       "Press the Yes button to continue the installation."
  )
)

(set #startup-text
  (cat "The assign GMS: will now be added to s:user-startup\n"
       "This assign is necessary for all GMS programs!"
  )
)

(set #select-text "Select Yes or No")
(set #addassign "Adding the assigns to s:user-startup")
(set #whereinst "Where should the Games Master System be installed?\n(A drawer called GMS will be created.)")
(set #whereinst-help "You can select a directory for the GMS files to be copied to.")
(set #demos-text "Do you want to install the demos?")
(set #demos-help "The Demos are not needed by GMS and only show the features. Many of them are very nice, so INSTALL THEM!!!")

(welcome "Welcome to the installation of " @app-name)

;***
(procedure P_CopyFiles
  (copyfiles (dest "GMS:") (source "") (choices "Demos"))
  (copyfiles (dest "GMS:") (source "")
    (choices "Utils" "Components" "System" "Prefs" "libs" "Games" "Fonts" "C" "Temp")
  )
  (copyfiles (dest "GMS:") (source "Demos.info"))
  (copyfiles (dest "GMS:") (source "Games.info"))
  (copyfiles (dest "GMS:") (source "IMPORTANT.TXT"))
  (copyfiles (dest "GMS:") (source "IMPORTANT.TXT.info"))
  (copyfiles (dest "GMS:") (source "README.TXT"))
  (copyfiles (dest "GMS:") (source "README.TXT.info"))
  (copyfiles (dest "GMS:") (source "REGISTER.TXT"))
  (copyfiles (dest "GMS:") (source "REGISTER.TXT.info"))
  (copyfiles (dest "GMS:") (source "Utils.info"))
)

;***
(if (= (exists "GMS:" (noreq)) 2)
  ( ; then
    (set #dest-dir "GMS:")

    (if (= (askbool (prompt #copy-gms-help)
             (help #select-text)
             (default 1)) 1 )
      ( ; then
        (P_CopyFiles)
        (complete 30)
      )
    )
    (set #maindir "GMS:")
    (startup "GMS"
      (prompt #addassign)
      (help #addassign)
      (command (cat "Assign GMS: " #maindir))
      (command "\nAssign C: GMS:c ADD")
    )
  ) ( ;else

    (set #maindir
      (tackon
        (askdir
          (prompt #whereinst)
          (help #whereinst-help)
          (default "Work:")
          (disk)
        ) "GMS"
      )
    )
    (makedir #maindir)
    (makeassign "GMS" #maindir)
    (if (= (askbool (prompt #copy-gms-help)
             (help #select-text)
             (default 1)) 1 )
      ( ; then
        (P_CopyFiles)
        (complete 50)
      )
    )
    (startup "GMS"
      (prompt #addassign)
      (help #addassign)
      (command (cat "Assign GMS: " #maindir))
      (command "\nAssign C: GMS:c ADD")
    )
  )
)

(complete 100)

(set @default-dest #maindir)

