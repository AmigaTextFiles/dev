.key FDDIR/A,HTML/S
.bra [
.ket ]

; $VER: GenFDIndex 39.2 (26.7.95)
; by B Simpson
;
; Script to automatically generate AmigaGuide or HTML index of FD files
; Needs FD: assigned to where FD files are.

if not val [HTML]
echo >T:__fd_temp_$$ "@database fd_index.guide*n@remark Generated on `Date`*n@node main *"Shared Library Function Definitions*"*n@contents *"Autodocs/main*"*n*n@{b}Index of FD files@{ub}*n"
pipe list DIR=[FDDIR] PAT=#?_lib.fd FILES LFORMAT " @{*"%m*" link *"FD:%n/main*"}" | psort >>t:__fd_temp_$$
echo >>T:__fd_temp_$$ "*n@endnode"
copy T:__fd_temp_$$ [FDDIR]FD_Index.guide quiet noreq
delete T:__fd_temp_$$ FORCE QUIET
else
echo >T:__fd_temp_$$ "<!DOCTYPE HTML PUBLIC *"-//IETF//DTD HTML//EN*">*n<!-- Generated on `Date` -->*n<HTML>*n<HEADER>*n<TITLE>Index of FD Files in directory [FDDIR]</TITLE>*n</HEADER>*n*n<BODY><P>*n<H2>Function Definition Files</H2>*n<P>*n<UL>*n"
list DIR=[FDDIR] PAT=#?_lib.fd FILES LFORMAT "*<li*>*<a HREF=*"%n*"*>%n*</a*>" | psort >>t:__fd_temp_$$
echo >>t:__fd_temp_$$ "</ul>*n*n<P><HR><P></BODY></HTML>"
copy T:__fd_temp_$$ [FDDIR]Index.html quiet noreq
delete T:__fd_temp_$$ FORCE QUIET
endif
