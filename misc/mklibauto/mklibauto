;
; This little script will scan all of the OS4 SDK's xml interface definitions
; and generate a complete libauto.a
;
; Use the NEWLIB argument to generate a newlib-compatible library
;
; Copyright (C) 2005 Fabian Nunez
;

.key FILENAME/A,NEWLIB/S

if <NEWLIB> EQ NEWLIB
  set cflags=-newlib -O
else
  set cflags=-O
endif

if exists T:libauto.tmp
  delete T:libauto.tmp all quiet
endif

if exists T:libauto.a
  delete T:libauto.a
endif

makedir T:libauto.tmp

pushcd T:libauto.tmp

list sdk:Include/interfaces/~(exec.xml|#?.dtd) lformat "echo parsing %n... || idltool -l *"%f%n*"" > T:runidltool
execute T:runidltool
delete T:runidltool

list #?.c lformat "echo compiling %n... || gcc $cflags -c *"%n*"" > T:rungcc
execute T:rungcc
delete T:rungcc

list #?.o lformat "echo *"adding %n to library...*" || ar rcs T:libauto.tmp.a *"%n*"" > T:runar
execute T:runar
delete T:runar

popcd

copy T:libauto.tmp.a <FILENAME>
delete T:libauto.tmp all quiet
delete T:libauto.tmp.a
