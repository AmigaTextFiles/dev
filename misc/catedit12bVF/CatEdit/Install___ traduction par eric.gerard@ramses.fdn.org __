; Script d'installation Installer pour CatEdit
;$VER:Installation 38.8 (8.8.94)

(set @default-dest "")

(set catedit "CatEdit")
(set catalogdir "Catalogs/")
(set reqtoolslib "Libs/reqtools.library")

;================================================================================
; Chaînes françaises

(set #bad-kick
  (cat "Vous devez avoir le Kickstart 2.04 ou au-dessus d'installé\npour utiliser CatEdit")
)

(set #install
  (cat "\nInstallation de CatEdit\n\n\n"
       "Ce script installe CatEdit 1.2b\n"
       "dans votre Amiga.\n\n\n\n"
       "CatEdit © 1993, 1994, 1995 Rafael D'Halleweyn\n"
       "Tous les droits réservés\n\n"
       "(reqtools.library © Nico François)")
)

(set #update
  (cat "\nInstallation de CatEdit\n\n\n"
       "Ce script met à jour CatEdit\n"
       "pour la version 1.2b.\n\n\n\n"
       "CatEdit © 1993, 1994, 1995 Rafael D'Halleweyn\n"
       "Tous les droits réservés\n\n"
       "(reqtools.library © Nico François)")
)

(set #where-catedit
  (cat "Dans quel répertoire souhaitez-vous que CatEdit soit installé ?")
)

(set #copying-catedit
  (cat "Copie de CatEdit")
)

(set #which-language
  (cat "\nQuelle langue souhaitez-vous installer ?")
)
(set #which-language-help
  (cat "\nL'Amiga   peut   opérer  dans    plusieurs   langues\n"
       "différentes. Si vous voulez que CatEdit  utilise  la\n"
       "même langue  que  le  Workbench  Amiga,  un  fichier\n"
       "catalogue doit être copié  dans  votre  disque  dur.\n"
       "Pour chaque langue supportée.\n\n"
       "Pour réduire la quantité d'espaces conssommé par les\n"
       "fichiers langue, vous pouvez sélectionner uniquement\n"
       "les fichiers des langues spécifiques à copier.\n\n"
       "Cochez simplement les boites des  langues  que  vous\n"
       "souhaitez voir disponible dans votre système.\n\n")
)

(set #copying-reqtools
  (cat "Copie de la reqtools.library dans LIBS:...")
)
(set #copying-reqtools-help
  (cat "CatEdit requière la reqtools.library dans votre répertoire LIBS:\n\n")
)

;================================================================================
; Soyez sûr que vous tournez avec une ROM 2.04

(if (< (/ (getversion) 65536) 37)
  (
    (abort #bad-kick)
  )
)

;================================================================================
;

(user 0)
(run 'SetEnv cateditpath "`C/FindCommand CatEdit`"'
  (safe)
)
(set catedit-dest (pathonly (getenv "cateditpath")))

(complete 5)

(user 1)
(message
  (if (= catedit-dest "")
    (#install)
    (#update)
  )
)
(welcome)
(set old_level @user-level)

(if (= catedit-dest "")
  (
    (user 2)
    (set catedit-dest
      (askdir
        (prompt #where-catedit)
        (help @askdir-help)
        (default "SYS:Tools")
      )
    )
    (user old_level)
  )
)

(copylib
  (prompt "\n" #copying-catedit)
  (help @copylib-help)
  (source catedit)
  (dest catedit-dest)
  (confirm)
)

(complete 50)

(if (NOT (exists (tackon catedit-dest "CatEdit.info")))
  (
    (copyfiles
      (source (cat catedit ".info"))
      (dest catedit-dest)
    )
    (tooltype
      (dest "CatEdit")
      (noposition)
    )
  )
)

(user old_level)

(complete 55)

(if (exists "SYS:Locale")
  (
    (if (exists "LOCALE:")
      (
        (user 2)
        (set lang
          (askoptions
            (prompt #which-language)
            (help #which-language-help @askoptions-help)
            (choices
              "Dansk"
              "Deutsch"
              "English"
              "Español"
              "Français"
              "Italiano"
              "Nederlands"
              "Polski"
              "Svenska")
            (default 0)
          )
        )
        (user old_level)
        (set n 0)
        (while (set language
            (select n
              "dansk"
              "deutsch"
              "english"
              "español"
              "français"
              "italiano"
              "nederlands"
              "polski"
              "svenska"
              "")
            )
          (
            (if (IN lang n)
              (
                (if (<> "english" language)
                  (
                    (makedir (tackon "LOCALE:Catalogs" language))
                    (copyfiles
                      (source (tackon (tackon catalogdir language) "catedit.catalog"))
                      (dest (tackon "LOCALE:Catalogs/" language)))
                  )
                )
              )
            )
            (set n (+ n 1))
          )
        )
      )
    )
  )
)

(complete 80)

(user old_level)
(copylib
  (prompt "\n" #copying-reqtools)
  (help #copying-reqtools-help @copylib-help)
  (source reqtoolslib)
  (dest "LIBS:")
  (confirm)
)

(complete 100)
(exit)
