; Installation script for AmiSlate
; by Jeremy Friesner

(transcript "On installing AmiSlate...")

(set TestSlateAssign (run "assign SlateRexx: EXISTS" (safe)))
(if (> TestSlateAssign 0)
	(set DefaultUpdate 0)	
	(set DefaultUpdate 1)
)

(set SlateUpdate
      (askchoice
         (prompt "Select which kind of install you want:")
         (choices "First Time Install" "Update")
         (default DefaultUpdate)
         (help "If you've never installed AmiSlate on your system before, select First Time Install.  If you already have an earlier version, select Update")
   ))
      

(if (= SlateUpdate 0)
	(
		(set SlateDir
			(askdir
				(prompt "Where would you like to install AmiSlate??\n(A drawer named \"AmiSlate\" will be created in the directory you select)")
				(help @askdir-help)
				(default "AmiTCP:")
			)
		)
		(set NewDir (tackon SlateDir "AmiSlate"))
		(makedir (NewDir) (infos))
	)
	(
		(set NewDir
			(askdir
				(prompt "Where is AmiSlate currently installed??\n(Select the drawer where the AmiSlate executable is located)")
				(help @askdir-help)
				(default "AmiTCP:")
			)
		)
	)
)

(set MyPrompt ("Copying files to %s." NewDir))

(if (= SlateUpdate 0)
	(
		; direct copy, then assign to the subdir
		(copyfiles
			(prompt MyPrompt)
			(help @copyfiles-help)
			(source "")
			(infos)
			(pattern "~(Install#?)")
			(dest NewDir)
		)
	)
	(
		; assigns already made, must copy manually
		(copyfiles
			(prompt MyPrompt)
			(help @copyfiles-help)
			(source "")
			(infos)
			(pattern "(AmiSlate|Doc|ExampleRexx|Readme)")
			(dest NewDir)
		)
		(copyfiles
			(prompt "Copying ARexx files to SlateRexx:")
			(help @copyfiles-help)
			(source "SlateRexx")
			(files)
			(pattern "#?.(rexx|slatefont)")
			(dest "SlateRexx:")
		)
		(copyfiles
			(prompt "Copying script files to SlateScripts:")
			(help @copyfiles-help)
			(source "SlateScripts")
			(files)
			(pattern "#?")
			(dest "SlateScripts:")
		)
	)
)

(if (= SlateUpdate 0)
 (
	(set AssignString1 ("assign SlateRexx: %s/SlateRexx" NewDir ))
	(run AssignString1)
	(set AssignString2 ("assign SlateScripts: %s/SlateScripts\n" NewDir))
	(run AssignString2)

	(set AssignString ("assign SlateRexx: %s/SlateRexx\nassign SlateScripts: %s/SlateScripts\n" NewDir NewDir))
	(startup "AmiSlate"
	        (prompt "For correct operation, AmiSlate needs two assigns to be present.  These assigns are \n\nSlateRexx: and SlateScripts:\n\n and should be assigned to the installed directories with the same names.  Do you wish me to put the appropriate assigns in your s:user-startup?")
	        (help "Sometimes a remote AmiSlate client will request the local one to run an ARexx macro or a AmiSlate recording.  These assigns will be the default directories for these scripts.")
	        (command AssignString)
	)
	(run AssignString)
 )
)

(set TestAmiTCPAssign (run "assign AmiTCP: EXISTS" (safe)))
(if (> TestAmiTCPAssign 0)
	(message "\n\nAmiTCP does not appear to be set up on your system.  (Specifically, the assign AmiTCP: has not been made)  Because of this, I am unable to configure the amitcp:db/services and amitcp:db/inetd.conf files.\n\nIf you wish to use the network functions of AmiSlate, you will need to set up AmiTCP and install again.")
	(
		(run "delete t:AmiSlate.AppendMeToServices QUIET")
		(if (= SlateUpdate 0)
		(
			(textfile
				(prompt "If you plan to be running AmiSlate in conjunction with AmiTCP, the line:\n\nAmiSlate      2955/tcp\n\nneeds to be present in your amitcp:db/services file.  Do you wish me to append this line to that file?\n\n(Note: this does not check to see if the line is already in the file!  If you've installed AmiSlate before, skip this part)")
				(help "Including this line in your amitcp:db/services file tells AmiTCP's inet daemon that all requests on port 2955 should be sent to the AmiSlate program for further processing.  If this line is not included, you will not be able to receive AmiSlate requests from other people.")
				(dest "t:AmiSlate.AppendMeToServices")
				(append "\nAmiSlate	2955/tcp\n")
				(confirm)
			)
		
			(set AddServiceString ("join amitcp:db/services t:AmiSlate.AppendMeToServices as t:AmiSlate.Temp1"))
			(set UpdateServiceString ("copy t:AmiSlate.Temp1 amitcp:db/services"))
		
			(if (exists "t:AmiSlate.AppendMeToServices")
				((run AddServiceString)
				 (run UpdateServiceString)
				 (run "delete t:AmiSlate.Temp1")
				 (run "delete t:AmiSlate.AppendMeToServices")
				)
			)
				
			(set ExecFile (tackon NewDir "AmiSlate"))
			(set InetDString (cat "AmiSlate stream tcp nowait root " ExecFile))
			(set InetDPrompt ("Also, if you plan to be running AmiSlate in conjunction with AmiTCP, the line:\n\n%s\n\nneeds to be present in your amitcp:db/inetd.conf file.  Do you wish me to append this line to that file?  (Note: this does not check to see if the line is already in the file!  If you've installed AmiSlate before, skip this part)" InetDString))
			(set InternalInetString (cat "\nAmiSlate    stream      tcp nowait root    " ExecFile))		
			(run "delete t:AmiSlate.AppendMeToInetD QUIET")
		
			(textfile
				(prompt InetDPrompt)
				(help "Including this line in your amitcp:db/inetd.conf file tells AmiTCP which program to run whenever it gets an AmiTCP request.  It is important that the file path and name in this line reflect the location of the AmiSlate executable.")
				(dest "t:AmiSlate.AppendMeToInetD")
				(append InternalInetString)
				(confirm)
			)
		
			(set AddInetString ("join amitcp:db/inetd.conf t:AmiSlate.AppendMeToInetD as t:AmiSlate.Temp2"))
			(set UpdateInetString ("copy t:AmiSlate.Temp2 amitcp:db/inetd.conf"))
		
			(if (exists "t:AmiSlate.AppendMeToInetD")
				((run AddInetString)
				 (run UpdateInetString)
				 (run "delete t:AmiSlate.Temp2")
				 (run "delete t:AmiSlate.AppendMeToInetD")
				)
			)
		)
		) ;;;;end if Update=0
	) ;;; end if AmiTCP>0 else... 
)
(message "\n\nAmiSlate is now installed.  Reboot your Amiga (just to be certain all assigns are made, etc.) and try it out!")

