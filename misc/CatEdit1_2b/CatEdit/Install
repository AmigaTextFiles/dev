; Installer installation script for CatEdit
;$VER:Install 38.8 (8.8.94)

(set @default-dest "")

(set catedit "CatEdit")
(set catalogdir "Catalogs/")
(set reqtoolslib "Libs/reqtools.library")

;================================================================================
; English strings

(set #bad-kick
  (cat "You must have Kickstart 2.04 or higher installed to use CatEdit!")
)

(set #install
  (cat "\nCatEdit installation\n\n\n"
       "This script installs CatEdit 1.2b\n"
       "on your Amiga.\n\n\n\n"
       "CatEdit © 1993, 1994, 1995 Rafael D'Halleweyn\n"
       "All rights reserved\n\n"
       "(reqtools.library © Nico François)")
)

(set #update
  (cat "\nCatEdit installation\n\n\n"
       "This script updates CatEdit\n"
       "to version 1.2b.\n\n\n\n"
       "CatEdit © 1993, 1994, 1995 Rafael D'Halleweyn\n"
       "All rights reserved\n\n"
       "(reqtools.library © Nico François)")
)

(set #where-catedit
  (cat "In wich drawer should CatEdit be installed?")
)

(set #copying-catedit
  (cat "Copying CatEdit")
)

(set #which-language
  (cat "\nWhich languages should be installed?")
)
(set #which-language-help
  (cat "\nThe Amiga can be operated in many different languages. "
       "If you want CatEdit to use the same language as the Amiga "
       "Workbench a catalog file must be copied to your harddisk "
       " for each language supported.\n\n"
       "To reduce the amount of space consumed by the language "
       "files, you can select to have only the files of specific "
       "languages copied.\n\n"
       "Simply check the boxes of the languages you wich to have "
       "available on your system.\n\n")
)

(set #copying-reqtools
  (cat "Copying reqtools.library to LIBS:...")
)
(set #copying-reqtools-help
  (cat "CatEdit requires the reqtools.library in your LIBS: drawer\n\n")
)

;================================================================================
; make sure we are running under a 2.04 ROM

(if (< (/ (getversion) 65536) 37)
  (
    (abort #bad-kick)
  )
)

;================================================================================
;

(user 0)
(run 'SetEnv cateditpath "`C/FindCommand CatEdit`"'
  (safe)
)
(set catedit-dest (pathonly (getenv "cateditpath")))

(complete 5)

(user 1)
(message
  (if (= catedit-dest "")
    (#install)
    (#update)
  )
)
(welcome)
(set old_level @user-level)

(if (= catedit-dest "")
  (
    (user 2)
    (set catedit-dest
      (askdir
        (prompt #where-catedit)
        (help @askdir-help)
        (default "SYS:Tools")
      )
    )
    (user old_level)
  )
)

(copylib
  (prompt "\n" #copying-catedit)
  (help @copylib-help)
  (source catedit)
  (dest catedit-dest)
  (confirm)
)

(complete 50)

(if (NOT (exists (tackon catedit-dest "CatEdit.info")))
  (
    (copyfiles
      (source (cat catedit ".info"))
      (dest catedit-dest)
    )
    (tooltype
      (dest "CatEdit")
      (noposition)
    )
  )
)

(user old_level)

(complete 55)

(if (exists "SYS:Locale")
  (
    (if (exists "LOCALE:")
      (
        (user 2)
        (set lang
          (askoptions
            (prompt #which-language)
            (help #which-language-help @askoptions-help)
            (choices
              "Dansk"
              "Deutsch"
              "English"
              "Español"
              "Français"
              "Italiano"
              "Nederlands"
              "Polski"
              "Svenska")
            (default 0)
          )
        )
        (user old_level)
        (set n 0)
        (while (set language
            (select n
              "dansk"
              "deutsch"
              "english"
              "español"
              "français"
              "italiano"
              "nederlands"
              "polski"
              "svenska"
              "")
            )
          (
            (if (IN lang n)
              (
                (if (<> "english" language)
                  (
                    (makedir (tackon "LOCALE:Catalogs" language))
                    (copyfiles
                      (source (tackon (tackon catalogdir language) "catedit.catalog"))
                      (dest (tackon "LOCALE:Catalogs/" language)))
                  )
                )
              )
            )
            (set n (+ n 1))
          )
        )
      )
    )
  )
)

(complete 80)

(user old_level)
(copylib
  (prompt "\n" #copying-reqtools)
  (help #copying-reqtools-help @copylib-help)
  (source reqtoolslib)
  (dest "LIBS:")
  (confirm)
)

(complete 100)
(exit)
