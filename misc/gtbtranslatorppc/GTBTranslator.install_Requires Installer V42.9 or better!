; -------------------------------------------------------
; Installer script for GTBTranslatorPPC V2.3
; -------------------------------------------------------

(set #welcome "This is the installer for GTBTranslatorPPC V2.3!")

(set #wrongversion
(cat "You have an old version of the program 'Installer' "
     "on your Amiga!\n\n"
     "The installation procedure needs at least Installer 42.9."
     "\n\nPlease check your System & obtain a newer version!"
))

(set #general-help 
(cat "The install script will copy all %s files &"
     "directories to the destination that you specify."
     ""
     "Finally, it will add an Assign GTBTranslator: Vol:destination/"
     "statement to your User-Startup file located in the "
     "S: directory."
))

(set #destdir-prompt "Select a Volume & Path where GTBTranslatorPPC will be added:")

(set #destdir-help   
(cat "Select the volume that you wish to place GTBTranslatorPPC in."
     "Then select a directory (if any) that will serve as a "
     "parent for GTBTranslatorPPC.  Example:  Select DH0: as the volume,"
     "then select a directory (such as Programs) & the installer"
     "will make a directory called DH0:Programs/GTBTranslator, where"
     "the GTBTranslatorPPC program will be placed."
     "NOTE:"
     "If this is changed in the future, remember to edit the Icon"
     "ToolTypes to reflect the actual location of the program."
))

(set #catdir-prompt "Select a Volume & Path where GTBTranslatorPPC will place Catalog files:")

(set #catdir-help   
(cat "Select the volume that you wish to place GTBTranslatorPPC catalog files in."
     "   Example:  Select System:Locale/Catalogs as the volume,"
     "then select a directory (such as english) & the installer"
     "will place all necessary catalog files for the program in System:Locale/Catalogs/english/"
))

(set #includedir-prompt "Select a Volume & Path where your Compiler #includes are located:")

(set #includedir-help   
(cat "Select the directory that you wish to place GTBTranslatorPPC"
     "source #includes in."
))

(set #no-diskspace "To install GTBTranslatorPPC into '%s', you need at least %ld bytes of free harddrive space!")

(set #startup-prompt "An Assign has to be added to S:User-Startup for GTBTranslatorPPC.")
(set #startup-help   "GTBTranslatorPPC won't work correctly without this!")

;------------------------------------------------------------------------
; Cleanup any temporary mess we created
(procedure CLEANUP
    ; Nothing to cleanup
    (delete (tackon @default-dest ".tempinstall") (all) (optional "force"))
)   ; CLEANUP

;------------------------------------------------------------------------
; Obtain an installation path
(procedure GETPATH #gp_default #gp_remdir #gp_msg #gp_help

    (set #gp_res "")
    (while (= "" #gp_res)
        (set #gp_res #gp_default)
        (if (not (exists #gp_res (noreq)))
          (
            ; Surfer 1.1 Hack:
            (if (= #gp_res "Work:")
              (
                (set #gp_res "")
                (set #gp_default @default-dest)
              )
            ) ; if

          )
        ) ; if

        (if (OR (= "" #gp_res) (= @user-level 2))
          (
            ; We had a problem. So we need to ask the user!
            (set #gp_res
                (expandpath
                    (askdir
                        (prompt #gp_msg)
                        (help #gp_help)
                        (default #gp_default)
                    )
                )
            )
          )
        ) ; if

        ; If the directory does not exist, retry!
        (if (<> "" #gp_res)
          (
            (if (not (exists #gp_res (noreq)))
                (set #gp_res "")
            ) ; if
          )
        ) ; if
    ) ; while

    ; return the result!
    (set #gp_res #gp_res)

) ; GETPATH

;------------------------------------------------------------------------
; We can't handle this script without Installer 42.9 or better!
(if (< @installer-version 2752521)
  (
    ; Big problem!
    (message #wrongversion)
    (exit (quiet))
  )
)

; find out how the user wants to install things
(welcome #welcome)

(complete 0)

; First find the Destination base:

(set #GTBTransDirBase (GETPATH "Work:" TRUE #destdir-prompt #destdir-help))

; Next find the Catalogs base:

(set #CatDir (GETPATH "Work:" TRUE #catdir-prompt #catdir-help))

; Next find the Include base:

(set #IncludeDirBase (GETPATH "Work:" TRUE #includedir-prompt #includedir-help))

; We have to create a drawer "GTBTranslator" in GTBTransDirBase:

(copyfiles (prompt "Copying Drawer (Icon file)...")
           (help    @copyfiles-help)
           (source  "ENV:sys/def_Drawer.info")
           (dest    #GTBTransDirBase)
           (newname "GTBTranslator.info")
)

(set #GTBTransDir (tackon #GTBTransDirBase "GTBTranslator"))

(set @default-dest #GTBTransDir)

; If this is a new installation, we check the diskspace!

    (if (not (exists @default-dest))
      (
        ; For a new setup, we should leave some space!
        (if (< (getdiskspace #GTBTransDirBase) 5000000)
            (abort (#no-diskspace #GTBTransDirBase 5000000))
        )

        (makedir @default-dest)

        (complete 9)

        (copyfiles (prompt  "Copying Catalog files...")
                   (help    @copyfiles-help)
                   (source  "GTBTranslator.cd")
                   (dest    #CatDir)
        )

        (copyfiles (prompt  "Copying Catalog files...")
                   (help    @copyfiles-help)
                   (source  "GTBTranslator.ct")
                   (dest    #CatDir)
        )

        (copyfiles (prompt  "Copying Linker file(s)...")
                   (help    @copyfiles-help)
                   (source  "CommonFuncsPPC.o")
                   (dest    #GTBTransDir)
        )

        (complete 25)

        (copyfiles (prompt  "Copying Support program(s)...")
                   (help    @copyfiles-help)
                   (source  "GTBScannerPPC")
                   (dest    #GTBTransDir)
                   (infos)
        )

        (copyfiles (prompt  "Copying Include files...")
                   (help    @copyfiles-help)
                   (source  "AmigaDOSErrs.h")
                   (dest    #IncludeDirBase)
        )

        (copyfiles (prompt  "Copying Include files...")
                   (help    @copyfiles-help)
                   (source  "CommonFuncs.h")
                   (dest    #IncludeDirBase)
        )

        (copyfiles (prompt  "Copying Help file(s)...")
                   (help    @copyfiles-help)
                   (source  "GTBTranslator.guide")
                   (dest    #GTBTransDir)
                   (infos)
        )

        (copyfiles (prompt  "Copying CommonFuncs Help file...")
                   (help    @copyfiles-help)
                   (source  "CommonFuncs.guide")
                   (dest    #GTBTransDir)
                   (infos)
        )

        (copyfiles (prompt  "Copying .readme file...")
                   (help    @copyfiles-help)
                   (source  "GTBTranslatorPPC.readme")
                   (dest    #GTBTransDir)
        )

        (complete 60)

        (copyfiles (prompt  "Copying test file...")
                   (help    @copyfiles-help)
                   (source  "TestProject.gui")
                   (dest    #GTBTransDir)
        )

        (copyfiles (prompt  "Copying Template files...")
                   (help    @copyfiles-help)
                   (source  "GenC.template")
                   (dest    #GTBTransDir)
        )

        (copyfiles (prompt  "Copying Template files...")
                   (help    @copyfiles-help)
                   (source  "GenAtalk.template")
                   (dest    #GTBTransDir)
        )

        (complete 83)

        (copyfiles (prompt  "Copying Main program...")
                   (help    @copyfiles-help)
                   (source  "GTBTranslatorPPC")
                   (dest    #GTBTransDir)
                   (infos)
        )

        (complete 98)
      )
    )

(startup    "GTBTranslatorPPC"
   (prompt  "An Assign needs to be added to the \"S:user-startup\".")
   (help    "GTBTranslatorPPC has to know where to find certain files.")
   (command "Assign GTBTranslator: " #GTBTransDir)
)

; If we have to add an Assign, we better create one right now
(makeassign "GTBTranslator" #GTBTransDir)

;------------------------------------------------------------------------
; That's it!

(complete 100)

(CLEANUP)
(exit)

(abort "Well, I tried!")
