; Installation script for Twilight Development System
; $VER: Install-TDS 2.12 (17.08.96)

(set @default-dest "SYS:")
(set default_lang 1)

(if (< (/ (getversion) 65536) 37)
  (abort "This program requires Kickstart 2.04 or greater to run !")
)

(set destdir
  (askdir
    (prompt "Select where to install TDS.\nA new drawer will be created.")
    (help @askdir-help)
    (default @default-dest)
    (newpath)
  )
)

(set @default-dest
  (tackon destdir "TDS")
)

(makedir @default-dest
  (prompt "Creating Drawer TDS")
  (help @makedir-help)
  (infos)
)

(copylib
  (prompt "Installing Twilight Editor")
  (help @copylib-help)
  (source "Ted")
  (dest @default-dest)
  (infos)
)

(copylib
  (prompt "Installing Twilight Make")
  (help @copylib-help)
  (source "TMake")
  (dest @default-dest)
  (infos)
)

(copylib
  (prompt "Installing Twilight Reference Generator")
  (help @copylib-help)
  (source "TRefs")
  (dest @default-dest)
)

(copylib
  (prompt "Installing Twilight Editor Frontend")
  (help @copylib-help)
  (source "Ed")
  (dest @default-dest)
)

(copyfiles
  (prompt "Installing Converters")
  (help @copyfiles-help)
  (source "converters")
  (dest (tackon @default-dest "converters"))
  (all)
)

(copyfiles
  (prompt "Installing Converters Source")
  (help @copyfiles-help)
  (source "ConvSrc")
  (dest (tackon @default-dest "ConvSrc"))
  (all)
  (infos)
)

(set lang 
  (askoptions 
    (prompt "Which languages should be installed ?")
    (help @askoptions-help)
    (choices "English"
             "Deutsch"
             "Français"
             "Italiano"
    )
    (default default_lang)
  )
)

(makedir (tackon @default-dest "catalogs"))
(set n 0)
(while 
  (set language 
    (select n  "english"
               "deutsch"
               "français"
               "italiano"
               "")
  )
  (
    (if (IN lang n)
      (
        (if (<> 0 n)
          (
            (set catalog_dir (cat "catalogs/" language))
            (copyfiles 
              (prompt "Installing Catalogs")
              (source catalog_dir)
              (dest (tackon @default-dest catalog_dir))
              (all)
            )
          )
        )
      )
    )
    (set n (+ n 1))
  )
)

(copyfiles
  (prompt "Installing Catalogs Sources")
  (help @copyfiles-help)
  (source "catalogs/source")
  (dest (tackon @default-dest "catalogs/source"))
  (all)
)

(makedir (tackon @default-dest "Docs") (infos))
(set n 0)
(while 
  (set language 
    (select n  "English"
;               "Deutsch"
;               "Français"
               "Italiano"
               "")
  )
  (
    (if (IN lang n)
      (
        (set docs_dir (cat "Docs/" language))
        (copyfiles 
          (prompt "Installing Docs")
          (source docs_dir)
          (dest (tackon @default-dest docs_dir))
          (all)
        )
      )
    )
    (set n (+ n 1))
  )
)

(if
  (exists "keyfile")
  (copyfiles
    (prompt "Installing Keyfile")
    (help @copyfiles-help)
    (source "keyfile")
    (dest @default-dest)
  )
)

(if
  (exists "TDS.key")
  (copyfiles
    (prompt "Installing Keyfile")
    (help @copyfiles-help)
    (source "TDS.key")
    (dest @default-dest)
  )
)

(copyfiles
  (prompt "Installing Icons")
  (help @copyfiles-help)
  (source "env/tds")
  (pattern "#?.info")
  (dest "envarc:tds")
)

(if
  (askbool (prompt "Do you want to use the eight colors icons ?")
           (help "The eight colors icons have been designed for the MagicWB.")
  )
  (copyfiles
    (prompt "Installing eight colors icons")
    (help @copyfiles-help)
    (source "Icons/")
    (pattern "#?.info")
    (dest @default-dest)
  )
)

(if
  (askbool (prompt "Do you want to install the example projects ?")
           (help "The example projects show how to use the features"
                 " of the TDS for building new projects."
           )
  )
  (copyfiles
    (prompt "Installing Examples")
    (help @copyfiles-help)
    (source "examples")
    (dest (tackon @default-dest "Examples"))
    (all)
    (infos)
  )
)

(startup "TDS"
  (prompt "Adding assign TDS: command to the file user-startup")
  (help @startup-help)
  (command "assign TDS: " @default-dest)
)

(makeassign "TDS" @default-dest)

