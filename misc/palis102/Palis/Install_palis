;; STR_VER: Install_palis 1.02 (10.2.1996)
;;
;; Install Palis to system
;; (c)1995-1996 by Codex Design Software

;; ============================================

(set @default-dest "C:")
(set @pretend 0)
(set @user-level 2)
(set #docdir "HELP:")

(set STATUS_OKAY   0)
(set STATUS_DOUBLE 1)

;; ============================================ HELLO

(complete 0)
(welcome "·C·O·D·E·X· ·D·E·S·I·G·N· ·S·O·F·T·W·A·R·E·\n"
         "presents:\n"
         "Palis V1.02\n"
         "welcome to its installation utility\n")

(complete 10)

;; ============================================ ALREADY ONE ?

(if (exists "C:Palis" (noreq))
    (
       (set @code STATUS_OKAY)
       (set @default-dest "C:")
       (message "\n"
		"Palis has already been found on your machine.\n"
		"\n"
		"It will be updated now.")
    )
    (
       (set @code (run "patchtest/patchtest LIB=patchtest/patchtest.library OFF=-30 QUIET"))

       (if (< 5 @code)
           (if (askbool (prompt "Help ! Error while checking whether you already "
           			 "have a patch-manager !\n"
           			 "Do you have one (Saferpatches, etc.) ?")
           		 (help   @askbool-help)
           		 (default 0)
          	)
          	(set @code 0)
          	(set @code 5)
          )
       )
       
       (if (= @code 0)
           (if (askbool (prompt "You already have a patch-manager installed.\n"
       			 "Do you want to install Palis V1.02, additionally\n"
       			 "(it won't be started automatically, then; "
       			 "you have to stop starting the other manager "
       			 "first) ?")
       		 (help   "It's neither neccessary nor wise to use two patch-managers.\n"
       			 "If you want to use Palis, you have to stop using "
       			 "your old one.\n"
       			 "If you are intersted in the sources how to "
       			 "patch a library only, simply copy the contence "
       			 "of the cdxMakePatches-directory anywhere you want.\n"
       			 "\n"
       			 @askbool-help)
       		 (choices " Yes " " No, thanks ")
       		 (default 0)
       	)
       	(set @code STATUS_DOUBLE)
       	(exit "Okay, see you again !" (quiet))
           )
           (set @code STATUS_OKAY)
       )
       
       
       ;; ============================================ COPY WHERE
       
       (complete 20)
       
       (set @default-dest (askdir (prompt "Where do you want to copy Palis"
       				   "(path must be valid even from "
       				   "s:user-startup !) ?")
       			   (help   "Select where to copy Palis.\n"
       			   	   "Since Palis will automatically be "
       			   	   "executed when starting your system "
       			   	   "copy it in a directory which "
       			   	   "is available to your system "
       			   	   "even before the Workbench is up !\n"
       			   	   "\n"
       			   	   @askdir-help)
       			   (default @default-dest)
       		   )
       )
    )
)
;; ============================================ WHAT ELSE

(complete 30)

(set opts (askoptions (prompt "Which additional products do you want ?")
		      (help   @askobtions-help)
		      (choices "Palis documentation (guide)"
		      	       "ViewPALIS (see which libraries are patched)"
			       "Source/Object how to patch a library"
			       "Patchtest (find out whether patch-manager exists)")
		      (default 3)
	  )
)

(if (= 1 (BITAND opts 1))
    (
	(set @copyguide TRUE)
	(set @guidedir (askdir (prompt "Where shall I copy the documentation (Palis.guide) ?")
			       (help   @askdir-help)
			       (newpath)
			       (default "HELPDIR:")
			)
	)
    )
    (set @copyguide FALSE)
)

(if (= 2 (BITAND opts 2))
    (
	(set @copyview TRUE)
	(set @viewdir (askdir (prompt "Where shall I copy the documentation (Palis.guide) ?")
			       (help   @askdir-help)
			       (newpath)
			       (default "SYS:utilities")
			)
	)
    )
    (set @copyview FALSE)
)

(if (= 4 (BITAND opts 4))
    (
    (set @copysrc TRUE)
    )
    (set @copysrc FALSE)
)

(if (= 8 (BITAND opts 8))
    (
	(set @copytest TRUE)
	(set @testdir (askdir (prompt "Where shall I copy Testpatch "
					"(sources, helpfile, etc.) ?")
			       (help   @askdir-help)
			       (newpath)
			       (default "SYS:Testpatch")
			)
	)
    )
    (set @copytest FALSE)
)

;; ============================================ COPY...

(complete 50)

(copyfiles (prompt "Copying Palis...")
           (help   "Fine, isn't it ;->")
           (dest   @default-dest)
           (source "Palis")
           (optional "nofail" "force")
)

(if (= @code STATUS_OKAY)
  (
    (set @command (tackon @default-dest "Palis"))
    (startup "Palis"
             (prompt "\n"
		     "I will now edit s:user-startup in order to "
		     "make Palis ran when you start your machine.\n"
		     "Note that Palis is _not_ active after the "
		     "installation... you have to reboot your "
		     "computer when the installation is been finished !")
             (help   "You can not start Palis savely now.\n"
                     "Please reset your machine when finished !")
             (command @command)
    )
  )
)

(complete 60)

(if @copyguide
  (copyfiles (prompt "Copying documentation...")
             (help   "Fine, isn't it ;->")
             (dest   @guidedir)
             (source "doc/Palis.guide")
             (infos)
             (optional "nofail" "force")
  )
)

(complete 65)

(if @copyguide
  (copyfiles (prompt "Copying ViewPALIS...")
             (help   "Fine, isn't it ;->")
             (dest   @viewdir)
             (source "ViewPALIS")
             (infos)
             (optional "nofail" "force")
  )
)

(complete 70)

(if @copysrc
  (
    (copyfiles (prompt "Copying include file to INCLUDE:")
             (help   "Fine, isn't it ;->")
             (dest   "INCLUDE:")
             (source "cdxMakePatches/cdxMakePatches.h")
             (optional "nofail" "force")
    )
    (copyfiles (prompt "Copying object file to LIB:")
             (help   "Fine, isn't it ;->")
             (dest   "INCLUDE:")
             (source "cdxMakePatches/cdxMakePatches.o")
             (optional "nofail" "force")
    )
    (makedir "INCLUDE:cdxMakePatches"
             (prompt "Creating INCLUDE:cdxMakePatches/ directory "
                     "in order to copy the source of cdxMakePatches.o "
                     "into it ...")
	     (safe)
    )
    (copyfiles (prompt "Copying source file to INCLUDE:cdxMakePatches/")
             (help   "Fine, isn't it ;->")
             (dest   "INCLUDE:cdxMakePatches/")
             (source "cdxMakePatches/cdxMakePatches.c")
             (optional "nofail" "force")
    )
  )
)

(complete 80)

(if @copytest
    (copyfiles (prompt "Copying Testpatch files...")
               (source "PatchTest/")
               (dest   @testdir)
               (all)
               (infos)
               (optional "askuser" "nofail")
    )
)


(complete 100)

(exit "Thank you for installing\n"
      "a\n"
      "·C·O·D·E·X· ·D·E·S·I·G·N· ·S·O·F·T·W·A·R·E·\n"
      "production !\n\n"
      "Gimme flames:\n"
      "codex@stern.mathematik.hu-berlin.de")

