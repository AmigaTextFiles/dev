<html>
<head>
<title>Ada 95 RM - Annex P: Syntax Summary
</title>
<link rev="made" href="mailto:Magnus.Kempe@di.epfl.ch">
</head>

<body>
<h4><a href="N.htm"><img src="left.gif" alt="Prev"></a> | <a href="toc.htm"><img src="up.gif" alt="Up"></a> | <img src="noright.gif" alt="____"> | <a href="N.htm"><img src="back.gif" alt="Back"></a> | <a href="index.htm"><img src="forward.gif" alt="Forward"></a><br>   <a href="toc.htm">TOC --</a> / <a href="toc-l1.htm">--.--</a> / <a href="toc-l2.htm">--.--.--</a> | <a href="index.htm">Index</a> | <a href="search.htm">Search</a> | <a href="syntax.htm">Syntax</a> | <a href="help.htm">Help</a></h4>

<hr>

<h2 align="center"><a name="0">Annex P: Syntax Summary
<br>(informative)</a></h2>
<dl><dt><a name="1">(1)</a><dd>This Annex summarizes the complete syntax of the language.  <a href="01-01-04.htm">See 1.1.4</a> for
a description of the notation used.
<pre><a href="02-01.htm#syntax">2.1</a>:
character ::= graphic_character | format_effector | other_control_function

<a href="02-01.htm#syntax">2.1</a>:
graphic_character ::= identifier_letter | digit | space_character | special_character

<a href="02-03.htm#syntax">2.3</a>:
identifier ::=
   identifier_letter {[underline] letter_or_digit}

<a href="02-03.htm#syntax">2.3</a>:
letter_or_digit ::= identifier_letter | digit

<a href="02-04.htm#syntax">2.4</a>:
numeric_literal ::= decimal_literal | based_literal

<a href="02-04-01.htm#syntax">2.4.1</a>:
decimal_literal ::= numeral [.numeral] [exponent]

<a href="02-04-01.htm#syntax">2.4.1</a>:
numeral ::= digit {[underline] digit}

<a href="02-04-01.htm#syntax">2.4.1</a>:
exponent ::= E [+] numeral | E - numeral

<a href="02-04-02.htm#syntax">2.4.2</a>:
based_literal ::=
   base # based_numeral [.based_numeral] # [exponent]

<a href="02-04-02.htm#syntax">2.4.2</a>:
base ::= numeral

<a href="02-04-02.htm#syntax">2.4.2</a>:
based_numeral ::=
   extended_digit {[underline] extended_digit}

<a href="02-04-02.htm#syntax">2.4.2</a>:
extended_digit ::= digit | A | B | C | D | E | F

<a href="02-05.htm#syntax">2.5</a>:
character_literal ::= 'graphic_character'

<a href="02-06.htm#syntax">2.6</a>:
string_literal ::= "{string_element}"

<a href="02-06.htm#syntax">2.6</a>:
string_element ::= "" | non_quotation_mark_graphic_character

A string_element is either a pair of quotation marks (""),
or a single graphic_character other than a quotation mark.

<a href="02-07.htm#syntax">2.7</a>:
comment ::= <em>--{non_end_of_line_character}</em>

<a href="02-08.htm#syntax">2.8</a>:
pragma ::=
   pragma identifier [(pragma_argument_association {, pragma_argument_association})];

<a href="02-08.htm#syntax">2.8</a>:
pragma_argument_association ::=
     [pragma_argument_identifier =&gt;] name
   | [pragma_argument_identifier =&gt;] expression

<a href="03-01.htm#syntax">3.1</a>:
basic_declaration ::=
     type_declaration                 | subtype_declaration
   | object_declaration               | number_declaration
   | subprogram_declaration           | abstract_subprogram_declaration
   | package_declaration              | renaming_declaration
   | exception_declaration            | generic_declaration
   | generic_instantiation

<a href="03-01.htm#syntax">3.1</a>:
defining_identifier ::= identifier

<a href="03-02-01.htm#syntax">3.2.1</a>:
type_declaration ::=  full_type_declaration
   | incomplete_type_declaration
   | private_type_declaration
   | private_extension_declaration

<a href="03-02-01.htm#syntax">3.2.1</a>:
full_type_declaration ::=
     type defining_identifier [known_discriminant_part] is type_definition;
   | task_type_declaration
   | protected_type_declaration

<a href="03-02-01.htm#syntax">3.2.1</a>:
type_definition ::=
     enumeration_type_definition | integer_type_definition
   | real_type_definition        | array_type_definition
   | record_type_definition      | access_type_definition
   | derived_type_definition

<a href="03-02-02.htm#syntax">3.2.2</a>:
subtype_declaration ::=
   subtype defining_identifier is subtype_indication;

<a href="03-02-02.htm#syntax">3.2.2</a>:
subtype_indication ::=  subtype_mark [constraint]

<a href="03-02-02.htm#syntax">3.2.2</a>:
subtype_mark ::= subtype_name

<a href="03-02-02.htm#syntax">3.2.2</a>:
constraint ::= scalar_constraint | composite_constraint

<a href="03-02-02.htm#syntax">3.2.2</a>:
scalar_constraint ::=
     range_constraint | digits_constraint | delta_constraint

<a href="03-02-02.htm#syntax">3.2.2</a>:
composite_constraint ::=
     index_constraint | discriminant_constraint

<a href="03-03-01.htm#syntax">3.3.1</a>:
object_declaration ::=
    defining_identifier_list : [aliased] [constant] subtype_indication [:= expression];
  | defining_identifier_list : [aliased] [constant] array_type_definition [:= expression];
  | single_task_declaration
  | single_protected_declaration

<a href="03-03-01.htm#syntax">3.3.1</a>:
defining_identifier_list ::=
  defining_identifier {, defining_identifier}

<a href="03-03-02.htm#syntax">3.3.2</a>:
number_declaration ::=
     defining_identifier_list : constant := static_expression;

<a href="03-04.htm#syntax">3.4</a>:
derived_type_definition ::= [abstract] new parent_subtype_indication [record_extension_part]

<a href="03-05.htm#syntax">3.5</a>:
range_constraint ::=  range range

<a href="03-05.htm#syntax">3.5</a>:
range ::=  range_attribute_reference
   | simple_expression .. simple_expression

<a href="03-05-01.htm#syntax">3.5.1</a>:
enumeration_type_definition ::=
   (enumeration_literal_specification {, enumeration_literal_specification})

<a href="03-05-01.htm#syntax">3.5.1</a>:
enumeration_literal_specification ::=  defining_identifier | defining_character_literal

<a href="03-05-01.htm#syntax">3.5.1</a>:
defining_character_literal ::= character_literal

<a href="03-05-04.htm#syntax">3.5.4</a>:
integer_type_definition ::= signed_integer_type_definition | modular_type_definition

<a href="03-05-04.htm#syntax">3.5.4</a>:
signed_integer_type_definition ::= range static_simple_expression .. static_simple_expression

<a href="03-05-04.htm#syntax">3.5.4</a>:
modular_type_definition ::= mod static_expression

<a href="03-05-06.htm#syntax">3.5.6</a>:
real_type_definition ::=
   floating_point_definition | fixed_point_definition

<a href="03-05-07.htm#syntax">3.5.7</a>:
floating_point_definition ::=
  digits static_expression [real_range_specification]

<a href="03-05-07.htm#syntax">3.5.7</a>:
real_range_specification ::=
  range static_simple_expression .. static_simple_expression

<a href="03-05-09.htm#syntax">3.5.9</a>:
fixed_point_definition ::= ordinary_fixed_point_definition | decimal_fixed_point_definition

<a href="03-05-09.htm#syntax">3.5.9</a>:
ordinary_fixed_point_definition ::=
   delta static_expression  real_range_specification

<a href="03-05-09.htm#syntax">3.5.9</a>:
decimal_fixed_point_definition ::=
   delta static_expression digits static_expression [real_range_specification]

<a href="03-05-09.htm#syntax">3.5.9</a>:
digits_constraint ::=
   digits static_expression [range_constraint]

<a href="03-06.htm#syntax">3.6</a>:
array_type_definition ::=
   unconstrained_array_definition | constrained_array_definition

<a href="03-06.htm#syntax">3.6</a>:
unconstrained_array_definition ::=
   array(index_subtype_definition {, index_subtype_definition}) of component_definition

<a href="03-06.htm#syntax">3.6</a>:
index_subtype_definition ::= subtype_mark range &lt;&gt;

<a href="03-06.htm#syntax">3.6</a>:
constrained_array_definition ::=
   array (discrete_subtype_definition {, discrete_subtype_definition}) of component_definition

<a href="03-06.htm#syntax">3.6</a>:
discrete_subtype_definition ::= discrete_subtype_indication | range

<a href="03-06.htm#syntax">3.6</a>:
component_definition ::= [aliased] subtype_indication

<a href="03-06-01.htm#syntax">3.6.1</a>:
index_constraint ::=  (discrete_range {, discrete_range})

<a href="03-06-01.htm#syntax">3.6.1</a>:
discrete_range ::= discrete_subtype_indication | range

<a href="03-07.htm#syntax">3.7</a>:
discriminant_part ::= unknown_discriminant_part | known_discriminant_part

<a href="03-07.htm#syntax">3.7</a>:
unknown_discriminant_part ::= (&lt;&gt;)

<a href="03-07.htm#syntax">3.7</a>:
known_discriminant_part ::=
   (discriminant_specification {; discriminant_specification})

<a href="03-07.htm#syntax">3.7</a>:
discriminant_specification ::=
   defining_identifier_list : subtype_mark [:= default_expression]
 | defining_identifier_list : access_definition [:= default_expression]

<a href="03-07.htm#syntax">3.7</a>:
default_expression ::= expression

<a href="03-07-01.htm#syntax">3.7.1</a>:
discriminant_constraint ::=
   (discriminant_association {, discriminant_association})

<a href="03-07-01.htm#syntax">3.7.1</a>:
discriminant_association ::=
   [discriminant_selector_name {| discriminant_selector_name} =&gt;] expression

<a href="03-08.htm#syntax">3.8</a>:
record_type_definition ::= [[abstract] tagged] [limited] record_definition

<a href="03-08.htm#syntax">3.8</a>:
record_definition ::=
    record
       component_list
    end record
  | null record

<a href="03-08.htm#syntax">3.8</a>:
component_list ::=
      component_item {component_item}
   | {component_item} variant_part
   |  null;

<a href="03-08.htm#syntax">3.8</a>:
component_item ::= component_declaration | representation_clause

<a href="03-08.htm#syntax">3.8</a>:
component_declaration ::=
   defining_identifier_list : component_definition [:= default_expression];

<a href="03-08-01.htm#syntax">3.8.1</a>:
variant_part ::=
   case discriminant_direct_name is
       variant
      {variant}
   end case;

<a href="03-08-01.htm#syntax">3.8.1</a>:
variant ::=
   when discrete_choice_list =&gt;
      component_list

<a href="03-08-01.htm#syntax">3.8.1</a>:
discrete_choice_list ::= discrete_choice {| discrete_choice}

<a href="03-08-01.htm#syntax">3.8.1</a>:
discrete_choice ::= expression | discrete_range | others

<a href="03-09-01.htm#syntax">3.9.1</a>:
record_extension_part ::= with record_definition

<a href="03-10.htm#syntax">3.10</a>:
access_type_definition ::=
    access_to_object_definition
  | access_to_subprogram_definition

<a href="03-10.htm#syntax">3.10</a>:
access_to_object_definition ::=
    access [general_access_modifier] subtype_indication

<a href="03-10.htm#syntax">3.10</a>:
general_access_modifier ::= all | constant

<a href="03-10.htm#syntax">3.10</a>:
access_to_subprogram_definition ::=
    access [protected] procedure parameter_profile
  | access [protected] function  parameter_and_result_profile

<a href="03-10.htm#syntax">3.10</a>:
access_definition ::= access subtype_mark

<a href="03-10-01.htm#syntax">3.10.1</a>:
incomplete_type_declaration ::= type defining_identifier [discriminant_part];

<a href="03-11.htm#syntax">3.11</a>:
declarative_part ::= {declarative_item}

<a href="03-11.htm#syntax">3.11</a>:
declarative_item ::=
    basic_declarative_item | body

<a href="03-11.htm#syntax">3.11</a>:
basic_declarative_item ::=
    basic_declaration | representation_clause | use_clause

<a href="03-11.htm#syntax">3.11</a>:
body ::= proper_body | body_stub

<a href="03-11.htm#syntax">3.11</a>:
proper_body ::=
    subprogram_body | package_body | task_body | protected_body

<a href="04-01.htm#syntax">4.1</a>:
name ::=
     direct_name                | explicit_dereference
   | indexed_component          | slice
   | selected_component         | attribute_reference
   | type_conversion            | function_call
   | character_literal

<a href="04-01.htm#syntax">4.1</a>:
direct_name ::= identifier | operator_symbol

<a href="04-01.htm#syntax">4.1</a>:
prefix ::= name | implicit_dereference

<a href="04-01.htm#syntax">4.1</a>:
explicit_dereference ::= name.all

<a href="04-01.htm#syntax">4.1</a>:
implicit_dereference ::= name

<a href="04-01-01.htm#syntax">4.1.1</a>:
indexed_component ::= prefix(expression {, expression})

<a href="04-01-02.htm#syntax">4.1.2</a>:
slice ::= prefix(discrete_range)

<a href="04-01-03.htm#syntax">4.1.3</a>:
selected_component ::= prefix . selector_name

<a href="04-01-03.htm#syntax">4.1.3</a>:
selector_name ::= identifier | character_literal | operator_symbol

<a href="04-01-04.htm#syntax">4.1.4</a>:
attribute_reference ::= prefix'attribute_designator

<a href="04-01-04.htm#syntax">4.1.4</a>:
attribute_designator ::=
    identifier[(static_expression)]
  | Access | Delta | Digits

<a href="04-01-04.htm#syntax">4.1.4</a>:
range_attribute_reference ::= prefix'range_attribute_designator

<a href="04-01-04.htm#syntax">4.1.4</a>:
range_attribute_designator ::= Range[(static_expression)]

<a href="04-03.htm#syntax">4.3</a>:
aggregate ::= record_aggregate | extension_aggregate | array_aggregate

<a href="04-03-01.htm#syntax">4.3.1</a>:
record_aggregate ::= (record_component_association_list)

<a href="04-03-01.htm#syntax">4.3.1</a>:
record_component_association_list ::=
    record_component_association {, record_component_association}
  | null record

<a href="04-03-01.htm#syntax">4.3.1</a>:
record_component_association ::=
   [ component_choice_list =&gt; ] expression

<a href="04-03-01.htm#syntax">4.3.1</a>:
component_choice_list ::=
     component_selector_name {| component_selector_name}
   | others

<a href="04-03-02.htm#syntax">4.3.2</a>:
extension_aggregate ::=
    (ancestor_part with record_component_association_list)

<a href="04-03-02.htm#syntax">4.3.2</a>:
ancestor_part ::= expression | subtype_mark

<a href="04-03-03.htm#syntax">4.3.3</a>:
array_aggregate ::=
  positional_array_aggregate | named_array_aggregate

<a href="04-03-03.htm#syntax">4.3.3</a>:
positional_array_aggregate ::=
    (expression, expression {, expression})
  | (expression {, expression}, others =&gt; expression)

<a href="04-03-03.htm#syntax">4.3.3</a>:
named_array_aggregate ::=
    (array_component_association {, array_component_association})

<a href="04-03-03.htm#syntax">4.3.3</a>:
array_component_association ::=
    discrete_choice_list =&gt; expression

<a href="04-04.htm#syntax">4.4</a>:
expression ::=
     relation {and relation} | relation {and then relation}
   | relation {or relation}  | relation {or else relation}
   | relation {xor relation}

<a href="04-04.htm#syntax">4.4</a>:
relation ::=
     simple_expression [relational_operator simple_expression]
   | simple_expression [not] in range
   | simple_expression [not] in subtype_mark

<a href="04-04.htm#syntax">4.4</a>:
simple_expression ::= [unary_adding_operator] term {binary_adding_operator term}

<a href="04-04.htm#syntax">4.4</a>:
term ::= factor {multiplying_operator factor}

<a href="04-04.htm#syntax">4.4</a>:
factor ::= primary [** primary] | abs primary | not primary

<a href="04-04.htm#syntax">4.4</a>:
primary ::=
   numeric_literal | null | string_literal | aggregate
 | name | qualified_expression | allocator | (expression)

<a href="04-05.htm#syntax">4.5</a>:
logical_operator                                      ::=  and | or  | xor

<a href="04-05.htm#syntax">4.5</a>:
relational_operator          ::=  =   | /=  | &lt;   | &lt;= | &gt; | &gt;=

<a href="04-05.htm#syntax">4.5</a>:
binary_adding_operator       ::=  +   | -   | &amp;

<a href="04-05.htm#syntax">4.5</a>:
unary_adding_operator        ::=  +   | <em>--</em>

<a href="04-05.htm#syntax">4.5</a>:
multiplying_operator         ::=  *   | /   | mod | rem

<a href="04-05.htm#syntax">4.5</a>:
highest_precedence_operator     ::=  **  | abs | not

<a href="04-06.htm#syntax">4.6</a>:
type_conversion ::=
    subtype_mark(expression)
  | subtype_mark(name)

<a href="04-07.htm#syntax">4.7</a>:
qualified_expression ::=
   subtype_mark'(expression) | subtype_mark'aggregate

<a href="04-08.htm#syntax">4.8</a>:
allocator ::=
   new subtype_indication | new qualified_expression

<a href="05-01.htm#syntax">5.1</a>:
sequence_of_statements ::= statement {statement}

<a href="05-01.htm#syntax">5.1</a>:
statement ::=
   {label} simple_statement | {label} compound_statement

<a href="05-01.htm#syntax">5.1</a>:
simple_statement ::= null_statement
   | assignment_statement              | exit_statement
   | goto_statement          | procedure_call_statement
   | return_statement        | entry_call_statement
   | requeue_statement       | delay_statement
   | abort_statement         | raise_statement
   | code_statement

<a href="05-01.htm#syntax">5.1</a>:
compound_statement ::=
     if_statement            | case_statement
   | loop_statement          | block_statement
   | accept_statement        | select_statement

<a href="05-01.htm#syntax">5.1</a>:
null_statement ::= null;

<a href="05-01.htm#syntax">5.1</a>:
label ::= &lt;&lt;label_statement_identifier&gt;&gt;

<a href="05-01.htm#syntax">5.1</a>:
statement_identifier ::= direct_name

<a href="05-02.htm#syntax">5.2</a>:
assignment_statement ::=
   variable_name := expression;

<a href="05-03.htm#syntax">5.3</a>:
if_statement ::=
    if condition then
      sequence_of_statements
   {elsif condition then
      sequence_of_statements}
   [else
      sequence_of_statements]
    end if;

<a href="05-03.htm#syntax">5.3</a>:
condition ::= boolean_expression

<a href="05-04.htm#syntax">5.4</a>:
case_statement ::=
   case expression is
       case_statement_alternative
      {case_statement_alternative}
   end case;

<a href="05-04.htm#syntax">5.4</a>:
case_statement_alternative ::=
   when discrete_choice_list =&gt;
      sequence_of_statements

<a href="05-05.htm#syntax">5.5</a>:
loop_statement ::=
   [loop_statement_identifier:]
      [iteration_scheme] loop
         sequence_of_statements
       end loop [loop_identifier];

<a href="05-05.htm#syntax">5.5</a>:
iteration_scheme ::= while condition
   | for loop_parameter_specification

<a href="05-05.htm#syntax">5.5</a>:
loop_parameter_specification ::=
   defining_identifier in [reverse] discrete_subtype_definition

<a href="05-06.htm#syntax">5.6</a>:
block_statement ::=
   [block_statement_identifier:]
       [declare
            declarative_part]
        begin
            handled_sequence_of_statements
        end [block_identifier];

<a href="05-07.htm#syntax">5.7</a>:
exit_statement ::=
   exit [loop_name] [when condition];

<a href="05-08.htm#syntax">5.8</a>:
goto_statement ::= goto label_name;

<a href="06-01.htm#syntax">6.1</a>:
subprogram_declaration ::= subprogram_specification;

<a href="06-01.htm#syntax">6.1</a>:
abstract_subprogram_declaration ::= subprogram_specification is abstract;

<a href="06-01.htm#syntax">6.1</a>:
subprogram_specification ::=
     procedure defining_program_unit_name  parameter_profile
   | function defining_designator  parameter_and_result_profile

<a href="06-01.htm#syntax">6.1</a>:
designator ::= [parent_unit_name . ]identifier | operator_symbol

<a href="06-01.htm#syntax">6.1</a>:
defining_designator ::= defining_program_unit_name | defining_operator_symbol

<a href="06-01.htm#syntax">6.1</a>:
defining_program_unit_name ::= [parent_unit_name . ]defining_identifier

<a href="06-01.htm#syntax">6.1</a>:
operator_symbol ::= string_literal

<a href="06-01.htm#syntax">6.1</a>:
defining_operator_symbol ::= operator_symbol

<a href="06-01.htm#syntax">6.1</a>:
parameter_profile ::= [formal_part]

<a href="06-01.htm#syntax">6.1</a>:
parameter_and_result_profile ::= [formal_part] return subtype_mark

<a href="06-01.htm#syntax">6.1</a>:
formal_part ::=
   (parameter_specification {; parameter_specification})

<a href="06-01.htm#syntax">6.1</a>:
parameter_specification ::=
    defining_identifier_list : mode  subtype_mark [:= default_expression]
  | defining_identifier_list : access_definition [:= default_expression]

<a href="06-01.htm#syntax">6.1</a>:
mode ::= [in] | in out | out

<a href="06-03.htm#syntax">6.3</a>:
subprogram_body ::=
    subprogram_specification is
       declarative_part
    begin
        handled_sequence_of_statements
    end [designator];

<a href="06-04.htm#syntax">6.4</a>:
procedure_call_statement ::=
    procedure_name;
  | procedure_prefix actual_parameter_part;

<a href="06-04.htm#syntax">6.4</a>:
function_call ::=
    function_name
  | function_prefix actual_parameter_part

<a href="06-04.htm#syntax">6.4</a>:
actual_parameter_part ::=
    (parameter_association {, parameter_association})

<a href="06-04.htm#syntax">6.4</a>:
parameter_association ::=
   [formal_parameter_selector_name =&gt;] explicit_actual_parameter

<a href="06-04.htm#syntax">6.4</a>:
explicit_actual_parameter ::= expression | variable_name

<a href="06-05.htm#syntax">6.5</a>:
return_statement ::= return [expression];

<a href="07-01.htm#syntax">7.1</a>:
package_declaration ::= package_specification;

<a href="07-01.htm#syntax">7.1</a>:
package_specification ::=
    package defining_program_unit_name is
      {basic_declarative_item}
   [private
      {basic_declarative_item}]
    end [[parent_unit_name.]identifier]

<a href="07-02.htm#syntax">7.2</a>:
package_body ::=
    package body defining_program_unit_name is
       declarative_part
   [begin
        handled_sequence_of_statements]
    end [[parent_unit_name.]identifier];

<a href="07-03.htm#syntax">7.3</a>:
private_type_declaration ::=
   type defining_identifier [discriminant_part] is [[abstract] tagged] [limited] private;

<a href="07-03.htm#syntax">7.3</a>:
private_extension_declaration ::=
   type defining_identifier [discriminant_part] is
     [abstract] new ancestor_subtype_indication with private;

<a href="08-04.htm#syntax">8.4</a>:
use_clause ::= use_package_clause | use_type_clause

<a href="08-04.htm#syntax">8.4</a>:
use_package_clause ::= use package_name {, package_name};

<a href="08-04.htm#syntax">8.4</a>:
use_type_clause ::= use type subtype_mark {, subtype_mark};

<a href="08-05.htm#syntax">8.5</a>:
renaming_declaration ::=
      object_renaming_declaration
    | exception_renaming_declaration
    | package_renaming_declaration
    | subprogram_renaming_declaration
    | generic_renaming_declaration

<a href="08-05-01.htm#syntax">8.5.1</a>:
object_renaming_declaration ::= defining_identifier : subtype_mark renames object_name;

<a href="08-05-02.htm#syntax">8.5.2</a>:
exception_renaming_declaration ::= defining_identifier : exception renames exception_name;

<a href="08-05-03.htm#syntax">8.5.3</a>:
package_renaming_declaration ::= package defining_program_unit_name renamespackage_name;

<a href="08-05-04.htm#syntax">8.5.4</a>:
subprogram_renaming_declaration ::= subprogram_specification renames callable_entity_name;

<a href="08-05-05.htm#syntax">8.5.5</a>:
generic_renaming_declaration ::=
    generic package       defining_program_unit_name renames generic_package_name;
  | generic procedure     defining_program_unit_name renames generic_procedure_name;
  | generic function      defining_program_unit_name renames generic_function_name;

<a href="09-01.htm#syntax">9.1</a>:
task_type_declaration ::=
   task type defining_identifier [known_discriminant_part] [is task_definition];

<a href="09-01.htm#syntax">9.1</a>:
single_task_declaration ::=
   task defining_identifier [is task_definition];

<a href="09-01.htm#syntax">9.1</a>:
task_definition ::=
     {task_item}
  [ private
     {task_item}]
  end [task_identifier]

<a href="09-01.htm#syntax">9.1</a>:
task_item ::= entry_declaration | representation_clause

<a href="09-01.htm#syntax">9.1</a>:
task_body ::=
   task body defining_identifier is
     declarative_part
   begin
     handled_sequence_of_statements
   end [task_identifier];
<a href="09-04.htm#syntax">9.4</a>:
protected_type_declaration ::=
  protected type defining_identifier [known_discriminant_part] is protected_definition;

<a href="09-04.htm#syntax">9.4</a>:
single_protected_declaration ::=
  protected defining_identifier is protected_definition;

<a href="09-04.htm#syntax">9.4</a>:
protected_definition ::=
    { protected_operation_declaration }
[ private
    { protected_element_declaration } ]
  end [protected_identifier]

<a href="09-04.htm#syntax">9.4</a>:
protected_operation_declaration ::= subprogram_declaration
     | entry_declaration
     | representation_clause

<a href="09-04.htm#syntax">9.4</a>:
protected_element_declaration ::= protected_operation_declaration
     | component_declaration

<a href="09-04.htm#syntax">9.4</a>:
protected_body ::=
  protected body defining_identifier is
   { protected_operation_item }
  end [protected_identifier];

<a href="09-04.htm#syntax">9.4</a>:
protected_operation_item ::= subprogram_declaration
     | subprogram_body
     | entry_body
     | representation_clause

<a href="09-05-02.htm#syntax">9.5.2</a>:
entry_declaration ::=
   entry defining_identifier [(discrete_subtype_definition)] parameter_profile;

<a href="09-05-02.htm#syntax">9.5.2</a>:
accept_statement ::=
   accept entry_direct_name [(entry_index)] parameter_profile [do
     handled_sequence_of_statements
   end [entry_identifier]];

<a href="09-05-02.htm#syntax">9.5.2</a>:
entry_index ::= expression

<a href="09-05-02.htm#syntax">9.5.2</a>:
entry_body ::=
  entry defining_identifier  entry_body_formal_part  entry_barrier is
    declarative_part
  begin
    handled_sequence_of_statements
  end [entry_identifier];

<a href="09-05-02.htm#syntax">9.5.2</a>:
entry_body_formal_part ::= [(entry_index_specification)] parameter_profile

<a href="09-05-02.htm#syntax">9.5.2</a>:
entry_barrier ::= when condition

<a href="09-05-02.htm#syntax">9.5.2</a>:
entry_index_specification ::= for defining_identifier in discrete_subtype_definition

<a href="09-05-03.htm#syntax">9.5.3</a>:
entry_call_statement ::= entry_name [actual_parameter_part];

<a href="09-05-04.htm#syntax">9.5.4</a>:
requeue_statement ::= requeue entry_name [with abort];

<a href="09-06.htm#syntax">9.6</a>:
delay_statement ::= delay_until_statement | delay_relative_statement

<a href="09-06.htm#syntax">9.6</a>:
delay_until_statement ::= delay until delay_expression;

<a href="09-06.htm#syntax">9.6</a>:
delay_relative_statement ::= delay delay_expression;

<a href="09-07.htm#syntax">9.7</a>:
select_statement ::=
   selective_accept
  | timed_entry_call
  | conditional_entry_call
  | asynchronous_select

<a href="09-07-01.htm#syntax">9.7.1</a>:
selective_accept ::=
  select
   [guard]
     select_alternative
{ or
   [guard]
     select_alternative }
[ else
   sequence_of_statements ]
  end select;

<a href="09-07-01.htm#syntax">9.7.1</a>:
guard ::= when condition =&gt;

<a href="09-07-01.htm#syntax">9.7.1</a>:
select_alternative ::=
   accept_alternative
  | delay_alternative
  | terminate_alternative

<a href="09-07-01.htm#syntax">9.7.1</a>:
accept_alternative ::=
  accept_statement [sequence_of_statements]

<a href="09-07-01.htm#syntax">9.7.1</a>:
delay_alternative ::=
  delay_statement [sequence_of_statements]

<a href="09-07-01.htm#syntax">9.7.1</a>:
terminate_alternative ::= terminate;

<a href="09-07-02.htm#syntax">9.7.2</a>:
timed_entry_call ::=
  select
   entry_call_alternative
  or
   delay_alternative
  end select;

<a href="09-07-02.htm#syntax">9.7.2</a>:
entry_call_alternative ::=
  entry_call_statement [sequence_of_statements]

<a href="09-07-03.htm#syntax">9.7.3</a>:
conditional_entry_call ::=
  select
   entry_call_alternative
  else
   sequence_of_statements
  end select;

<a href="09-07-04.htm#syntax">9.7.4</a>:
asynchronous_select ::=
  select
   triggering_alternative
  then abort
   abortable_part
  end select;

<a href="09-07-04.htm#syntax">9.7.4</a>:
triggering_alternative ::= triggering_statement [sequence_of_statements]

<a href="09-07-04.htm#syntax">9.7.4</a>:
triggering_statement ::= entry_call_statement | delay_statement

<a href="09-07-04.htm#syntax">9.7.4</a>:
abortable_part ::= sequence_of_statements

<a href="09-08.htm#syntax">9.8</a>:
abort_statement ::= abort task_name {, task_name};

<a href="10-01-01.htm#syntax">10.1.1</a>:
compilation ::= {compilation_unit}

<a href="10-01-01.htm#syntax">10.1.1</a>:
compilation_unit ::=
    context_clause library_item
  | context_clause subunit

<a href="10-01-01.htm#syntax">10.1.1</a>:
library_item ::= [private] library_unit_declaration
  | library_unit_body
  | [private] library_unit_renaming_declaration

<a href="10-01-01.htm#syntax">10.1.1</a>:
library_unit_declaration ::=
     subprogram_declaration | package_declaration
   | generic_declaration  | generic_instantiation

<a href="10-01-01.htm#syntax">10.1.1</a>:
library_unit_renaming_declaration ::=
   package_renaming_declaration
 | generic_renaming_declaration
 | subprogram_renaming_declaration

<a href="10-01-01.htm#syntax">10.1.1</a>:
library_unit_body ::= subprogram_body | package_body

<a href="10-01-01.htm#syntax">10.1.1</a>:
parent_unit_name ::= name

<a href="10-01-02.htm#syntax">10.1.2</a>:
context_clause ::= {context_item}

<a href="10-01-02.htm#syntax">10.1.2</a>:
context_item ::= with_clause | use_clause

<a href="10-01-02.htm#syntax">10.1.2</a>:
with_clause ::= with library_unit_name {, library_unit_name};

<a href="10-01-03.htm#syntax">10.1.3</a>:
body_stub ::= subprogram_body_stub | package_body_stub | task_body_stub | protected_body_stub

<a href="10-01-03.htm#syntax">10.1.3</a>:
subprogram_body_stub ::= subprogram_specification is separate;

<a href="10-01-03.htm#syntax">10.1.3</a>:
package_body_stub ::= package body defining_identifier is separate;

<a href="10-01-03.htm#syntax">10.1.3</a>:
task_body_stub ::= task body defining_identifier is separate;

<a href="10-01-03.htm#syntax">10.1.3</a>:
protected_body_stub ::= protected body defining_identifier is separate;

<a href="10-01-03.htm#syntax">10.1.3</a>:
subunit ::= separate (parent_unit_name) proper_body

<a href="11-01.htm#syntax">11.1</a>:
exception_declaration ::= defining_identifier_list : exception;

<a href="11-02.htm#syntax">11.2</a>:
handled_sequence_of_statements ::=
     sequence_of_statements
  [exception
     exception_handler
    {exception_handler}]

<a href="11-02.htm#syntax">11.2</a>:
exception_handler ::=
  when [choice_parameter_specification:] exception_choice {| exception_choice} =&gt;
     sequence_of_statements

<a href="11-02.htm#syntax">11.2</a>:
choice_parameter_specification ::= defining_identifier

<a href="11-02.htm#syntax">11.2</a>:
exception_choice ::= exception_name | others

<a href="11-03.htm#syntax">11.3</a>:
raise_statement ::= raise [exception_name];

<a href="12-01.htm#syntax">12.1</a>:
generic_declaration ::= generic_subprogram_declaration | generic_package_declaration

<a href="12-01.htm#syntax">12.1</a>:
generic_subprogram_declaration ::=
     generic_formal_part  subprogram_specification;

<a href="12-01.htm#syntax">12.1</a>:
generic_package_declaration ::=
     generic_formal_part  package_specification;

<a href="12-01.htm#syntax">12.1</a>:
generic_formal_part ::= generic {generic_formal_parameter_declaration | use_clause}

<a href="12-01.htm#syntax">12.1</a>:
generic_formal_parameter_declaration ::=
      formal_object_declaration
    | formal_type_declaration
    | formal_subprogram_declaration
    | formal_package_declaration

<a href="12-03.htm#syntax">12.3</a>:
generic_instantiation ::=
     package defining_program_unit_name is
         new generic_package_name [generic_actual_part];
   | procedure defining_program_unit_name is
         new generic_procedure_name [generic_actual_part];
   | function defining_designator is
         new generic_function_name [generic_actual_part];

<a href="12-03.htm#syntax">12.3</a>:
generic_actual_part ::=
   (generic_association {, generic_association})

<a href="12-03.htm#syntax">12.3</a>:
generic_association ::=
   [generic_formal_parameter_selector_name =&gt;] explicit_generic_actual_parameter

<a href="12-03.htm#syntax">12.3</a>:
explicit_generic_actual_parameter ::= expression | variable_name
   | subprogram_name | entry_name | subtype_mark
   | package_instance_name

<a href="12-04.htm#syntax">12.4</a>:
formal_object_declaration ::=
    defining_identifier_list : mode subtype_mark [:= default_expression];

<a href="12-05.htm#syntax">12.5</a>:
formal_type_declaration ::=
    type defining_identifier[discriminant_part] is formal_type_definition;

<a href="12-05.htm#syntax">12.5</a>:
formal_type_definition ::=
      formal_private_type_definition
    | formal_derived_type_definition
    | formal_discrete_type_definition
    | formal_signed_integer_type_definition
    | formal_modular_type_definition
    | formal_floating_point_definition
    | formal_ordinary_fixed_point_definition
    | formal_decimal_fixed_point_definition
    | formal_array_type_definition
    | formal_access_type_definition

<a href="12-05-01.htm#syntax">12.5.1</a>:
formal_private_type_definition ::= [[abstract] tagged] [limited] private

<a href="12-05-01.htm#syntax">12.5.1</a>:
formal_derived_type_definition ::= [abstract] new subtype_mark [with private]

<a href="12-05-02.htm#syntax">12.5.2</a>:
formal_discrete_type_definition ::= (&lt;&gt;)

<a href="12-05-02.htm#syntax">12.5.2</a>:
formal_signed_integer_type_definition ::= range &lt;&gt;

<a href="12-05-02.htm#syntax">12.5.2</a>:
formal_modular_type_definition ::= mod &lt;&gt;

<a href="12-05-02.htm#syntax">12.5.2</a>:
formal_floating_point_definition ::= digits &lt;&gt;

<a href="12-05-02.htm#syntax">12.5.2</a>:
formal_ordinary_fixed_point_definition ::= delta &lt;&gt;

<a href="12-05-02.htm#syntax">12.5.2</a>:
formal_decimal_fixed_point_definition ::= delta &lt;&gt; digits &lt;&gt;

<a href="12-05-03.htm#syntax">12.5.3</a>:
formal_array_type_definition ::= array_type_definition

<a href="12-05-04.htm#syntax">12.5.4</a>:
formal_access_type_definition ::= access_type_definition

<a href="12-06.htm#syntax">12.6</a>:
formal_subprogram_declaration ::= with subprogram_specification [is subprogram_default];

<a href="12-06.htm#syntax">12.6</a>:
subprogram_default ::= default_name | &lt;&gt;

<a href="12-06.htm#syntax">12.6</a>:
default_name ::= name

<a href="12-07.htm#syntax">12.7</a>:
formal_package_declaration ::=
    with package defining_identifier is new generic_package_name  formal_package_actual_part;

<a href="12-07.htm#syntax">12.7</a>:
formal_package_actual_part ::=
    (&lt;&gt;) | [generic_actual_part]

<a href="13-01.htm#syntax">13.1</a>:
representation_clause ::= attribute_definition_clause
      | enumeration_representation_clause
      | record_representation_clause
      | at_clause

<a href="13-01.htm#syntax">13.1</a>:
local_name ::= direct_name
      | direct_name'attribute_designator
      | library_unit_name

<a href="13-03.htm#syntax">13.3</a>:
attribute_definition_clause ::=
      for local_name'attribute_designator use expression;
    | for local_name'attribute_designator use name;

<a href="13-04.htm#syntax">13.4</a>:
enumeration_representation_clause ::=
    for first_subtype_local_name use enumeration_aggregate;

<a href="13-04.htm#syntax">13.4</a>:
enumeration_aggregate ::= array_aggregate

<a href="13-05-01.htm#syntax">13.5.1</a>:
record_representation_clause ::=
    for first_subtype_local_name use
      record [mod_clause]
        {component_clause}
      end record;

<a href="13-05-01.htm#syntax">13.5.1</a>:
component_clause ::=
    component_local_name at position range first_bit .. last_bit;

<a href="13-05-01.htm#syntax">13.5.1</a>:
position ::= static_expression

<a href="13-05-01.htm#syntax">13.5.1</a>:
first_bit ::= static_simple_expression

<a href="13-05-01.htm#syntax">13.5.1</a>:
last_bit ::= static_simple_expression

<a href="13-08.htm#syntax">13.8</a>:
code_statement ::= qualified_expression;

<a href="13-12.htm#syntax">13.12</a>:
restriction ::= restriction_identifier
    | restriction_parameter_identifier =&gt; expression

<a href="J-03.htm#syntax">J.3</a>:
delta_constraint ::= delta static_expression [range_constraint]

<a href="J-07.htm#syntax">J.7</a>:
at_clause ::= for direct_name use at expression;

<a href="J-08.htm#syntax">J.8</a>:
mod_clause ::= at mod static_expression;
</pre></dl><hr>
<h4><a href="N.htm"><img src="left.gif" alt="Prev"></a> | <a href="toc.htm"><img src="up.gif" alt="Up"></a> | <img src="noright.gif" alt="____"> | <a href="N.htm"><img src="back.gif" alt="Back"></a> | <a href="index.htm"><img src="forward.gif" alt="Forward"></a><br>   <a href="toc.htm">TOC --</a> / <a href="toc-l1.htm">--.--</a> / <a href="toc-l2.htm">--.--.--</a> | <a href="index.htm">Index</a> | <a href="search.htm">Search</a> | <a href="syntax.htm">Syntax</a> | <a href="help.htm">Help</a></h4>

<p>
<a href="http://lglwww.epfl.ch/Ada/"><img src="la-icon.gif" alt="Ada WWW Home"></a>
 -- Email comments, additions, corrections, gripes, kudos, etc. to:
<address>
<a href="author.htm">Magnus Kempe</a>
 -- <a href="mailto:Magnus.Kempe@di.epfl.ch">Magnus.Kempe@di.epfl.ch</a><br>
<a href="cright.htm">Copyright statement</a><br>
Page last generated: 95-03-12
</address>
</body>
</html>
