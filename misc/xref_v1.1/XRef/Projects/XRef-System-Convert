;
; $PROJECT: Include & AutoDoc convert installer-script
;
; $VER: XRef-System-Convert 1.2 (03.10.94)
; by
;
; Stefan Ruppert , Windthorststraße 5 , 65439 Flörsheim , GERMANY
;
; (C) Copyright 1994
; All Rights Reserved !
;

(set toolsdir
(cat "/Tools/c"
))

(set #generate-what
(cat "What AmigaGuide files would you generate ?"
))

(set #system-include-dir
(cat "Select your system include directory :"
))

(set #system-autodoc-dir
(cat "Select your system autodoc directory :"
))

(set #which-autodoc-destdir
(cat "Select the directory to put the generated AutoDoc AmigaGuide files :"
))

(set #which-include-destdir
(cat "Select the directory to put the generated Include AmigaGuide files :"
))


(set opt (askoptions (prompt #generate-what)
                     (help #generate-what-help)
                     (choices "System Autodocs"
                              "System Includes")
                     (default 0)
         )
)

(if (>= opt 0)
    (
        (
            (set includedir (askdir (prompt #system-include-dir)
                                    (help #system-include-dir-help)
                                    (default "Include:")
                            )
            )
            (set autodocdir (askdir (prompt #system-autodoc-dir)
                                    (help #system-autodoc-dir-help)
                                    (default "Autodocs:")
                            )
            )
        )

        (set vernum (getversion "xref.library" (resident)))
        (if (>= vernum 0)
            (
                (message "Removing all XRefFiles in memory with :\n\n"
                         "ExpungeXRef FORCE")
                (run (tackon toolsdir "ExpungeXRef") " FORCE")
            )
        )

        (message (cat "Now generating System AutoDocs XRef's with :\n\n"
                      "MakeXRef FROM " autodocdir " TO T:sys_autodoc.xref CATEGORY SysAutoDoc VERBOSE"))

        (run (cat (tackon toolsdir "MakeXRef") " FROM " autodocdir " TO T:sys_autodoc.xref CATEGORY SysAutoDoc VERBOSE >CON:////MakeXRef-System-AutoDocs"))

        (message (cat "Now generating System Include XRef's with :\n\n"
                      "MakeXRef FROM " includedir " TO T:sys_include.xref CATEGORY SysInclude PATH AG:SysInc VERBOSE"))

        (run (cat (tackon toolsdir "MakeXRef") " FROM " includedir " TO T:sys_include.xref CATEGORY SysInclude PATH AG:SysInc VERBOSE >CON:////MakeXRef-System-Includes"))

        (message (cat "Now loading the generated XReffiles with :\n\n"
                      "LoadXRef T:sys_autodoc.xref T:sys_include.xref"))

        (run (cat (tackon toolsdir "LoadXRef") " T:sys_autodoc.xref T:sys_include.xref"))

        (if (IN opt 0)
            (
                (set destdir (askdir (prompt #which-autodoc-destdir)
                                     (help #which-autodoc-destdir-help)
                                     (default "Work:")
                             )
                )
                (message (cat "Now Converting System Autodocs to AmigaGuide files with :\n\n"
                              "XRefConvert FROM " autodocdir " TO " destdir " CATEGORY Sys#?"))
                (run (cat (tackon toolsdir "XRefConvert") " FROM " autodocdir " TO " destdir " CATEGORY Sys#? VERBOSE >CON:////XRefConvert-System-AutoDocs"))
            )
        )

        (if (IN opt 1)
            (
                (set destdir (askdir (prompt #which-include-destdir)
                                     (help #which-include-destdir-help)
                                     (default "Work:")
                             )
                )
                (message (cat "Now Converting System Includes to AmigaGuide files with :\n\n"
                              "XRefConvert FROM " includedir " TO " destdir " CATEGORY Sys#?"))
                (run (cat (tackon toolsdir "XRefConvert") " FROM " includedir " TO " destdir " CATEGORY Sys#? VERBOSE >CON:////XRefConvert-System-Includes"))
            )
        )
    )
)


