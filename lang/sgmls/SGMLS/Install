;
; Installation script for sgmls
;
; created on 13.06.1995 by Kai Hofmann
;

(if (= @language "english")
    (
     (set #welcome (cat "Welcome to the sgmls parser!\n\n"))
     (set #iuquest ("What do you want to do with the %s parser?\n\n" @app-name))
     (set #iuc1    "Install")
     (set #iuc2    "Uninstall")
     (set #use37   ("You must be using OS 2.04 or higher to install and use the %s parser" @app-name))
     (set #exedestdir ("Where do you want to install the %s parser?" @app-name))

     (set #docdestdir "To which directory should I copy the .doc files?")
     (set #del1 "Deleting .info file - it is created by an installer bug!")

     (set #remove ("Removing %s" @app-name))
     (set #notfound ("Sorry, could not found %s" @app-name))
     (set #endunin ("\nUninstalling %s finished, but if you have installed the docs, so please remove this by hand!" @app-name))
    )
)

(if (= 0 (askchoice (prompt #iuquest)
                    (choices #iuc1 #iuc2)
                    (default 0)
                    (help @askchoice-help)
         )
    )
    (
     ; Install
     (if (< (/ (getversion) 65536) 37)
         (abort #use37)
     )
     (welcome #welcome)
     (complete 0)
     (set @default-dest "c:")
     (set @default-dest (askdir (prompt #exedestdir)
                                (default @default-dest)
                                (help @askdir-help)
                        )
     )
     (if (= @language "english") ;then
         (
          (set #cplprompt ("Copying %s to %s" @app-name @default-dest))
         )
     )
     (copyfiles (prompt #cplpromt)
                (source "bin/")
                (dest @default-dest)
                (all)
                (optional "askuser")
                (help @copyfiles-help)
     )
     (complete 50)
     (set destination (askdir (prompt #docdestdir)
                              (default "SYS:")
                              (help @askdir-help)
                      )
     )
     (if (= @language "english") ;then
         (
          (set #cplprompt ("Copying %s to %s" @app-name destination))
         )
     )
     (copyfiles (prompt #cplpromt)
                (source "doc/")
                (dest destination)
                (infos)
                (all)
                (optional "askuser")
                (help @copyfiles-help)
     )
     (delete (tackon destination ".info")
             (prompt #del1)
             (optional "force")
             (help @delete-help)
     )
     (complete 100)
    )
;else
    (
     ; Uninstall
     (if (exists "c:sgmls" (noreq))
         (delete "c:sgmls"
                 (prompt #remove)
                 (help @delete-help)
                 (optional "force")
         )
         (message #notfound)
     )
     (complete 33)
     (if (exists "c:sgmlsasp" (noreq))
         (delete "c:sgmlsasp"
                 (prompt #remove)
                 (help @delete-help)
                 (optional "force")
         )
         (message #notfound)
     )
     (complete 66)
     (if (exists "c:rast" (noreq))
         (delete "c:rast"
                 (prompt #remove)
                 (help @delete-help)
                 (optional "force")
         )
         (message #notfound)
     )
     (complete 100)
     (message #endunin)
     (exit (quiet))
    )
)
