// finds prime-numbers, conv from a C-source
// change user settings

#include <vm.nah>

int maxnum;
int proutp;
int maxline;
int showtime;
int filen;

lint sqrlimit;
lint limit;
lint max2;

// USER SETTINGS ---------------------------------------------------------

    push_i      10, L0;                 maxnum, numbers per line
    pull_i      L0, maxnum;

    push_i      1, L0;                  proutp, for printer-formatted output set to 1
    pull_i      L0, proutp;

    push_i      69, L0;                 maxline, max lines per page
    pull_i      L0, maxline;

    push_i      1, L0;                  showtime, print time info (0 / 1)
    pull_i      L0, showtime;

    push_l      1000L, L0;               sqrlimit, number limit
    pull_l      L0, sqrlimit;

// -----------------------------------------------------------------------

    mul_l       L0, L0, L1;
    pull_l      L1, limit;
    push_i      1, L2;
    sub_l       L1, L2, L3;
    pull_l      L3, max2;

    push_i      1, vmuse;

    push_l      limit, L0;
    push_i      _intsize, L1;
    mul_l       L0, L1, vmbsize;

    push_l      30000L, vmcachesize;

    byte primbuf[limit];

// initialize array by zero

    push_i      0, L0;                  null
    push_i      -1, L1;                 counter
    push_l      max2, L2;               max2

lab init;
    inc_l       L1;
    move_b_a    L0, primbuf, L1;

    ls_jmp_l    L1, L2, init;


// open file

    push_i      1, L0;                                          filenumber
    pull_i      L0, filen;
    fopen       L0, "primes.txt", "w";

    push_i      1, L1;
    push_i      showtime, L2;
    eq_l        L1, L2, L3;
    jsr_l       L3, start_time;

    print_c;

// search
// L2 = search, L3 = i, L4 = j, L5 = sqrlimit, L6 = max2, L7 = max

    push_i      0, L0;                  null
    push_i      1, L1;                  one
    push_i      0, L2;
    push_i      2, L3;                  i
    push_l      sqrlimit, L5;
    push_l      max2, L6;

lab search;
    neq_jmp_l   L2, L0, search_end;

    move_a_b    primbuf, L3, L7;
    neq_jmp_l   L7, L1, no_inc;

    inc_l       L3;

lab no_inc;
    add_l       L3, L3, L4;

lab search_2;
    gr_jmp_l    L4, L6, search_2_end;
    move_b_a    L1, primbuf, L4;
    add_l       L4, L3, L4;
    jmp         search_2;

lab search_2_end;
    greq_l      L3, L5, L2;
    inc_l       L3;

// print

    push_i      3, L9;
    locate      L9, L9;
    print_s     "done: ";
    print_l     L3;
    print_s     " of ";
    print_l     L5;
    print_n     L1;
    jmp search;

lab search_end;

    push_i      5, L2;
    push_i      1, L3;
    locate      L2, L3;

// L2 = search, L3 = i, L4 = j, L5 = sqrlimit, L6 = max2, L7 = max
// L8 = maxnum, L9 = maxline, L10 = proutp
// L11 = startnum, L12 = linecount, L13 = prnum, L14 = prspace, L15 = filen

    push_i      0, L0;
    push_i      1, L1;
    push_i      2, L3;
    push_l      max2, L6;
    push_i      maxnum, L8;
    push_i      maxline, L9;
    push_i      proutp, L10;
    push_i      0, L11;
    push_i      0, L12;
    push_i      10, L13;
    push_i      6, L14;
    push_i      filen, L15;
    push_l      100000L, L16;
    push_i      10, L17;
    push_i      4, L18;
    push_i      3, L19;

lab print;
    gr_jmp_l    L3, L6, print_end;

    move_a_b    primbuf, L3, L7;

    neq_jmp_l   L7, L0, next_print;
    inc_l       L11;
    neq_jmp_l   L11, L1, printno_marg;
    neq_jmp_l   L10, L1, printno_marg;

    fwrite_sp   L15, L18;

lab printno_marg;
    lseq_jmp_l  L3, L13, no_dec_prspace;
    greq_jmp_l  L3, L16, no_dec_prspace;

    mul_l       L13, L17, L13;
    dec_l       L14;

lab no_dec_prspace;
    fwrite_sp   L15, L14;
    fwrite_sl    L15, L3;

    neq_jmp_l   L11, L8, next_print;

    fwrite_n    L15, L1;

    neq_jmp_l   L10, L1, startnum_zero;
    inc_l       L12;
    neq_jmp_l   L12, L9, startnum_zero;

    fwrite_n    L15, L19;
    push_i      0, L12;

lab startnum_zero;
    push_i      0, L11;

lab next_print;
    inc_l       L3;
    jmp         print;

lab print_end;
    push_i      2, L1;
    fwrite_n    L15, L1;

    push_i      1, L1;
    push_i      showtime, L2;

    eq_jsr_l    L1, L2, stop_time;

    fclose      L15;
    push_i      0, L0;
    exit        L0;


lab start_time;
    push_i      filen, L0;
    fwrite_s    L0, "start-time ";
    jsr         showtime;
    rts;

lab stop_time;
    push_i      filen, L0;
    fwrite_s    L0, "stop-time ";
    jsr         showtime;
    rts;

lab showtime;
    time;

    push_i      _year, L0;
    push_i      _month, L1;
    push_i      _day, L2;
    push_i      _hour, L3;
    push_i      _min, L4;
    push_i      _sec, L5;

    push_i      0, L6;                  null
    push_i      filen, L7;              filenumber


// write day

    push_i      10, L8;                 ten
    ls_l        L2, L8, L9;
    jsr_l       L9, write_null;
    fwrite_sl   L7, L2;
    fwrite_s    L7, "-";


// write month

    ls_l        L1, L8, L9;
    jsr_l       L9, write_null;
    fwrite_sl   L7, L1;
    fwrite_s    L7, "-";


// write year

    fwrite_sl   L7, L0;
    push_i      2, L9;
    fwrite_sp   L7, L9;


// write hour

    ls_l        L3, L8, L9;
    jsr_l       L9, write_null;
    fwrite_sl   L7, L3;
    fwrite_s    L7, ":";


// write min

    ls_l        L4, L8, L9;
    jsr_l       L9, write_null;
    fwrite_sl   L7, L4;
    fwrite_s    L7, ":";


// write sec

    ls_l        L5, L8, L9;
    jsr_l       L9, write_null;
    fwrite_sl   L7, L5;
    push_i      2, L9;
    fwrite_n    L7, L9;
    rts;

lab write_null;
    fwrite_sl   L7, L6;
    rts;

