// tcp client
//
// sends messages

    #include <socket.nah>


    #setreg_l       L0, null;
    #setreg_l       L1, one;
    #setreg_l       L2, two;
    #setreg_l       L3, client;
    #setreg_l       L4, err_sock;
    #setreg_l       L5, err_sock_ok;
    #setreg_l       L6, err_connrefused;
    #setreg_l       L7, err_nosys;
    #setreg_l       L8, port;
    #setreg_l       L9, client_open;
    #setreg_l       L10, len;
    #setreg_l       L11, if;
    #setreg_l       L12, args;

    string ip[16];
    string buf[256];
    string ip_arg[256];
    string sh[10];
    string lg[8];

    push_i          0, null;
    push_i          1, one;
    push_i          2, two;
    push_i          ERR_SOCK_OK, err_sock_ok;
    push_i          ERR_CONNREFUSED, err_connrefused;
    push_i          ERR_NOSYS, err_nosys;

    move_l          null, client;
    move_l          null, client_open;
    move_s          "/shutdown", sh;
    move_s          "/logout", lg;


//  check arguments

    argnum          args;
    neq_jmp_l       args, one, show_args;

    argstr          null, ip_arg;

    argstr          one, buf;
    val_l           buf, port;

//  open client

    hostbyname      ip_arg, ip;
    push_i          _sock, err_sock;
    eq_jmp_l        err_sock, err_sock_ok, ip_ok;

    hostbyaddr      ip_arg, ip;

lab ip_ok;
    scopen          client, ip, port;
    jsr             check_err;
    move_l          one, client_open;


//  print commands

    print_n         two;
    print_s         "commands:";
    print_n         one;
    print_s         lg;
    print_s         "   : logout";
    print_n         one;
    print_s         sh;
    print_s         " : shutdown server";
    print_n         two;

lab send;
    print_s         "message? ";
    input_s         buf;
    strlen          buf, len;

//  check for empty string

	eq_jsr_l		len, null, empty_string;

    swrite_l        client, len;
    jsr             check_err;

    swrite_s        client, buf;
    jsr             check_err;

    eq_s            buf, lg, if;
    jmp_l           if, send_end;

    eq_s            buf, sh, if;
    jmp_l           if, send_end;

    jmp             send;

lab send_end;
    scclose         client;
    jsr             check_err;
    move_l          null, client_open;

lab end;
    eq_jsr_l        client_open, one, close_client;

    exit            null;


lab close_client;
    scclose         client;
    rts;

lab check_err;
    push_i          _sock, err_sock;
    neq_jmp_l       err_sock, err_sock_ok, error;
    rts;

lab error;
    print_s         "socket error: ";
    print_l         err_sock;

    eq_jsr_l        err_sock, err_connrefused, no_server;

    eq_jsr_l        err_sock, err_nosys, no_tcp;

    print_n         one;
    jmp             end;

lab empty_string;
	move_s			" ", buf;
	move_l			one, len;
	rts;

lab no_server;
    print_n         one;
    print_s         "server not found!";
    print_n         one;
    rts;

lab no_tcp;
    print_n         one;
    print_s         "tcp stack not running!";
    print_n         one;
    rts;

lab show_args;
    print_s         "client <ip> <port>";
    print_n         one;
    jmp             end;

