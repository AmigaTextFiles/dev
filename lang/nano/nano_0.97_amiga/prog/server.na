// tcp server
//
// prints the client messages

    #include <socket.nah>


    #setreg_l       L0, null;
    #setreg_l       L1, one;
    #setreg_l       L2, two;
    #setreg_l       L3, server;
    #setreg_l       L4, err_sock;
    #setreg_l       L5, err_sock_ok;
    #setreg_l       L6, err_addrinuse;
    #setreg_l       L7, err_nosys;
    #setreg_l       L8, port;
    #setreg_l       L9, server_open;
    #setreg_l       L10, server_act_open;
    #setreg_l       L11, len;
    #setreg_l       L12, shutdown;
    #setreg_l       L13, if;
    #setreg_l       L14, args;

    string ip[16];
    string buf[256];
    string ip_arg[256];
    string sh[10];
    string lg[8];

    push_i          0, null;
    push_i          1, one;
    push_i          2, two;
    push_i          ERR_SOCK_OK, err_sock_ok;
    push_i          ERR_ADDRINUSE, err_addrinuse;
    push_i          ERR_NOSYS, err_nosys;

    move_l          null, server;
    move_l          null, server_open;
    move_l          null, server_act_open;
    move_s          "/shutdown", sh;
    move_s          "/logout", lg;


//  check arguments

    argnum          args;
    neq_jmp_l       args, one, show_args;

    argstr          null, ip_arg;

    argstr          one, buf;
    val_l           buf, port;

//  open server

    hostbyname      ip_arg, ip;
    push_i          _sock, err_sock;
    eq_jmp_l        err_sock, err_sock_ok, ip_ok;

    hostbyaddr      ip_arg, ip;

lab ip_ok;
    ssopen          server, ip, port;
    jsr             check_err;
    move_l          one, server_open;


lab wait_conn;
    print_n         one;
    print_s         "waiting...";
    print_n         two;

    ssopenact       server;
    jsr             check_err;
    move_l          one, server_act_open;

    clientaddr      server, buf;
    print_s         "get message from: ";
    print_s         buf;
    print_n         two;

lab read;
    sread_l         server, len;
    jsr             check_err;

    sread_s         server, buf, len;
    jsr             check_err;

    print_s         buf;
    print_n         one;

    eq_s            buf, lg, if;
    jmp_l           if, read_end;

    eq_s            buf, sh, shutdown;
    jmp_l           shutdown, read_end;

    jmp             read;

lab read_end;
    wait_s          one;
    print_n         one;
    print_s         "client logged out.";
    print_n         one;

    sscloseact      server;
    jsr             check_err;
    move_l          null, server_act_open;

    jmp_l           shutdown, shutdown;

    jmp             wait_conn;

lab shutdown;
    wait_s          one;
    print_s         "shutdown...";
    print_n         two;

    ssclose         server;
    jsr             check_err;
    move_l          null, server_open;

lab end;
    eq_jsr_l        server_act_open, one, close_act_server;
    eq_jsr_l        server_open, one, close_server;

    exit            null;


lab close_act_server;
    sscloseact      server;
    rts;

lab close_server;
    ssclose         server;
    rts;

lab check_err;
    push_i          _sock, err_sock;
    neq_jmp_l       err_sock, err_sock_ok, error;
    rts;

lab error;
    print_s         "socket error: ";
    print_l         err_sock;

    eq_jsr_l        err_sock, err_addrinuse, addrinuse;

    eq_jsr_l        err_sock, err_nosys, no_tcp;

    print_n         one;
    jmp             end;

lab addrinuse;
    print_n         one;
    print_s         "address already in use!";
    print_n         one;
    rts;

lab no_tcp;
    print_n         one;
    print_s         "tcp stack not running!";
    print_n         one;
    rts;

lab show_args;
    print_s         "server <ip> <port>";
    print_n         one;
    jmp             end;

