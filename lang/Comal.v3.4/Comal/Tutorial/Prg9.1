// Program 9.1

USE CITWindow
USE CITGadgets
USE Turtle
USE Figures

graphicscreen(0)
ht

DIM TriangleGad OF ButtonGadget
TriangleGad.Size(120,20)
TriangleGad.Position(0,0)
TriangleGad.Label("Triangle",INSIDE)
TriangleGad.EventHandler(TriangleEvent())
ComalWindow.InsObject(TriangleGad,Error)
PROC TriangleEvent(Id OF USHORT)
  clearscreen
  triangle
ENDPROC TriangleEvent

DIM SquareGad OF ButtonGadget
SquareGad.Size(120,20)
SquareGad.Position(120,0)
SquareGad.Label("Square",INSIDE)
SquareGad.EventHandler(SquareEvent())
ComalWindow.InsObject(SquareGad,Error)
PROC SquareEvent(Id OF USHORT)
  clearscreen
  square
ENDPROC SquareEvent

DIM PentagonGad OF ButtonGadget
PentagonGad.Size(120,20)
PentagonGad.Position(240,0)
PentagonGad.Label("Pentagon",INSIDE)
PentagonGad.EventHandler(PentagonEvent())
ComalWindow.InsObject(PentagonGad,Error)
PROC PentagonEvent(Id OF USHORT)
  clearscreen
  pentagon
ENDPROC PentagonEvent

DIM HexagonGad OF ButtonGadget
HexagonGad.Size(120,20)
HexagonGad.Position(360,0)
HexagonGad.Label("Hexagon",INSIDE)
HexagonGad.EventHandler(HexagonEvent())
ComalWindow.InsObject(HexagonGad,Error)
PROC HexagonEvent(Id OF USHORT)
  clearscreen
  hexagon
ENDPROC HexagonEvent

DIM Close OF ButtonGadget
Close.Size(138,20)
Close.Position(480,0)
Close.Label("STOP",INSIDE)
ComalWindow.InsObject(Close,Error)

IF Error THEN
  PRINT "Could not create one or more gadgets"
ENDIF

viewport(0,width-1,0,height-21)

WHILE NOT Close.Pressed DO WAIT

textscreen


// ************* end of main program **********

MODULE Figures

  USE Turtle

  EXPORT triangle,square,pentagon,hexagon

  PROC triangle
    Polygon(3)
  ENDPROC triangle

  PROC square
    Polygon(4)
  ENDPROC square

  PROC pentagon
    Polygon(5)
  ENDPROC pentagon

  PROC hexagon
    Polygon(6)
  ENDPROC hexagon

  PROC Polygon(n)
    LOOP n TIMES
      forward(50)
      right(360/n)
    ENDLOOP
  ENDPROC Polygon

ENDMODULE Figures
