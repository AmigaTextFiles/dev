@database Contents
@master PCQ:Hyper/pcq/pcq3.guide
@$VER: 1.0
@author "Nils Sjoholm"
@(c) "@ Nils Sjoholm"
@remark Created with Heddley v1.1 (c) Edd Dumbill 1994

@node "Main" "PCQ/Internal"




                         PCQ/Internal

                         Read to Sin

                          @{" Read    " link "read" 0}
                          @{" Readln  " link "readln" 0}
                          @{" ReOpen  " link "reopen" 0}
                          @{" Reset   " link "reset" 0}
                          @{" ReWrite " link "rewrite" 0}
                          @{" Round   " link "round" 0}
                          @{" Sin     " link "sin" 0}













@endnode

@node "read" "PCQ/Read"

    PCQ/Read

    NAME
        Read - input data from a PCQ file

    SYNOPSIS
        PROCEDURE Read([VAR InputFile; ] References );

    DESCRIPTION
        The Read procedure gets input from the specified file,
        or from the standard input file Input if no file is
        specified.  It assigns to each of the variable
        references the next value from the file.  If the
        file is a typed file, each reference must have the
        same type as the file.  If it is a Text file, the
        references can have one of several different types,
        and the sequence of characters read is converted to
        that type.

        If the reference is an Integer, Short, Word, or Byte
        type, Read first skips over any 'white space', e.g.
        spaces, tabs, etc.  It then reads a decimal number,
        possibly beginning with a minus sign.  The procedure
        reads all the decimal digits, and leaves the file
        pointer on the first character following the last
        digit.  If there were no digits before the first
        letter, Read sets IOResult to 59.  If there are no
        digits before the end of file, it sets IOResult to
        58.

        If the reference is a Char type, Read assigns to it
        the next character in the file.  Standard Pascal
        converts control characters into spaces, but PCQ
        passes them through unchanged.

        If the reference is a Real type, Read inputs the
        integer portion as described above.  If the next
        character is a decimal point, Read continues reading
        digits until the next character is a non-digit.  The
        file pointer is left on that digit.  The decimal point
        is optional, and there does not have to be any digits
        following the decimal point.  There does, however,
        have to be at least one digit before a decimal point.

        If the reference is a String type, Read inputs
        all the characters up to, but not including, the
        next end-of-line marker into the string.  The string
        must be long enough to hold all these characters.
        The file pointer is left on the end-of-line marker.

        If the reference is an Array of Char, Read inputs
        characters until it fills the entire array, or until
        the end-of-line is reached, whichever comes first.
        If it hits the end-of-line before filling the array,
        it pads the remaining part of the array with spaces.
        The file pointer is left either on the next character
        or the end-of-line marker.

        Read will set IOResult if it has any errors.  If
        IOResult is not zero when Read is called, no input
        will take place.

    EXAMPLE

        PROGRAM ShowRead;
        VAR
            i : Integer;
            r : Real;
            a : ARRAY [0..9] OF Char;
        BEGIN
            Read(i, a, r);
            Writeln(i, a:11, r:0:2);
        END.

        Given the input ' 456abcdefgh' (new line)
                        '      45.'
        this program will write: '456 abcdefgh  45.00'

    INPUTS

        InputFile  : A PCQ file variable, which must be opened
                     for input using Reopen or Reset
        References : Any number of variable references

    RESULTS
        None

    BUGS

    SEE ALSO

        @{" Readln  " link "readln" 0} @{" Write   " link "PCQ:Hyper/pcq/pcq4.guide/write" 0} @{" Writeln " link "PCQ:Hyper/pcq/pcq4.guide/writeln" 0}
        @{" Reopen  " link "reopen" 0} @{" Reset   " link "reset" 0} @{" Get     " link "PCQ:Hyper/pcq/pcq2.guide/get" 0}

@endnode

@node "readln" "PCQ/Readln"


    PCQ/Readln

    NAME
        Readln - Read input, then read the rest of the line

    SYNOPSIS
        PROCEDURE Readln([VAR InputFile : Text;] References );

    DESCRIPTION
        Readln calls Read to fill in each of the variables
        references, then continues reading from the file until
        either the end of file is reached, or the end-of-line
        marker has been read.  In the latter case the file
        pointer is placed on the first character of the next
        line.

        Readln can only be called on Text files, since Typed
        files do not use lines.

    EXAMPLE

        PROGRAM ShowReadln;
        VAR
            i,j : Integer;
        BEGIN
            Readln(i);
            Readln(j);
            Writeln(i, ',', j);
        END.

        Given the input: '456 The rest of the line is ignored' (new line)
                         '' (new line)
                         '678' (new line)

        the program will write: '456,678'

    INPUTS

        InputFile  : An open PCQ Text file
        References : Any number of variable references.

    RESULTS
        None

    BUGS

    SEE ALSO

        @{" Read    " link "read" 0} @{" Write   " link "PCQ:Hyper/pcq/pcq4.guide/write" 0} @{" Writeln " link "PCQ:Hyper/pcq/pcq4.guide/writeln" 0}
        @{" Reopen  " link "reopen" 0} @{" Reset   " link "reset" 0} @{" Get     " link "PCQ:Hyper/pcq/pcq2.guide/get" 0}

@endnode

@node "reopen" "PCQ/ReOpen"


    PCQ/ReOpen

    NAME
        ReOpen - open a file for reading

    SYNOPSIS
        FUNCTION ReOpen(FileName        : String;
                        VAR FileVar     : Any file type
                    [ ; BufferSize      : Integer ] ) : Boolean;

    DESCRIPTION
        ReOpen attempts to open an existing file for reading.
        ReOpen will return True if the file opens correctly, and
        False otherwise.  The opened file behaves exactly as if
        it was opened with Reset - this routine is provided just
        for convenience.

        ReOpen does not set IOResult, so you don't have to check
        it after a failed call.  If you want specific information
        about why the file could not be opened, use Reset and
        check IOResult directly.

        The BufferSize parameter is optional - if it is omitted,
        the compiler will use the value 128.

    EXAMPLE

        PROGRAM ShowText;
        {$I "Include:Utils/StringLib.i"}
        {$I "Include:Utils/Parameters.i"}
        VAR
            c        : Char;
            InFile   : Text;
            FileName : String;
        BEGIN
            FileName := AllocString(256);
            GetParam(1,FileName);
            IF ReOpen(FileName, InFile, 10000) THEN BEGIN
                WHILE NOT EOF(InFile) DO BEGIN
                    Read(InFile, c);
                    Write(c);
                END;
                Close(InFile);
            END ELSE
                Writeln('Could NOT open ', FileName);
        END.

    INPUTS

        FileName   : The AmigaDOS file name, with path if necessary.
        FileVar    : Any Pascal file type
        BufferSize : The number of bytes to allocate as a file buffer.
                     If this parameter is omitted, 128 is used.

    RESULTS
        TRUE if the file opened correctly, or
        FALSE if anything went wrong.

    BUGS

    SEE ALSO

        @{" Reset    " link "reset" 0} @{" Rewrite  " link "rewrite" 0} @{" Open     " link "PCQ:Hyper/pcq/pcq2.guide/open" 0} @{" IOResult " link "PCQ:Hyper/pcq/pcq2.guide/ioresult" 0}

@endnode

@node "reset" "PCQ/Reset"

    PCQ/Reset

    NAME
        Reset - open a file for reading

    SYNOPSIS
        PROCEDURE Reset(VAR F    : Any FILE TYPE;
                        FileName : String
                        [ ; BufferSize : Integer ] );

    DESCRIPTION
        The Reset procedure opens a PCQ file for input.  If the
        file cannot be opened, IOResult is set to a non-zero
        value.  If you are using automatic IO checking, the
        default, your program will terminate.  If you have used
        the {$I-} option to set IO checking to manual, you will
        need to check the IOResult function before continuing.

        If you specify a BufferSize, Reset will attempt to
        allocate a buffer of that size (actually the closest
        multiple of the record size).  If the BufferSize parameter
        is not used, the default is 128.

    EXAMPLE

        PROGRAM OpenUp;
        {$I "Include:Utils/StringLib.i"}
        {$I "Include:Utils/Parameters.i"}
        {$I-  set IO checking to manual }
        VAR
            FileName : String;
            TestFile : Text;
        BEGIN
            FileName := AllocString(256);
            GetParam(1,FileName);
            Reset(TestFile,FileName,1024);
            IF IOResult = 0 THEN BEGIN
                Writeln('The FILE is open');
                Close(TestFile);
            END ELSE
                Writeln('Could NOT open ', FileName);
        END.

    INPUTS

        F          : Any PCQ File type
        FileName   : A String specifying the file name and path, if
                     necessary.
        BufferSize : The size of the input buffer to allocate.

    RESULTS
        None

    BUGS

    SEE ALSO

        @{" Reopen  " link "reopen" 0} @{" Rewrite " link "rewrite" 0} @{" Open    " link "PCQ:Hyper/pcq/pcq2.guide/open" 0}

@endnode

@node "rewrite" "PCQ/ReWrite"


    PCQ/Rewrite

    NAME
        Rewrite - open a file for output

    SYNOPSIS
        PROCEDURE ReWrite(VAR F      : Any FILE TYPE;
                          FileName   : String
                      [ ; BufferSize : Integer ] );

    DESCRIPTION
        Rewrite opens a PCQ file for output, erasing any existing
        file of the same name.  If the file does not open
        correctly, IOResult is set to a non-zero value, which
        will cause programs using automatic IO checking to
        abort.

        If a buffer size is not specified, a value of 128 is used.

    EXAMPLE

        PROGRAM ShowRewrite;
        {$I "Include:Utils/Parameters.i"}
        {$I "Include:Utils/StringLib.i"}
        {$I- turn off automatic IO checking }
        VAR
            FileName : String;
            TempFile : Text;
        BEGIN
            FileName := AllocString(256);
            GetParam(1,FileName);
            ReWrite(TempFile, FileName, 10);
            IF IOResult = 0 THEN BEGIN
                Writeln('FILE opened correctly');
                Close(TempFile);
            END ELSE
                Writeln('Could NOT open ', FileName);
        END.

    INPUTS

        F          : Any file variable.  It should not already
                     be open.
        FileName   : The file name and path if necessary
        BufferSize : The size of the buffer to allocate for
                     this file.  This value is optional, and
                     if it's not supplied 128 is used.

    RESULTS
        None

    BUGS

    SEE ALSO

        @{" Open   " link "PCQ:Hyper/pcq/pcq2.guide/open" 0} @{" Reset  " link "reset" 0} @{" Reopen " link "reopen" 0}

@endnode

@node "round" "PCQ/Round"

    PCQ/Round

    NAME
        Round - convert a Real value to an Integer by rounding

    SYNOPSIS
        FUNCTION Round(Expr : Real) : Integer;

    DESCRIPTION
        This routine converts a floating point value to an Integer
        by rounding it to the nearest whole value.  This routine
        is implemented as Trunc(Expr + 0.5) for positive Expr or
        Trunc(Expr - 0.5) for negative values.  Thus it is somewhat
        slower than the Trunc function, and it always rounds 0.5
        up for positive values or down for negative.

    EXAMPLE

        PROGRAM ShowRound;
        VAR
            FloatValue : Real;
        BEGIN
            Write('Please enter a Real value: ');
            Readln(FloatValue);
            Writeln(FloatValue:0:3, ' rounds TO ',
                                Round(FloatValue));
        END.

    INPUTS

        Expr : Any Real type expression

    RESULTS
        An integer formed by rounding Expr to the nearest whole
        number.

    BUGS

    SEE ALSO

        @{" Trunc " link "PCQ:Hyper/pcq/pcq4.guide/trunc" 0} @{" Float " link "PCQ:Hyper/pcq/pcq2.guide/float" 0}

@endnode

@node "sin" "PCQ/Sin"

    PCQ/Sin

    NAME
        Sin - return the sine of an expression

    SYNOPSIS
        FUNCTION Sin(Radians : Real) : Real;

    DESCRIPTION
        The Sin function returns the sine of the specified value
        in radians.  This routine will normally use a private
        sine approximation algorithm, but if you use the $N+
        directive, it will call the more accurate mathtrans
        library routine.

        If your program uses degrees, you can convert to radians
        using Degrees * (180 / PI), where PI is 3.14159....

    EXAMPLE

        PROGRAM ShowSin;
        VAR
            Value : Real;
        BEGIN
            Write('Enter a value IN radians: ');
            Readln(Value);
            Writeln('The sine of ', Value:0:3, ' is ', Sin(Value):0:3);
        END.

    INPUTS

        Radians : A Real type expression of an angle in radians.

    RESULTS
        The sine of the specified angle

    BUGS

    SEE ALSO

        @{" Cos    " link "PCQ:Hyper/pcq/pcq1.guide/cos" 0} @{" Tan    " link "PCQ:Hyper/pcq/pcq4.guide/tan" 0} @{" ArcTan " link "PCQ:Hyper/pcq/pcq1.guide/arctan" 0}


@endnode


     
