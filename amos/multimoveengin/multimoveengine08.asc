' Multimove engine demo Version 0.8  
' Designed & Written by Marcin 'Maginus' Dlugosz 
' email: maginus@poczta.onet.pl  
' you can use this engine to create RTS or RPG (diablo on amiga ;-) games  
' This program is Public Domain. 
' lo=numer of objects 'liczba obiektow   
' krok=speed of move 'szybkosc ruchu 
' x1(),y1 -start cordinates 'poczatkowe polozenie  
' x2(),y2 - end cordinates 'koncowe polozenie
' x#(),y#() - actual coordinates' aktualne polozenie 
' ruch()= status of object, 0=in position 1=move 'stan obiektu, 0=spoczynek 1=ruch   
' a1#(),a2#(), a#(), b#() you need this to trajectory 'dane potrzebne do wyliczenia trajektori 
' zo=number of chosen obieckt. nr zaznaczonego obiektu   
Screen Open 0,320,256,16,Lowres
Double Buffer 
LO=10
KROK=1
Dim X1(LO),Y1(LO),X2(LO),Y2(LO),A1#(LO),A2#(LO),A#(LO),B#(LO),X#(LO),Y#(LO),RUCH(LO)
Global X1(),Y1(),X2(),Y2(),LO,ZO,A1#(),A2#(),A#(),B#(),X#(),Y#(),KROK,RUCH()
Load Fsel$("","move.abk","Load Objects","")
Curs Off : Flash Off : Get Bob Palette : Hide On 
Pen 3
Print "Press left mouse key to chose obiect and then press right mouse key to move it"
For I=1 To LO
   X1(I)=Rnd(320)
   Y1(I)=Rnd(200)
   Bob I,X1(I),Y1(I),1
Next 
For I=1 To LO
   X#(I)=X1(I)
   Y#(I)=Y1(I)
Next 
Do 
   XM=X Screen(X Mouse)
   YM=Y Screen(Y Mouse)
   Bob 67,XM,YM,2
   If Mouse Key=1
      For I=1 To LO
         A=Bob Col(67)
         B=Col(I)
         If B=-1
            ZO=I
         End If 
      Next 
   End If 
   If Mouse Key=2 and ZO>0
      X2(ZO)=XM
      Y2(ZO)=YM
      X1(ZO)=X#(ZO)
      Y1(ZO)=Y#(ZO)
      WZOR
   End If 
   CHECKMOVE
   Wait Vbl 
Loop 
Procedure WZOR
   I=ZO
   Trap A1#(I)=-(Y1(I)-Y2(I))
   Trap A2#(I)=-(X1(I)-X2(I))
   Trap A#(I)=A1#(I)/A2#(I)
   Trap B#(I)=X2(I)*(-A#(I))+Y2(I)
   Trap X#(I)=X1(I)
   RUCH(I)=1
End Proc
Procedure CHECKMOVE
   For I=1 To LO
      If X1(I)<>X2(I) and Y1(I)<>Y2(I) and RUCH(I)=1
         MOVE[I]
      End If 
   Next 
End Proc
Procedure MOVE[I]
   If X2(I)>X1(I)
      X#(I)=X#(I)+KROK
      Y#(I)=A#(I)*X#(I)+B#(I)
      Bob I,X#(I),Y#(I),1
   End If 
   If X1(I)>X2(I)
      X#(I)=X#(I)-KROK
      Y#(I)=A#(I)*X#(I)+B#(I)
      Bob I,X#(I),Y#(I),1
   End If 
   CHECKIFEND[I]
End Proc
Procedure CHECKIFEND[I]
   If X#(I)=X2(I) and Y#(I)=Y2(I)
      RUCH(I)=0
      X1(I)=X2(I)
      Y1(I)=Y2(I)
   End If 
End Proc
