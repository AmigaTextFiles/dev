' AMOS Professional Registration Editor
' By Fredrik Rambris <fredrik@rambris.com> 
' $VER: AmosReg.AMOS 1.0 (12.03.2024)  
'

' The registration info is stored in a section of data in the AMOSPro
' executable adjacent to the name for 'dos.libray' 
' The structure is:
'
' LONG      Offset to second copy of username  
' BYTE[12]  "dos.library",0
' WORD      length of registration number
' BYTE[14]  registration number, XORed by $73
' WORD      length of username 
' BYTE[32]  username, XORed by $A5 
' .... code .... 
' at "dos.library" + offset: 
' WORD      length of username 
' BYTE[32]  username, XORed by $5A (called UserSecu) 

' When reading both copies of username is checked to be equal and a checkmark
' is lit up besides the Username field. The version of AMOSPro found with its
' source online does not seem to check the second copy 
'
' The registration number was originally created by checksumming track 0 and 
' track 80 of the installation disk and prefixing it with the first three
' letters of your last name. 
'
' This is all available to read in the somewhat obfuscated installer.
' I have tried to make the code a bit clearer to read. 


INIT

Repeat 
   P=Dialog(1)
   If P=4 Then REG_LOAD
   If P=5 Then REG_SAVE
   Wait Vbl 
Until P=1


CLEANUP

End 




Procedure INIT
   Global _STATUSX,_STATUSY
   Global REQ_WIDTH,REQ_HEIGHT
   
   STATUS_HEIGHT=10
   
   REQ_WIDTH=416
   REQ_WIDTH=REQ_WIDTH-REQ_WIDTH mod 32
   REQ_HEIGHT=64+STATUS_HEIGHT
   
   
   Resource Screen Open 0,640,200,0
   Flash Off : Curs Off : Cls 0 : Paper 0 : Pen 1 : Ink 4,0,0
   Wait Vbl 
   
   X1=(Screen Width/2)-(REQ_WIDTH/2)
   Y1=(Screen Height/2)-(REQ_HEIGHT/2)
   X2=X1+REQ_WIDTH
   Y2=Y1+REQ_HEIGHT
   Limit Mouse X Hard(X1),Y Hard(Y1) To X Hard(X2),Y Hard(Y2-STATUS_HEIGHT)
   
   _STATUSX=X1
   _STATUSY=Y2-STATUS_HEIGHT
   
   'I$="" 
   'Open In 1,"Work:Coding/AmosReg_interface.txt" 
   'I$=Input$(1,Lof(1)) 
   'Close 1 
   
   I$=I$+"SIze 1 VAr,2 VAr;"
   I$=I$+"BAse ScreenWidth SizeX-2/,ScreenHeight SizeY-2/;"
   I$=I$+"SAve 1;"
   I$=I$+"LIne 24,0,28,SizeX;"
   I$=I$+"BUtton 1,0,0,24,10,0,0,1;"
   I$=I$+"[UNpack 0,0,22 ButtonPosition+;][ButtonReturn 0;]"
   I$=I$+"PRint 32,1,'AMOS Professional Registration',7;"
   I$=I$+"BOx 0,11,1,SizeX,54;"
   
   I$=I$+"PRint 16,20,'Reg no',3;"
   I$=I$+"LIne 88,19,44,232;"
   I$=I$+"EDit 2,104,20,15,14,'',2,3;"
   
   I$=I$+"PRint 16,36,'Username',3;"
   I$=I$+"LIne 88,35,44,376;"
   I$=I$+"EDit 3,104,36,33,32,'',2,3;"
   
   I$=I$+"BUtton 4,0,54,SizeX 2/,10,0,0,1;[LIne 0,0,41 ButtonPosition 3*+,SizeX;SetVar 0,'Load';PRint 0 VAr CenterX,1,0 VAr, 3;][ButtonReturn 0;]"
   I$=I$+"BUtton 5,SizeX 2/,54,SizeX 2/,10,0,0,1;[LIne 0,0,41 ButtonPosition 3*+,SizeX;SetVar 0,'Save';PRint 0 VAr CenterX,1,0 VAr, 3;][ButtonReturn 0;]"
   
   I$=I$+"BUtton 6,384,34,24,12,0,0,0;[UNpack 0,0,15 3 VAr+;][]"
   
   I$=I$+"EXit;"
   
   
   On Error Goto _IERR
   Dialog Open 1,I$
   Vdialog(1,1)=REQ_WIDTH
   Vdialog(1,2)=REQ_HEIGHT
   R=Dialog Run(1)
   
   
   Pop Proc
   _IERR:
   Print Mid$(I$,Edialog,80)
End Proc

Procedure CLEANUP
   Dialog Close 1
   Cls 
   
End Proc

Procedure REG_LOAD
   Dialog Update 1,2,REGNO$
   Dialog Update 1,3,USERNAME$
   _STATUS[""]
   
   
   PATH$=Fsel$("","AMOSPro","Select AMOSPro executable to read from","(Will not modify file)")
   'PATH$="AMOSPro_System:AMOSPro"
   
   If PATH$=""
      _STATUS["Aborted"]
      Pop Proc
   End If 
   
   ' String to search for 
   _DOSNAME$="dos.library"
   
   ' Offset of _DOSNAME 
   _DOSPOS=0
   
   
   Reserve As Work 10,10240
   Bload PATH$,10
   
   _DOSPOS=Hunt(Start(10) To Start(10)+Length(10)-1,_DOSNAME$)
   If(_DOSPOS>0)
      Add _DOSPOS,-Start(10)
      _REGPOS=_DOSPOS+Len(_DOSNAME$)+3
      _NAMEPOS=_REGPOS+16
      _USERSECUPOS=Leek(Start(10)+_DOSPOS-4)+_DOSPOS+2
      
      _REGLEN=Deek(Start(10)+_REGPOS-2)
      _NAMELEN=Deek(Start(10)+_NAMEPOS-2)
      
      If _REGLEN>0
         
         _XOR_STR[Peek$(Start(10)+_REGPOS,_REGLEN),$73]
         REGNO$=Param$
         
         _XOR_STR[Peek$(Start(10)+_NAMEPOS,_NAMELEN),$A5]
         USERNAME$=Param$
         
         _XOR_STR[Peek$(Start(10)+_USERSECUPOS,_NAMELEN),$5A]
         USERSECU$=Param$
         
         If USERNAME$=USERSECU$
            Vdialog(1,3)=1
            Dialog Update 1,6,1
         Else 
            Vdialog(1,3)=0
            Dialog Update 1,6,0 : Rem Just to redraw 
         End If 
         
         Dialog Update 1,2,REGNO$
         Dialog Update 1,3,USERNAME$
      Else 
         _STATUS["Not registered"]
      End If 
   Else 
      _STATUS["String 'dos.library' not found"]
   End If 
   
   Erase 10
   
End Proc

Procedure REG_SAVE
   _STATUS[""]
   REGNO$=Rdialog$(1,2)
   USERNAME$=Rdialog$(1,3)
   
   If Len(REGNO$)>14 Then REGNO$=Mid$(REGNO$,1,14)
   If Len(USERNAME$)>32 Then USERNAME$=Mid$(USERNAME$,1,32)
   
   
   ' Generate the three "codes".
   ' Length + XORed string + NULL fill
   
   ' Regno
   _XOR_STR[REGNO$,$73]
   CODE1$=Chr$(0)+Chr$(Len(REGNO$))+Param$
   If Len(REGNO$)<14
      CODE1$=CODE1$+String$(Chr$(0),14-Len(REGNO$))
   End If 
   
   ' Username 
   _XOR_STR[USERNAME$,$A5]
   CODE2$=Chr$(0)+Chr$(Len(USERNAME$))+Param$
   If Len(USERNAME$)<32
      CODE2$=CODE2$+String$(Chr$(0),32-Len(USERNAME$))
   End If 
   
   ' UserSecu 
   _XOR_STR[USERNAME$,$5A]
   CODE3$=Chr$(0)+Chr$(Len(USERNAME$))+Param$
   If Len(USERNAME$)<32
      CODE3$=CODE3$+String$(Chr$(0),32-Len(USERNAME$))
   End If 
   
   
   PATH$=Fsel$("","AMOSPro","Select AMOSPro executable to write to","Make sure you have a backup!")
   'PATH$="AMOSPro_System:AMOSPro"
   
   If PATH$=""
      _STATUS["Aborted"]
      Pop Proc
   End If 
   
   ' String to search for 
   _DOSNAME$="dos.library"
   
   ' Offset of _DOSNAME 
   _DOSPOS=0
   
   ' Load the first 10k from file 
   Reserve As Work 10,10240
   Bload PATH$,10
   
   _DOSPOS=Hunt(Start(10) To Start(10)+Length(10)-1,_DOSNAME$)
   If(_DOSPOS>0)
      Add _DOSPOS,-Start(10)
      _REGPOS=_DOSPOS+Len(_DOSNAME$)+3
      _NAMEPOS=_REGPOS+16
      _USERSECUPOS=Leek(Start(10)+_DOSPOS-4)+_DOSPOS+2
      
      ' Open for output without erasing the existing file
      Append 1,PATH$
      
      Pof(1)=_REGPOS-2
      Print #1,CODE1$;
      
      Pof(1)=_NAMEPOS-2
      Print #1,CODE2$;
      
      Pof(1)=_USERSECUPOS-2
      Print #1,CODE3$;
      
      Close 1
      
      _STATUS["Registration written to file"]
   Else 
      _STATUS["String 'dos.library' not found"]
   End If 
   
   Erase 10
End Proc

Procedure _XOR_STR[_STR$,_XOR]
   OUT$=""
   For POS=1 To Len(_STR$)
      OUT$=OUT$+Chr$(Asc(Mid$(_STR$,POS,1)) xor _XOR)
   Next POS
End Proc[OUT$]

Procedure _STATUS[MSG$]
   Global _STATUSX,_STATUSY
   Global REQ_WIDTH
   
   Ink 0
   Bar _STATUSX,_STATUSY To _STATUSX+REQ_WIDTH,_STATUSY+10
   
   Ink 3
   Text _STATUSX,_STATUSY+Text Base,MSG$
End Proc
