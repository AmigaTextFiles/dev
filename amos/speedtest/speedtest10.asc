' SPEED TEST FOR WinUAE version 1.0  
' Designed & Written by Marcin 'Maginus' Dlugosz 
' email: maginus@poczta.onet.pl    
' This program is Public Domain. 
' sorry but all texts in program are in polish 
' This program show speed of our amiga or emulator.
' lower nuber in test results then faster computer you have
' I write this program because all profesional benchmarks
' dont show how fast is emulation of amiga on WinUAE 
' And I wanted check if I remember how write programs in AMOS  

' rozpis przyciskow i opcji
' 1=wyjscie
' 2=start
' 3=help 
' 4=mat
' 5=gfx  
_INITSCREEN
Dim OPCJA(10)
NAME$="Speed test for WinUAE"
VER$="1.0"
CRE$="Maginus"
WMAT#=0
WGFX#=0
OPCJA(4)=1
OPCJA(5)=1
Global NAME$,VER$,CRE$,WMAT#,WGFX#,OPCJA()
_INITGUI
Do 
   KEY=Mouse Zone
   MK=Mouse Key
   If KEY=1 and MK=1
      _EXIT
   End If 
   If KEY=3 and MK=1
      _HELP
   End If 
   If KEY=4 and MK=1
      _OPCJA4
   End If 
   If KEY=5 and MK=1
      _OPCJA5
   End If 
   If KEY=2 and MK=1
      SPRAWDZOPCJE
   End If 
Loop 
Procedure _INITSCREEN
   Screen Open 0,320,256,4,Lowres
   Palette $AAA,$0,$FFF,$689
   Flash Off : Curs Off 
   Ink 0 : Paint 0,0,0
   Screen Open 1,640,246,4,Hires
   Palette $AAA,$0,$FFF,$689
   Flash Off : Curs Off 
   Ink 0 : Paint 0,0,0
   Reserve Zone 20
End Proc
Procedure _3DWIN0[X1,Y1,X2,Y2]
   Ink 1
   Draw X2,Y1 To X2,Y2
   Draw X1,Y2 To X2,Y2
   Ink 2
   Draw X1,Y1 To X2,Y1
   Draw X1,Y1 To X1,Y2
End Proc
Procedure _3DWIN1[X1,Y1,X2,Y2]
   Ink 2
   Draw X2,Y1 To X2,Y2
   Draw X1,Y2 To X2,Y2
   Ink 1
   Draw X1,Y1 To X2,Y1
   Draw X1,Y1 To X1,Y2
End Proc
Procedure _MAINMENU[KOLOR,NAME$,VER$,CRE$]
   _3DWIN0[0,0,639,10]
   Ink 3 : Paint 3,3
   Ink KOLOR,3
   Text 20,8,NAME$+" v"+VER$+" by "+CRE$
   _3DWIN0[3,2,14,8]
   _3DWIN1[6,4,11,6]
   Set Zone 1,3,2 To 14,8
End Proc
Procedure _EXIT
   CZAS#=0.5
   _3DWIN1[3,2,14,8]
   Wait CZAS#*50
   End 
End Proc
Procedure _TEXTKEY[NR,KOLOR,X,Y,NAZWA$]
   ILE=(Len(NAZWA$))*8+16
   _3DWIN0[X,Y,X+ILE,Y+10]
   Ink 3 : Paint X+2,Y+2
   Ink KOLOR,3
   Text X+8,Y+8,NAZWA$
   Set Zone NR,X,Y To X+ILE,Y+10
End Proc
Procedure _TEXTKEY1[NR,KOLOR,X,Y,NAZWA$]
   ILE=(Len(NAZWA$))*8+16
   _3DWIN1[X,Y,X+ILE,Y+10]
   Ink 3 : Paint X+2,Y+2
   Ink KOLOR,3
   Text X+8,Y+8,NAZWA$
   Set Zone NR,X,Y To X+ILE,Y+10
End Proc
Procedure _WYNIKI
   _3DWIN1[400,20,630,50]
   Ink 3 : Paint 402,22
   Ink 1,3
   Text 402,28,"Wyniki (podane w 1/50s):"
   Text 402,38,"MAT:"+Str$(WMAT#)
   Text 402,48,"GFX:"+Str$(WGFX#)
   Text 502,38,"RAZEM:"+Str$(WMAT#+WGFX#)
End Proc
Procedure _OPENKONSOLA
   Paper 3 : Curs Pen 2 : Curs On : Pen 2
   Wind Open 1,20,74,76,19
End Proc
Procedure _WPISKONSOLA[TEKST$]
   Print TEKST$;
End Proc
Procedure _INITGUI
   _MAINMENU[2,NAME$,VER$,CRE$]
   _TEXTKEY[2,2,10,20,"Start"]
   _TEXTKEY[3,2,80,20,"Help"]
   _TEXTKEY1[4,2,10,40,"MAT"]
   _TEXTKEY1[5,2,60,40,"GFX"]
   _3DWIN1[10,70,630,230]
   Ink 3 : Paint 11,71
   _WYNIKI
   _OPENKONSOLA
End Proc
Procedure _HELP
   _3DWIN1[80,20,128,30]
   Wait 20
   Print "Program ten sprawdza szybkosc dzialania komputera/emulatora. Wykonuje dwa testy (matematyczny i graficzny) a nastepnie pokazuje czas (w 1/50s) w jakim testy zostaly wykonane. Im mniejszy jest wynik tym komputer/emulator jest szybszy."
   Print "Ten program jest raczej przeznaczony dla emulatorow Amigi a nie dla samego komputera, a powstal z trzech powodow: 1) bo chcialem sprawdzic jak zmiana ruznych opcji wplynie na szybkosc emulacji pod WinUAE. 2) Standardowe benchmarki z ";
   Print "Amigi nie dzialaly poprawnie. 3) bo chcialem sprawdzic czy potrafie jeszcze cokolwiek zrobic na Amige."
   _3DWIN0[80,20,128,30]
End Proc
Procedure _OPCJA4
   Wait 20
   If OPCJA(4)=1
      Print "Test matematyczny zostal wylaczony."
      OPCJA(4)=0
      _3DWIN0[10,40,50,50]
   Else 
      Print "Test matematyczny zostal wlaczony"
      OPCJA(4)=1
      _3DWIN1[10,40,50,50]
   End If 
End Proc
Procedure _OPCJA5
   Wait 20
   If OPCJA(5)=1
      Print "Test graficzny zostal wylaczony."
      OPCJA(5)=0
      _3DWIN0[60,40,100,50]
   Else 
      Print "Test graficzny zostal wlaczony"
      OPCJA(5)=1
      _3DWIN1[60,40,100,50]
   End If 
End Proc
Procedure SPRAWDZOPCJE
   _3DWIN1[10,20,66,30]
   Wait 20
   If OPCJA(4)=0 and OPCJA(5)=0
      Print "Nie morzesz robic testu jesli masz wszystko wylaczone!!!"
   End If 
   If OPCJA(4)=0 and OPCJA(5)=1
      TESTGFX
   End If 
   If OPCJA(4)=1 and OPCJA(5)=0
      TESTMAT
   End If 
   If OPCJA(4)=1 and OPCJA(5)=1
      TESTMAT
      TESTGFX
   End If 
   _WYNIKI
   _3DWIN0[10,20,66,30]
   
End Proc
Procedure TESTGFX
   Print "Rozpoczecie testu graficznego..."
   Timer=0
   Screen 0
   For X=0 To 320
      For Y=0 To 256
         Ink 1 : Plot X,Y
      Next 
   Next 
   WGFX#=Timer
   Ink 0 : Paint 0,0
   Screen 1
   Print "Test graficzny zakonczony sukcesem"
   Print "Wynik: ";WGFX#
End Proc
Procedure TESTMAT
   Print "Rozpoczecie testu matematycznego..."
   Timer=0
   For X#=1 To 25
      For Y#=1 To 25
         For Z#=1 To 25
            C#=Sin(X#)*Cos(Y#)/Tan(Z#)
         Next 
      Next 
   Next 
   WMAT#=Timer
   Print "Test matematyczny zakonczony sukcesem"
   Print "Wynik: ";WMAT#
End Proc

