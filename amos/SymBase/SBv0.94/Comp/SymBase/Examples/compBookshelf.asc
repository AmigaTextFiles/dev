



Gui Amiga Os
Request Wb
ver$="$VER: BookShelf 1.0 (2001.11.10)"
ap$=chr$(34)
_view=1
O_EVENT=true
Global ap$, MEZO, _LOCALE

Load "BookShelf.abk",10
Gui Mouse Mode 1
Amos To Back
Gui Sensitive Off
Gui Mouse Mode 1

Gui Open 1,1,10
Gui Writing %001

 Lib Open 1,"locale.library",0
 Areg(0)=0
 _LOCALE=Lib Call(1,Lvo("OpenLocale"))

list_len=(374-188)/Gui X Font
list_tag_h=(208-23)/(Gui Y Font+4)

Db Use "Book.dbf"

rlist_row=(208-23)/Gui Y Font-1
Gosub dbf_setslider
Gosub dbf_rlist

For I=1 To 7
    Gui Set 1,I,-1,1
Next I

Repeat

  If O_EVENT
        EV=Gui Wait
        WIN=Gui Window
        CODE=Gui Code
  End If

  O_EVENT=True

  If WIN=1

        If EV=4
                a=Db Recno
                Db Append Blank
                Gui Set$ 1,5,""
                Gui Set$ 1,1,""
                Gui Set$ 1,2,""
                Gui Set 1,6,0,2001
                Gui Set 1,3,0,0
                _select=Db recno
                Db Goto a
                Gosub dbf_rlist
                Gosub dbf_setslider
        Else If EV=8
                _view=Gui Read (1,8)+1
                Db Goto 1
                Gui Ink 0
                Gui Bar 188,23+5 To 374,208
                Gosub dbf_rlist
        Else If EV=7
                a=Db Recno
                MEZO=Gui Read (1,7)+1
                QUICK[1,Db Reccount]
                Db Goto a
                Gosub dbf_rlist
        Else If EV=0 and Db State
                a=Code+1
                If a<>Db Recno and Db Reccount>0
                        Db Goto a
                        Gosub dbf_rlist
                End If
        Else If EV=5 or EV=1 or EV=2
                a=Db Recno
                Db Goto _select
                a$=Gui Read$ (1,EV)
                If EV=5
                   If a$<>Db Get$(1)
                       Db Put$ a$,1
                   End If
                Else If EV=1
                   If a$<>Db Get$(2)
                       Db Put$ a$,2
                   End If
                Else If EV=2
                   If a$<>Db Get$(3)
                       Db Put$ a$,3
                   End If
                End If
                Db Goto a
                Gosub dbf_rlist
        Else If EV=6 or EV=3
                a=Db Recno
                Db Goto _select
                b=Gui Read (1,EV)
                If EV=6
                   If b<>Db Get(4)
                       Db Putn b,4
                   End If
                Else If EV=3
                   If b<>Db Get(5)
                       Db Putn b,5
                   End If
                End If
                Db Goto a
                Gosub dbf_rlist
        Else If EV=-11
'                                       left click
                _slider=Db Recno
                _WX=Gui Mouse Wx
                _WY=Gui Mouse Wy
                If _WX>188 and _WX<374
                    If _WY>23 and _WY<208
                        _select=(_WY-23)/Gui Y Font+Gui Read (1,0)
                        If _select>0 and _select<=Db Reccount
                            For I=1 To 7
                                Gui Set 1,I,-1,0
                            Next I
                            Db Goto _select
                            Gosub dbf_read
                            Db Goto _slider
                        End If
                    End If
                End If
        End If
  End If

Until EV=-1 and WIN=1

VEG:

x=Gui Close (1)
Erase 10

If Db Opencount
   For I=1 To Db Opencount
      Db Select First 
      Db Close 
   Next I
End If 
If _LOCALE
        Areg(0)=_LOCALE
        X=Lib Call(1,Lvo("CloseLocale"))
        Lib Close 1
End If
End



dbf_read:
    Gui Set$ 1,5,Db Get$(Db Field("title"))
    Gui Set$ 1,1,Db Get$(2)
    Gui Set$ 1,2,Db Get$(3)
    Gui Set 1,6,0,Db Get(4)
    Gui Set 1,3,0,Db Get (5)
Return

dbf_rlist:
'Fill the14 text gadget with record data starting from record FI to list_len char
        Db Cutspace Off
        a=Db Recno
        For r=1 to rlist_row
                a$=Db Get$(_view)
                Gui Text 188+2,23+r*Gui Y Font,Left$(a$,23)
                Exit If Db Recno=Db Reccount
                Db Skip
        Next r
        Db Goto a
        Db Cutspace On
Return

dbf_setslider:
Gui Set 1,0,0,Max(Db Recno-1,0)
Gui Set 1,0,1,Max(Db Reccount-rlist_row-1,Db Reccount)
Gui Set 1,0,2,rlist_row
Return


Procedure QUICK[BAL,JOBB]
   I=BAL : J=JOBB
   Db Goto(I+J)/2 : Inc _GOTO
   KOZEPKULCS$=Db Get$(MEZO)

   While I<=J

      Db Goto I
      I$=Db Get$(MEZO)
      Db Goto J
      J$=Db Get$(MEZO)

      Do
         STRNCMP[I$,KOZEPKULCS$]
         Exit If Param>=0
         Inc I
         Db Goto I
         I$=Db Get$(MEZO)
       Loop

      Do
         STRNCMP[J$,KOZEPKULCS$]
         Exit If Param<=0
         Dec J
         Db Goto J
         J$=Db Get$(MEZO)
      Loop

      If I<=J
         Db Swap I,J
         Inc I
         Dec J
      End If

   Wend

   If BAL<J
      Proc QUICK[BAL,J]
   End If

   If I<JOBB
      Proc QUICK[I,JOBB]
   End If

End Proc

Procedure STRNCMP[STRING1$,STRING2$]
   Areg(0)=_LOCALE
   Areg(1)=Varptr(STRING1$)
   Areg(2)=Varptr(STRING2$)
   Dreg(0)=-1
   Dreg(1)=2
   X=Lib Call(1,Lvo("StrnCmp"))
End Proc [X]
