'-> Presentazione by Stefano Maria Regattin
'd> 25 maggio 2010 da Quadretti
'------------------------------- 

'screen height 
Global ALTEZZASCHERMO

'screen grab block identifier number 
Global BLOCCO

'scroll start variable 
Global INIZIODELLOSCORRIMENTO

'screen width
Global LARGHEZZASCHERMO

'square side length
Global LATOQUADRETTO

'number of squares 
Global QUADRETTI

'text to scroll  
Global TESTODASCORRERE$


'this line evaluates the screen height 
If Ntsc=True Then ALTEZZASCHERMO=200 Else ALTEZZASCHERMO=256

'sample bank name
BANCODISUONI$="SamplesCreator2010.Abk"


BLOCCO=1

'screen colours
C0LORISCHERMO=4

'screen width
LARGHEZZASCHERMO=320

'square side lenght
LATOQUADRETTO=16

'squares 
QUADRETTI=10

'sounds
SUONI=23

'Esc key char value
TASTOFUGA$=Chr$(27)

'text to scroll
TESTODASCORRERE$="Ciao Ciao Chow Chow Ciupa Ciupa Ciapa Ciapa..."


'opens the screen with four colours as required to obtain the mouse pointer image
Screen Open SCHERMO,LARGHEZZASCHERMO,ALTEZZASCHERMO,C0LORISCHERMO,BASSARISOLUZIONE

'this procedure creates the pointer
Proc CREAILPUNTATORE

'closes the screen 
Screen Close SCHERMO

'screen colours
C0LORISCHERMO=32

BANCODISUONICARICATO=Exist(BANCODISUONI$)
If BANCODISUONICARICATO=True Then Load BANCODISUONI$

'opens the screen with thirtytwo colours 
Screen Open SCHERMO,LARGHEZZASCHERMO,ALTEZZASCHERMO,C0LORISCHERMO,BASSARISOLUZIONE

'these commands disable the text cursor and its flashing, create a logic hidden screen, disable the automatic redrawing features and hides the mouse pointer 
Curs Off : Flash Off : Double Buffer : Autoback 0 : Update Off : Wait Vbl : Hide On : Limit Mouse 

'creates the colours palette 
Palette $60,0,$BAA,$A99,$988,$BA5,$A94,$983,$BBA,$AA9,$998,$ABA,$9A9,$898,$5BA,$4A9,$398,$ABB,$9AA,$899,$AAB,$99A,$889,$B5A,$A49,$938,$BAB,$A9A,$989,$5A5,$494,$383

'sets the background color and the text colour 
Paper 0 : Pen 1 : Cls 

If BANCODISUONICARICATO=True
 Print "Sounds loaded, OK."
Else 
 Print "Sounds not loaded, I need the sound bank";
 Print "[";BANCODISUONI$;"]!" : Bell 
End If 
Screen Swap : Wait 100 : Cls : Locate 0,0

'some info texts to the hidden screen
Print "LEFT MOUSE BUTTON CLICK to play a sound.";
Print "RIGHT MOUSE BUTTON CLICK to change it."
Print "Click a (mouse) button to continue..."

'shows the new mouse pointer and the screen with the texts 
Show On : Screen Swap 

'scroll start variable 
INIZIODELLOSCORRIMENTO=False

'wait for a (mouse) key clicked
Repeat 
 Wait Vbl 
 
 TASTODELTOPOCLICCATO=Mouse Click : TASTOPREMUTO$=Inkey$
 
 If TASTODELTOPOCLICCATO>0 Then INIZIODELLOSCORRIMENTO=True
 If TASTOPREMUTO$<>"" Then INIZIODELLOSCORRIMENTO=True
 
Until INIZIODELLOSCORRIMENTO=True

'clears both the visible and its mirrored copy 
Cls 0 : Screen Swap : Cls 0

'this procedure creates the squares  
Proc CREAIQUADRETTI

'scroll end variable   
If TASTOPREMUTO$=TASTOFUGA$ Then FINEDELLOSCORRIMENTO=True Else FINEDELLOSCORRIMENTO=False

'number of the sound to play 
If BANCODISUONICARICATO=True Then SUONO=1


'main loop 
Repeat 
 
'launches the scroll procedure 
 Proc SCORRIMENTO
 
'update the screens
 Screen Swap : Wait 24 : Wait Vbl 
 
'this string retains the char value of the key pressed 
 TASTOPREMUTO$=Inkey$
 If TASTOPREMUTO$=TASTOFUGA$ Then FINEDELLOSCORRIMENTO=True
 
 If BANCODISUONICARICATO=True
  
'this variable retains the clicked mouse button
  TASTODELTOPOCLICCATO=Mouse Click

  If TASTODELTOPOCLICCATO=1
   Sam Play SUONO
  End If 
  If TASTODELTOPOCLICCATO=2
   Inc SUONO
   If SUONO=SUONI+1
    SUONO=1
   End If 
  End If 
 End If 
 
Until FINEDELLOSCORRIMENTO=True

Every Off : Erase All : Screen Close SCHERMO : End 

Procedure CREAILPUNTATORE
 Cls 0
 PUNTATORE=QUADRETTI+1 : X0=0 : X1=X0+LATOQUADRETTO-1 : Y0=0 : Y1=Y0+LATOQUADRETTO-1
 Ink 18 : Polygon X0,Y0 To X1,Y0 To X1,Y1 To X0,Y1
 Ink 17 : Polyline X0,Y1-1 To X0,Y0 To X1-1,Y0
 Ink 19 : Polyline X0+1,Y1 To X1,Y1 To X1,Y0+1
 Ink 17 : Circle X0+4,Y0+4,3 : Circle X0+11,Y0+4,3
 Circle X0+4,Y0+4,2 : Circle X0+11,Y0+4,2
 Ink 16 : Box X0+3,Y0+3 To X0+5,Y0+5 : Plot X0+4,Y0+4
 Box X0+10,Y0+3 To X0+12,Y0+5 : Plot X0+11,Y0+4
 Ink 19 : Box X0+7,Y0+7 To X0+8,Y0+9
 Ink 16 : Box X0+4,Y0+12 To X0+11,Y0+13
 Get Sprite PUNTATORE,X0,Y0 To X1+1,Y1+1 : Hot Spot PUNTATORE,$11
 Change Mouse 3+PUNTATORE
End Proc
Procedure CREAIQUADRETTI
 Cls 0
 X0=0 : Y0=0 : X1=LATOQUADRETTO-1 : Y1=LATOQUADRETTO-1
 For QUADRETTO=1 To QUADRETTI
  C0LORE=QUADRETTO*3
  Ink C0LORE : Polygon X0,Y0 To X1,Y0 To X1,Y1 To X0,Y1
  Ink C0LORE-1 : Polyline X0,Y1-1 To X0,Y0 To X1-1,Y0
  Ink C0LORE+1 : Polyline X0+1,Y1 To X1,Y1 To X1,Y0+1
'  Ink 31 : Circle X0+4,Y0+4,3 : Circle X0+11,Y0+4,3 
'  Circle X0+4,Y0+4,2 : Circle X0+11,Y0+4,2
'  Ink 0 : Box X0+3,Y0+3 To X0+5,Y0+5 : Plot X0+4,Y0+4 
'  Box X0+10,Y0+3 To X0+12,Y0+5 : Plot X0+11,Y0+4
  Get Bob QUADRETTO,X0,Y0 To X1+1,Y1+1
 Next 
End Proc
Procedure SCORRIMENTO
 If INIZIODELLOSCORRIMENTO=True
  For YQUADRETTO=0 To ALTEZZASCHERMO-LATOQUADRETTO Step LATOQUADRETTO
   For XQUADRETTO=0 To LARGHEZZASCHERMO-LATOQUADRETTO Step LATOQUADRETTO
    QUADRETTO=Rnd(QUADRETTI-1)+1 : Paste Bob XQUADRETTO,YQUADRETTO,QUADRETTO
   Next XQUADRETTO
  Next YQUADRETTO
  XQUADRETTO=LARGHEZZASCHERMO-LATOQUADRETTO
  Get Block BLOCCO,LATOQUADRETTO,0,XQUADRETTO,ALTEZZASCHERMO
  INIZIODELLOSCORRIMENTO=False
 Else 
  Put Block BLOCCO,0,0
  XQUADRETTO=LARGHEZZASCHERMO-LATOQUADRETTO
  For YQUADRETTO=0 To ALTEZZASCHERMO-LATOQUADRETTO Step LATOQUADRETTO
   QUADRETTO=Rnd(QUADRETTI-1)+1 : Paste Bob XQUADRETTO,YQUADRETTO,QUADRETTO
  Next YQUADRETTO
  Get Block BLOCCO,LATOQUADRETTO,0,XQUADRETTO,ALTEZZASCHERMO
 End If 
End Proc
