'-> Presentazione by Stefano Maria Regattin
'd> 25 maggio 2010 da Quadretti
'm> 28 giugno 2010 
'm> 20 luglio 2010 
'm> 31 agosto 2010 
'------------------

'screen height 
Global ALTEZZASCHERMO

'screen grab block identifier number 
Global BLOCCO

'points line count 
Global CONTOLINEADIPUNTI

'scroll count
Global CONTOSCORRIMENTO

'scroll start variable 
Global INIZIODELLOSCORRIMENTO

'screen width
Global LARGHEZZASCHERMO

'text screen width 
Global LARGHEZZASCHERMOTESTO

'square side length
Global LATOQUADRETTO

'number of squares 
Global QUADRETTI

'text screen identifier number 
Global SCHERMOTESTO

'text to scroll  
Global TESTODASCORRERE$

'this line evaluates the screen height 
If Ntsc=True Then ALTEZZASCHERMO=200 Else ALTEZZASCHERMO=256

'sample bank name
BANCODISUONI$="SamplesCreator2010.Abk"

'block identifier number 
BLOCCO=1

'text points line
CONTOLINEADIPUNTI=0

'screen colours
C0LORISCHERMO=4

'square side lenght
LATOQUADRETTO=16

'screen width
LARGHEZZASCHERMO=320

'squares 
QUADRETTI=10

'sounds
SUONI=23

'Esc key char value
TASTOFUGA$=Chr$(27)

'text to scroll
TESTODASCORRERE$="Ciao Ciao Chow Chow Ciupa Ciupa Ciapa Ciapa Ciopa Ciopa Sciopa Sciopa..."

'scroll screen identifier number 
SCHERMOSCORRIMENTO=0

'font size 
GRANDEZZAFONTE=8

'text screen height
ALTEZZASCHERMOTESTO=GRANDEZZAFONTE

'text screen colours 
C0LORISCHERMOTESTO=2

'text screen width 
LARGHEZZASCHERMOTESTO=GRANDEZZAFONTE*Len(TESTODASCORRERE$)

'text screen identifier number 
SCHERMOTESTO=1

Screen Open SCHERMOTESTO,LARGHEZZASCHERMOTESTO,ALTEZZASCHERMOTESTO,C0LORISCHERMOTESTO,BASSARISOLUZIONE
Curs Off : Flash Off : Paper 0 : Pen 1 : Cls : Palette 0,$FFF : Text 0,6,TESTODASCORRERE$

'positions the screen
Screen Display SCHERMOTESTO,128,40+ALTEZZASCHERMO-ALTEZZASCHERMOTESTO,LARGHEZZASCHERMOTESTO,ALTEZZASCHERMOTESTO


'opens the scroll screen with four colours as required to obtain the mouse pointer image 
Screen Open SCHERMO,LARGHEZZASCHERMO,ALTEZZASCHERMO,C0LORISCHERMO,BASSARISOLUZIONE

'this procedure creates the pointer
Proc CREAILPUNTATORE

'closes the screen 
Screen Close SCHERMO

'screen colours
C0LORISCHERMO=32

BANCODISUONICARICATO=Exist(BANCODISUONI$)
If BANCODISUONICARICATO=True Then Load BANCODISUONI$

'opens the screen with thirtytwo colours 
Screen Open SCHERMO,LARGHEZZASCHERMO+LATOQUADRETTO,ALTEZZASCHERMO,C0LORISCHERMO,BASSARISOLUZIONE

'positions the screen
Screen Display SCHERMO,128,40,LARGHEZZASCHERMO,ALTEZZASCHERMO

'these commands disable the text cursor and its flashing, create a logic hidden screen, disable the automatic redrawing features and hides the mouse pointer 
Curs Off : Flash Off : Double Buffer : Autoback 0 : Update Off : Wait Vbl : Hide On : Limit Mouse 128,40 To 128+LARGHEZZASCHERMO,40+ALTEZZASCHERMO

'creates the colours palette 
Palette $60,0,$BAA,$A99,$988,$BA5,$A94,$983,$BBA,$AA9,$998,$ABA,$9A9,$898,$5BA,$4A9,$398,$ABB,$9AA,$899,$AAB,$99A,$889,$B5A,$A49,$938,$BAB,$A9A,$989,$5A5,$494,$383

'sets the background color and the text colour 
Paper 0 : Pen 1 : Cls 

'puts the text screen over the scroll screen 
Screen To Front SCHERMOTESTO

Def Scroll 1,1,0 To LARGHEZZASCHERMO+LATOQUADRETTO,ALTEZZASCHERMO,-1,0

If BANCODISUONICARICATO=True
 Print "Sounds loaded, OK."
Else 
 Print "Sounds not loaded, I need the sound bank"
 Print "[";BANCODISUONI$;"]!" : Bell 
End If 

Screen Swap : Wait 100 : Cls : Locate 0,0

'some info texts to the hidden screen
Print "LEFT MOUSE BUTTON CLICK to play a sound."
Print "RIGHT MOUSE BUTTON CLICK to change it."
Print "Click a (mouse) button to continue..."

'shows the new mouse pointer and the screen with the texts 
Screen Swap : Show On 

'scroll start variable 
INIZIODELLOSCORRIMENTO=False

'wait for a (mouse) key clicked
Repeat 
 Wait Vbl 
 
 TASTODELTOPOCLICCATO=Mouse Click : TASTOPREMUTO$=Inkey$
 
 If TASTODELTOPOCLICCATO>0 Then INIZIODELLOSCORRIMENTO=True
 If TASTOPREMUTO$<>"" Then INIZIODELLOSCORRIMENTO=True
 
Until INIZIODELLOSCORRIMENTO=True

'clears both the visible and its mirrored copy 
Cls 

'this procedure creates the squares  
Proc CREAIQUADRETTI

'clears both the visible and its mirrored copy 
Cls 

'scroll end variable   
If TASTOPREMUTO$=TASTOFUGA$ Then FINEDELLOSCORRIMENTO=True Else FINEDELLOSCORRIMENTO=False

'number of the sound to play 
If BANCODISUONICARICATO=True Then SUONO=1


'main loop 
Repeat 
 
'launches the scroll procedure 
 Proc SCORRIMENTO
 
'this string retains the char value of the key pressed 
 TASTOPREMUTO$=Inkey$
 If TASTOPREMUTO$=TASTOFUGA$ Then FINEDELLOSCORRIMENTO=True
 
 If BANCODISUONICARICATO=True
  
'this variable retains the clicked mouse button
  TASTODELTOPOCLICCATO=Mouse Click
  
  If TASTODELTOPOCLICCATO=1
   Sam Play SUONO
  End If 
  If TASTODELTOPOCLICCATO=2
   Inc SUONO
   If SUONO=SUONI+1
    SUONO=1
   End If 
  End If 
 End If 
 
Until FINEDELLOSCORRIMENTO=True

Erase All : Screen Close SCHERMO : Screen Close SCHERMOTESTO : End 

Procedure CREAILPUNTATORE
 Cls 0
 PUNTATORE=QUADRETTI+1 : X0=0 : X1=X0+LATOQUADRETTO-1 : Y0=0 : Y1=Y0+LATOQUADRETTO-1
 Ink 18 : Polygon X0,Y0 To X1,Y0 To X1,Y1 To X0,Y1
 Ink 17 : Polyline X0,Y1-1 To X0,Y0 To X1-1,Y0
 Ink 19 : Polyline X0+1,Y1 To X1,Y1 To X1,Y0+1
 Ink 17 : Circle X0+4,Y0+4,3 : Circle X0+11,Y0+4,3
 Circle X0+4,Y0+4,2 : Circle X0+11,Y0+4,2
 Ink 16 : Box X0+3,Y0+3 To X0+5,Y0+5 : Plot X0+4,Y0+4
 Box X0+10,Y0+3 To X0+12,Y0+5 : Plot X0+11,Y0+4
 Ink 19 : Box X0+7,Y0+7 To X0+8,Y0+9
 Ink 16 : Box X0+4,Y0+12 To X0+11,Y0+13
 Get Sprite PUNTATORE,X0,Y0 To X1+1,Y1+1 : Hot Spot PUNTATORE,$11
 Change Mouse 3+PUNTATORE
End Proc
Procedure CREAIQUADRETTI
 Cls 
 X0=0 : Y0=0 : X1=LATOQUADRETTO-1 : Y1=LATOQUADRETTO-1
 For QUADRETTO=1 To QUADRETTI
  C0LORE=QUADRETTO*3
  Ink C0LORE : Polygon X0,Y0 To X1,Y0 To X1,Y1 To X0,Y1
  Ink C0LORE-1 : Polyline X0,Y1-1 To X0,Y0 To X1-1,Y0
  Ink C0LORE+1 : Polyline X0+1,Y1 To X1,Y1 To X1,Y0+1
'  Ink 31 : Circle X0+4,Y0+4,3 : Circle X0+11,Y0+4,3 
'  Circle X0+4,Y0+4,2 : Circle X0+11,Y0+4,2
'  Ink 0 : Box X0+3,Y0+3 To X0+5,Y0+5 : Plot X0+4,Y0+4 
'  Box X0+10,Y0+3 To X0+12,Y0+5 : Plot X0+11,Y0+4
  Get Bob QUADRETTO,X0,Y0 To X1+1,Y1+1
 Next 
End Proc
Procedure SCORRIMENTO
 If INIZIODELLOSCORRIMENTO=True
  For YQUADRETTO=0 To ALTEZZASCHERMO-LATOQUADRETTO Step LATOQUADRETTO
   For XQUADRETTO=0 To LARGHEZZASCHERMO Step LATOQUADRETTO
    QUADRETTO=Rnd(QUADRETTI-1)+1 : Paste Bob XQUADRETTO,YQUADRETTO,QUADRETTO
   Next XQUADRETTO
  Next YQUADRETTO
  XQUADRETTO=LARGHEZZASCHERMO
  INIZIODELLOSCORRIMENTO=False
 Else 
  Scroll 1 : Inc CONTOSCORRIMENTO
  If CONTOSCORRIMENTO=LATOQUADRETTO
   XQUADRETTO=LARGHEZZASCHERMO
   For YQUADRETTO=0 To ALTEZZASCHERMO-LATOQUADRETTO Step LATOQUADRETTO
    Screen SCHERMOTESTO
    PUNTO=Point(CONTOLINEADIPUNTI,YQUADRETTO/LATOQUADRETTO)
    Screen SCHERMOSCORRIMENTO
    If PUNTO=1
     Ink 1 : Bar XQUADRETTO,YQUADRETTO To XQUADRETTO+LATOQUADRETTO-1,YQUADRETTO+LATOQUADRETTO-1
    Else 
     QUADRETTO=Rnd(QUADRETTI-1)+1 : Paste Bob XQUADRETTO,YQUADRETTO,QUADRETTO
    End If 
   Next YQUADRETTO
   CONTOSCORRIMENTO=0 : Add CONTOLINEADIPUNTI,1,1 To LARGHEZZASCHERMOTESTO-1
  End If 
 End If 
 Screen Swap : Wait Vbl : Screen Copy Physic To Logic
End Proc
