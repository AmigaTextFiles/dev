;-> BootPicture by Stefano Maria Regattin
;i> 11 giugno 2012
;m> 14,20 giugno 2012
;m> 6,7,8 marzo-Festa Delle Donne 2013
;m> 27,28,29 novembre 2013
;m> 6 febbraio 2014
;m> 24,27 aprile 2018
;m> 8 maggio 2018
;m> 9 giugno 2018
;m> 20 ottobre 2018
;m> 8,9,10,15,20,21 maggio 2019
;m> 18,19,23 luglio 2019
;m> 15,22,23 agosto 2019
;m> 3 settembre 2019
;m> 29 febbraio 2020
;m> 6,7,8,9,10,11,13,14,17,20 marzo 2020
;----------------------------------------
JMP InizioDelProgramma
Dc.b "$VER: BootPicture 1.7 (20.3.2020)"
Even
InizioDelProgramma
#FinestraDiSfondo=256;       $100
#FinestraAttiva=4096;        $1000
#FinestraSenzaBordi=2048;    $800
#TipoFinestra=#FinestraDiSfondo+#FinestraSenzaBordi+#FinestraAttiva

#FinestraAttivata=262144;    $40000
#FinestraDisattivata=524288; $80000
#HaiPremutoITastiDelTopo=8

#AreaGrafica=0
#FinestraImma=0
#ListaImmagini=0
#PuntatoreDelTopo=0
#SchermoImma=0
#SchermoNeroCoprente=1
#Suono=0
#TavolozzaImma=0
#TavolozzaNeroCoprente=1

#CScr=$80000039 ;screen colours
#DScr=$80000025 ;screen depht
#IScr=$80000032 ;screen identifier (should be equal to screen mode)
#MScr=$8000002D ;screen mode
#XScr=$80000023 ;screen width
#YScr=$80000024 ;screen height

AltezzaSchermo.w=DispHeight:CassettoImma.b=False:ColoreChiaro.w=1:MessaggiMostrati.b=False:Problemi.b=0:RipetiIlSuono.b=False
XLunFinestra.w=640:YLunFinestra.b=8:ListaImmagini$="RAM:BootPictures.list":TitoloProgramma$="BootPicture 1.7"
If FromCLI=True
 Parametri.b=NumPars
 If Parametri=0 OR Parametri>8 Then NPrint "Wrong parameters number!":MessaggiMostrati=True
 If Parametri=1
  If Par$(1)="?"
   NPrint "BootPicture [PICTURE|PIX]/K PixPath [DRAWER]/S [SOUND|SND]/K SndPath [LOOPSND]/S"
   NPrint "WAIT/A/K/N Time (50ths*Sec) PixPath with / is equal to DRAWER"
   MessaggiMostrati=True
  EndIf
 Else
  For Parametro.b=1 To Parametri
   Select Par$(Parametro)
   Case "DRAWER"
    CassettoImma=True
   Case "PICTURE"
    If Parametro<Parametri Then Imma$=Par$(Parametro+1) Else NPrint "PixPath missing!":MessaggiMostrati=True
   Case "PIX"
    If Parametro<Parametri Then Imma$=Par$(Parametro+1) Else NPrint "PixPath missing!":MessaggiMostrati=True
   Case "LOOPSND"
    RipetiIlSuono=True
   Case "SND"
    If Parametro<Parametri Then Suono$=Par$(Parametro+1) Else NPrint "SndPath missing!":MessaggiMostrati=True
   Case "SOUND"
    If Parametro<Parametri Then Suono$=Par$(Parametro+1) Else NPrint "SndPath missing!":MessaggiMostrati=True
   Case "WAIT"
    If Parametro<Parametri Then TempoDiAttesa.w=Val(Par$(Parametro+1)) Else NPrint "Time missing!":MessaggiMostrati=True
   End Select
  Next Parametro
 EndIf
 If MessaggiMostrati=False
  TitoloSchermo$=Centre$(TitoloProgramma$+" by Stefano Maria Regattin on Fri 20 Mar 2020",78)
  InitPalette #TavolozzaNeroCoprente,2
  PalRGB #TavolozzaNeroCoprente,1,15,15,15
  Screen #SchermoNeroCoprente,25,TitoloSchermo$
  Use Palette #TavolozzaNeroCoprente
  Use Screen #SchermoNeroCoprente
  YPosFinestra.w=AltezzaSchermo*2-YLunFinestra
  PercorsoProgramma$=ProgDir$
  If Len(Imma$)>1 AND Right$(Imma$,1)="/" Then CassettoImma=True:Imma$=UnLeft$(Imma$,1)
  If Imma$<>"" AND Instr(Imma$,":")=0 Then Imma$=PercorsoProgramma$+"/"+Imma$
  If Suono$<>"" AND Instr(Suono$,":")=0 Then Suono$=PercorsoProgramma$+"/"+Suono$
  If CassettoImma=True AND Imma$<>""
   CassettoImma$=Imma$:If Right$(CassettoImma$,1)<>"/" Then CassettoImma$+"/"
   Exec "C:List >"+ListaImmagini$+" "+CassettoImma$+" QUICK"
   ListaImma.b=ReadFile(#ListaImmagini,ListaImmagini$):FileInput #ListaImmagini
   If ListaImma=True
    Immagini.b=-128
    Repeat
     Immagine$=Edit$(80)
     If Immagine$<>"" AND Right$(Immagine$,11)<>"blocks used" Then Immagini+1
    Until Eof(#ListaImmagini)=True OR Immagini=127
    PopInput
    If Immagini>-128
     ImmaScelta.b=Rnd(Immagini+128)-128
     ListaImma.b=ReadFile(#ListaImmagini,ListaImmagini$):FileInput #ListaImmagini
     If ListaImma=True
      Immagine.b=-128
      Repeat
       Immagine$=Edit$(30)
       If Immagine$<>""
        Immagine+1
        If Immagine=ImmaScelta Then ImmaScelta=-128
        If Immagine=127 Then ImmaScelta=-128
        If Eof(#ListaImmagini)=True Then ImmaScelta=-128
       EndIf
      Until ImmaScelta=-128
      PopInput
      Imma$=Immagine$
     EndIf
    Else
     NPrint "Images missing into drawer "+Imma$+"!"
    EndIf
   Else
    NPrint "Drawer content list missing!"
   EndIf
  EndIf
  If CassettoImma=True Then Imma$=CassettoImma$+Imma$
  If Exists(Imma$)>0 Then ImmagineTrovata.b=True Else ImmagineTrovata=False
  If ImmagineTrovata=True
   ILBMInfo Imma$:XLunImma.w=ILBMWidth:YLunImma.w=ILBMHeight:ProfImma.b=ILBMDepth:ModoImma.l=ILBMViewMode:ColoImma.w=2^ProfImma
   If ModoImma BitTst 15=True Then XLunImma=640 Else XLunImma=320
   If ModoImma BitTst 2=True Then YLunImma=AltezzaSchermo*2 Else Then YLumImma=AltezzaSchermo
   InitPalette #TavolozzaImma,ColoImma
   ScreenTags #SchermoImma,Imma$,#XScr,XLunImma,#YScr,YLunImma,#DScr,ProfImma,#MScr,ModoImma,#IScr,ModoImma,#CScr,ColoImma
   HideScreen #SchermoImma
  EndIf
  If TempoDiAttesa>0
   InitShape #PuntatoreDelTopo,16,16,2
   ShapesBitMap #PuntatoreDelTopo,#AreaGrafica
   Circlef 7,7,7,1:Line 7,2,7,12,0:Line 2,7,12,7,0:Circle 7,7,7,2
   Free BitMap #AreaGrafica
   ShowScreen #SchermoNeroCoprente
   ScreensBitMap #SchermoNeroCoprente,#AreaGrafica
   If ImmagineTrovata=True
    Free BitMap #AreaGrafica
    LoadScreen #SchermoImma,Imma$,#TavolozzaImma
    Use Palette #TavolozzaImma
    ShowScreen #SchermoImma
    ScreensBitMap #SchermoImma,#AreaGrafica
    HideScreen #SchermoNeroCoprente
    CloseScreen #SchermoNeroCoprente
    Free Palette #TavolozzaNeroCoprente
    Use Screen #SchermoImma
    ColoreChiaro=FindColor(#TavolozzaImma,191,191,191,16)
    XLunFinestra=XLunImma:YLunFinestra=YLunImma/64
    If ModoImma BitTst 2=False Then YPosFinestra=AltezzaSchermo-YLunFinestra Else YPosFinestra=AltezzaSchermo*2-YLunFinestra
   EndIf
   Window #FinestraImma,0,YPosFinestra,XLunFinestra,YLunFinestra,#TipoFinestra,"",-1,-1
   WPointer #PuntatoreDelTopo:MenusOff
   If ImmagineTrovata=True Then BitMaptoWindow #AreaGrafica,#FinestraImma,0,YPosFinestra,0,0,XLunFinestra,YLunFinestra
   If Exists(Suono$)>0 Then SuonoTrovato.b=True Else SuonoTrovato=False
   If SuonoTrovato=True
    LoadSound #Suono,Suono$:Suono.b=Rnd(1)
    If RipetiIlSuono=True Then LoopSound #Suono,3 Else Sound #Suono,Suono:Delay_ 12:Sound #Suono,1-Suono
   EndIf
   BitMapOutput #AreaGrafica:FlushEvents #HaiPremutoITastiDelTopo
   Repeat
    WBox 0,0,ScreenWidth*TempoTrascorso.w/TempoDiAttesa,YLunFinestra-1,ColoreChiaro
    EventoFinestra.l=Event
    If EventoFinestra=#HaiPremutoITastiDelTopo
     TastoPremutoDelTopo.b=Joyb(0):If TastoPremutoDelTopo=0 Then TastoPremutoDelTopo=Joyb(1)
     Select TastoPremutoDelTopo
     Case 1
      Locate 0,YPosFinestra/8-1:Colour ColoreChiaro:Print Centre$("*Press Right mouse button to exit. :)*",ScreenWidth/8)
      TastoSinistroPremutoDelTopo.b=True
     Case 2
      Pop Select:Pop If:Pop Repeat:Goto FineCicloDiAttesa
     End Select
    EndIf
    If EventoFinestra=#FinestraDisattivata
     If TastoSinistroPremutoDelTopo=False
      Locate 0,YPosFinestra/8-1:Colour ColoreChiaro
      If Problemi<127 Then Problemi+1:Problemi$=UStr$(Problemi) Else Problemi$="Too many"
      Print Centre$("*Something has gone wrong: "+Problemi$+" prob :(*",ScreenWidth/8)
     EndIf
     Activate #FinestraImma
    EndIf
    VWait
    If ImmagineTrovata=True Then ShowScreen #SchermoImma Else ShowScreen #SchermoNeroCoprente
    TempoTrascorso+1
   Until TempoTrascorso=TempoDiAttesa
   FineCicloDiAttesa
   If ImmagineTrovata=True
    CloseWindow #FinestraImma
    HideScreen #SchermoImma
    CloseScreen #SchermoImma
    Free Palette #TavolozzaImma
   EndIf
   Free Shape #PuntatoreDelTopo
  EndIf
  If SuonoTrovato=True AND RipetiIlSuono=True Then Quiet 3
  If SuonoTrovato=True Then Free Sound #Suono
  If Exists(ListaImmagini$)>0 Then KillFile ListaImmagini$
 EndIf
Else
 WBStartup
 Request TitoloProgramma$,"Please launch it from CLI|or better from script!","Okay"
EndIf
End
