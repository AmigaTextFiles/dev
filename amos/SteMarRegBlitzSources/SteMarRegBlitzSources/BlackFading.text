;-> BlackFading by Stefano Maria Regattin
;d> 27 May 2020 from RandomTiles.text
;-------------------------------------
#MinimumScreenHeight=256

BlackColour.w=1

;black fading effects
Statement BlackFading{Fading.b}
SHARED BlackColour
 Select Fading
 Case 0
  If WBHeight>#MinimumScreenHeight Then SquareHeight.b=2 Else SquareHeight=1
  SquaresRow.b=InnerWidth/16:SquaresFound.w=SquaresRow*(InnerHeight/(8*SquareHeight))
  Dim Squares.b(SquaresFound-1)
  BlackSquares.w=0
  Repeat
   SquareFound.w=Rnd(SquaresFound-BlackSquares-1)+1:BlackSquare.w=0
   For Square.w=0 To SquaresFound-1
    If Squares(Square)=False Then SquareFound-1:If SquareFound=0 Then BlackSquare=Square
   Next Square
   XPosSquare.w=BlackSquare MOD SquaresRow*16
   YPosSquare.w=BlackSquare/SquaresRow*8*SquareHeight
   WBox XPosSquare,YPosSquare,XPosSquare+15,YPosSquare+8*SquareHeight-1,BlackColour:VWait
   Squares(BlackSquare)=True:BlackSquares+1
  Until BlackSquares=SquaresFound
 Case 1
  XEvenPoint.b=InnerWidth MOD 2:YEvenPoint.b=InnerHeight MOD 2
  For XPosPoint.w=0 To InnerWidth-1 Step 2
   For YPosPoint.w=0 To InnerHeight-1 Step 2
    WPlot XPosPoint,YPosPoint,BlackColour
   Next YPosPoint
   VWait
  Next XPosPoint
  For YPosPoint=0 To InnerHeight-1 Step 2
   For XPosPoint=1 To InnerWidth-1 Step 2
    WPlot XPosPoint,YPosPoint,BlackColour
   Next XPosPoint
   VWait
  Next YPosPoint
  For XPosPoint=InnerWidth-2+XEvenPoint To 0 Step -2
   For YPosPoint=InnerHeight-1-YEvenPoint To 0 Step -2
    WPlot XPosPoint,YPosPoint,BlackColour
   Next YPosPoint
   VWait
  Next XPosPoint
  For YPosPoint=InnerHeight-1-YEvenPoint To 0 Step -2
   For XPosPoint=InnerWidth-1-XEvenPoint To 0 Step -2
    WPlot XPosPoint,YPosPoint,BlackColour
   Next XPosPoint
   VWait
  Next YPosPoint
 Case 2
  XEvenPoint=InnerWidth MOD 2:YEvenPoint=InnerHeight MOD 2
  For YPosPoint=0 To InnerHeight-1 Step 2
   For XPosPoint=0 To InnerWidth-1 Step 2
    WPlot XPosPoint,YPosPoint,BlackColour
    WPlot InnerWidth-1-XEvenPoint-XPosPoint,InnerHeight-1-YEvenPoint-YPosPoint,BlackColour
   Next XPosPoint
   VWait
  Next YPosPoint
  For YPosPoint=0 To InnerHeight-1 Step 2
   For XPosPoint=1 To InnerWidth-1 Step 2
    WPlot XPosPoint,YPosPoint,BlackColour
    WPlot InnerWidth-1-XEvenPoint-XPosPoint,InnerHeight-1-YEvenPoint-YPosPoint,BlackColour
   Next XPosPoint
   VWait
  Next YPosPoint
 Case 3
  For Degrees.f=0 To 180 Step 0.1
   XIniLine.w=InnerWidth/2-Sin(Degrees*Pi/180)*InnerWidth:XIniLine=Min(Max(XIniLine,0),InnerWidth-1)
   YIniLine.w=InnerHeight/2+Cos(Degrees*Pi/180)*InnerHeight:YIniLine=Min(Max(YIniLine,0),InnerHeight-1)
   XFinLine.w=InnerWidth/2+Sin(Degrees*Pi/180)*InnerWidth:XFinLine=Min(Max(XFinLine,0),InnerWidth-1)
   YFinLine.w=InnerHeight/2-Cos(Degrees*Pi/180)*InnerHeight:YFinLine=Min(Max(YFinLine,0),InnerHeight-1)
   DegreesExclusion.b=Degrees MOD 90
   If DegreesExclusion<31 OR DegreesExclusion>59 Then Wline XIniLine,YIniLine,XFinLine,YFinLine,BlackColour:VWait
  Next Degrees
 End Select
End Statement

;************************************
;* procedures end and program start *
;************************************

FindScreen 0,"Workbench Screen"
Window 0,0,11,208,149,$1408,"BlackFading",BlackColour,-1
ProgramEnd.b=0
Repeat
 FlushEvents
 WindowEvent.l=WaitEvent
 Select WindowEvent
 Case 512
  ProgramEnd+1
  If WCursY=InnerHeight Then WPrintScroll
  If ProgramEnd=1 Then NPrint "Press again..."
 Case 1024
  TastoPremuto$=Inkey$
  If TastoPremuto$=Chr$(27)
   ProgramEnd+1
   If WCursY=InnerHeight Then WPrintScroll
   If ProgramEnd=1 Then NPrint "Press again..."
  EndIf
 Default
  Fading.b=Rnd(127) MOD 4
  If WCursY=InnerHeight Then WPrintScroll
  NPrint "Fading number ",Fading+1
  Delay_ 50
  BlackFading{Fading}
  WCls
  Wline 0,0,InnerWidth-1,0,0
 End Select
Until ProgramEnd=2
Print "Bye."
Delay_ 50
CloseWindow 0
End
