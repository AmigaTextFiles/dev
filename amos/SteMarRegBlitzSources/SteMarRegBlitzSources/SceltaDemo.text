;-> SceltaDemo by Stefano Maria Regattin
;d> 26 ottobre 2021 da SceltaGioco
;m> 27 ottobre 2021
;m> 4,7,11,15,29 novembre 2021
;m> 1 dicembre 2021
;-------------------

JMP InizioDelProgramma

Dc.b "$VER: SceltaDemo 1.0 (1.12.2021)"

Even

InizioDelProgramma

;longest buttons text
TestoPiuLungoBottoni$=" TheReturnOfTheCube "

;text characters
CaratteriTesti.b=Len(TestoPiuLungoBottoni$)


;------------------------------------------
;queste sono le proprieta' di una finestra
;these are the properties of a window
;-------------------------------------

;window sizing gadget
#BottoneDimensionamentoFinestra=1

;draggable window
#SpostamentoFinestra=2

;window depth gadget
#ProfonditaFinestra=4

;window close gadget
#BottoneChiusuraFinestra=8

;window border right side (you must set GIMME ZERO ZERO to use it)
#DisegnoBordoDestra=16

;window bottom side (you must set GIMME ZERO ZERO to use it)
#DisegnoBordoSotto=32

;backdrop window
#FinestraComeSfondo=256

;gimme zero zero
#DammiZeroZero=1024

;borderless window
#FinestraSenzaBordi=2048

;window activated
#FinestraAttivata=4096

;window properties
#TipoFin=#DammiZeroZero+#FinestraAttivata


;---------------------------------------
;questi sono gli eventi di una finestra
;these are the events of a window
;---------------------------------

;you have pressed a mouse button
#HaiPremutoUnTastoDelTopo=8

;you have moved the mouse
#HaiMossoIlTopo=16

;you have pressed a gadget
#HaiPremutoUnBottone=32

;you have released a gadget
#HaiRilasciatoUnBottone=64

;you have activated a menu
#HaiAttivatoUnMenu=256

;you have closed the window
#HaiChiusoLaFinestra=512

;you have pressed a key
#HaiPremutoUnTasto=1024

;you have inserted a disk
#HaiInseritoUnDisco=32768

;you have removed a disk
#HaiToltoUnDisco=65536

;you have activated the window
#HaiAttivatoLaFinestra=262144

;you have deactivated the window
#HaiDisattivatoLaFinestra=524288


;---------------------------------------------------
;questi sono alcuni dei codici ASCII della tastiera
;these are several ASCII codes of the keyboard
;----------------------------------------------

;line feed
#NuovaLineaASCII=10

;escape key
#TastoFugaASCII=27

;cursor up key
#TastoCursoreSuASCII=28

;cursor down key
#TastoCursoreGiuASCII=29

;cursor right key
#TastoCursorDestraASCII=30

;cursor left key
#TastoCursorSinistraASCII=31

;space character value
#TastoSpazio=32


;---------------
;altre costanti
;other constants
;----------------

;boot screen colours
#Colori=4

;window referring number
#Finestra=0

;topaz font size
#GrandezzaFonte=8

;triangles' size
#GrandezzaTriangolo=12

;buttons height
#AltezzaBottoni=2+#GrandezzaFonte+2

;program reading
#LetturaProgramma=0

;gadget list referring number
#ListaBottoni=0

;workbench screen referring number
#SchermoDAvvio=0

;sound
#Suono=0

;palette referring number
#TavolaColori=0

;button text width
LunghezzaTestoBottoni.w=CaratteriTesti*#GrandezzaFonte

Dim XPuntoTriangolo.w(4),YPuntoTriangolo.w(4)

WBStartup
WbToScreen #SchermoDAvvio
Use Screen #SchermoDAvvio

XLunSchermoDAvvio.w=WBWidth
YLunSchermoDAvvio.w=WBHeight

InitPalette #TavolaColori,#Colori
For Colore.w=0 To #Colori-1
 If CheckAGA=True
  AGAPalRGB #TavolaColori,Colore,AGARed(Colore),AGAGreen(Colore),AGABlue(Colore)
 Else
  PalRGB #TavolaColori,Colore,Red(Colore),Green(Colore),Blue(Colore)
 EndIf
Next Colore
ColoreBianco.w=FindColor(#TavolaColori,255,255,255,16)
ColoreNero.w=FindColor(#TavolaColori,0,0,0,16)

PercorsoProgramma$=ProgDir$:Suono$=PercorsoProgramma$+"/Biupp.8svx"
If Exists(Suono$)>0 SuonoPresente.b=True Else SuonoPresente=False
If SuonoPresente=True Then LoadSound #Suono,Suono$

BorderPens ColoreBianco,ColoreNero
NumeroBottoni.b=0:TipoBottone=0:YPosBottone.w=0
GadgetPens ColoreBianco
TextGadget #ListaBottoni,0,YPosBottone,TipoBottone,1," ADV-Relievo        ":NumeroBottoni+1:YPosBottone+#AltezzaBottoni
TextGadget #ListaBottoni,0,YPosBottone,TipoBottone,2," Andy And Blondie   ":NumeroBottoni+1:YPosBottone+#AltezzaBottoni
TextGadget #ListaBottoni,0,YPosBottone,TipoBottone,3," ATV_mdiv2          ":NumeroBottoni+1:YPosBottone+#AltezzaBottoni
TextGadget #ListaBottoni,0,YPosBottone,TipoBottone,4," Avec               ":NumeroBottoni+1:YPosBottone+#AltezzaBottoni
TextGadget #ListaBottoni,0,YPosBottone,TipoBottone,5," Bees               ":NumeroBottoni+1:YPosBottone+#AltezzaBottoni
TextGadget #ListaBottoni,0,YPosBottone,TipoBottone,6," ChaosLand          ":NumeroBottoni+1:YPosBottone+#AltezzaBottoni
TextGadget #ListaBottoni,0,YPosBottone,TipoBottone,7," Cromatics          ":NumeroBottoni+1:YPosBottone+#AltezzaBottoni
TextGadget #ListaBottoni,0,YPosBottone,TipoBottone,8," ExtraLife          ":NumeroBottoni+1:YPosBottone+#AltezzaBottoni
TextGadget #ListaBottoni,0,YPosBottone,TipoBottone,9," Fanatix            ":NumeroBottoni+1:YPosBottone+#AltezzaBottoni
TextGadget #ListaBottoni,0,YPosBottone,TipoBottone,10," HerNumb            ":NumeroBottoni+1:YPosBottone+#AltezzaBottoni
TextGadget #ListaBottoni,0,YPosBottone,TipoBottone,11," Oberon             ":NumeroBottoni+1:YPosBottone+#AltezzaBottoni
TextGadget #ListaBottoni,0,YPosBottone,TipoBottone,12," OutaSpace          ":NumeroBottoni+1:YPosBottone+#AltezzaBottoni
TextGadget #ListaBottoni,0,YPosBottone,TipoBottone,13," Potentiam Agite    ":NumeroBottoni+1:YPosBottone+#AltezzaBottoni
TextGadget #ListaBottoni,0,YPosBottone,TipoBottone,14," Perfect Circle     ":NumeroBottoni+1:YPosBottone+#AltezzaBottoni
TextGadget #ListaBottoni,0,YPosBottone,TipoBottone,15," Sote Sma...        ":NumeroBottoni+1:YPosBottone+#AltezzaBottoni
TextGadget #ListaBottoni,0,YPosBottone,TipoBottone,16," SuperAutoDrome2    ":NumeroBottoni+1:YPosBottone+#AltezzaBottoni
TextGadget #ListaBottoni,0,YPosBottone,TipoBottone,17," TheReturnOfTheCube ":NumeroBottoni+1:YPosBottone+#AltezzaBottoni
TextGadget #ListaBottoni,0,YPosBottone,TipoBottone,18," Together           ":NumeroBottoni+1:YPosBottone+#AltezzaBottoni
TextGadget #ListaBottoni,0,YPosBottone,TipoBottone,19," UnderWorld         ":NumeroBottoni+1:YPosBottone+#AltezzaBottoni
TextGadget #ListaBottoni,0,YPosBottone,TipoBottone,20," UP-Rough           ":NumeroBottoni+1:YPosBottone+#AltezzaBottoni
GadgetPens 3
TextGadget #ListaBottoni,0,YPosBottone,TipoBottone,21," Esci               ":NumeroBottoni+1:YPosBottone+#AltezzaBottoni
GadgetPens ColoreNero
TextGadget #ListaBottoni,0,YPosBottone,TipoBottone,0, " Resetta Amiga!     ":NumeroBottoni+1

AltezzaFinestra.w=NumeroBottoni*#AltezzaBottoni+#GrandezzaTriangolo
XLunFin.w=4+4+LunghezzaTestoBottoni+4+4
YLunFin.w=11+AltezzaFinestra+2
XPosFin.w=(XLunSchermoDAvvio-XLunFin)/2
YPosFin.w=(YLunSchermoDAvvio-YLunFin)/2
Window #Finestra,XPosFin,YPosFin,XLunFin,YLunFin,#TipoFin,"Quale vuoi lanciare?",ColoreNero,ColoreBianco,#ListaBottoni
WTitle "Quale vuoi lanciare?","SceltaDemo 1.0 made in Italy by Stefano Maria Regattin on Wed 1 Dec 2021"
DemoScelto.b=22:FineDelProgramma.b=False:XIniBot.b=3:XFinBot.w=XIniBot+LunghezzaTestoBottoni+1:YIniBot.w=0:YFinBot.w=0
Triangoli.w=InnerWidth/#GrandezzaTriangolo*2+2:Gosub DisegnaTriangoli
Repeat
 VWait
 If DemoScelto=22
  YDelTopo.w=WMouseY:YDelTopo=Min(Max(YDelTopo,0),AltezzaFinestra-#AltezzaBottoni)/#AltezzaBottoni*#AltezzaBottoni+1
  If YDelTopo<>YIniBot AND YDelTopo<InnerHeight-#GrandezzaTriangolo
   Wline XIniBot-1,YFinBot,XIniBot-1,YIniBot,XFinBot+1,YIniBot,XFinBot+1,YFinBot,XIniBot-1,YFinBot,0
   Wline XIniBot,YFinBot,XIniBot,YIniBot,XFinBot,YIniBot,XFinBot,YFinBot,XIniBot,YFinBot,0
   WColour 0:WLocate XIniBot+2,YIniBot+1:Print "*":WLocate XIniBot+2+#GrandezzaFonte*19,YIniBot+1:Print "*"
   YIniBot=YDelTopo:YFinBot=YIniBot+#GrandezzaFonte+1
   Wline XIniBot-1,YFinBot,XIniBot-1,YIniBot,XFinBot+1,YIniBot,XFinBot+1,YFinBot,XIniBot-1,YFinBot,3
   Wline XIniBot,YFinBot,XIniBot,YIniBot,XFinBot,YIniBot,XFinBot,YFinBot,XIniBot,YFinBot,3
   WColour 3:WLocate XIniBot+2,YIniBot+1:Print "*":WLocate XIniBot+2+#GrandezzaFonte*19,YIniBot+1:Print "*"
   If SuonoPresente=True Then Sound #Suono,1,64
  EndIf
 EndIf
 EventoFinestra.l=Event
 Select EventoFinestra
 Case #HaiPremutoUnTasto
  TastoPremuto.b=Asc(Inkey$)
  Select TastoPremuto
  Case #TastoFugaASCII
   FineDelProgramma=True
  End Select
 Case #HaiRilasciatoUnBottone
  DemoScelto=GadgetHit
  Select DemoScelto
  Case 0
   Reboot
  Case 1
   Percorso$="Demo:AVD-Relievo"
   Programma$="AVD-Relievo.exe"
   Argomenti$=""
  Case 2
   Percorso$="Demo:"
   Programma$="Andy_And_Blondie"
   Argomenti$=""
  Case 3
   Percorso$="Demo:ATW_mdiv2"
   Programma$="MDIV2_Amiga68k.exe"
   Argomenti$=""
  Case 4
   Percorso$="Demo:Avec"
   Programma$="Start"
   Argomenti$=""
  Case 5
   Percorso$="Demo:Bees"
   Programma$="Bees.exe"
   Argomenti$=""
  Case 6
   Percorso$="Demo:ChaosLand"
   Programma$="vd-chaos.exe"
   Argomenti$=""
  Case 7
   Percorso$="Demo:Cromatics"
   Programma$="Cromatics_UUSI.exe"
   Argomenti$=""
  Case 8
   Percorso$="Demo:ExtraLife"
   Programma$="ExtraLife.exe"
   Argomenti$=""
  Case 9
   Percorso$="Demo:Fanatix"
   Programma$="Fanatix.BAT"
   Argomenti$=""
  Case 10
   Percorso$="Demo:HerNumb"
   Programma$="hERnUMB"
   Argomenti$=""
  Case 11
   Percorso$="Demo:Oberon"
   Programma$="oberon"
   Argomenti$=""
  Case 12
   Percorso$="Demo:OutaSpace"
   Programma$="OutaSpace.exe"
   Argomenti$=""
  Case 13
   Percorso$="Demo:PerfectCircle"
   Programma$="PerfectCircle.exe"
   Argomenti$=""
  Case 14
   Percorso$="Demo:Poenitentiam"
   Programma$="PoenitentiamAgite"
   Argomenti$=""
  Case 15
   Percorso$="Demo:Sote_sma..."
   Programma$="Start"
   Argomenti$=""
  Case 16
   Percorso$="Demo:scx_sad2"
   Programma$="SCX-Superautodrome2.exe"
   Argomenti$=""
  Case 17
   Percorso$="Demo:TheReturnOfTheCube"
   Programma$="The Return Of The Cube"
   Argomenti$=""
  Case 18
   Percorso$="Demo:pht_together"
   Programma$="Together.exe"
   Argomenti$=""
  Case 19
   Percorso$="Demo:UnderWorld"
   Programma$="UnderWorld"
   Argomenti$=""
  Case 20
   Percorso$="Demo:UP-Rough"
   Programma$="Start"
   Argomenti$=""
  Case 21
   FineDelProgramma=True
  End Select
  If DemoScelto>0 AND DemoScelto<21
   ChangeDir Percorso$
   FileAperto.b=ReadFile(#LetturaProgramma,Programma$)
   If FileAperto=True
    LunghezzaProgramma.l=Lof(#LetturaProgramma):FileInput #LetturaProgramma:FileDiTesto.b=True
    For Byte.l=0 To LunghezzaProgramma-1
     CarattereLetto.w=Edit(1)
     If CarattereLetto<#TastoSpazio
      If CarattereLetto<>#NuovaLineaASCII
       FileDiTesto:If SuonoPresente=True Then Sound #Suono,1,64
       Pop For:Goto UscitaLettura
      EndIf
     EndIf
    Next Byte
UscitaLettura
    PopInput:CloseFile #LetturaProgramma
    If FileDiTesto=True
     Gosub Triangoli:Exec "C:Execute "+Programma$
    Else
     Gosub Triangoli:DemoLanciato.l=Run(Programma$,Argomenti$)
     If DemoLanciato=0 Then DemoLanciato=Request("SceltaDemo 1.0","Has the demo ran?","Yes|No")
     If DemoLanciato=0 Then Gosub DisegnaTriangoli:Gosub Triangoli:Execute_ Programma$,Argomenti$,0
    EndIf
    Gosub DisegnaTriangoli
   Else
    Request "SceltaDemo 1.0",Programma$+" does not exists!","Okay"
   EndIf
   Request "SceltaDemo 1.0","At end click Okay","Okay":DemoScelto=22
  EndIf
 Case #HaiDisattivatoLaFinestra
  If SuonoPresente=True Then Sound #Suono,1,64
 End Select
Until FineDelProgramma=True
CloseWindow #Finestra
Free Sound #Suono
End
.DisegnaTriangoli
 YPosTriangolo.w=InnerHeight-#GrandezzaTriangolo
 WBox 0,YPosTriangolo,InnerWidth-1,InnerHeight-1,0
 For Triangolo=0 To Triangoli-1
  XPosTriangolo.w=Triangolo*#GrandezzaTriangolo
  XPuntoTriangolo(0)=XPosTriangolo
  YPuntoTriangolo(0)=YPosTriangolo
  XPuntoTriangolo(1)=XPosTriangolo+#GrandezzaTriangolo/2-1
  YPuntoTriangolo(1)=YPosTriangolo+#GrandezzaTriangolo-1
  XPuntoTriangolo(2)=XPosTriangolo+#GrandezzaTriangolo-1
  YPuntoTriangolo(2)=YPosTriangolo
  XPuntoTriangolo(3)=XPosTriangolo
  YPuntoTriangolo(3)=YPosTriangolo+#GrandezzaTriangolo-1
  XPuntoTriangolo(4)=XPosTriangolo+#GrandezzaTriangolo-1
  YPuntoTriangolo(4)=YPosTriangolo+#GrandezzaTriangolo-1
  Wline XPuntoTriangolo(0),YPuntoTriangolo(0),XPuntoTriangolo(1),YPuntoTriangolo(1),1
  Wline XPuntoTriangolo(1),YPuntoTriangolo(1),XPuntoTriangolo(2),YPuntoTriangolo(2),1
  Wline XPuntoTriangolo(2),YPuntoTriangolo(2),XPuntoTriangolo(0),YPuntoTriangolo(0),1
  Wline XPuntoTriangolo(3),YPuntoTriangolo(3),XPuntoTriangolo(4),YPuntoTriangolo(4),1
 Next Triangolo
Return
.ProgressoTriangoli
 YPosTriangolo=InnerHeight-#GrandezzaTriangolo
 XPosTriangolo=Triangolo*#GrandezzaTriangolo/2
 If Triangolo MOD 2=0
  XPuntoTriangolo(0)=XPosTriangolo-#GrandezzaTriangolo/2+1:XPuntoTriangolo(0)=Max(XPuntoTriangolo(0),1)
  XPuntoTriangolo(1)=XPosTriangolo+1:XPuntoTriangolo(1)=Min(XPuntoTriangolo(1),InnerWidth-1)
  XPuntoTriangolo(2)=XPosTriangolo+#GrandezzaTriangolo/2-1:XPuntoTriangolo(2)=Min(XPuntoTriangolo(2),InnerWidth-1)
  YPuntoTriangolo(0)=YPosTriangolo+#GrandezzaTriangolo-2
  YPuntoTriangolo(1)=YPosTriangolo+1
  YPuntoTriangolo(2)=YPosTriangolo+#GrandezzaTriangolo-2
  Wline XPuntoTriangolo(0),YPuntoTriangolo(0),XPuntoTriangolo(1),YPuntoTriangolo(1),2
  Wline XPuntoTriangolo(1),YPuntoTriangolo(1),XPuntoTriangolo(2),YPuntoTriangolo(2),2
  Wline XPuntoTriangolo(2),YPuntoTriangolo(2),XPuntoTriangolo(0),YPuntoTriangolo(0),2
 Else
  XPuntoTriangolo(0)=XPosTriangolo-#GrandezzaTriangolo/2+1
  XPuntoTriangolo(1)=XPosTriangolo-1:XPuntoTriangolo(1)=Min(XPuntoTriangolo(1),InnerWidth-1)
  XPuntoTriangolo(2)=XPosTriangolo+#GrandezzaTriangolo/2-2:XPuntoTriangolo(2)=Min(XPuntoTriangolo(2),InnerWidth-1)
  YPuntoTriangolo(0)=YPosTriangolo+1
  YPuntoTriangolo(1)=YPosTriangolo+#GrandezzaTriangolo-2
  YPuntoTriangolo(2)=YPosTriangolo+1
  Wline XPuntoTriangolo(0),YPuntoTriangolo(0),XPuntoTriangolo(1),YPuntoTriangolo(1),2
  Wline XPuntoTriangolo(1),YPuntoTriangolo(1),XPuntoTriangolo(2),YPuntoTriangolo(2),2
  Wline XPuntoTriangolo(2),YPuntoTriangolo(2),XPuntoTriangolo(0),YPuntoTriangolo(0),2
 EndIf
Return
.Triangoli
 Gosub DisegnaTriangoli:Triangolo=0
 For Triangolo=0 To Triangoli-1
  Gosub ProgressoTriangoli:Delay_ 10
 Next Triangolo
Return
