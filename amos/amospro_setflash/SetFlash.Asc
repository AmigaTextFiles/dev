' Sets the default flash 
' By Fredrik Rambris <fredrik@rambris.com> 
' $VER: SetFlash.AMOS 1.0 (24.03.2024) 
'  
' Makes a backup of the original configfile with a .old suffix 
' Can be adapted to edit any of the strings in the config


CFGFILE$="S:AMOSPro_Interpreter_Config"

BNKNUM=10
MXSTR=47
Dim CFGSTR$(MXSTR)
Global CFGFILE$,BNKNUM,MXSTR,CFGSTR$()
_DEFAULT_FLASH$="(000,2)(440,2)(880,2)(bb0,2)(dd0,2)(ee0,2)(ff2,2)(ff8,2)(ffc,2)(fff,2)(aaf,2)(88c,2)(66a,2)(226,2)(004,2)(001,2)"


_LOAD_CFG

' Set the flash
CFGSTR$(46)=""

_SAVE_CFG

End 

Procedure _STRINGS
   ' 1-15 = List of system files
   ' 16-42 = List of exensions
   ' 43 = Printer port
   ' 44 = Serial port 
   ' 46 = Cursor flash
   ' 47 = Negative directory filter     
End Proc

Procedure _LOAD_CFG
   MXTXTLEN=(MXSTR*257)+176+4+8
   
   Reserve As Work BNKNUM,MXTXTLEN
   
   Bload CFGFILE$,Start(BNKNUM)
   If Peek$(Start(BNKNUM),4)="PId1"
      ' Offset to text chunk 
      STROFFSET=Leek(Start(BNKNUM)+4)
      
      ' Address to text chunk
      TXTSTART=Start(BNKNUM)+8+STROFFSET
      
      If Peek$(TXTSTART,4)="PIt1"
         ' Length of text chunk 
         TXTLEN=Leek(TXTSTART+4)
         
         CURPTR=TXTSTART+8
         
         For ST=1 To MXSTR
            STRLEN=Peek(CURPTR+1) : Add CURPTR,2
            Exit If STRLEN=$FF
            CFGSTR$(ST)=Peek$(CURPTR,STRLEN)
            Add CURPTR,STRLEN
         Next ST
      End If 
   End If 
End Proc

Procedure _SAVE_CFG
   If Peek$(Start(BNKNUM),4)="PId1"
      ' Offset to text chunk 
      STROFFSET=Leek(Start(BNKNUM)+4)
      
      ' Address to text chunk
      TXTSTART=Start(BNKNUM)+8+STROFFSET
      
      If Peek$(TXTSTART,4)="PIt1"
         CURPTR=TXTSTART+4
         
         TXTLENPTR=CURPTR : Rem Will write to later
         Add CURPTR,4
         
         For ST=1 To MXSTR
            Poke CURPTR,0
            Poke CURPTR+1,Len(CFGSTR$(ST))
            Poke$ CURPTR+2,CFGSTR$(ST)
            Add CURPTR,2+Len(CFGSTR$(ST))
         Next 
         
         Poke CURPTR,0 : Poke CURPTR+1,$FF : Add CURPTR,2
         Poke CURPTR,0 : Rem Blank last byte for safety 
         
         Add CURPTR,CURPTR mod 2 : Rem Even 
         
         TXTLEN=CURPTR-TXTLENPTR-4
         Loke TXTLENPTR,TXTLEN

         ' A little safety switcharoo         
         Trap Kill CFGFILE$+".old"
         Bsave CFGFILE$+".new",Start(BNKNUM) To CURPTR
         Rename CFGFILE$ To CFGFILE$+".old"
         Rename CFGFILE$+".new" To CFGFILE$
      End If 
   End If 
End Proc
