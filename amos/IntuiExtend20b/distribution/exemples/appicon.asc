'**************************************
'                                     *
'     IntuiExtend.Lib 2.0/@1995-98    *
'                                     *
'          by CIERP Philippe.         *
'                                     *
'          from AMIGAzette 83         *
'                                     *
'**************************************
'
' Command
'  -Wb Create Msgport    
'  -Wb Get Deficon     
'  -App Create Icon      
'  -Wb Get Msg     
'  -App Get Numarg     
'  -App Get Arglist    
'  -Wb Reply Msg     
'  -App Free Icon      
'  -Wb Free Diskobject       
'  -Wb Erase Msgport     
'
Screen Open 0,640,256,2,$8000
Curs Off : Cls 0 : Colour 1,$AAA
'
Amos To Back 
'
'Equate définition 
WBDISK=1
WBDRAVER=2
WBTOOL=3
WBPROJECT=4
WBGARBAGE=5
WBDEVICE=6
WBKICK=7
WBAPPICON=8
'
'Création du Port-Message
MY_PORT=Wb Create Msgport
If MY_PORT>0
   'Réserver l'icône à utiliser   
   D_OBJ=Wb Get Deficon(WBTOOL)
   If D_OBJ>0
      'Créer et afficher la AppIcon
      APPICON=App Create Icon(D_OBJ,MY_PORT,"Phil-AppIcon"+Chr$(0))
      If APPICON>0
         MSG=Wb Get Msg(MY_PORT)
         If MSG=$80000
            'NumArg=0/DoubleClick else Number of Args. 
            NUMARG=App Get Numarg
            If NUMARG=0
               Print "AppIcon activée..."
            Else 
               Print "Nombre de paramètre:";NUMARG
               Print "Liste des Paramètres...";Chr$(10)
               For T=1 To NUMARG
                  ARGLIST$=App Get Arglist(T)
                  Print "Argument n°";T;" >> ";Left$(ARGLIST$,30)
               Next T
            End If 
         End If 
         'Renvoyer le message à Intuition 
         Wb Reply Msg 
         'Libérer la AppIcon
         App Free Icon APPICON
      End If 
      'Libérer l'icône réservée  
      Wb Free Diskobject D_OBJ
   End If 
   'Libérer le Port-Message 
   Wb Erase Msgport(MY_PORT)
End If 
'
Amos To Front 
Print Chr$(10);"Pressez une touche..."
Wait Key 
