'**************************************
'                                     *
'     IntuiExtend.Lib 2.0/@1995-98    *
'                                     *
'          by CIERP Philippe.         *
'                                     *
'          from AMIGAzette 83         *
'                                     *
'**************************************
'
'Fonction utilisée dans cet exemple: 
'Rt Lib Open           Rt Lib Close    
'Rt Text Req()         Rt String Req()     
'Rt Number Req()       Rt Palette Req()    
'Rt File Req()         Rt Get Name$    
'Rt Get Dir$           Rt Multifile Req    
'Rt Get Flist$()       Rt Free Flist     
'Rt Dir Req()          Rt Font Req()     
'Rt Get Font Name      Rt Get Font Size    
'Rt Screen Mode Req    Rt Get Display Width  
'Rt Get Display Height Rt Get Display Depth
'Rt Get Overscan Type  Rt Get Autoscroll 
'
Amos To Back 
'
'Ouverture de la reqTools.library
If Rt Lib Open=0 Then End 
'
A$="Salut, voici un exemple d'utilisation"+Chr$(10)+"de la ReqTools.library avec AMOS et AMOS-Pro."+Chr$(10)
A$=A$+"je vous laisse juger par vous même des différentes"+Chr$(10)+"possibilités que cela vous offre..."+Chr$(10)+Chr$(10)+"Philippe."
If Rt Text Req(A$,"Suite|Quitter","IntuiExtend.Lib 1.4/@1995-96",4,2)=0 Then End 
'
A$="Le Text Requester"+Chr$(10)+"=Rt Text Req()"+Chr$(10)
A$=A$+"Vous permet de donner des informations,"+Chr$(10)+"de proposer un choix illimité etc, etc..."
B=Rt Text Req(A$,"B1|B2|B3|B4|B5|B6|Annuler","IntuiExtend.Lib 1.4/@1995-96",4,2)
If B>0
B=Rt Text Req("Vous avez sélectionnez le"+Chr$(10)+"Bouton B"+(Str$(B)-" "),"Suite","IntuiExtend.Lib 1.4/@1995-96",4,2)
End If 
'
If Rt Text Req("Le String Requester"+Chr$(10)+"=Rt String Req()"+Chr$(10)+"c'est un requester d'entrée de chaines"+Chr$(10)+"de caractères...","Tester|Suite","IntuiExtend.Lib 1.4/@1995-96",4,2)=1
B$=Rt String Req("Quel est votre prénom ?","Prénom...",30,2,2)
B=Rt Text Req("Votre prénom est"+Chr$(10)+B$+".","Suite","IntuiExtend.Lib 1.4/@1995-96",4,2)
End If 
'
If Rt Text Req("Le Number Requester"+Chr$(10)+"=Rt Number Req()"+Chr$(10)+"c'est un requester d'entrée de chiffres"+Chr$(10)+"et de nombre entier (sans point)","Tester|Suite","IntuiExtend.Lib 1.4/@1995-96",4,2)=1
NUMBER=1993 : Rem Nombre par défaut
If Rt Number Req("Entrez une nouvelle valeur...",Varptr(NUMBER))=1
A$="Bouton 'Ok' sélectionné avec la valeur:"+Str$(NUMBER)
Else 
A$="Bouton 'Cancel' sélectionné"
End If 
B=Rt Text Req(A$,"Suite","IntuiExtend.Lib 1.4/@1995-96",4,2)
End If 
'
A$="Le Palette Requester"+Chr$(10)+"=Rt Palette Req()"+Chr$(10)
A$=A$+"Vous donne facilement accès à une"+Chr$(10)+"palette sans casse-tête..."+Chr$(10)
A$=A$+"Change en temp réel les couleurs et"+Chr$(10)+"vous retourne la couleur sélectionnée"
If Rt Text Req(A$,"Tester|Suite","IntuiExtend.Lib 1.4/@1995-96",4,2)=1
B=Rt Palette Req("Amusez-vous...",0,0)
If B>=0
A$="Bouton 'Ok' sélectionné avec la couleur:"+Str$(B)
Else 
A$="Bouton 'Cancel' sélectionné, pas de couleur."
End If 
B=Rt Text Req(A$,"Suite","IntuiExtend.Lib 1.4/@1995-96",4,2)
End If 
'
If Rt Text Req("Le File Requester"+Chr$(10)+"=Rt File Req()"+Chr$(10)+"c'est le requester de fichier"+Chr$(10)+"très utilisé en OS2 et 3.0+","Tester|Suite","IntuiExtend.Lib 1.4/@1995-96",4,2)=1
If Rt File Req("Sélectionnez un fichier...",Dir$,"Sans nom...","",2)=1
A$="Fichier: "+Rt Get Name$
A$=A$+Chr$(10)+"Répertoire: "+Rt Get Dir$
Else 
A$="Bouton 'Cancel' sélectionné"
End If 
B=Rt Text Req(A$,"Suite","IntuiExtend.Lib 1.4/@1995-96",4,2)
End If 
'
A$="Le Multifile Requester"+Chr$(10)+"=Rt Multifile Req()"+Chr$(10)
A$=A$+"c'est un requester de fichier"+Chr$(10)+"qui permet de sélectionner plusieurs"+Chr$(10)
A$=A$+"fichiers via la touche SHIFT."
If Rt Text Req(A$,"Tester|Suite","IntuiExtend.Lib 1.4/@1995-96",4,2)=1
B=Rt Multifile Req("Sélectionnez plusieurs fichiers...",Dir$,2)
If B>0
A$="Directory '"+Rt Get Dir$+"'"+Chr$(10)+Chr$(10)
For T=1 To B
Trap A$=A$+"Fichier"+Str$(T)+": "+Rt Get Flist$(T)+Chr$(10)
Next T
If Errtrap>0
A$="Trop de fichiers ont été selectionnés."
End If 
Else 
A$="Bouton 'Cancel' sélectionné"
End If 
Rt Free Flist : Rem Libérer la FList 
B=Rt Text Req(A$+Chr$(0),"Suite","IntuiExtend.Lib 1.4/@1995-96",4,2)
End If 
'
If Rt Text Req("Le Directory Requester"+Chr$(10)+"=Rt Dir Req()"+Chr$(10)+"c'est un requester de sélection"+Chr$(10)+"de répertoire."+Chr$(0),"Tester|Suite","IntuiExtend.Lib 1.4/@1995-96",4,2)=1
If Rt Dir Req("Sélectionnez un répertoire...",Dir$,2)=1
A$="Directory '"+Rt Get Dir$+"'"
Else 
A$="Bouton 'Cancel' sélectionné"
End If 
B=Rt Text Req(A$+Chr$(0),"Suite","IntuiExtend.Lib 1.4/@1995-96",4,2)
End If 
'
If Rt Text Req("Le Font Requester"+Chr$(10)+"=Rt Font Req()"+Chr$(10)+"c'est un requester de sélection"+Chr$(10)+"de police de caractère."+Chr$(0),"Tester|Suite","IntuiExtend.Lib 1.4/@1995-96",4,2)=1
If Rt Font Req("Sélectionnez une police...",0,0)=1
A$="Font name: "+Rt Get Font Name+Chr$(10)
A$=A$+"Font size:"+Str$(Rt Get Font Size)
Else 
A$="Bouton 'Cancel' sélectionné"
End If 
B=Rt Text Req(A$+Chr$(0),"Suite","IntuiExtend.Lib 1.4/@1995-96",4,2)
End If 
'
If Rt Text Req("Le Screen Mode Requester"+Chr$(10)+"=Rt Screen Mode Req()"+Chr$(10)+"c'est un requester de sélection"+Chr$(10)+"de mode d'écran."+Chr$(0),"Tester|Salut !!!","IntuiExtend.Lib 1.4/@1995-96",4,2)=1
If Rt Screen Mode Req("Sélectionnez une type d'écran...",0,0)=1
VMODE=Rt Get Display Id
W=Rt Get Display Width
H=Rt Get Display Height
D=Rt Get Display Depth
A$="   Mode video: "+Hex$(VMODE)+Chr$(10)
A$=A$+"      Largeur:"+Str$(W)+Chr$(10)
A$=A$+"      Hauteur:"+Str$(H)+Chr$(10)
A$=A$+"   Profondeur: "+Str$(D)+Chr$(10)
A$=A$+"Overscan Type: "+Str$(Rt Get Overscan Type)+Chr$(10)
A$=A$+"   AutoScroll: "+Str$(Rt Get Autoscroll)
Else 
A$="Bouton 'Cancel' sélectionné"
End If 
B=Rt Text Req(A$+Chr$(0),"Salut !!!","IntuiExtend.Lib 1.4/@1995-96",4,2)
End If 
'
Amos To Front 
'
'Refermer la librairie 
Rt Lib Close 
