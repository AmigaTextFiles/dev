'**************************************
'                                     *
'     IntuiExtend.Lib 2.0/@1995-98    *
'                                     *
'          by CIERP Philippe.         *
'                                     *
'          from AMIGAzette 83         *
'                                     *
'**************************************
'
' Commande 
'Wb 3d Make Object   
'Wb 3d Erase Object  
'Wb 3d Edge  
'Wb 3d Shape 
'
'Wb 3d Angle 
'Wb 3d Centre  
'Wb 3d Eye 
'
' Init de l'écran
Screen Open 0,640,256,2,$8000
Curs Off : Cls 0 : Colour 1,$AAA
Ink 1,0
Double Buffer 
Autoback 0
'
' Init de l'espace 3d
Wb 3d Centre 320,128
Wb 3d Angle 90,90,90
Wb 3d Eye 0,0,-600
'
' Init des l'objets 3d   
Restore CUBE1
Read A1,B1
OBJ1=Wb 3d Make Object(A1,B1)
'
Restore CUBE2
Read A2,B2
OBJ2=Wb 3d Make Object(A2,B2)
'
' Creation des point (edge) de l'objet 
Restore E_CUBE1
For NB=1 To A1
   Read P11,P12,P13
   Wb 3d Edge P11,P12,P13 To NB,OBJ1
Next NB
'
Restore E_CUBE2
For NB=1 To A2
   Read P21,P22,P23
   Wb 3d Edge P21,P22,P23 To NB,OBJ2
Next NB
'
'
' Creation des polygone (shape) de l'objet 
Restore S_CUBE1
For NB=1 To B1
   Read S10,S11,S12,S13
   Wb 3d Shape S10,S11,S12,S13 To NB,OBJ1
Next NB
'
Restore S_CUBE2
For NB=1 To B2
   Read S20,S21,S22,S23
   Wb 3d Shape S20,S21,S22,S23 To NB,OBJ2
Next NB
'
'
ANGZ1=90
ANGY1=90
ANGX1=90
'
ANGZ2=90
ANGY2=90
ANGX2=90
'
While Mouse Key<>2
   Wb 3d Angle ANGX1,ANGY1,ANGZ1
   Add ANGZ1,1,1 To 360
   Add ANGY1,2,1 To 360
   Add ANGX1,3,1 To 360
   Wb 3d Draw Object OBJ1
   '
   Wb 3d Angle ANGX2,ANGY2,ANGZ2
   Add ANGZ2,-1,1 To 360
   Add ANGY2,-2,1 To 360
   Add ANGX2,-3,1 To 360
   Wb 3d Draw Object OBJ2
   '
   Screen Swap 
   Wait Vbl 
   Cls 0
Wend 
'
' A la fin il faut liberer l'objet 3d
E0=Avail Mem(Mpublic)
Wb 3d Erase Object OBJ1
Wb 3d Erase Object OBJ2
E1=Avail Mem(Mpublic)
Default 
Print E0-E1
'
End 
'
CUBE1:
Data 8
Data 6
E_CUBE1:
Data -100,-100,-100
Data 100,-100,-100
Data -100,100,-100
Data 100,100,-100
Data -100,-100,100
Data 100,-100,100
Data -100,100,100
Data 100,100,100
S_CUBE1:
Data 1,2,4,3
Data 5,1,3,7
Data 2,6,8,4
Data 3,4,8,7
Data 5,6,2,1
Data 7,8,6,5

CUBE2:
Data 8
Data 6
E_CUBE2:
Data -50,-50,-50
Data 50,-50,-50
Data -50,50,-50
Data 50,50,-50
Data -50,-50,50
Data 50,-50,50
Data -50,50,50
Data 50,50,50
S_CUBE2:
Data 1,2,4,3
Data 5,1,3,7
Data 2,6,8,4
Data 3,4,8,7
Data 5,6,2,1
Data 7,8,6,5

'
' Figure de decomposition de l'objet CUBE
'
'      7----8
'      |    |  
'      |    |  
'      | #6 |  
'      |    |  
'      5----6
'      |    |  
'      |    |    
'      | #5 |    
'      |    |      
' 5----1----2----6   
' |    |    |    |       
' |    |    |    |           
' | #2 | #1 | #3 |         
' |    |    |    |         
' 7----3----4----8   
'      |    |    
'      |    |    
'      | #4 |    
'      |    |    
'      7----8
