' cODED bY ßudda/NFA 
' StARtEd : a long time ago  
' FiNiSHed: a while ago  

' -- Set Enviroment -- 
Break Off 
Amos Lock 
Amos To Back 
Areg(0)=0

' -- Grab Command Line --
'Command Line$="-1 cd0:music/48tpi.abk"
C$=Command Line$

' -- Break Down Command Line --
Q$=Upper$(Right$(C$,5))
If Q$="QUIET" Then Q=1
OPTION=Val(Left$(C$,2)-"-")
MNAME$=Mid$(C$,4,Len(C$)-3)
If Q=1 Then MNAME$=Left$(MNAME$,Len(MNAME$)-6)

' -- Define Messages To User --
Dim MES$(3)
MES$(0)="      *** ESC to stop music! ***"
MES$(1)="   *** SPACE BAR to stop music! ***"
MES$(2)="    *** You CAN'T stop music ! ***"

' -- Define Various ESCape Code Sequences -- 
ESC$=Chr$(27)+"["
CUL0$=ESC$+"30m"
CUL1$=ESC$+"31m"
CUL2$=ESC$+"32m"
CUL3$=ESC$+"33m"
ITALON$=ESC$+"3m"
ITALOFF$=ESC$+"23m"
BOLDON$=ESC$+"1m"
BOLDOFF$=ESC$+"22m"

' -- Open CLI Window --
If Q=0
   Open Port 1,"RAW:160/50/320/155/NFAPlay Command Line V1.2"
   Print #1,CUL1$+ESC$+"4m"+ITALON$+"cODED bY "+ITALOFF$+"ßudda"+CUL3$+BOLDON$+" ,"+BOLDOFF$+CUL1$+"          "+ITALON$+"aNSI bY "+ITALOFF$+"Elf"+ESC$+"24m"+CUL3$+BOLDON$
   Print #1,"         ____ /|___/\_______ /|"
   Print #1,"    ___/\\"+CUL2$+"\"+CUL3$+" //"+CUL2$+"."+CUL3$+"|"+CUL2$+"|"+CUL3$+"         "+CUL2$+"/"+CUL3$+"//"+CUL2$+"|"+CUL3$+"|"
   Print #1,"   ("+CUL2$+"|"+CUL3$+"    \\//  |"+CUL2$+"|"+CUL3$+"   _____ // "+CUL2$+"|"+CUL3$+"|"
   Print #1,"   |"+CUL2$+"¦"+CUL3$+"     \/  "+CUL2$+"."+CUL3$+"|"+CUL2$+"¦"+CUL3$+"    "+CUL2$+"\"+CUL3$+"\  ¬/  "+CUL2$+"¦"+CUL3$+"|"
   Print #1,"   |"+CUL2$+":"+CUL3$+"      \   |"+CUL2$+":"+CUL3$+"   _ "+CUL2$+","+CUL3$+"\ /   "+CUL2$+":"+CUL3$+"|"
   Print #1,"   |"+CUL2$+"."+CUL3$+"  \      "+CUL2$+":"+CUL3$+"|"+CUL2$+"."+CUL3$+"   |¯¯¯/ /  "+CUL2$+"."+CUL3$+"|"
   Print #1,"   |   /\     "+CUL2$+"¦"+CUL3$+"|    |  / /\   |"
   Print #1,"   |"+CUL2$+"."+CUL3$+" //\\    "+CUL2$+"|"+CUL3$+")"+CUL2$+"."+CUL3$+"   | / /  \ "+CUL2$+"."+CUL3$+"|"
   Print #1,"   | //"+CUL2$+"/"+CUL3$+" \\/¯¯¯|    |/  \ /\\ |"
   Print #1,"   |/ ¯¯¯¯     |    \¯¯¯¯/"+CUL2$+"/"+CUL3$+" \¯¯"+BOLDOFF$+ITALON$+CUL1$+" p"+CUL2$+"L"+CUL1$+"a"+CUL2$+"Y"+ITALOFF$+BOLDON$+CUL3$
   Print #1,"   `           ¯¯¯¯¯¯    ¯¯¯¯"+BOLDOFF$
End If 

' -- Test Command Line --
If C$<>"" and OPTION<2 Then Goto PLIT

' -- Show Usage Of Program --
BAD:
If Q=0
   RESULT=Intcall(-96) : Rem display beep 
   Print #1,CUL2$+"Usage: "+CUL1$+"NFAPlayCLi -"+CUL3$+"n <module> "+CUL2$+"«QUIET»"
   Print #1,CUL3$+" n "+CUL1$+"="+ITALON$+" Way To Stop Music"+ITALOFF$
   Print #1,CUL2$+"    0-ESC · 1-SPACE · 2-NO STOPPING!"
   Print #1,CUL3$+" module "+CUL1$+ITALON$+"= Module To load"+ITALOFF$
   Wait 60
End If 
Goto BYE

' -- Check Module Exists --
PLIT:
If Exist(MNAME$)=False
   If Q=0
      Print #1,ITALON$+CUL3$+"cOuldN't gET a GriP oN"+ITALOFF$
      Print #1,CUL1$+Left$(MNAME$,37)
   End If 
   Goto BYE
End If 

' -- Load Specified Module --
If Q=0 Then Print #1,CUL2$+" pLEaSE wAIt lOAdInG aNd pLayINg...."

Open In 2,MNAME$
CHK$=Input$(2,4)
Close 2

' -- Is It PowerPacked? -- 
If CHK$="PP20" Then Goto BAD

' -- Play Module --
If CHK$="AmBk"
   Load MNAME$,3
   If Peek$(Start(3)-8,8)="Tracker "
      Track Play 3
   Else 
      Music 1
   End If 
   If Q=0
      Print #1,""
      Print #1,CUL3$+"           AMOS Abk Module"
   End If 
   Goto WW
End If 

If CHK$="MMD0" or CHK$="MMD1"
   Med Load MNAME$,3
   Med Play 3
   If Q=0
      Print #1,""
      Print #1,CUL3$+"               MED Module"
   End If 
   Goto WW
End If 

Track Load MNAME$,3
Track Play 3
Track Loop On 
If Q=0 Then Print #1,CUL3$+" NAME:"+CUL1$+Peek$(Start(3),22) : Print #1,CUL3$+" LENGTH:"+CUL1$+Str$(Peek(Start(3)+950))+CUL3$+"           SIZE:"+CUL1$+Str$(Length(3))


' -- Wait For Music Stop --
WW:
If Q=0 Then Print #1,CUL2$+MES$(OPTION)
If OPTION=2 Then Do : Loop 
Do 
   If OPTION=0 and Key State(69)=True Then Goto BYE
   If OPTION=1 and Key State(64) Then Goto BYE
Loop 

' -- Quit Program -- 
BYE:
Erase 3
Wait 100






