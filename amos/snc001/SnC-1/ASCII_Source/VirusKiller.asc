' Virus Killer 
' cODED bY ßudda/NFA 
' StARtEd : 1994 
' FiNiSHed: later in 1994  

Screen Open 0,640,200,2,Hires
Amos Lock : Break Off 
Curs Off : Hide On : Flash Off 
Palette $0,$0 : Cls 0 : Pen 1 : Paper 0
Draw 0,97 To 640,97 : Draw 0,125 To 640,125
Locate 0,13 : Centre "MEM-EXECUTER" : Locate 0,14 : Centre "coded by BUDDA/NFA"
' ****** CREATE RAINBOWS ********
Set Rainbow 0,0,16,"","","" : Rainbow 0,0,71,16
Set Rainbow 1,0,16,"","","" : Rainbow 1,0,236,16
Restore RDATA1
For C=0 To 15 : Read CVA : Rain(0,C)=CVA : Next C
For C=0 To 15 : Read CVA : Rain(1,C)=CVA : Next C : View 
' ****** MOVE IN RAINBOWS ***********
R1=71 : R2=236
While R1<131 and R2>176
   Inc R1 : Rainbow 0,0,R1,16
   Dec R2 : Rainbow 1,0,R2,16
   Wait Vbl 
Wend 
Fade 3,$0,$FFF
Wait 60
Fade 5,$0,$9AD
Wait 40
' **** DEM VARIABLES ******
_EXECBASE=4
_EXECBASE=Leek(_EXECBASE)
' ******** CHECK VIRUS *********** 
Restore VEC_NAMES
Locate 0,13 : Print Space$(80)
Locate 0,14 : Print Space$(80)
For I=0 To 4
   Read VECTOR$,OFFSET : N=_EXECBASE+OFFSET : N=Leek(N)
   Locate 0,13 : Print Space$(80) : Locate 0,13 : Centre VECTOR$
   If N : Print : Centre "AbnORMal" : Goto MENU
   Else : Print : Centre "OkAY" : End If : Wait 30
Next 
Locate 0,14 : Print Space$(80) : Locate 0,13 : Centre "memory all clear, continuing boot-up."
Wait 80 : End 
MENU:
Locate 0,13 : Centre "-= Vectors Changed =-"
Locate 0,14 : Centre "A Possible Virus Has Been Located In Memory!"
Wait 150
Locate 0,13 : Print Space$(80) : Locate 0,14 : Print Space$(80)
Locate 0,13 : Centre "the choice is yours..."
Locate 0,14 : Centre "LEFT BUTTON - KiLL        RIGHT BUTTON - IGNORE"
Do 
   If Mouse Click=1 Then Goto DESTROY_VIRUS
   If Mouse Click=2 Then End 
Loop 
' ********* KILL VIRUS *********** 
DESTROY_VIRUS:
Restore KRESET : For I=0 To 109 : Read N : N$=N$+Chr$(N) : Next I : Call Varptr(N$)
' ******** DA-DA-DA-DA,DA-DA DATA ********** 
VEC_NAMES:
Data "ColdCapture",$2A,"CoolCapture ",$2E
Data "WarmCapture",$32,"KickMemPtr",$222,"KickTagPtr",$226
KRESET:
Data 51,252,64,0,0,223,240,154,44,120
Data 0,4,32,86,145,252,0,0,2,118
Data 48,60,33,0,66,152,81,200,255,252
Data 32,124,0,0,0,0,48,60,0,254
Data 66,152,81,200,255,252,61,124,170,170
Data 0,36,45,124,204,204,204,204,0,38
Data 45,124,0,48,0,0,0,62,45,124
Data 0,222,0,0,0,78,45,124,187,187
Data 187,187,0,82,45,124,221,221,221,221
Data 2,42,32,124,1,0,0,0,34,124
Data 0,255,255,236,36,81,145,202,78,208
RDATA1:
Data $0,$101,$202,$303,$404,$505,$606,$707
Data $808,$909,$A0A,$B0B,$C0C,$D0D,$E0E,$F0F
Data $F0F,$E0E,$D0D,$C0C,$B0B,$A0A,$909,$808
Data $707,$606,$505,$404,$303,$202,$101,$0






