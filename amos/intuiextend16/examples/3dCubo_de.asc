'**************************************
'                                     *
'     IntuiExtend.Lib 1.4/@1995-96    *
'                                     *
'          by CIERP Philippe.         *
'                                     *
'          from AMIGAzette 83         *
'                                     *
'**************************************
'
Screen Open 0,320,256,4,0
Curs Off : Flash Off : Cls 0
Double Buffer : Autoback 0
'
Restore P
Read P
Dim PSET(2,P),X(P),Y(P)
For T=0 To P
   Read PSET(0,T),PSET(1,T),PSET(2,T)
Next T
'
Restore G
Read G
Dim POLY(2,G)
For T=0 To G
   Read POLY(0,T),POLY(1,T),POLY(2,T)
Next T
'
Wb 3d Eye 0,0,500
Wb 3d Centre Screen Width/2,Screen Height/2
'
While Mouse Key<>2
   Add AX,1,0 To 359
   Add AY,1,0 To 359
   Add AZ,1,0 To 359
   Wb 3d Angle AX,AY,AZ
   For T=0 To P
      Wb 3d Point PSET(0,T),PSET(1,T),PSET(2,T)
      X(T)=Wb 3d X : Y(T)=Wb 3d Y
   Next T
   For T=0 To G
      X0=X(POLY(0,T)) : Y0=Y(POLY(0,T))
      X1=X(POLY(1,T)) : Y1=Y(POLY(1,T))
      X2=X(POLY(2,T)) : Y2=Y(POLY(2,T))
      Polyline X0,Y0 To X1,Y1 To X2,Y2 To X0,Y0
   Next T
   Locate ,0 : Print Timer
   Screen Swap 
   Cls 0 : Timer=0
Wend 
'
P:
Data 8-1
Data -100,-100,-100
Data 100,-100,-100
Data 100,100,-100
Data -100,100,-100
Data -100,-100,100
Data 100,-100,100
Data 100,100,100
Data -100,100,100
'
G:
Data 12-1
Data 0,1,3
Data 1,2,3
Data 4,5,7
Data 5,6,7
Data 0,4,7
Data 7,3,0
Data 1,5,4
Data 4,0,1
Data 2,6,5
Data 5,1,2
Data 3,7,6
Data 6,2,3
