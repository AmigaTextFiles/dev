'**************************************
'                                     *
'     IntuiExtend.Lib 1.4/@1995-96    *
'                                     *
'          by CIERP Philippe.         *
'                                     *
'          from AMIGAzette 83         *
'                                     *
'**************************************
'
Amos To Back 
Wb Locker 1
'
Wb Wind Open SCR To 164,56,300,66,2+4+8+$2000000
W=Wb Wind Base : R=Wb Wind Rastport(W)
TIT0=Str Store("IntuiExtend.Lib v1.0b")
TIT1=Str Store("CIERP Philippe presente l'extention Intuition (Shareware) AMOS/AMOSPro")
Wb Wind Title W To TIT0,TIT1
'
Wb Gfx Mode R,1 : Wb Gfx Ink R To 2,0
Wb Gfx Text "Selectionnez un bouton S.V.P...",23,23 To R
'
Wb Bevel Box R To 1,2,11,13,285,28
GADSTR$="Philippe"+String$(Chr$(0),42)
UNDO$="Philippe"+String$(Chr$(0),42)
'
Wb Gfx Ink R To 1,0
'
BOUT1[R,10,48,88,2,1,"Ok"]
BGAD0=Wb Init Bool Gadget(0,11,49,86,11,2) : Wb Bevel Gadget BGAD0
Wb Insert Gadget W To BGAD0 : Wb Refresh Gadget W,BGAD0
'
BOUT1[R,105,48,88,2,1,"Refaire"]
BGAD1=Wb Init Bool Gadget(1,106,49,86,11,2) : Wb Bevel Gadget BGAD1
Wb Insert Gadget W To BGAD1 : Wb Refresh Gadget W,BGAD1
'
BOUT1[R,200,48,88,2,1,"Annuler"]
BGAD2=Wb Init Bool Gadget(2,201,49,86,11,2) : Wb Bevel Gadget BGAD2
Wb Insert Gadget W To BGAD2 : Wb Refresh Gadget W,BGAD2
'
BOUT[R,12,32,273,11,1,2,""]
BOUT[R,11,31,275,13,2,1,""]
'
ST3=Wb Init Str Gadget(3,13,34,273,9,Varptr(GADSTR$),50,33,0)
Wb Insert Gadget W To ST3 : Wb Refresh Gadget W,ST3
'
Do 
   '
   X=X Wind(W)
   Y=Y Wind(W)
   '
   Z=Get Msg(W)
   Z1=Get Msg Iadr
   '
   If Z1=BGAD0 : Exit : End If 
   If Z1=BGAD1
      Poke$ Varptr(GADSTR$),Peek$(Varptr(UNDO$),50)
      Wb Refresh Gadget W,ST3
   End If 
   If Z1=BGAD2 or Z=$200
      GADSTR$=""
      Exit 
   End If 
   '
Loop 
'
RR:
Default 
Print GADSTR$
'
Str Free TIT0
Str Free TIT1
Wb Locker 0
Amos To Front 
'
Wb Remove Gadget W,BGAD0
Wb Remove Gadget W,BGAD1
Wb Remove Gadget W,BGAD2
Wb Remove Gadget W,ST3
Wb Free Bool Gadget BGAD0
Wb Free Bool Gadget BGAD1
Wb Free Bool Gadget BGAD2
Wb Free Str Gadget ST3
Wb Wind Close W
Wait Key 
'
Procedure BOUT[R,X,Y,W,H,C0,C1,T$]
   X1=(X+44)-(Len(T$)*4)
   Wb Bevel Box R To C0,C1,X,Y,X+W,Y+H
   Wb Gfx Text T$,X1,Y+8 To R
End Proc
'
Procedure BOUT1[R,X,Y,W,C0,C1,T$]
   X1=(X+44)-(Len(T$)*4)
   Wb Gfx Text T$,X1,Y+9 To R
End Proc
