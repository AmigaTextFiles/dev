' Off His Rocker!  
' ¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯  
' cODED bY ßudda/NFA 
' StARtEd : 14/10/1995 
' FiNiSHed: 15/10/1995 

' --- load in a kewl mod by Tez/NFA! 
'Med Load "work:modules/nfa/tez/mod.garagetrance",3 : Rem change the path to your copy of the mod!!! 
'Med Play 3
Make Icon Mask 

Load "Planet_Rocker.abk"

' --- two screens for this effect
For SCR=0 To 1
   Screen Open SCR,336,256,16,Lowres
   Flash Off 
   Curs Off 
   Get Bob Palette 
   Hide On 
   Cls 0
   Ink 10
Next SCR
Double Buffer 

' --- setup stars and hide from screen 
Bob Update Off 
U=1
For T=2 To 4
   Bob T,-100,-100,U
   Inc U
   Channel T To Bob T
   STARZ$="A:Let X=Z(100);Let X=X+100;Let Y=Z(30);Let Y=Y+45;P P P P P P;Jump A;"
   Amal T,STARZ$
Next T
Screen Hide 0 : Screen 0

' --- the all important variables
JX=Screen Width/2
JY=Screen Height/2
_MAX_X=200
_MIN_X=120
FRAME=2
Timer=0

Do 
   ' --- draw funky triangle background 
   ' - horizontal top/bottom  
   For XXX=0 To Screen Width Step 168
      T_X=XXX+84
      Polygon XXX,0 To T_X,0 To JX,JY
      Polygon XXX,Screen Height To T_X,Screen Height To JX,JY
   Next XXX
   ' - verticleleft/right 
   For YYY=100 To 200 Step 110
      T_Y=YYY+55
      Polygon 0,YYY To 0,T_Y To JX,JY
      Polygon Screen Width,YYY To Screen Width,T_Y To JX,JY
   Next YYY
   
   ' --- move centre point left/right 
   If Q=True
      Add JX,6
      Add JY,-1
   Else 
      Add JX,-6
      Add JY,1
   End If 
   
   ' --- check center limits
   If JX=>_MAX_X
      Q=False
   End If 
   If JX<=_MIN_X
      Q=True
   End If 
   
   ' --- change face picture
   If Timer>300
      Add FRAME,1,1 To 2
   End If 
   F Paste Icon 125,70,FRAME
   
   ' --- update viewing screen
   Screen Copy 0 To Logic(1)
   
   ' --- clear old screen 
   Blit Clear -1
   Bob Draw 
   Screen Swap 1
   Wait Vbl 
   
   ' --- sync. stars with music 
   If Timer>595 and Timer<605 Then Amal On 
Loop 






