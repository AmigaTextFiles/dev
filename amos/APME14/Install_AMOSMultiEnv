(transcript "Installing AMOS Loader...")

(message "AMOSPro Multi-Envoironment Loader System V1.2\n\nBy Paul Hickman (ph@doc.ic.ac.uk)\n\n\nThis script will patch AMOSPro V1.12 or V2.00, and install the loader programs on your AMOSPro disk/directory\n\nReqtools library is required, and this will be copied to LIBS: if the version supplied here is newer than your current version")
(welcome)



;get the users AMOSPro directory.

(set #AMOSPro
	(askdir
	(prompt "Please select your AMOSPro_System: disk/directory.") 
	(help @ask-dirhelp)
	(default "AMOSPro_System:")
	(newpath)
	)
)

(set @default-dest #AMOSPro)
(complete 5)







; Now detect if the AMOSPro directory is infact the root directory of
; volume AMOSPro_System.

(set AMOSdisk (= (getassign (getdevice #AMOSPro) "d") "AMOSPro_System:"))





;Now copy reqtools library to LIBS: if necessary

(copylib
    (help @copylib-help)
    (prompt "Installing ReqTools.Library...")
    (source "Reqtools.library")
    (dest "libs:")
    (confirm)
    (noposition)
)

(complete 15)







;Now check that there is a copy of AMOSPro in the AMOS
;directory.


(If (= (exists (tackon #AMOSPro "AMOSPro")) 0)
   (abort "The file AMOSPro appears to be missing from the AMOS directory you selected!\n\nPlease try again with the correct directory.")
)

(complete 20)

;Now patch it. An already patched copy can safely be re-patched.

(working "Patching File AMOSPro")
(run (cat "PatchAMOSPro " (tackon #AMOSPro "AMOSPro")))


(complete 30)







;Now copy the program files accross

(copyfiles
    (help @copyfiles-help)
    (prompt "Installing AMOS Loader")
    (source "GetAMOSProEnv")
    (noposition)
    (dest #AMOSPro)
)

(complete 40)

(copyfiles
    (help @copyfiles-help)
    (prompt "Installing New AMOSPro Icon")
    (source "AmosIcon")
    (newname "Amos.info")
    (noposition)
    (dest #AMOSPro)
)

(complete 50)







; Now create the config's directory & copy the default config

(makedir (tackon #AMOSPro "APSystem/Interpreter_Configs"))

(copyfiles
	(prompt "Copying Default Configuration")
	(help	@copyfiles-help)
	(source "s:AMOSPro_Interpreter_Config")
	(dest	(tackon #AMOSPro "APSystem/Interpreter_Configs"))
	(newname "Default.Config")
)


(complete 60)





;Now get the users extra commands for the script file.

(set #extracommands "")
(set #command "5")

(until (= #command "")

	(set #command
	    (askstring
		    (prompt "A Script file is created by this installer to load AMOSPro. If you would like to insert extra commands into this script just before AMOSPro is run, type each one into the string gadget below, and press proceed.\n\nPress proceed with the string gadget empty when finished.")
		    (help (cat "Type each command into the string gadget, "
			       "and then press proceed.\n\nWhen you have "
			       "finished, press proceed with the string gadget empty.\n\n"
				   "The commands you enter will be executed just before "
				   "AMOSPro is run, and only if AMOSPro is going to be "
				   "executed - If you press Cancel in the file requester,"
				   "or an error occurs, your commands will NOT be run.\n\n"
				   "The most likely commands you will want to insert here "
				   "are to setup assigns to the directories where you keep "
				   "your AMOSPro programs."))
		    (default "")
	    )
	)

	(set #extracommands
		(cat #extracommands "    " #command "\n")
	)
)

(complete 60)






; Next, create the script

(textfile
    (prompt "Creating AMOSPro Loader Script")
    (help "No Help Available")
	(dest (tackon #AMOSPro "Amos"))
	(append ".key prog\n.bra {\n.ket }\n")
	(append "; $VER: AmosPro Loader 1.2 (10.6.94)\n;\n; By Paul Hickman (ph@doc.ic.ac.uk)\n\n\n")
    (append "FailAt 11\n")
    (if (= AMOSdisk 0)
        (append (cat (cat "assign AMOSPro_System: \"" (expandpath #AMOSPro)) "\"\n"))
    )
    (append "GetAMOSProEnv AMOSPro_System:APSystem/Interpreter_Configs\n")
    (append "IF NOT WARN\n    CD AMOSPro_System:\n\n")
    (append #extracommands)
    (append "    AMOSPro_System:AMOSPro {prog}\n    C:Delete T:AMOSPro_Interpreter_Config\nENDIF\n")
)

(complete 80)





; Finally, if on a floppy disk, user may wish startup sequence modified


(if (= AMOSdisk 1)
	(message "I notice you are installing this to a floppy disk.\n\nTo use the AMOS Envoironment selector, you must double click on the AMOSPro loader icon.\n\n\nYou should make sure that your startup-sequence loads workbench, not AMOSPro directly.")
)

(complete 100)
(exit "\nTo load AMOS, double click on the \"Amos\" icon, instead of the old \"AMOSPro\" icon.\n\n\n\n© Paul Hickman - June 1994")
