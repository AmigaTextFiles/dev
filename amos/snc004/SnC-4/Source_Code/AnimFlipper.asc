'AnimFlipper -- an example of stringing together anims and IFFs
'               using the  EXHIBITANIM[] and EXHIBIT[] procedures. 
'               © 1993, 1996 Steve Tiffany ... Freeware  

'Include everything through `Autoback 0' in your program:

' NOTE: Due to space limitations on Sauce 'n' Code #4 the example anims
'       and pics have been removed and all demo lines REM'd out. 

'Change these 3 screen values to match your project's anims and IFFs...  
SCRNWIDTH=320 : SCRNHEIGHT=200 : NUMCOLORS=2

Screen Open 0,SCRNWIDTH,SCRNHEIGHT,NUMCOLORS,Lowres
   Flash Off : Curs Off : Hide : Cls 0
   SCRNCENTER[SCRNWIDTH,SCRNHEIGHT]
   Screen Display 0,XOFFSET,YOFFSET,SCRNWIDTH,SCRNHEIGHT
Screen Open 1,SCRNWIDTH,SCRNHEIGHT,NUMCOLORS,Lowres
   Screen Display 1,XOFFSET,YOFFSET,SCRNWIDTH,SCRNHEIGHT
   Flash Off : Hide : Cls 0 : Double Buffer : Autoback 0


'Parameters for EXHIBIT[] are as follows:  
' PIC$....... The path and file name of the IFF picture to show, 
'             in quotes. 
' TRIGGER.... What to wait for before displaying this picture: 
'               If value is -1 then wait for mouse click.  
'               If value is 0 then display it as soon as it's loaded.  
'               If value is greater than 0 then Wait TRIGGER 
'                  (60 = 1 second in NTSC or 50 = 1 second in PAL).      

'Replace w/actual IFF's on your hard disk, including path... 
Rem EXHIBIT["Instant.pic",0] 
Rem EXHIBIT["WaitMouse.pic",-1]  
Rem EXHIBIT["Wait240.pic",240] 


'Parameters for EXHIBITANIM[] are as follows:
' AN$........ The path and file name of the anim you want to play, 
'             in quotes. 
' TRIGGER.... What to wait for before displaying first frame of this anim:   
'               If value is -1 then wait for mouse click.  
'               If value is 0 then display it as soon as it's loaded.  
'               If value is greater than 0 then Wait TRIGGER 
' HOLDFRAME1  What to wait for with the first frame displayed before the 
'             rest of the anim starts to play:     
'               Same values as TRIGGER.  
' MIDFRAME... How long to Wait between anim frames. Value of 0 plays   
'             fastest.  Higher numbers play slower.      
' NUMLOOPS... How many times the anim should play.     
'             Recommended value is 1.  

'Replace w/actual anims on your hard disk, including path... 
Rem EXHIBITANIM["HoldFirstFrameTilClick.anim",180,-1,1,1]
Rem EXHIBITANIM["NoWaitFast.anim",0,0,0,1] 
Rem EXHIBITANIM["3LoopSlow.anim",0,0,6,3]  

'Note that if you don't specify path (e.g., "Work:Anim/..."), the anims  
'need to be in the same directory as the program, as in this example.  
'(And the program's got to be launched from that directory, such as by   
'clicking its icon.)     

Fade 2 : Wait 30
Close : Show On : Edit 

Procedure EXHIBIT[PIC$,TRIGGER]
   Screen 0 : Load Iff PIC$
   Screen 1 : Screen Copy 0 To 1
   If TRIGGER=-1
      While Mouse Click=0 : Wend 
   Else If TRIGGER=0
      Wait Vbl 
   Else 
      Wait TRIGGER
   End If 
   Get Palette 0 : Screen Swap 
End Proc

Procedure EXHIBITANIM[AN$,TRIGGER,HOLDFRAME1,MIDFRAME,NUMLOOPS]
'load anim of 999 or fewer frames... 
   Open In 1,AN$
      MEMORY=Frame Length(1,999)
      NOF=Frame Load(1 To 10,999)
      Close 1
      Add NOF,-2

'prepare to show first frame of anim...  
   Screen 0 : FLAG=Frame Play(10,1)
   Screen 1 : Screen Copy 0 To 1

   If TRIGGER=-1
      While Mouse Click=0 : Wend 
   Else If TRIGGER=0
      Wait Vbl 
   Else 
      Wait TRIGGER
   End If 

'actually show first frame of anim...  
   Get Palette 0 : Screen Swap : Wait Vbl 
   Screen Copy Physic To Logic
   If HOLDFRAME1=-1
      While Mouse Key=0 : Wend 
   Else If HOLDFRAME1=0
      Wait Vbl 
   Else 
      Wait HOLDFRAME1
   End If 

'play anim...    
   For K=2 To NOF
      FLAG=Frame Play(FLAG,1)
      Screen Swap : Wait Vbl 
      Wait Frame Param+MIDFRAME
   Next K

'play it again...? 
If NUMLOOPS>1
   For J=2 To NUMLOOPS
      FLAG=Frame Play(10,1)
      Screen Swap : Wait Vbl 
      Screen Copy Physic To Logic
      For K=2 To NOF
         FLAG=Frame Play(FLAG,1)
         Screen Swap : Wait Vbl 
         Wait Frame Param+MIDFRAME
      Next K
   Next J
End If 
Erase 10
End Proc

Procedure SCRNCENTER[SCRNWIDTH,SCRNHEIGHT]
   Shared XOFFSET,YOFFSET
'If config file exists, read offsets and use them to center the screen.
'Otherwise, ask user to center the screen, take those offsets and write  
'them to a new config file in the user's S: directory or ram:...   

'Determine correct config file name... 
If SCRNWIDTH=320 or SCRNWIDTH=640
   If SCRNHEIGHT=200 or SCRNHEIGHT=400
      FILE$="AmosXyOffsetNTSC.config"
   Else If SCRNHEIGHT=256 or SCRNHEIGHT=512
      FILE$="AmosXyOffsetPAL.config"
   End If 
Else If SCRNWIDTH=352 or SCRNWIDTH=704
   If SCRNHEIGHT=240 or SCRNHEIGHT=480
      FILE$="AmosXyOverscanNTSC.config"
   Else If SCRNHEIGHT=290 or SCRNHEIGHT=566
      FILE$="AmosXyOverscanPAL.config"
   End If 
End If 

'Check for permanent config file...
SFILE$="S:"+FILE$
If Exist(SFILE$)
   Open In 1,SFILE$
   Input #1,XOFFSET,YOFFSET
   Close 1 : Pop Proc
End If 

'If no permanent config file, check Ram: for temporary one...  
RFILE$="Ram:"+FILE$
If Exist(RFILE$)
   Open In 1,RFILE$
   Input #1,XOFFSET,YOFFSET
   Close 1 : Pop Proc
End If 

'If neither exists, ask user to center screen... 
CENTERIT:
   Pen 1 : Paper 0 : Cls 0 : Colour 1,$EEE : Hide 
   Box 0,0 To SCRNWIDTH-1,SCRNHEIGHT-1
   Box 0,2 To SCRNWIDTH-1,SCRNHEIGHT-3
   Box 0,4 To SCRNWIDTH-1,SCRNHEIGHT-5

   Locate ,10 : Centre "Center screen with mouse.        "
   If SCRNWIDTH=352 or SCRNWIDTH=704
      Print 
      Print : Centre "Try to show a single white line  "
      Print : Centre "around each edge of the screen.  "
   End If 
      Print 
      Print : Centre "Then click the left mouse button."

   X Mouse=190 : Y Mouse=30 : Wait 10
   While Mouse Click<>1 : 
      XOFFSET=X Mouse-88 : YOFFSET=Y Mouse-80
      Gosub CUTXOFFSET
      Screen Display 0,XOFFSET,YOFFSET,SCRNWIDTH,SCRNHEIGHT
   Wend 

'Ask user where to write config file...  
   Cls 0,1,5 To SCRNWIDTH-2,SCRNHEIGHT-6 : Ink 1 : Gr Writing 0
   Text 19,22,"Save centering offsets permanently."
   Text 45,36,"(Write 11-byte file to S:)"
   Text 19,70,"Save centering offsets temporarily."
   Text 40,84,"(Write 11-byte file to Ram:)"
   Text 67,126,"Proceed without saving."
   Text 90,173,"Re-center screen." : Box 0,0 To 319,50
   Box 0,50 To 319,100 : Box 0,100 To 319,150 : Box 0,150 To 319,199
   Show On : Gr Writing 1

'Get user's response...
PICKIT:
   While Mouse Click=0 : X=X Screen(X Mouse) : Y=Y Screen(Y Mouse) : Wend 
   If X>319 or Y>199
      Goto PICKIT
   End If 
   If Y<50
      Open Out 1,SFILE$
         Print #1,XOFFSET : Print #1,YOFFSET
      Close 1 : Cls 0 : Pop Proc
   End If 
   If Y<100
      Open Out 1,RFILE$
         Print #1,XOFFSET : Print #1,YOFFSET
      Close 1 : Cls 0 : Pop Proc
   End If 
   If Y<150
      Cls 0 : Pop Proc
   End If 
   Cls 0 : Goto CENTERIT

CUTXOFFSET:
   If XOFFSET<80 Then XOFFSET=64 : Return 
   If XOFFSET<96 Then XOFFSET=80 : Return 
   If XOFFSET<112 Then XOFFSET=96 : Return 
   If XOFFSET<128 Then XOFFSET=112 : Return 
   If XOFFSET<144 Then XOFFSET=128
   Return 
End Proc
