' *** Circular Buttons Routine *** 
' By ANDREW BALLARD (Tøølbox/iNDi) 
' the first number in the array is the button number 
' the second holds the following button info:
' 1=X,  2=Y,  3=Radius;

Dim BUTTONS(5,3) : Rem Enough for 6 buttons (0 -> 5)
Global BUTTONS()

' *********************************************************************
' Demonstrate routine

Screen Open 0,320,256,16,Lowres
Curs Off 

CIRC_BUTTON[1,100,100,10] : Rem Make 3 test buttons  
CIRC_BUTTON[2,50,90,30]
CIRC_BUTTON[3,130,50,40]

Locate 0,16 : Print "Press LEFT Mouse Button In Circles Area"
Print "Press RIGHT Mouse Button To Quit"
Locate 0,0 : Print "Button:"
Do 
   BUTTON_TEST[3]
   Print At(7,0);Param
   Exit If Mouse Key=2
Loop 

' *********************************************************************

Procedure CIRC_BUTTON[N,X,Y,R]
   ' Parameters are: button number, X-coord, Y-coord, Radius  
   Circle X,Y,R : Rem You can replace this with your own fancy button drawer 
   BUTTONS(N,1)=X : Rem Fill array with button info  
   BUTTONS(N,2)=Y
   BUTTONS(N,3)=R
End Proc
Procedure BUTTON_TEST[M]
   ' Parameter is amount of buttons to test 
   If Mouse Key=1
      XM=X Screen(X Mouse) : Rem Get mouse coords 
      YM=Y Screen(Y Mouse)
      For N=1 To M : Rem Go through all current buttons in turn 
         XB=BUTTONS(N,1) : Rem Get coords of button to be tested
         YB=BUTTONS(N,2)
         ' Equation works out distance between mouse and button centre  
         DIST=((XM-XB)^2)+((YM-YB)^2) : Rem Pythagoras Theorem 
         DIST=Sqr(DIST)
         ' If distance from button centre to mouse, is lower than radius    
         If DIST<BUTTONS(N,3)
            Pop Proc[N]
         End If 
      Next N
   End If 
End Proc[0]
