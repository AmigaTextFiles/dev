' Radio Buttons Without AMOS Pro Interface 
' cODED bY ßudda/NFA 

' I wrote this in such a way so that I could 
' easily transfer it into the C language for 
' my own GUI on the PeeCee.
' Don't worry! 
'               I hate PeeCees but it's for college! 


Dim X(10,2),Y(10,2)
Global X(),Y()
Curs Off 

' --- Create a string holding all button texts seperated by |'s
RADIO$="|Button On The Hillside1|Butt2|Button3|Button4|B5"
MX[100,50,RADIO$,5,2]

Do 
   MZ=Mouse Zone
   If MZ>0 and Mouse Key=1
      Locate 0,0
      MX[100,50,RADIO$,5,MZ]
   End If 
Loop 


Procedure MX[X,Y,TXT$,YSPC,PUSH]
   PRASE[TXT$,-1]
   _TOT_BUT=Val(Param$)
   
   Reserve Zone _TOT_BUT
   
   For K=1 To _TOT_BUT
      X1=X
      Y1=Y+((10+YSPC)*K)
      X2=X1+10
      Y2=Y1+10
      X(K,0)=X1
      Y(K,0)=Y1
      X(K,1)=X2
      Y(K,1)=Y2
      Ink 2 : Bar X1,Y1 To X2,Y2
      Set Zone K,X1,Y1 To X2,Y2
      If K=PUSH
         Ink 3 : Paint X1+1,Y1+1
      End If 
      PRASE[TXT$,K]
      Text X2+10,Y1+10,Param$
   Next K
End Proc
Procedure PRASE[TXT$,V]
   ' --- V = -1 then return total buttons     
   If V=-1
      For I=1 To Len(TXT$)
         A$=Mid$(TXT$,I,1)
         If A$="|" : Inc T : End If 
      Next I
      A$=Str$(T)
   End If 
   
   ' --- Grab Text Number 'V' 
   If V>0
      TEMP=1
      For BUT=1 To V
         _INSTR[TXT$,"|",TEMP]
         TEMP=Param+1
      Next BUT
      
      TEMP1=TEMP
      _INSTR[TXT$,"|",TEMP1] : TEMP2=Param
      TEMP2=TEMP2-TEMP1
      A$=Mid$(TXT$,TEMP1,TEMP2)
   End If 
End Proc[A$]
Procedure _INSTR[HOST$,GUEST$,POS]
   For K=POS To Len(HOST$)
      A$=Mid$(HOST$,K,Len(GUEST$))
      If A$=GUEST$ Then RESULT=K : Exit 
   Next K
   If RESULT=0 Then RESULT=Len(HOST$)+1
End Proc[RESULT]
