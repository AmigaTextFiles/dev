'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ 
'         This package is FREEWARE and © 1995-1996 Dairymen Soft     
'If you use my extension, please CREDIT ME in your program and spread it 
'worldwide so everybody can see what AMOS is able to do!   Enjoy.....
'
'     Name:  AMOSPro Gui File Converter  
'   Author:  Pietro Ghizzoni   
'   E-Mail:  ghizzo@galactica.it 
'  Version:  1.5 
'      REV:  37.300    
'
'You can contact me at the above E-Mail address or via snail-mail:   
'
'                           Pietro Ghizzoni  
'                          Via Termine  70/b 
'                      29017 Fiorenzuola D'Arda
'                          Piacenza - ITALY  
'
'                         Phone: 0523-940138 
'
'If you want, please send me a postcard... i'm a collectionist!
'
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ 
Set Accessory 

'Program initialisation
'~~~~~~~~~~~~~~~~~~~~~~~~
Global BLEN,_DATAS,WORK,WORKL,_GENERIC,_BUTTON,_CHECKBOX,_INTEGER,_LISTVIEW,CGO,PGO
Global _MX,_NUMBER,_CYCLE,_PALETTE,_SCROLLER,_SLIDER,_STRING,_TEXT,_NUM,_SAVEIT,IMG
Global _STRUCTS,_TAG,LABEL,GKIND,NGAD,BEVEL,MEN,ITXT,GP,WP,BP,IP,MP,WP2,NOWIN,IMAGE
Global WINDT,GFN$

BLEN=10240 : WORKL=350 : _SAVEIT=True
RESERVE_BANK[10,0,WORKL] : RESERVE_BANK[11,0,BLEN] : RESERVE_BANK[12,0,BLEN]
RESERVE_BANK[13,0,BLEN] : RESERVE_BANK[14,0,BLEN]

_GENERIC=0 : _BUTTON=1 : _CHECKBOX=2 : _INTEGER=3 : _LISTVIEW=4 : _MX=5
_NUMBER=-1 : _CYCLE=7 : _PALETTE=8 : _SCROLLER=9 : _SLIDER=11 : _STRING=12
_TEXT=13 : _NUM=14 : NOWIN=True


_START:
WORK=Start(10) : _STRUCTS=Start(11) : _TAG=Start(12) : LABEL=Start(13) : IMAGE=0
GKIND=Start(14) : MEN=0 : NGAD=0 : BEVEL=0 : ITXT=0 : GP=0 : MP=0 : BP=0 : IP=0
IMG=0 : Trap Erase 15
For I=10 To 14 : Fill Start(I) To Start(I)+Length(I),0 : Next 


'Load & parse Gui File 
'~~~~~~~~~~~~~~~~~~~~~~~ 
PARSEGUI


'Create Gui Structures 
'~~~~~~~~~~~~~~~~~~~~~~
CREATE["GADA",3,GP] : CREATE["MEDA",4,MP] : _WIND
CREATE["BBOX",1,BP] : CREATE["ITXT",2,IP]


'Build & Save Gui Bank 
'~~~~~~~~~~~~~~~~~~~~~~
_SAVE_BANK

If NOWIN=False Then Goto _START

Close 


'Send the GUI bank to the previous program 
'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ 
If Prg Under
   
   Load GFN$ : Bsend 20 : Erase All 
   
End If 


Procedure ER[E$,V]
   Print E$ : If V=1 Then End 
End Proc
Procedure _ADDGAD[DT]
   
   
   'Gadget Name 
   '~~~~~~~~~~~~~ 
   Inc NGAD : _ADDLAB[Peek$(WORK+30,80,Chr$(0))+Chr$(0)] : TG=WORK+168
   
   'Custom Gadget 
   '~~~~~~~~~~~~~~~ 
   If Param=True and Deek(WORK+148)<2
      Doke WORK+148,0 : CUSTOM=True : IMAGE=True
   End If 
   
   'Insert Gadget Info
   '~~~~~~~~~~~~~~~~~~~~
   Add _DATAS,4 : Doke GKIND,Deek(WORK+148) : Add GKIND,2
   
   
   'NewGadget Structure 
   '~~~~~~~~~~~~~~~~~~
   Doke _STRUCTS,Deek(WORK) : Rem         Left Edge
   Doke _STRUCTS+2,Deek(WORK+2) : Rem     Top Edge 
   Doke _STRUCTS+4,Deek(WORK+4) : Rem     Width  
   Doke _STRUCTS+6,Deek(WORK+6) : Rem     Height   
   Loke _STRUCTS+8,0 : Rem                Gadget Text (APTR)
   Loke _STRUCTS+12,0 : Rem               Text Atrrib (APTR) 
   Doke _STRUCTS+16,Deek(WORK+16) : Rem   Gadget ID  
   Loke _STRUCTS+18,Leek(WORK+18) : Rem   Flags  
   Loke _STRUCTS+22,0 : Rem               Visual Info (APTR)   
   Loke _STRUCTS+26,0 : Rem               User Data  
   
   Add _STRUCTS,30 : USER=_STRUCTS-4
   
   If CUSTOM=True
      
      If Length(15)=False
         
         IMG:
         Repeat 
            F$=Fsel$("*.Img","","Load a Image Bank")
         Until Exist(F$)=True
         
         Open In 3,F$ : L=Lof(3) : A$=Input$(3,4) : Close 3
         If A$="IMG!"
            Reserve As Work 15,L+2 : Bload F$,Start(15) : IMG=Start(15)+4
         Else 
            Goto IMG
         End If 
         
      End If 
      
      For I=0 To 1
         L=Deek(IMG+20) : Add L,22 : Copy IMG,IMG+L To _STRUCTS
         Doke USER-22,Deek(IMG+4) : Doke USER-20,Deek(IMG+6)
         Add IMG,L : Add _STRUCTS,L
      Next I
      
   End If 
   
   'Gadget TAGS 
   '~~~~~~~~~~~~~ 
   Do 
      
      T=Leek(TG) : Exit If T=0
      If T<$80000000
         ER["Unknow TAG!",1]
      Else 
         Loke _TAG,T : Loke _TAG+4,Leek(TG+4) and(T><$80080006) : Add _TAG,8 : Add T,-$80080000
         If TX=False
            If T=$E or T=$2D or T=$9 or T=$B : TX=True : End If 
         End If 
      End If 
      Add TG,8
      
   Loop 
   Loke _TAG,0 : Add _TAG,4
   
   If TG=WORK+168 Then NO_TAG=True
   
   If NO_TAG=False
      
      'Parsing Gadget Labels 
      '~~~~~~~~~~~~~~~~~~~~~~~ 
      LTYPE=Deek(WORK+148) : If LTYPE=0 or TX=False : Pop Proc : End If : TX=False
      On LTYPE Gosub NOP,NOP,NOP,LIST,LIST,NOP,LIST,NOP,NOP,NOP,NOP,STRING,STRING,NOP
      
      If Len(S$)
         
         'Gadget Labels 
         '~~~~~~~~~~~~~~~ 
         _ADDLAB[S$]
         
      End If 
      
      Goto NOP
      
      LIST:
      SN=WORK+204 : N=Deek(SN-4) : LTYPE=0
      
      'Update gng_UserData 
      '~~~~~~~~~~~~~~~~~~~~~ 
      Loke USER,N-1 : Add _DATAS,N*4+4 : S$=""
      
      If N>0
         Repeat 
            Repeat 
               A$=Peek$(SN,80,Chr$(0)) : Inc SN
            Until Asc(A$)>31
            Add SN,Len(A$) : A$=A$+Chr$(0) : EVEN[A$]
            A$=Param$ : Dec N : S$=S$+A$
         Until N=0
         S$=S$+Chr$(1)+Chr$(0)
      End If 
      Return 
      
      STRING:
      ST=WORK+DT-2+(Peek(ST)=0) : Repeat : A=Peek(ST) : Dec ST : Until A=0 : Add ST,3
      S$=Peek$(ST,80,Chr$(0))+Chr$(0)
      If Upper$(S$)-Chr$(0)="PBAR" and LTYPE=_TEXT : S$=Chr$(0) : Loke USER,1 : End If 
      LTYPE=0
      Return 
      
   End If 
   
   NOP:
   If LTYPE : Pop : End If 
   
End Proc
Procedure EVEN[A$]
   If Len(A$) mod 2><0 Then A$=A$+Chr$(0)
End Proc[A$]
Procedure RESERVE_BANK[N,T,S]
   If T=0
      Trap Reserve As Work N,S
   Else 
      Trap Reserve As Data N,S
   End If 
   If Errtrap Then ER["Not enough memory to allocate buffer",1]
End Proc[Errn]
Procedure _SAVE_BANK
   
   If _SAVEIT
      
      'Open destination file 
      '~~~~~~~~~~~~~~~~~~~~~~~ 
      Repeat 
         F$=Fsel$("*.abk","","Select destination file")
      Until F$><""
      If Upper$(Right$(F$,3))><"ABK" : F$=F$+".Abk" : End If 
      Open Out 2,F$ : GFN$=F$
      
      'AMOSPro Bank Header 
      '~~~~~~~~~~~~~~~~~~~~~ 
      Poke$ WORK,"AmBk"
      Doke WORK+4,20 : Rem               Bank Number  
      Doke WORK+6,1+IMAGE : Rem          Flag  
      Loke WORK+8,0 : Rem                Lenght of The Bank+8  
      Poke$ WORK+12,"Gui     " : Rem     Bank Name  
      
      Ssave 2,WORK To WORK+20 : _SAVEIT=False
      
   End If 
   
   
   'Set Label-End 
   '~~~~~~~~~~~~~~~~~~~~~~~~
   Doke LABEL,768 : Add LABEL,2
   
   
   'Current GUI offset
   '~~~~~~~~~~~~~~~~~~~~
   PGO=CGO : CGO=Pof(2)
   
   'Gui Bank Header 
   '~~~~~~~~~~~~~~~~~ 
   Doke WORK,0 : Rem                                     Next Gui 
   Doke WORK+2,0 : Rem                                   Prev Gui 
   Loke WORK+4,0 : Rem                                   Next Window (APTR)   
   Loke WORK+8,0 : Rem                                   Prev Window (APTR)   
   Doke WORK+12,0 : Rem                                  Window ID   
   Loke WORK+14,0 : Rem                                  Window Adr  (APTR)    
   Loke WORK+18,0 : Rem                                  Gadget Adr  (APTR)    
   Loke WORK+22,0 : Rem                                  Menu Adr    (APTR)  
   Doke WORK+26,44+_DATAS : Rem                          Gui Kind        
   Doke WORK+28,Deek(WORK+26)+NGAD*2 : Rem               Gui Tags 
   Doke WORK+30,Deek(WORK+28)+_TAG-Start(12) : Rem       Gui Structures 
   Doke WORK+32,Deek(WORK+30)+_STRUCTS-Start(11) : Rem   Gui Labels     
   Doke WORK+34,NGAD : Rem                               # of Gadgets     
   Doke WORK+36,BEVEL : Rem                              # of BevelBox 
   Doke WORK+38,ITXT : Rem                               # of Intuition Text
   Doke WORK+40,Sgn(MEN) : Rem                           Menus Check
   Doke WORK+42,0 : Rem                                  Header Info 
   
   Ssave 2,WORK To WORK+44
   
   'Gadget Address
   '~~~~~~~~~~~~~~~~      
   If _DATAS>WORKL
      Reserve As Work 10,_DATAS+4 : WORKL=Length(10) : WORK=Start(10)
   End If 
   Fill WORK To WORK+WORKL,0 : Trap Ssave 2,WORK To WORK+_DATAS
   
   
   'Gui Kind  
   '~~~~~~~~~~~~~ 
   Trap Ssave 2,Start(14) To GKIND
   
   
   'Gui Tags  
   '~~~~~~~~~~~~~ 
   Trap Ssave 2,Start(12) To _TAG
   
   
   'Gui Structures  
   '~~~~~~~~~~~~~~~~~~~ 
   Trap Ssave 2,Start(11) To _STRUCTS
   
   
   'Gui Labels  
   '~~~~~~~~~~~~~~~ 
   Trap Ssave 2,Start(13) To LABEL
   
   
   'Gui Window Header 
   '~~~~~~~~~~~~~~~~~~~ 
   Doke WORK,Leek(WINDT+4) : Rem         Left Top 
   Doke WORK+2,Leek(WINDT+12) : Rem      Top 
   Doke WORK+4,Leek(WINDT+20) : Rem      Width 
   Doke WORK+6,Leek(WINDT+28) : Rem      Height
   Loke WORK+8,0 : Rem                   Visual Info
   Loke WORK+12,0 : Rem                  Screen Pointer
   
   'Text Attributes Structure 
   '~~~~~~~~~~~~~~~~~~~~~~~~~~~ 
   Loke WORK+16,0 : Rem                  Font Name 
   Doke WORK+20,0 : Rem                  Size  
   Poke WORK+22,0 : Rem                  Style 
   Poke WORK+23,1 : Rem                  Flags 
   
   'Refresh Data
   '~~~~~~~~~~~~~~
   Doke WORK+24,0 : Rem                  Font Width
   Doke WORK+26,0 : Rem                  Font Height 
   Doke WORK+28,0 : Rem                  Offset Y
   Loke WORK+30,0 : Rem                  Background Data 
   Loke WORK+34,0 : Rem                  Background Tags 
   Loke WORK+38,0 : Rem                  Labels
   Doke WORK+42,0 : Rem                  Window Left Edge
   Doke WORK+44,0 : Rem                  Window Top  Edge
   Doke WORK+46,0 : Rem                  Window Width  
   Doke WORK+48,0 : Rem                  Window Height 
   
   HLEN=50 : Trap Ssave 2,WORK To WORK+HLEN
   
   
   'Gui End 
   '~~~~~~~~~~  
   FEND=Pof(2)
   
   
   If PGO
      
      'Update Next/Prev Gui Offset   
      '~~~~~~~~~~~~~~~~~~~~~~~~
      Doke WORK,CGO-PGO : Pof(2)=PGO : Ssave 2,WORK To WORK+2
      Pof(2)=CGO+2 : Ssave 2,WORK To WORK+2
      
   End If 
   
   'Update Header Offset
   '~~~~~~~~~~~~~~~~~~~~~~
   Pof(2)=CGO+42 : Doke WORK,(FEND-HLEN)-CGO : Ssave 2,WORK To WORK+2
   
   
   'Update Bank Length  
   '~~~~~~~~~~~~~~~~~~~~    
   Pof(2)=8 : Loke WORK,Lof(2)-12+$80000000 : Ssave 2,WORK To WORK+4
   
   
   
   Pof(2)=FEND
   
   
End Proc
Procedure PARSE[T$]
   
   A$=Input$(1,8)
   If Instr(A$,T$)
      If Left$(A$,5)=Chr$(0)+T$ : Pof(1)=Pof(1)-7 : A$=Input$(1,8) : End If 
      NG=Pof(1)+Leek(Varptr(A$)+4) : DT=NG-Pof(1)
      If DT>WORKL : Reserve As Work 10,DT+4 : WORKL=DT+4 : WORK=Start(10) : End If 
      Sload 1 To Start(10),DT : Pof(1)=NG
   Else 
      DT=0
   End If 
   
End Proc[DT]
Procedure _ADDBBOX[DT]
   
   Inc BEVEL
   
   'BevelBox Structure  
   '~~~~~~~~~~~~~~~~~~~~
   
   Doke _STRUCTS,Deek(WORK) : Rem         X Coord
   Doke _STRUCTS+2,Deek(WORK+2) : Rem     Y Coord
   Doke _STRUCTS+4,Deek(WORK+4) : Rem     Width
   Doke _STRUCTS+6,Deek(WORK+6) : Rem     Height 
   
   
   Loke _TAG,$80080034 : Loke _TAG+4,0 : Add _TAG,8 : Rem   VisualInfo 
   
   If Deek(WORK+8)
      Loke _TAG,$80080033 : Loke _TAG+4,1 : Add _TAG,8 : Rem  Recessed
   End If 
   
   Add _TAG,4 : Rem    TAG_DONE
   
   Add _STRUCTS,8
   
   
End Proc
Procedure CREATE[T$,T,C]
   
   If C
      
      Pof(1)=C
      Repeat 
         
         PARSE[T$] : Exit If Param=0
         
         If T=1
            _ADDBBOX[Param]
         Else If T=2
            _ADDTEXT[Param]
         Else If T=3
            _ADDGAD[Param]
         Else If T=4
            _ADDMENU[Param]
         End If 
         F=Free
         
      Until Pof(1)>=WP2 or Eof(1)
      
      If MEN and T=4 : Add _STRUCTS,20 : End If 
      
   End If 
   
End Proc
Procedure _ADDTEXT[DT]
   
   'IntuiText Text
   '~~~~~~~~~~~~~~~~
   T$=Peek$(WORK+20,79,Chr$(0))+Chr$(0)
   
   If Len(T$)>1
      
      _ADDLAB[T$]
      
      'IntuiText Structure 
      '~~~~~~~~~~~~~~~~~~~~~~~~~~
      Inc ITXT
      Poke _STRUCTS,Peek(WORK) : Rem           Front Pen
      Poke _STRUCTS+1,Peek(WORK+1) : Rem       BackPen  
      Poke _STRUCTS+2,Peek(WORK+2) : Rem       DrawMode 
      Poke _STRUCTS+3,0 : Rem                  Round To Word   
      Doke _STRUCTS+4,0 : Rem                  LeftEdge  (Sensitive) 
      Doke _STRUCTS+6,0 : Rem                  TopEdge   (Sensitive) 
      Loke _STRUCTS+8,0 : Rem                  ITextFont 
      Loke _STRUCTS+12,0 : Rem                 IText
      Loke _STRUCTS+16,0 : Rem                 NextText 
      Doke _STRUCTS+20,Deek(WORK+4) : Rem      LeftEdge  (Original)  
      Doke _STRUCTS+22,Deek(WORK+6) : Rem      TopEdge   (Original)
      
      Add _STRUCTS,24
      
   End If 
   
   
End Proc
Procedure _ADDMENU[DT]
   
   Inc MEN
   
   'NewMenu Structure 
   '~~~~~~~~~~~~~~~~~~~ 
   Doke _STRUCTS,Deek(WORK) : Rem          Menu Type 
   Loke _STRUCTS+2,0 : Rem                 Menu Label 
   Loke _STRUCTS+6,0 : Rem                 Command Key
   Doke _STRUCTS+10,Deek(WORK+10) : Rem    Menu Flags
   Loke _STRUCTS+12,Leek(WORK+12) : Rem    Mutual Exclude
   Loke _STRUCTS+16,0 : Rem                User Data 
   
   Add _STRUCTS,20
   
   
   'Menu Label
   '~~~~~~~~~~~~
   If Leek(WORK+2)><-1
      _ADDLAB[Peek$(WORK+20,79,Chr$(0))+Chr$(0)]
   Else 
      Loke _STRUCTS-18,-1
   End If 
   
   
   'Command Key 
   '~~~~~~~~~~~~~ 
   CH=Peek(WORK+134)
   If CH
      _ADDLAB[Chr$(CH)+Chr$(0)] : Loke _STRUCTS-14,1
   End If 
   
   
End Proc
Procedure PARSEGUI
   
   If NOWIN=True
      
      ' Load Gui File
      '~~~~~~~~~~~~~~~~
      L0AD:
      Repeat 
         F$=Fsel$("*.gui","","Select a Gui File")
      Until Exist(F$)
      
      Open In 1,F$ : A$=Input$(1,16)
      If Left$(A$,4)><"FORM" or Right$(A$,8)><"GXUIGGUI"
         Close 1 : Goto L0AD
      End If 
      A$=Input$(1,4) : CH=Leek(Varptr(A$)) : Pof(1)=Pof(1)+CH : NOWIN=True
      
   Else 
      Pof(1)=WP2 : NOWIN=True
   End If 
   
   'Parsing Gui File
   '~~~~~~~~~~~~~~
   Repeat 
      
      A$=Input$(1,12) : CH=Leek(Varptr(A$)+4) : Pof(1)=Pof(1)-4 : CHUNK$=Right$(A$,4)
      
      If CHUNK$="GXWD"
         If NOWIN=False : Exit : End If 
         Pof(1)=Pof(1)-4 : A$=Input$(1,4) : CH1=Leek(Varptr(A$)) : WP2=Pof(1)+CH1
         NOWIN=WP2=Lof(1)
         Pof(1)=Pof(1)+8 : A$=Input$(1,4) : CH=Leek(Varptr(A$)) : WP=Pof(1)
         
      Else If CHUNK$="GXBX"
         BP=Pof(1)+4
         
      Else If CHUNK$="GXTX"
         IP=Pof(1)+4
         
      Else If CHUNK$="GXGA"
         GP=Pof(1)+4
         
      Else If CHUNK$="GXMN"
         MP=Pof(1)+4
         
      End If 
      
      Pof(1)=Pof(1)+CH
      
   Until Eof(1)
   
End Proc
Procedure _WIND
   
   'Read Window Datas 
   '~~~~~~~~~~~~~~~~~~~ 
   Pof(1)=WP-4 : A$=Input$(1,4) : CH=Leek(Varptr(A$)) : Sload 1 To Start(10),CH
   
   'Window Name 
   '~~~~~~~~~~~~~ 
   _ADDLAB[Peek$(WORK+34,79,Chr$(0))+Chr$(0)]
   
   
   'Window Tags 
   '~~~~~~~~~~~~~ 
   TGS=WORK+236 : WINDT=_TAG
   
   Repeat 
      Loke _TAG,Leek(TGS) : Loke _TAG+4,Leek(TGS+4) : Add _TAG,8 : Add TGS,8
      If Leek(_TAG-8)=$8000006A Then _IDCMP=Leek(_TAG-4) or $200000 : Loke _TAG-4,0
   Until(TGS-WORK)=CH
   
   Loke _TAG,$8000006C : Loke _TAG+4,0 : Loke _TAG+8,$8000006F : Loke _TAG+12,0
   Loke _TAG+16,0 : Loke _TAG+20,_IDCMP : Add _TAG,24
   
   
   'Window Screen Name
   '~~~~~~~~~~~~~~~~~~~~
   A$=Peek$(WORK+114,79,Chr$(0))+Chr$(0) : If A$="GadToolsBox V2.0b © 1991-1993"+Chr$(0) Then A$=Chr$(2)
   _ADDLAB[A$]
   
   
End Proc
Procedure _ADDLAB[A$]
   
   'Add Label in the Label Chain  
   '~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~  
   If A$=Chr$(0) Then A$=Chr$(2)+Chr$(0) : NN=True
   EVEN[A$] : A$=Param$
   Poke$ LABEL,A$ : Add LABEL,Len(A$)
   
End Proc[NN]

'                            AMIGA RULEZ!! 
