; this script demonstrates how to upload a file to an FTP server using the lowlevel interface
;
; note that you can have this much easier by just using Hollywood's UploadFile()
; with "Adapter" set to "hURL" --> see the chapter about hURL's high-level interface
; in the hURL documentation for details

@REQUIRE "hurl"

; this function must return the data to be uploaded
Function p_ReadFunc(len)
	
	If rlen + len > flen Then len = flen - rlen
	
	If len > 0
		rlen = rlen + len
		Return(ReadBytes(1, len))
	Else
		Return("")
	EndIf
			
EndFunction	

usr$ = StringRequest("", "User?")
pwd$ = StringRequest("", "Password?")
f$ = FileRequest("Select file to upload")

OpenFile(1, f$)
flen = FileLength(1)

; create easy object and configure it
e = hurl.Easy({URL = "ftp://example.com/testdir/testfile.dat", UserName = usr$, Password = pwd$, ReadFunction = p_ReadFunc, Upload = True, InFileSize = flen})

; transfer data
e:Perform()

; destroy easy object
e:Close()

End
