; this script demonstrates downloading multiple files at the same time using the lowlevel interface

@REQUIRE "hurl"

Function p_WriteData(data$, did)
	WriteBytes(did, data$)
EndFunction

f1 = OpenFile(Nil, "hollywood.html", #MODE_WRITE)
f2 = OpenFile(Nil, "paypal.html", #MODE_WRITE)
f3 = OpenFile(Nil, "amazon.html", #MODE_WRITE)

; setup first transfer
c1 = hurl.Easy()
c1:SetOpt_URL("https://www.hollywood-mal.com/")
c1:SetOpt_WriteFunction(p_WriteData, f1)
c1:SetOpt_FollowLocation(True)

; setup second transfer
c2 = hurl.Easy()
c2:SetOpt_URL("https://www.paypal.com/")
c2:SetOpt_WriteFunction(p_WriteData, f2)
c2:SetOpt_FollowLocation(True)

; setup third transfer
c3 = hurl.Easy()
c3:SetOpt_URL("https://www.amazon.com/")
c3:SetOpt_WriteFunction(p_WriteData, f3)
c3:SetOpt_FollowLocation(True)

; setup multi object
m = hurl.Multi()

; add the three transfer objects to the multi object
m:AddHandle(c1)
m:AddHandle(c2)
m:AddHandle(c3)

; transfer data!
remain = 3
While remain > 0

	; do some work
	Local last = m:Perform() 
  
  	If last < remain
    	
    		While 1
    			; get result and remove handle
      			Local msg, result, remaining, xh = m:InfoRead()
      			
      			; print status
      			DebugPrint(msg, result, remaining, xh, xh = c1)
      			
      			; are we done?
			If remaining = 0 Then Break
    		Wend
  	EndIf
  
  	remain = last

	; prevent CPU hogging
	m:Wait(1.0)
Wend

; remove handles from multi object
m:RemoveHandle(c1)
m:RemoveHandle(c2)
m:RemoveHandle(c3)

; close easy handles
c1:Close()
c2:Close()
c3:Close()

; close multi handle
m:Close()

; close files
CloseFile(f1)
CloseFile(f2)
CloseFile(f3)

End
 
