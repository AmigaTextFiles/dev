;
; $VER: hURL-Setup 2.0 (30.04.23)
;
; Done by Andreas Falkenhahn
;

(message (cat "\nWelcome to the Setup utility for\n\n"
	"hURL 2.0\n\n"
	"- The Multi-Protocol Data Transfer Plugin for Hollywood -\n\n"
	"Copyright (C) 2017-2023 by Andreas Falkenhahn\n\n"
	"http://www.hollywood-mal.com/"
))

(welcome)

(set basepath
	(askdir
		(prompt  "Where do you want to have hURL installed? (a drawer will be created)")
		(help    @askdir-help)
		(default "SYS:")
	)
)

(set sstr (substr basepath (- (strlen basepath) 1) 1))
(if (or (= sstr "/") (= sstr ":"))
	(set destpath (cat basepath "hURL"))
	(set destpath (cat basepath "/hURL"))
)

(set @default-dest destpath)

(set defaultplat 0)

(if (exists "SYS:MorphOS/C/Assign") (
	(set defaultplat 3)
) (
	(if (exists "SYS:System/GrimReaper") (
		(set defaultplat 1)
	))
))

(set whichver
	(askchoice
		(prompt  "Please select your platform")
		(help    @askoptions-help)
		(choices "AmigaOS 3 (m68k)" "AmigaOS 4 (ppc)" "AROS (x86)" "MorphOS (ppc)")		
		(default defaultplat)
	)
)

(set #platform "m68k-amigaos")

(if (= whichver 1) (
	(set #platform "ppc-amigaos")
))

(if (= whichver 2) (
	(set #platform "x86-aros")
))

(if (= whichver 3) (
	(set #platform "ppc-morphos")
))

(makedir destpath)
(makedir "LIBS:Hollywood")

(copylib
	(source (cat "Platforms/" #platform "/hurl.hwp"))
	(dest   "LIBS:Hollywood")
	(prompt "hurl.hwp")
	(help @copylib-help)
	(optional "nofail" "force" "askuser")
	(confirm)
)

(copyfiles
	(source  "Data.icon")
	(dest    basepath)
	(newname "hURL.info")
	(optional force oknodelete)
)

(copyfiles
	(source "Data")
	(dest   destpath)
	(all)
	(optional "nofail" "force" "askuser")
)

(if (exists "Hollywood:LinkerPlugins" (noreq)) (

	(copyfiles
		(source "Platforms")
		(dest   "Hollywood:LinkerPlugins")
		(all)
		(optional "nofail" "force" "askuser")
	)
))

(set #cmdline (cat "Assign hURL: \"" destpath "\""))

(startup
	(prompt  "\nAn assign has to be inserted in your user-startup. Please click Continue.")
	(help    @startup-help)
	"hURL"
	(command #cmdline)
	(confirm)
)

(run #cmdline)

(exit)
