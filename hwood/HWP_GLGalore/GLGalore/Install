;
; $VER: GL Galore Setup 1.1 (20.05.17)
;
; Done by Andreas Falkenhahn
;

(message (cat "\nWelcome to the Setup utility for\n\n"
	"GL Galore 1.1\n\n"
	"Copyright (C) 2014-2017 by Andreas Falkenhahn"
))

(welcome)

(set basepath
	(askdir
		(prompt  "Where do you want to have GL Galore installed? (a drawer will be created)")
		(help    @askdir-help)
		(default "SYS:")
	)
)

(set sstr (substr basepath (- (strlen basepath) 1) 1))
(if (or (= sstr "/") (= sstr ":"))
	(set destpath (cat basepath "GLGalore"))
	(set destpath (cat basepath "/GLGalore"))
)

(set whichver
	(askchoice
		(prompt  "Please select your platform.")
		(help    @askoptions-help)
		(choices "AmigaOS 3 (FPU)" "MorphOS" "AmigaOS 4" "AROS (x86)")
	)
)

; AmigaOS3 (default)
(set #platform "AmigaOS3_FPU")

; MorphOS
(if (= whichver 1) (
	(set #platform "MorphOS")
))

; AmigaOS4
(if (= whichver 2) (
	(set #platform "AmigaOS4")
))

; AROS
(if (= whichver 3) (
	(set #platform "AROS")
))

(set @default-dest destpath)

(makedir destpath)
(makedir "LIBS:Hollywood")

(copylib
	(source (cat "Platforms/" #platform "/glgalore.hwp"))
	(dest   "LIBS:Hollywood")
	(prompt "glgalore.hwp")
	(help @copylib-help)
	(optional "nofail" "force" "askuser")
	(confirm)
)

(copyfiles
	(source  "Data.icon")
	(dest    basepath)
	(newname "GLGalore.info")
	(optional force oknodelete)
)

(copyfiles
	(source "Data")
	(dest   destpath)
	(all)
	(optional "nofail" "force" "askuser")
)

(if (exists "Hollywood:GUI" (noreq)) (

	(makedir "Hollywood:Examples/OpenGL")

	(copyfiles
		(source "Data/Examples")
		(dest   "Hollywood:Examples/OpenGL")
		(all)
		(optional "nofail" "force" "askuser")
	)
))

(set #cmdline (cat "Assign GLGalore: \"" destpath "\""))

(startup
	(prompt  "\nAn assign has to be inserted in your user-startup. Please click Continue.")
	(help    @startup-help)
	"GLGalore"
	(command #cmdline)
	(confirm)
)

(run #cmdline)

(message (cat "\nGL Galore has been installed successfully.\n\n"
	"Please note that for a butter-smooth OpenGL experience, you should run "
	"your GL Galore scripts in fullscreen mode. Only in fullscreen mode will "
	"all graphics be perfectly synchronized with your monitor's vertical refresh "
	"for butter-smooth 2D and 3D animation. In windowed mode, there might be "
	"marginal refresh glitches."))

(message (cat "\nGL Galore comes with almost 20 ready-to-run examples.\n\n"
	"The examples have been integrated into your Hollywood GUI (only "
	"if Hollywood is installed on this system). Now that you have installed "
	"GL Galore, there will be a new OpenGL tab in the Hollywood GUI which "
	"allows you to conveniently launch the example scripts that come with GL Galore.\n\n"
	"Click to see a screenshot that shows how to launch the example scripts "
	"that come with GL Galore."))

(run ("Sys:Utilities/MultiView GUI.png"))

(message (cat "\nTo view GL Galore's documentation, use Workbench or Ambient to go to the drawer "
	"where you installed GL Galore. Alternatively, you can also launch GL Galore's "
	"documentation from the \"Plugins\" dialog of the Hollywood GUI.\n\n"
	"For any questions you might have, please join the official forums at\n\n"
	"http://www.hollywood-mal.com/\n\n"
	"Thank you for installing GL Galore! Have fun with this plugin!"))

(exit)

