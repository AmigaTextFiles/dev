/*
** warp.hws
**
** This is an example of what extreme field of view can do.....
**
** Hollywood port by Andreas Falkenhahn based on C code found in the MiniGL SDK
*/

@VERSION 6,0

@REQUIRE "glgalore"

@BRUSH 1, "stars.png"
@BRUSH 2, "flare.png"

Const #WIDTH = 640
Const #HEIGHT = 480

Const #BACKPLANE = -8.0
Const #TEXTU = 0.0
Const #TEXTV = 2.0

@DISPLAY {Width = #WIDTH, Height = #HEIGHT, Sizeable = True, Title = "Warp"}

Function p_TexInit(num)

	gl.BindTexture(#GL_TEXTURE_2D, num)
	gl.TexImageFromBrush(0, num)

	gl.TexParameter(#GL_TEXTURE_2D, #GL_TEXTURE_MIN_FILTER, #GL_LINEAR)
	gl.TexParameter(#GL_TEXTURE_2D, #GL_TEXTURE_MAG_FILTER, #GL_LINEAR)

	gl.Enable(#GL_TEXTURE_2D)

EndFunction

Function p_DrawFlare(texnum)

	Local w = width / 10.0
	Local h = w
	Local x = width / 2.0 - w / 2.0
	Local y = height / 2.0 - h / 2.0

	gl.BindTexture(#GL_TEXTURE_2D, texnum)
	gl.Enable(#GL_BLEND)
	gl.BlendFunc(#GL_ONE, #GL_ONE)

	gl.PushMatrix()
	gl.MatrixMode(#GL_PROJECTION)
	gl.LoadIdentity()
	gl.Ortho(0, width, height, 0, -1, 1)
	gl.MatrixMode(#GL_MODELVIEW)
	gl.LoadIdentity()

	gl.Color(1.0, 1.0, 1.0)
	gl.Begin(#GL_TRIANGLE_FAN)
	gl.TexCoord(0.0, 0.0)
	gl.Vertex(x,y)
	gl.TexCoord(1.0, 0.0)
	gl.Vertex(x+w, y)
	gl.TexCoord(1.0, 1.0)
	gl.Vertex(x+w,y+h)
	gl.TexCoord(0.0, 1.0)
	gl.Vertex(x,y+h)
	gl.End()

	gl.PopMatrix()

	gl.Disable(#GL_BLEND)

EndFunction

Function p_DrawStarfield(tex1, off)

	gl.BindTexture(#GL_TEXTURE_2D, tex1)

	gl.Color(1.0, 1.0, 1.0, 1.0)
	gl.Begin(#GL_QUADS)

	gl.TexCoord(#TEXTU, #TEXTV+off)
	gl.Vertex(-1.0,  1.0,  #BACKPLANE)
	gl.TexCoord(#TEXTU, off)
	gl.Vertex(-1.0,  1.0,   1.0)
	gl.TexCoord(1.0, off)
	gl.Vertex(-1.0, -1.0,   1.0)
	gl.TexCoord(1.0, #TEXTV+off)
	gl.Vertex(-1.0, -1.0,  #BACKPLANE)

	gl.TexCoord(#TEXTU, #TEXTV+off)
	gl.Vertex( 1.0,  1.0,  #BACKPLANE)
	gl.TexCoord(#TEXTU, off)
	gl.Vertex( 1.0,  1.0,   1.0)
	gl.TexCoord(1.0, off)
	gl.Vertex( 1.0, -1.0,   1.0)
	gl.TexCoord(1.0, #TEXTV+off)
	gl.Vertex( 1.0, -1.0,  #BACKPLANE)

	gl.TexCoord(#TEXTU, #TEXTV+off)
	gl.Vertex(-1.0,  1.0,  #BACKPLANE)
	gl.TexCoord(#TEXTU, off)
	gl.Vertex(-1.0,  1.0,   1.0)
	gl.TexCoord(1.0, off)
	gl.Vertex( 1.0,  1.0,   1.0)
	gl.TexCoord(1.0, #TEXTV+off)
	gl.Vertex( 1.0,  1.0,  #BACKPLANE)

	gl.TexCoord(#TEXTU, #TEXTV+off)
	gl.Vertex(-1.0, -1.0,  #BACKPLANE)
	gl.TexCoord(#TEXTU, off)
	gl.Vertex(-1.0, -1.0,   1.0)
	gl.TexCoord(1.0, off)
	gl.Vertex( 1.0, -1.0,   1.0)
	gl.TexCoord(1.0, #TEXTV+off)
	gl.Vertex( 1.0, -1.0,  #BACKPLANE)

	gl.End()

EndFunction

Function p_Reshape(msg)

	width = msg.width
	height = msg.height

	gl.MatrixMode(#GL_PROJECTION)
	gl.LoadIdentity()
	glu.Perspective(70.0, 1.3333333, 1.0, 100.0)

	gl.MatrixMode(#GL_MODELVIEW)
	gl.Viewport(0, 0, width, height)
	gl.ClearColor(0.5, 0.5, 0.5, 1.0)
	gl.ClearDepth(1.0)

EndFunction

Function p_DoFrame()

	gl.Clear(#GL_DEPTH_BUFFER_BIT|#GL_COLOR_BUFFER_BIT)

	gl.MatrixMode(#GL_PROJECTION)
	gl.LoadIdentity()
	glu.Perspective(fov, 1.3333333, 1.0, 100.0)

	gl.MatrixMode(#GL_MODELVIEW)
	gl.LoadIdentity()

	gl.Scale(20.0, 20.0, 20.0)
	gl.Rotate(startilt, 0.0, 0.0, 1.0)
	gl.Disable(#GL_CULL_FACE)

	p_DrawStarfield(1, t_off)
	p_DrawFlare(2)

	startilt = startilt + 0.1
	t_off = t_off + 0.007

EndFunction

Function p_Init(w, h)

	fov = 170.0

	p_Reshape({width = w, height = h})

	gl.Disable(#GL_DEPTH_TEST)
	gl.Clear(#GL_COLOR_BUFFER_BIT|#GL_DEPTH_BUFFER_BIT)

	p_TexInit(1)
	p_TexInit(2)

	gl.ClearColor(0.3, 0.3, 0.3, 1.0)
	gl.Color(1.0, 1.0, 1.0)
	gl.BlendFunc(#GL_SRC_ALPHA, #GL_ONE_MINUS_SRC_ALPHA)

	gl.Enable(#GL_TEXTURE_2D)
	gl.TexEnv(#GL_TEXTURE_ENV, #GL_TEXTURE_ENV_MODE, #GL_REPLACE)

	gl.ShadeModel(#GL_SMOOTH)

EndFunction

Function p_KeyFunc(msg)

	Switch msg.Key
	Case "+"
		If fov < 165 Then fov = fov + 5
	Case "-"
		If fov > 30 Then fov = fov - 5
	EndSwitch

EndFunction

p_Init(#WIDTH, #HEIGHT)

InstallEventHandler({SizeWindow = Function(msg) p_Reshape(msg) EndFunction, ModeSwitch = Function(msg) p_Init(msg.width, msg.height) EndFunction, OnKeyDown = Function(msg) p_KeyFunc(msg) EndFunction})

; Setup a hardware doublebuffer managed by GL Galore	
BeginDoubleBuffer(True)

EscapeQuit(True)

Repeat
	p_DoFrame()

	; Swap buffers
	Flip

	CheckEvent
Forever

