/****************************************************************
**                                                             **
** Name:        Dynamic1                                       **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        23.05.14                                       **
** Interpreter: Hollywood 5.2                                  **
** Licence:     Sample program                                 **
** Function:    Demonstrates dynamic creation of windows       **
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (23.05.14)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Make sure we have at least Hollywood 5.2!
*/
@VERSION 5,2


/*
** This script requires the MUI Royale plugin
*/
@REQUIRE "MUIRoyale"


/*
** MUI apps require this information
*/
@APPTITLE "Dynamic1"
@APPVERSION "$VER: Dynamic 1.0 (23.05.14)"
@APPCOPYRIGHT "©2014, Andreas Falkenhahn"
@APPAUTHOR "Andreas Falkenhahn"
@APPDESCRIPTION "Demonstrates dynamic creation of windows."


/*
** We don't need our Hollywood display because we have a MUI window
*/
@DISPLAY {Hidden = True}


/*
** Close window and free its object if it has been created programmatically
*/
Function p_CloseWindow(id)

	mui.Set(id, "open", False)
	mui.DoMethod("app", "removewindow", id)
	mui.FreeObject(id)

	opencount = opencount - 1
	If opencount = 0 Then End

EndFunction


/*
** Handles all incoming events
*/
Function p_EventFunc(msg)

	Switch msg.action
	Case "MUIRoyale":
		Switch msg.attribute
		Case "CloseRequest":
			p_CloseWindow(msg.id)
		Case "Pressed":
			If LeftStr(msg.id, 3) = "new"
				count = count + 1
				opencount = opencount + 1
				mui.CreateObject(ReplaceStr(FileToString("Dynamic1.xml"), "%COUNT", count))
				mui.DoMethod("app", "addwindow", "win" .. count)
				mui.Set("win" .. count, "open", True)
			ElseIf LeftStr(msg.id, 5) = "close"
				p_CloseWindow(ReplaceStr(msg.id, "close", "win"))
			ElseIf LeftStr(msg.id, 4) = "dump"
				Local idx = Val(ReplaceStr(msg.id, "dump", ""))
				mui.DoMethod("lv" .. idx, "clear")
				Local t = mui.Get("app", "windowlist")
				For Local k = 0 To ListItems(t) - 1 Do mui.DoMethod("lv" .. idx, "insert", "bottom", t[k])
			EndIf
 		EndSwitch

	case "HideWindow":
		mui.Set("app", "iconified", True)

	case "ShowWindow":
		mui.Set("app", "iconified", False)
	EndSwitch

EndFunction

count = 1
opencount = 1

; dynamically create MUI GUI from an external *.xml file definition
mui.CreateGui([[
<?xml version="1.0" encoding="iso-8859-1"?>
<application id="app" base="DYNAMIC1">
]] .. ReplaceStr(FileToString("Dynamic1.xml"), "%COUNT", count) .. "</application>")

; listen to these events!
InstallEventHandler({MUIRoyale = p_EventFunc, HideWindow = p_EventFunc, ShowWindow = p_EventFunc})

; main loop!
Repeat
	WaitEvent
Forever

