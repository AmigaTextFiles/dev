/****************************************************************
**                                                             **
** Name:        Class3                                         **
** Author:      Andreas Falkenhahn, original by Stefan Stuntz  **
** Version:     1.0                                            **
** Date:        08.08.12                                       **
** Interpreter: Hollywood 5.2                                  **
** Licence:     Sample program                                 **
** Function:    Hollywood port of this demo program from the   **
**              MUI 3.8 development kit                        **
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (08.08.12)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Make sure we have at least Hollywood 5.2!
*/
@VERSION 5,2


/*
** This script requires the MUI Royale plugin
*/
@REQUIRE "MUIRoyale"


/*
** MUI apps require this information
*/
@APPTITLE "Class3"
@APPVERSION "$VER: Class3 19.5 (12.02.97)"
@APPCOPYRIGHT "©1993, Stefan Stuntz"
@APPAUTHOR "Stefan Stuntz"
@APPDESCRIPTION "Demonstrate the use of custom classes."


/*
** Our hidden Hollywood display acts as our custom class
*/
@DISPLAY {Width = 120, Height = 90, Hidden = True, Color = #WHITE}


/*
** Scroll our brush
*/
Function p_DoScroll(sx, sy)

	Local w = GetAttribute(#BRUSH, curbr, #ATTRWIDTH)
	Local h = GetAttribute(#BRUSH, curbr, #ATTRHEIGHT)
	Local newbr = CreateBrush(Nil, w, h, #WHITE)

	SelectBrush(newbr)
	DisplayBrush(curbr, sx, sy)
	EndSelect

	FreeBrush(curbr)

	curbr = newbr
	DisplayBrush(curbr, 0, 0)

EndFunction

/*
** Handles all incoming events
*/
Function p_EventFunc(msg)

	Switch msg.action
	Case "MUIRoyale":
		Switch msg.attribute
		Case "CloseRequest":
			End
 		EndSwitch

	Case "HideWindow":
		mui.Set("app", "iconified", True)

	Case "ShowWindow":
		mui.Set("app", "iconified", False)

	Case "SizeWindow":
		FreeBrush(curbr)

		curbr = CreateBrush(Nil, msg.width, msg.height, #WHITE)
		DisplayBrush(curbr, 0, 0)

	Case "OnMouseMove":
		If IsLeftMouse() = True

			SelectBrush(curbr)
			Plot(msg.x, msg.y, #BLACK)
			EndSelect

			DisplayBrush(curbr, 0, 0)
		EndIf

	Case "OnKeyDown":
		Local sx = 0
		Local sy = 0

		Switch msg.key
		Case "LEFT":
			sx = 1
		Case "RIGHT":
			sx = -1
		Case "UP":
			sy = 1
		Case "DOWN":
			sy = -1
		EndSwitch

		If (sx <> 0) Or (sy <> 0) Then p_DoScroll(sx, sy)
	EndSwitch

EndFunction

curbr = CreateBrush(Nil, 120, 90, #WHITE)
DisplayBrush(curbr, 0, 0)

; dynamically create MUI GUI from an external *.xml file definition
mui.CreateGui(FileToString("Class3.xml"))

; listen to these events!
InstallEventHandler({MUIRoyale = p_EventFunc, HideWindow = p_EventFunc, ShowWindow = p_EventFunc, SizeWindow = p_EventFunc, OnKeyDown = p_EventFunc, OnMouseMove = p_EventFunc})

; main loop!
Repeat
	WaitEvent
Forever

