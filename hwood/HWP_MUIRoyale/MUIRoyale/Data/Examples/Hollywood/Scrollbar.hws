/****************************************************************
**                                                             **
** Name:        Scrollbar                                      **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        26.08.12                                       **
** Interpreter: Hollywood 5.2                                  **
** Licence:     Sample program                                 **
** Function:    Demonstrates how to use MUI from Hollywood     **
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (26.08.12)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Make sure we have at least Hollywood 5.2!
*/
@VERSION 5,2


/*
** This script requires the MUI Royale plugin
*/
@REQUIRE "MUIRoyale"


/*
** MUI apps require this information
*/
@APPTITLE "Scrollbar"
@APPVERSION "$VER: Scrollbar 1.0 (26.08.12)"
@APPCOPYRIGHT "(C) Copyright 2012 by Andreas Falkenhahn"
@APPAUTHOR "Andreas Falkenhahn"
@APPDESCRIPTION "Demonstrates how to connect the scrollbar to a Hollywood widget"


/*
** We don't need our Hollywood display because we have a MUI window
*/
@DISPLAY {Hidden = True, Width = 340, Height = 600, Color = #WHITE}


/*
** Compute the size of the overlapping rectangle
*/
Function p_GetOverlapRect(left1, top1, width1, height1, left2, top2, width2, height2)

	Local bottom1, bottom2
	Local over_right, over_bottom
	Local right1, right2
	Local rettop, retleft

	bottom1 = top1 + height1 - 1
	bottom2 = top2 + height2 - 1
	right1 = left1 + width1 - 1
	right2 = left2 + width2 - 1

	If bottom1 > bottom2
		over_bottom = bottom2
	Else
		over_bottom = bottom1
	EndIf

	If top1 < top2
		rettop = top2
	Else
		rettop = top1
	EndIf

	If right1 > right2
		over_right = right2
	Else
		over_right = right1
	EndIf

	If left1 < left2
		retleft = left2
	Else
		retleft = left1
	EndIf

	Return(retleft, rettop, over_right - retleft + 1, over_bottom - rettop + 1)

EndFunction


/*
** Draw thumbnails
*/
Function p_DrawThumbs(scrolly)

	Local drwx, drwy, drwwidth, drwheight = 0, 10, 322, 242
	Local srcx, srcy, width, height = 0, scrolly, 322, GetAttribute(#DISPLAY, 0, #ATTRHEIGHT)

	Local tmp = CreateBrush(Nil, width, height, #WHITE)
	SelectBrush(tmp)

	For Local k = 0 To 25

		; is this thumbnail visible?
		If Collision(#BOX, drwx, drwy, drwwidth, drwheight, srcx, srcy, width, height) = True

			; get visible part of thumbnail
			Local ox, oy, owidth, oheight = p_GetOverlapRect(drwx, drwy, drwwidth, drwheight, srcx, srcy, width, height)

			; and draw it!
			DisplayBrushPart(k + 1, ox - drwx, oy - drwy, ox - srcx, oy - srcy, owidth, oheight)
		EndIf

		; leave 10 pixels space between thumbnails
		drwy = drwy + drwheight + 10
	Next

	EndSelect

	DisplayBrush(tmp, #CENTER, 0)
	FreeBrush(tmp)

EndFunction


/*
** Adjust scrollbar properties
*/
Function p_AdjustScrollbar()

	Local fullheight = 26 * 242 + 26 * 10
	Local visheight = GetAttribute(#DISPLAY, 0, #ATTRHEIGHT)

	mui.Set("scrollbar", "entries", fullheight, "visible", visheight)

EndFunction


/*
** Scroll "delta" entries
*/
Function p_DoScroll(delta)

	Local y = mui.Get("scrollbar", "first")
	Local height = mui.Get("scrollbar", "entries")

	y = y + delta
	If y < 0 Then y = 0
	If y > height Then y = height

	mui.Set("scrollbar", "first", y)

EndFunction


/*
** Handles all incoming events
*/
Function p_EventFunc(msg)

	Switch msg.action
	Case "MUIRoyale":
		Switch msg.attribute
		Case "CloseRequest":
			End
		Case "First":
			p_DrawThumbs(msg.triggervalue)
 		EndSwitch

	Case "HideWindow":
		mui.Set("app", "iconified", True)

	Case "ShowWindow":
		mui.Set("app", "iconified", False)

	Case "SizeWindow":
		p_AdjustScrollbar()
		p_DrawThumbs(mui.Get("scrollbar", "first"))

	Case "OnWheelDown":
		p_DoScroll(50)

	Case "OnWheelUp":
		p_DoScroll(-50)

	Case "OnKeyDown":
		Local delta = 10

		; shift can be used to accelerate scrolling
		If (IsKeyDown("LSHIFT") = True) Or (IsKeyDown("RSHIFT") = True) Then delta = 75

		Switch msg.key
		Case "DOWN":
			p_DoScroll(delta)
		Case "UP":
			p_DoScroll(-delta)
		EndSwitch
	EndSwitch

EndFunction

; load thumbnail brushes
For Local k = 0 To 25 Do LoadBrush(k + 1, "Scrollbar/" .. k .. ".png")

; dynamically create MUI GUI from an external *.xml file definition
mui.CreateGui(FileToString("Scrollbar.xml"))

p_AdjustScrollbar()
p_DrawThumbs(0)

; listen to these events!
InstallEventHandler({MUIRoyale = p_EventFunc, HideWindow = p_EventFunc, ShowWindow = p_EventFunc, SizeWindow = p_EventFunc,
	OnWheelDown = p_EventFunc, OnWheelUp = p_EventFunc, OnKeyDown = p_EventFunc})

; main loop!
Repeat
	WaitEvent
Forever

