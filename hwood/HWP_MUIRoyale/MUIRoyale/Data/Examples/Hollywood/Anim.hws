/****************************************************************
**                                                             **
** Name:        Anim                                           **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        26.12.12                                       **
** Interpreter: Hollywood 5.2                                  **
** Licence:     Sample program                                 **
** Function:    Demonstrates how to use MUI from Hollywood     **
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (26.12.12)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Make sure we have at least Hollywood 5.2!
*/
@VERSION 5,2


/*
** This script requires the MUI Royale plugin
*/
@REQUIRE "MUIRoyale"


/*
** MUI apps require this information
*/
@APPTITLE "Anim"
@APPVERSION "$VER: Anim 1.0 (26.12.12)"
@APPCOPYRIGHT "(C) Copyright 2012 by Andreas Falkenhahn"
@APPAUTHOR "Andreas Falkenhahn"
@APPDESCRIPTION "Demonstrates how to use Hollywood anims from MUI"


/*
** External data
*/
@ANIM 1, "Anim/Amy_Walks.anim"


/*
** We don't need our Hollywood display because we have a MUI window
*/
@DISPLAY {Width = 320, Height = 200, Hidden = True}


/*
** Draws the next anim frame
*/
Function p_AnimFunc()

	Local numframes = GetAttribute(#ANIM, 1, #ATTRNUMFRAMES)
	
	curframe = Wrap(curframe + 1, 1, numframes + 1)
	
	; draw to a backbuffer first because the anim can be moved freely around
	SelectBrush(1)
	Cls
	DisplayAnimFrame(1, offx, offy, curframe, {Width = swidth, Height = sheight})
	EndSelect
	
	; draw back buffer to display
	DisplayBrush(1, 0, 0)
	
EndFunction


/*
** Handle anim dragging
*/
Function p_MouseFunc()

	If IsLeftMouse() = True
	
		Local mx, my = MouseX(), MouseY()
		If (grabx = -1) And (graby = -1) Then grabx, graby = mx - offx, my - offy
		offx = mx - grabx
		offy = my - graby
		
	Else
		grabx, graby = -1, -1	
	EndIf
	
EndFunction


/*
** Handles all incoming events
*/	
Function p_EventFunc(msg)

	; has the size of our Hollywood class changed?
	If msg.Action = "SizeWindow"
      		swidth = msg.Width
		sheight = msg.Height
      		CreateBrush(1, swidth, sheight)
      		Return
   	EndIf
      
	; handle MUI Royale events
   	Switch msg.Class
  	Case "Window":
      		Switch msg.Attribute
      		Case "CloseRequest":
	 		End
      		EndSwitch
      		
   	Case "Button":
      		Switch msg.Attribute
      		Case "Pressed":
	 		Switch msg.ID
	 		Case "play":
	    			SetInterval(1, p_AnimFunc, 50)
	    			SetInterval(2, p_MouseFunc, 20)
			Case "stop":
				If HaveObject(#INTERVAL, 1) = True
	            			ClearInterval(1)
		    			ClearInterval(2)
		    		EndIf	
	 		EndSwitch
      		EndSwitch         
   	EndSwitch
   
EndFunction

; create backbuffer
swidth, sheight = 320, 200
CreateBrush(1, swidth, sheight)

; dynamically create MUI GUI from an external *.xml file definition
mui.CreateGUI(FileToString("Anim.xml"))

; listen to these events!
InstallEventHandler({MUIRoyale = p_EventFunc, SizeWindow = p_EventFunc})

; main loop!
Repeat
	WaitEvent
Forever 
