/****************************************************************
**                                                             **
** Name:        Image                                          **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        31.08.12                                       **
** Interpreter: Hollywood 5.2                                  **
** Licence:     Sample program                                 **
** Function:    Demonstrates how to use MUI from Hollywood     **
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (31.08.12)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Make sure we have at least Hollywood 5.2!
*/
@VERSION 5,2


/*
** This script requires the MUI Royale plugin
*/
@REQUIRE "MUIRoyale"


/*
** MUI apps require this information
*/
@APPTITLE "Image"
@APPVERSION "$VER: Image 1.0 (31.08.12)"
@APPCOPYRIGHT "(C) Copyright 2012 by Andreas Falkenhahn"
@APPAUTHOR "Andreas Falkenhahn"
@APPDESCRIPTION "Demonstrates how to use Hollywood brushes from MUI"


/*
** External data
*/
@BRUSH 1, "Image/hw70.png", {LoadAlpha = True}


/*
** We don't need our Hollywood display because we have a MUI window
*/
@DISPLAY {Hidden = True}


/*
** Handles all incoming events
*/
Function p_EventFunc(msg)

	Switch msg.action
	Case "MUIRoyale":
		Switch msg.attribute
		Case "CloseRequest":
			End
		Case "Pressed":
			mui.Set("image", "brush", db + 1)
			db = Not db
 		EndSwitch

	Case "HideWindow":
		mui.Set("app", "iconified", True)

	Case "ShowWindow":
		mui.Set("app", "iconified", False)
	EndSwitch

EndFunction

db = 1

CopyBrush(1, 2)
FlipBrush(2, False)

; dynamically create MUI GUI from an external *.xml file definition
mui.CreateGui(FileToString("Image.xml"))

; listen to these events!
InstallEventHandler({MUIRoyale = p_EventFunc, HideWindow = p_EventFunc, ShowWindow = p_EventFunc})

; main loop!
Repeat
	WaitEvent
Forever

