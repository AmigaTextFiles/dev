/****************************************************************
**                                                             **
** Name:        Virtual                                        **
** Author:      Andreas Falkenhahn, original by Stefan Stuntz  **
** Version:     1.0                                            **
** Date:        28.07.12                                       **
** Interpreter: Hollywood 5.2                                  **
** Licence:     Sample program                                 **
** Function:    Hollywood port of this demo program from the   **
**              MUI 3.8 development kit                        **
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (28.07.12)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Make sure we have at least Hollywood 5.2!
*/
@VERSION 5,2


/*
** This script requires the MUI Royale plugin
*/
@REQUIRE "MUIRoyale"


/*
** MUI apps require this information
*/
@APPTITLE "VirtualDemo"
@APPVERSION "$VER: VirtualDemo 19.5 (12.02.97)"
@APPCOPYRIGHT "©1993, Stefan Stuntz"
@APPAUTHOR "Stefan Stuntz"
@APPDESCRIPTION "Show virtual groups."


/*
** We don't need our Hollywood display because we have a MUI window
*/
@DISPLAY {Hidden = True}


/*
** Handles all incoming events
*/
Function p_EventFunc(msg)

	Switch msg.action
	Case "MUIRoyale":
		Switch msg.attribute
		Case "CloseRequest":
			End
		Case "Active":
			mui.Set("pgr", "activepage", msg.triggervalue)
		Case "Pressed":
			Switch msg.id
			Case "bt1":
				mui.Set("gr", "top", 0)
			Case "bt2":
				mui.Set("gr", "left", 0)
			Case "bt3":
				mui.Set("gr", "left", 9999)
			Case "bt4":
				mui.Set("gr", "top", 9999)
			EndSwitch
 		EndSwitch

	case "HideWindow":
		mui.Set("app", "iconified", True)

	case "ShowWindow":
		mui.Set("app", "iconified", False)
	EndSwitch

EndFunction

; dynamically create MUI GUI from an external *.xml file definition
mui.CreateGui(FileToString("Virtual.xml"))

; listen to these events!
InstallEventHandler({MUIRoyale = p_EventFunc, HideWindow = p_EventFunc, ShowWindow = p_EventFunc})

; main loop!
Repeat
	WaitEvent
Forever

