/****************************************************************
**                                                             **
** Name:        Dynamic2                                       **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        24.05.14                                       **
** Interpreter: Hollywood 5.2                                  **
** Licence:     Sample program                                 **
** Function:    Demonstrates dynamic creation of objects       **
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (24.05.14)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Make sure we have at least Hollywood 5.2!
*/
@VERSION 5,2


/*
** This script requires the MUI Royale plugin
*/
@REQUIRE "MUIRoyale"


/*
** MUI apps require this information
*/
@APPTITLE "Dynamic2"
@APPVERSION "$VER: Dynamic2 1.0 (24.05.14)"
@APPCOPYRIGHT "©2014, Andreas Falkenhahn"
@APPAUTHOR "Andreas Falkenhahn"
@APPDESCRIPTION "Demonstrates dynamic creation of objects."


/*
** We don't need our Hollywood display because we have a MUI window
*/
@DISPLAY {Hidden = True}


/*
** Handles all incoming events
*/
Function p_EventFunc(msg)

	Switch msg.action
	Case "MUIRoyale":
		Switch msg.attribute
		Case "CloseRequest":
			End
		Case "Pressed":
			If msg.id = "add"
				mui.CreateObject("<button id=\"button" .. count .. "\" notify=\"pressed\">Button " .. count .. "</button>")
				mui.DoMethod("container", "initchange")
				If count = 1 Then mui.DoMethod("container", "remove", "blankspace")
				mui.DoMethod("container", "addtail", "button" .. count)
				mui.DoMethod("container", "exitchange", false)
				count = count + 1
			ElseIf (msg.id = "rem") And (count > 1)
				count = count - 1
				mui.DoMethod("container", "initchange")
				mui.DoMethod("container", "remove", "button" .. count)
				If count = 1 Then mui.DoMethod("container", "addtail", "blankspace")
				mui.DoMethod("container", "exitchange", false)
				mui.FreeObject("button" .. count)
			ElseIf msg.id = "dump"
				Local t = mui.Get("container", "childlist")
				For Local k = 0 To ListItems(t) - 1 Do DebugPrint(t[k].id, t[k].class, t[k].muiclass)
			ElseIf LeftStr(msg.id, 6) = "button"
				mui.Request("Dynamic 2", "You've pressed button " .. UnrightStr(msg.id, 6) .. "!", "OK")
			EndIf
 		EndSwitch

	case "HideWindow":
		mui.Set("app", "iconified", True)

	case "ShowWindow":
		mui.Set("app", "iconified", False)
	EndSwitch

EndFunction

count = 1

; dynamically create MUI GUI from an external *.xml file definition
mui.CreateGui(FileToString("Dynamic2.xml"))

; listen to these events!
InstallEventHandler({MUIRoyale = p_EventFunc, HideWindow = p_EventFunc, ShowWindow = p_EventFunc})

; main loop!
Repeat
	WaitEvent
Forever

