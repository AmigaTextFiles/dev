/****************************************************************
**                                                             **
** Name:        Dynamic4                                       **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        25.05.14                                       **
** Interpreter: Hollywood 5.2                                  **
** Licence:     Sample program                                 **
** Function:    Demonstrates dynamic creation of menu items    **
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (24.05.14)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Make sure we have at least Hollywood 5.2!
*/
@VERSION 5,2


/*
** This script requires the MUI Royale plugin
*/
@REQUIRE "MUIRoyale"


/*
** MUI apps require this information
*/
@APPTITLE "Dynamic4"
@APPVERSION "$VER: Dynamic4 1.0 (25.05.14)"
@APPCOPYRIGHT "©2014, Andreas Falkenhahn"
@APPAUTHOR "Andreas Falkenhahn"
@APPDESCRIPTION "Demonstrates dynamic creation of menu items."


/*
** We don't need our Hollywood display because we have a MUI window
*/
@DISPLAY {Hidden = True}


/*
** Handles all incoming events
*/
Function p_EventFunc(msg)

	Switch msg.action
	Case "MUIRoyale":
		Switch msg.attribute
		Case "CloseRequest":
			End
		Case "Pressed":
			If msg.id = "addsingle"
				mui.CreateObject("<menuitem id=\"single" .. count .. "\" notify=\"selected\">Item " .. count .. "</menuitem>")
				mui.DoMethod("strip", "insert", "single" .. count, IIf(count = 1, "men_separator", "single" .. count - 1))
				If count = 1 Then mui.DoMethod("strip", "insert", "separator", "single1")
				count = count + 1
			ElseIf (msg.id = "remsingle") And (count > 1)
				count = count - 1
				mui.DoMethod("strip", "remove", "single" .. count)
				mui.FreeObject("single" .. count)
				If count = 1 Then mui.DoMethod("strip", "remove", "separator")
			ElseIf msg.id = "addroot"
				mui.CreateObject(ReplaceStr(ReplaceStr(ReplaceStr(root$, "%COUNT", rcount), "%ID", "root"), "%TAG", "menu"))
				mui.DoMethod("strip", "addtail", "root" .. rcount)
				rcount = rcount + 1
 			ElseIf (msg.id = "remroot") And (rcount > 1)
				rcount = rcount - 1
				mui.DoMethod("strip", "remove", "root" .. rcount)
				mui.FreeObject("root" .. rcount)
			ElseIf msg.id = "addsub"
				mui.CreateObject(ReplaceStr(ReplaceStr(ReplaceStr(root$, "%COUNT", scount), "%ID", "sub"), "%TAG", "submenu"))
				mui.DoMethod("projectmenu", "addhead", "sub" .. scount)
				If scount = 1 Then mui.DoMethod("strip", "insert", "separator2", "sub1")
				scount = scount + 1
			ElseIf (msg.id = "remsub") And (scount > 1)
				scount = scount - 1
				mui.DoMethod("strip", "remove", "sub" .. scount)
				mui.FreeObject("sub" .. scount)
				If scount = 1 Then mui.DoMethod("strip", "remove", "separator2")
 			EndIf
		Case "Selected":
			If msg.id = "men_quit"
				End
			ElseIf msg.id = "men_about"
				MUI.Request("Menus", "Some little about window.", "OK")
			Else
				MUI.Request("Menus", "Your selection: " .. mui.Get(msg.id, "title"), "OK")
			EndIf
		EndSwitch

	case "HideWindow":
		mui.Set("app", "iconified", True)

	case "ShowWindow":
		mui.Set("app", "iconified", False)
	EndSwitch

EndFunction

count = 1
scount = 1
rcount = 1

; we need to use <submenu> for compatibility with MUI 3.8 because 3.8 requires sub menus to be
; derived from MUIC_Menuitem whereas 4.0 also accepts MUIC_Menu
; it is *not* necessary to use <submenu> when defining complete menu trees because in that case
; MUI Royale is clever enough to do all arrangements on its own; for isolated objects, however,
; we need to specify whether they should be submenus (i.e. <submenu>) or top-level menus (i.e. <menu>)
root$ = [[
<%TAG title="Menu %COUNT" id="%ID%COUNT">
	<item id="%IDa%COUNT" notify="selected">One</item>
	<item id="%IDb%COUNT" notify="selected">Two</item>
	<item id="%IDc%COUNT" notify="selected">Three</item>
	<item id="%IDd%COUNT" notify="selected">From New York</item>
	<item id="%IDe%COUNT" notify="selected">To Germany</item>
</%TAG>
]]

; dynamically create MUI GUI from an external *.xml file definition
mui.CreateGui(FileToString("Dynamic4.xml"))

; listen to these events!
InstallEventHandler({MUIRoyale = p_EventFunc, HideWindow = p_EventFunc, ShowWindow = p_EventFunc})

; main loop!
Repeat
	WaitEvent
Forever

