/****************************************************************
**                                                             **
** Name:        Class1                                         **
** Author:      Andreas Falkenhahn, original by Stefan Stuntz  **
** Version:     1.0                                            **
** Date:        07.08.12                                       **
** Interpreter: Hollywood 5.2                                  **
** Licence:     Sample program                                 **
** Function:    Hollywood port of this demo program from the   **
**              MUI 3.8 development kit                        **
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (07.08.12)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Make sure we have at least Hollywood 5.2!
*/
@VERSION 5,2


/*
** This script requires the MUI Royale plugin
*/
@REQUIRE "MUIRoyale"


/*
** MUI apps require this information
*/
@APPTITLE "Class1"
@APPVERSION "$VER: Class1 19.5 (12.02.97)"
@APPCOPYRIGHT "©1993, Stefan Stuntz"
@APPAUTHOR "Stefan Stuntz"
@APPDESCRIPTION "Demonstrate the use of custom classes."


/*
** Our hidden Hollywood display acts as our custom class
*/
@DISPLAY {Width = 120, Height = 90, Hidden = True}


/*
** Draw our custom class
*/
Function p_Draw()

	Local w = GetAttribute(#DISPLAY, 0, #ATTRWIDTH)
	Local h = GetAttribute(#DISPLAY, 0, #ATTRHEIGHT)

	Local tmp = CreateBrush(Nil, w, h, backcolor)

	SelectBrush(tmp)

	For Local k = 0 To w - 1 Step 5
		Line(0, h - 1, k, 0, frontcolor)
		Line(w - 1, h - 1, k, 0, frontcolor)
	Next

	EndSelect

	DisplayBrush(tmp, 0, 0)

EndFunction

/*
** Handles all incoming events
*/
Function p_EventFunc(msg)

	Switch msg.action
	Case "MUIRoyale":
		Switch msg.attribute
		Case "CloseRequest":
			End
 		EndSwitch

	Case "HideWindow":
		mui.Set("app", "iconified", True)

	Case "ShowWindow":
		mui.Set("app", "iconified", False)

	Case "SizeWindow":
		p_Draw()
	EndSwitch

EndFunction

backcolor = GetPen(0, True)
frontcolor = #BLACK

CreateBGPic(2, 120, 90, backcolor)
DisplayBGPic(2)

p_Draw()

; dynamically create MUI GUI from an external *.xml file definition
mui.CreateGui(FileToString("Class1.xml"))

; listen to these events!
InstallEventHandler({MUIRoyale = p_EventFunc, HideWindow = p_EventFunc, ShowWindow = p_EventFunc, SizeWindow = p_EventFunc})

; main loop!
Repeat
	WaitEvent
Forever

