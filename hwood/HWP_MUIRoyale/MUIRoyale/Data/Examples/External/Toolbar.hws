/****************************************************************
**                                                             **
** Name:        Toolbar                                        **
** Author:      Andreas Falkenhahn, original by A. Ranieri     **
** Version:     1.0                                            **
** Date:        24.08.12                                       **
** Interpreter: Hollywood 5.2                                  **
** Licence:     Sample program                                 **
** Function:    Hollywood port of this demo program from the   **
**              TheBar MCC development kit                     **
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (24.08.12)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Make sure we have at least Hollywood 5.2!
*/
@VERSION 5,2


/*
** This script requires the MUI Royale plugin
*/
@REQUIRE "MUIRoyale"


/*
** MUI apps require this information
*/
@APPTITLE "TheBar Demo1"
@APPVERSION "$VER: TheBarDemo1 1.0 (24.06.2003)"
@APPCOPYRIGHT "Copyright 2003 by Alfonso Ranieri"
@APPAUTHOR "Alfonso Ranieri <alforan@tin.it>"
@APPDESCRIPTION "TheBar example"


/*
** Toolbar images
*/
@BRUSH 100, "Toolbar/Read.toolbar"
@BRUSH 101, "Toolbar/Read_S.toolbar"
@BRUSH 102, "Toolbar/Read_G.toolbar"


/*
** We don't need our Hollywood display because we have a MUI window
*/
@DISPLAY {Hidden = True}


/*
** Handles all incoming events
*/
Function p_EventFunc(msg)

	Switch msg.action
	Case "MUIRoyale":
		Switch msg.attribute
		Case "CloseRequest":
			End
		Case "Pressed":
			Local appearanceT = {"TextGfx", "Gfx", "Text"}
			Local labelPosT = {"Bottom", "Top", "Right", "Left"}
			Local appearanceV = mui.Get("appearance", "active")
			Local labelPosV = mui.Get("labelpos", "active")
			Local borderlessV = mui.Get("borderless", "selected")
			Local sunnyV = mui.Get("sunny", "selected")
			Local raisedV = mui.Get("raised", "selected")
			Local scaledV = mui.Get("scaled", "selected")

			mui.Set("tb", "viewmode", appearanceT[appearanceV], "labelpos", labelPosT[labelPosV], "borderless", borderlessV, "sunny", sunnyV, "raised", raisedV, "scaled", scaledV)
		EndSwitch

	case "HideWindow":
		mui.Set("app", "iconified", True)

	case "ShowWindow":
		mui.Set("app", "iconified", False)
	EndSwitch

EndFunction

Local tb = {100, 101, 102}
k = 1

; create single images from toolbar
For Local i = 0 To 2
	For Local j = 0 To 10
		CreateBrush(k, 24, 18)
		SelectBrush(k)
		DisplayBrushPart(tb[i], j * 24 + j, 0, 0, 0, 24, 18)
		EndSelect
		SetBrushTransparency(k, $AAAAAA)
		k = k + 1
	Next
Next

; dynamically create MUI GUI from an external *.xml file definition
mui.CreateGui(FileToString("Toolbar.xml"))

; listen to these events!
InstallEventHandler({MUIRoyale = p_EventFunc, HideWindow = p_EventFunc, ShowWindow = p_EventFunc})

; main loop!
Repeat
	WaitEvent
Forever

