/****************************************************************
**                                                             **
** Name:        TextEditor                                     **
** Author:      Andreas Falkenhahn, original by Allan Odgaard  **
** Version:     1.1                                            **
** Date:        29.06.14                                       **
** Interpreter: Hollywood 5.2                                  **
** Licence:     Sample program                                 **
** Function:    Hollywood port of this demo program from the   **
**              TheBar MCC development kit                     **
**                                                             **
** History:                                                    **
**                                                             **
** 1.1: (29.06.14)                                             **
**                                                             **
** - now supports TextEditor.Color                             **
**                                                             **
** 1.0: (24.08.12)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Make sure we have at least Hollywood 5.2!
*/
@VERSION 5,2


/*
** This script requires the MUI Royale plugin
*/
@REQUIRE "MUIRoyale"


/*
** MUI apps require this information
*/
@APPTITLE "TextEditor-Demo"
@APPVERSION "$VER: TextEditor-Demo V1.0 (6-Aug-97)"
@APPCOPYRIGHT "®1997 Allan Odgaard"
@APPAUTHOR "Allan Odgaard"
@APPDESCRIPTION "TextEditor.mcc demonstration program"


/*
** Toolbar images
*/
@BRUSH 100, "TextEditor/Toolstrip.ilbm"
@BRUSH 101, "TextEditor/Toolstrip_G.ilbm"


/*
** We don't need our Hollywood display because we have a MUI window
*/
@DISPLAY {Hidden = True}


/*
** Handles all incoming events
*/
Function p_EventFunc(msg)

	Switch msg.action
	Case "MUIRoyale":
		Switch msg.attribute
		Case "CloseRequest":
			If mui.Get("editor", "haschanged") = True
				Local r = mui.Request("Warning", "\27cText is modified. Save it?", "_Proceed|*_Save|_Cancel")
				If r = 0 Then Break
				If r = 2
					Local f$ = FileRequest("Select", "txt", #REQ_SAVEMODE)
					If f$ = "" Then Break

					StringToFile(mui.Get("editor", "contents"), f$)
				EndIf
			EndIf
			End
		Case "AreaMarked":
			mui.Set("tb_cut", "disabled", Not msg.triggervalue)
			mui.Set("tb_copy", "disabled", Not msg.triggervalue)
		Case "UndoAvailable":
			mui.Set("tb_undo", "disabled", Not msg.triggervalue)
		Case "Selected"
			If msg.id = "tb_color"
				mui.Set("editor", "nonotify", True, "color", IIf(msg.triggervalue = True, #RED, #BLACK))
			ElseIf MidStr(msg.muiuserdata, 0, 1) = "_"
				mui.Set("editor", "nonotify", True, "align", UnrightStr(msg.muiuserdata, 1))
			Else
				mui.Set("editor", "nonotify", True, msg.muiuserdata, msg.triggervalue)
			EndIf
		Case "Color":
			mui.Set("tb_color", "selected", msg.triggervalue)
		Case "Align":
			mui.Set("tb_" .. mui.Get("editor", "align"), "selected", True)
		Case "Pressed":
			If msg.id = "tb_load"
				Local f$ = FileRequest("Select", "#?")
				If f$ <> ""
					mui.Set("editor", "contents", FileToString(f$), "haschanged", false)
				EndIf
			Else
				mui.DoMethod("editor", UnrightStr(msg.id, 3))
				If msg.id = "tb_clear" Then mui.Set("editor", "nonotify", true, "haschanged", false)
			EndIf
		Case "MouseObject":
			If msg.triggervalue = "(none)"
				mui.Set("status", "contents", "")
			Else
				mui.Set("status", "contents", mui.Get(msg.triggervalue, "shorthelp"))
			EndIf
		Default:
			If msg.attribute = "StyleBold" Or msg.attribute = "StyleItalic" Or msg.attribute = "StyleUnderline"
				mui.Set(msg.notifydata, "selected", msg.triggervalue)
			EndIf
 		EndSwitch

	case "HideWindow":
		mui.Set("app", "iconified", True)

	case "ShowWindow":
		mui.Set("app", "iconified", False)
	EndSwitch

EndFunction

Local tb = {100, 101}
k = 1

; create single images from toolbar
For Local i = 0 To 1
	For Local j = 0 To 13
		CreateBrush(k, 24, 18)
		SelectBrush(k)
		DisplayBrushPart(tb[i], j * 24 + j, 0, 0, 0, 24, 18)
		EndSelect
		SetBrushTransparency(k, IIf(i = 1, $959595, $AFAFAF))
		k = k + 1
	Next
Next

; dynamically create MUI GUI from an external *.xml file definition
mui.CreateGui(FileToString("TextEditor.xml"))
mui.Set("win", "activeobject", "editor")

; listen to these events!
InstallEventHandler({MUIRoyale = p_EventFunc, HideWindow = p_EventFunc, ShowWindow = p_EventFunc})

; main loop!
Repeat
	WaitEvent
Forever

