/****************************************************************
**                                                             **
** Name:        Listtree                                       **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        30.12.12                                       **
** Interpreter: Hollywood 5.2                                  **
** Licence:     Sample program                                 **
** Function:    Demonstrates Listtree.mcc                      **
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (30.12.12)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Make sure we have at least Hollywood 5.2!
*/
@VERSION 5,2


/*
** This script requires the MUI Royale plugin
*/
@REQUIRE "MUIRoyale"


/*
** MUI apps require this information
*/
@APPTITLE "Listtree"
@APPVERSION "$VER: Listtree 1.0 (30.12.12)"
@APPCOPYRIGHT "©2012, Andreas Falkenhahn"
@APPAUTHOR "Andreas Falkenhahn"
@APPDESCRIPTION "Shows listtree MCC."


/*
** We don't need our Hollywood display because we have a MUI window
*/
@DISPLAY {Hidden = True}


/*
** Handles all incoming events
*/
Function p_EventFunc(msg)

	Switch msg.action
	Case "MUIRoyale":
		Switch msg.attribute
		Case "CloseRequest":
			End
		Case "Pressed":
			Switch msg.id
			Case "open"
				mui.DoMethod("listtree", "open", "root", "all")
			Case "close"
				mui.DoMethod("listtree", "close", "root", "all")	
			EndSwitch
 		EndSwitch

	case "HideWindow":
		mui.Set("app", "iconified", True)

	case "ShowWindow":
		mui.Set("app", "iconified", False)
	EndSwitch

EndFunction

; dynamically create MUI GUI from an external *.xml file definition
mui.CreateGui(FileToString("Listtree.xml"))

; listen to these events!
InstallEventHandler({MUIRoyale = p_EventFunc, HideWindow = p_EventFunc, ShowWindow = p_EventFunc})

; main loop!
Repeat
	WaitEvent
Forever

