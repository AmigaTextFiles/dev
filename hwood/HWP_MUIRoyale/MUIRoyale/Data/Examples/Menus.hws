/****************************************************************
**                                                             **
** Name:        Menus                                          **
** Author:      Andreas Falkenhahn, original by Stefan Stuntz  **
** Version:     1.0                                            **
** Date:        29.07.12                                       **
** Interpreter: Hollywood 5.2                                  **
** Licence:     Sample program                                 **
** Function:    Hollywood port of this demo program from the   **
**              MUI 3.8 development kit                        **
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (29.07.12)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Make sure we have at least Hollywood 5.2!
*/
@VERSION 5,2


/*
** This script requires the MUI Royale plugin
*/
@REQUIRE "MUIRoyale"


/*
** MUI apps require this information
*/
@APPTITLE "Menus"
@APPVERSION "$VER: Menus 19.5 (12.02.97)"
@APPCOPYRIGHT "©1994 by Stefan Stuntz"
@APPAUTHOR "Stefan Stuntz"
@APPDESCRIPTION "Demonstrate MUI's menu classes."


/*
** We don't need our Hollywood display because we have a MUI window
*/
@DISPLAY {Hidden = True}


/*
** Handles all incoming events
*/
Function p_EventFunc(msg)

	Switch msg.action
	Case "MUIRoyale":
		Switch msg.attribute
		Case "CloseRequest":
			End
		Case "Active":
			Switch msg.id
			Case "radio":
				mui.Set("men_hw" .. msg.triggervalue + 1, "selected", True)
			Default:
				mui.Set(msg.muiuserdata, "disabled", Not msg.triggervalue)
			EndSwitch
		Case "Selected":
			Switch msg.id
			Case "men_about":
				MUI.Request("Menus", "Some little about window.", "OK")
			Case "men_quit":
				End
			Default:
				If LeftStr(msg.id, 2) = "cm"
					mui.Set(msg.muiuserdata, "nonotify", true, "selected", msg.triggervalue)
				ElseIf msg.notifydata = "radio"
					mui.Set(msg.notifydata, "active", msg.muiuserdata)
				Else
					mui.Set(msg.muiuserdata, "nonotify", true, "selected", mui.Get(msg.id, "selected"))
				EndIf
			EndSwitch
		Case "Pressed":
			Switch msg.id
			Case "btadd":
				mui.DoMethod("strip", "insert", "menumisc", "men_edit")
				mui.Set("btadd", "disabled", True)
				mui.Set("btrem", "disabled", False)
			Case "btrem":
				mui.DoMethod("strip", "remove", "menumisc")
				mui.Set("btadd", "disabled", False)
				mui.Set("btrem", "disabled", True)
			Case "bttog":
				Local x$ = mui.Get("menumisc", "title")
				mui.Set("menumisc", "title", IIf(x$ = "Misc", "Miscodil", "Misc"))
				Local x$ = mui.Get("possi", "title")
				mui.Set("possi", "title", IIf(x$ = "Possible", "elbissoP", "Possible"))
			EndSwitch
 		EndSwitch

	case "HideWindow":
		mui.Set("app", "iconified", True)

	case "ShowWindow":
		mui.Set("app", "iconified", False)
	EndSwitch

EndFunction

; dynamically create MUI GUI from an external *.xml file definition
mui.CreateGui(FileToString("Menus.xml"))

; listen to these events!
InstallEventHandler({MUIRoyale = p_EventFunc, HideWindow = p_EventFunc, ShowWindow = p_EventFunc})

; main loop!
Repeat
	WaitEvent
Forever

