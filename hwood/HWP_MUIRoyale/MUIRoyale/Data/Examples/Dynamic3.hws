/****************************************************************
**                                                             **
** Name:        Dynamic3                                       **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        24.05.14                                       **
** Interpreter: Hollywood 5.2                                  **
** Licence:     Sample program                                 **
** Function:    Demonstrates dynamic creation of objects       **
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (24.05.14)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Make sure we have at least Hollywood 5.2!
*/
@VERSION 5,2


/*
** This script requires the MUI Royale plugin
*/
@REQUIRE "MUIRoyale"


/*
** MUI apps require this information
*/
@APPTITLE "Dynamic3"
@APPVERSION "$VER: Dynamic3 1.0 (24.05.14)"
@APPCOPYRIGHT "©2014, Andreas Falkenhahn"
@APPAUTHOR "Andreas Falkenhahn"
@APPDESCRIPTION "Demonstrates dynamic page groups."


/*
** We don't need our Hollywood display because we have a MUI window
*/
@DISPLAY {Hidden = True}


/*
** Closes a tab
*/
Function p_CloseTab(id$)

	mui.DoMethod("register", "closepage", id$)
	mui.FreeObject(id$)
	If mui.Get("register", "pages") = 0 Then count = 0

EndFunction

/*
** Handles all incoming events
*/
Function p_EventFunc(msg)

	Switch msg.action
	Case "MUIRoyale":
		Switch msg.attribute
		Case "CloseRequest":
			If msg.id = "win" Then End
			p_CloseTab(msg.triggervalue)
		Case "Pressed":
			If msg.id = "add"
				count = count + 1
				mui.CreateObject(ReplaceStr(groupdef$, "%COUNT", count))
				mui.DoMethod("register", "addpage", "page" .. count)
			ElseIf msg.id = "rem"
				If mui.Get("register", "pages") >= 1
					Local id$ = mui.DoMethod("register", "getpageid", "active")
					p_CloseTab(id$)
 				EndIf
			ElseIf LeftStr(msg.id, 6) = "button"
				mui.Request("Dynamic3", "You clicked " .. msg.id .. " on " .. mui.DoMethod("register", "getpageid", "active"), "OK")
			EndIf
 		EndSwitch

	case "HideWindow":
		mui.Set("app", "iconified", True)

	case "ShowWindow":
		mui.Set("app", "iconified", False)
	EndSwitch

EndFunction

If mui.IsVersion4() = False
	SystemRequest("Dynamic3", "This examples requires MUI 4.0!", "OK")
	End
EndIf

count = 1

groupdef$ = [[
<vgroup title="Page %COUNT" id="page%COUNT">
<texteditor/>
<button id="button%COUNT" notify="pressed">Click me</button>
</vgroup>
]]

; dynamically create MUI GUI from an external *.xml file definition
mui.CreateGui(ReplaceStr(FileToString("Dynamic3.xml"), "%GROUP", ReplaceStr(groupdef$, "%COUNT", count)))

; listen to these events!
InstallEventHandler({MUIRoyale = p_EventFunc, HideWindow = p_EventFunc, ShowWindow = p_EventFunc})

; main loop!
Repeat
	WaitEvent
Forever

