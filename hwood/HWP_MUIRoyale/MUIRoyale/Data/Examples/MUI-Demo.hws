/****************************************************************
**                                                             **
** Name:        AppWindow                                      **
** Author:      Andreas Falkenhahn, original by Stefan Stuntz  **
** Version:     1.0                                            **
** Date:        27.07.12                                       **
** Interpreter: Hollywood 5.2                                  **
** Licence:     Sample program                                 **
** Function:    Hollywood port of this demo program from the   **
**              MUI 3.8 development kit                        **
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (27.07.12)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Make sure we have at least Hollywood 5.2!
*/
@VERSION 5,2


/*
** This script requires the MUI Royale plugin
*/
@REQUIRE "MUIRoyale"


/*
** MUI apps require this information
*/
@APPTITLE "MUI-Demo"
@APPVERSION "$VER: MUI-Demo 19.6 (12.02.97)"
@APPCOPYRIGHT "Copyright ©1992-95, Stefan Stuntz"
@APPAUTHOR "Stefan Stuntz"
@APPDESCRIPTION "Demonstrate the features of MUI."


/*
** We don't need our Hollywood display because we have a MUI window
*/
@DISPLAY {Hidden = True}


/*
** Handles all incoming events
*/
Function p_EventFunc(msg)

	Switch msg.action
	Case "MUIRoyale":
		Switch msg.attribute
		Case "CloseRequest":
			Switch msg.id
			Case "wi_master":
				End
			Default:
				mui.Set(msg.id, "open", False)
			EndSwitch

		Case "Selected":
			Switch msg.id
			Case "mn_quit":
				End
			Case "mn_about":
				mui.Request("MUI-Demo", "MUI-Demo\n© 1992-95 by Stefan Stuntz", "OK")
			Case "mn_aboutmui":
				mui.DoMethod("app", "aboutmui")
			Case "mn_aboutmuiroyale":
				mui.DoMethod("app", "aboutmuiroyale")
			Case "mn_muisettings":
				mui.DoMethod("app", "openconfigwindow")
			EndSwitch

		Case "Pressed":
			Switch msg.id
 			Case "bt_quit":
				End
			Default:
				mui.Set(msg.muiuserdata, "open", True)
			EndSwitch

		Case "Active":
			Switch msg.id
			Case "lv_brian":
				mui.Set("st_brian", "contents", mui.DoMethod("lv_brian", "getentry", "active"))
 			Case "mt_computer":
				mui.Set("cy_computer", "active", msg.triggervalue)
				mui.Set("lv_computer", "active", msg.triggervalue)
			Default:
				mui.Set(msg.muiuserdata, "active", msg.triggervalue)
 			EndSwitch

		Case "First":
			Switch msg.id
			Case "pr_propa":
				mui.Set("pr_proph", "first", msg.triggervalue)
				mui.Set("pr_propv", "first", msg.triggervalue)
				mui.Set("ga_gauge2", "current", msg.triggervalue)
			Case "pr_proph":
				mui.Set("pr_propl", "first", msg.triggervalue)
				mui.Set("pr_propr", "first", msg.triggervalue)
			Case "pr_propv":
				mui.Set("pr_propt", "first", msg.triggervalue)
				mui.Set("pr_propb", "first", msg.triggervalue)
			EndSwitch

		Case "Current":
			Switch msg.id
			Case "ga_gauge2":
				mui.Set("ga_gauge1", "current", msg.triggervalue)
				mui.Set("ga_gauge3", "current", msg.triggervalue)
			EndSwitch

		Case "DoubleClick":
			Switch msg.id
			Case "lv_volumes":
				mui.Set("lv_directory", "directory", mui.DoMethod(msg.id, "getentry", "active"))
			EndSwitch
		EndSwitch

	case "HideWindow":
		mui.Set("app", "iconified", True)

	case "ShowWindow":
		mui.Set("app", "iconified", False)
	EndSwitch

EndFunction

; dynamically create MUI GUI from an external *.xml file definition
mui.CreateGui(FileToString("MUI-Demo.xml"))

LVT_Brian = {"Cheer up, Brian. You know what they say.",
"Some things in life are bad,",
"They can really make you mad.",
"Other things just make you swear and curse.",
"When you're chewing on life's grissle,",
"Don't grumble, give a whistle.",
"And this'll help things turn out for the best,",
"And...",
"",
"Always look on the bright side of life",
"Always look on the light side of life",
"",
"If life seems jolly rotten,",
"There's something you've forgotten,",
"And that's to laugh, and smile, and dance, and sing.",
"When you're feeling in the dumps,",
"Don't be silly chumps,",
"Just purse your lips and whistle, that's the thing.",
"And...",
"",
"Always look on the bright side of life, come on!",
"Always look on the right side of life",
"",
"For life is quite absurd,",
"And death's the final word.",
"You must always face the curtain with a bow.",
"Forget about your sin,",
"Give the audience a grin.",
"Enjoy it, it's your last chance anyhow,",
"So...",
"",
"Always look on the bright side of death",
"Just before you draw your terminal breath.",
"",
"Life's a piece of shit,",
"When you look at it.",
"Life's a laugh, and death's a joke, it's true.",
"You'll see it's all a show,",
"Keep 'em laughing as you go,",
"Just remember that the last laugh is on you.",
"And...",
"",
"Always look on the bright side of life !",
"",
"..."}

For Local k = 0 To ListItems(LVT_Brian) - 1 Do mui.DoMethod("lv_brian", "insert", "bottom", LVT_Brian[k])
mui.Set("lv_brian", "active", 0)

; listen to these events!
InstallEventHandler({MUIRoyale = p_EventFunc, HideWindow = p_EventFunc, ShowWindow = p_EventFunc})

; main loop!
Repeat
	WaitEvent
Forever

