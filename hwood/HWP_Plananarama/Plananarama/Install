;
; $VER: Plananarama-Setup 2.2 (09.12.23)
;
; Done by Andreas Falkenhahn
;

(message (cat "\nWelcome to the Setup utility for\n\n"
	"Plananarama 2.2\n\n"
	"- The Planar Graphics Engine for Hollywood -\n\n"
	"Copyright (C) 2014-2023 by Andreas Falkenhahn\n\n"
	"http://www.hollywood-mal.com/"
))

(welcome)

(set basepath
	(askdir
		(prompt  "Where do you want to have Plananarama installed? (a drawer will be created)")
		(help    @askdir-help)
		(default "SYS:")
	)
)

(set sstr (substr basepath (- (strlen basepath) 1) 1))
(if (or (= sstr "/") (= sstr ":"))
	(set destpath (cat basepath "Plananarama"))
	(set destpath (cat basepath "/Plananarama"))
)

(set @default-dest destpath)

(set defaultplat 0)

(set whichver
	(askchoice
		(prompt  "Please select your platform")
		(help    @askoptions-help)
		(choices "AmigaOS 3 (m68k)")		
		(default defaultplat)
	)
)

(set #platform "m68k-amigaos")

(makedir destpath)
(makedir "LIBS:Hollywood")

(copylib
	(source (cat "Platforms/" #platform "/plananarama.hwp"))
	(dest   "LIBS:Hollywood")
	(prompt "plananarama.hwp")
	(help @copylib-help)
	(optional "nofail" "force" "askuser")
	(confirm)
)

(copyfiles
	(source  "Data.icon")
	(dest    basepath)
	(newname "Plananarama.info")
	(optional force oknodelete)
)

(copyfiles
	(source "Data")
	(dest   destpath)
	(all)
	(optional "nofail" "force" "askuser")
)

(if (exists "Hollywood:GUI" (noreq)) (

	(makedir "Hollywood:Examples/Plananarama")

	(copyfiles
		(source "Data/Examples")
		(dest   "Hollywood:Examples/Plananarama")
		(all)
		(optional "nofail" "force" "askuser")
	)
))

(if (exists "Hollywood:LinkerPlugins" (noreq)) (

	(copyfiles
		(source "Platforms")
		(dest   "Hollywood:LinkerPlugins")
		(all)
		(optional "nofail" "force" "askuser")
	)
))

(set #cmdline (cat "Assign Plananarama: \"" destpath "\""))

(startup
	(prompt  "\nAn assign has to be inserted in your user-startup. Please click Continue.")
	(help    @startup-help)
	"Plananarama"
	(command #cmdline)
	(confirm)
)

(run #cmdline)

(message (cat "\nPlananarama comes with some ready-to-run examples.\n\n"
	"The examples have been integrated into your Hollywood GUI (only "
	"if Hollywood is installed on this system). Now that you have installed "
	"Plananarama, there will be a new Plananarama tab in the Hollywood GUI "
	"which allows you to conveniently launch the example scripts that come "
	"with Plananarama."))

(message (cat "\nIMPORTANT INFORMATION:\n\nIF YOU UPGRADED FROM A PREVIOUS "
	"VERSION OF PLANANARAMA, YOU NEED TO REBOOT YOUR SYSTEM NOW TO COMPLETE "
	"THE INSTALLATION."))

(exit)
