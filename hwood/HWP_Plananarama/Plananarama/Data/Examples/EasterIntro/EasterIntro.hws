/****************************************************************
**                                                             **
** Name:        EasterIntro                                    **
** Author:      Andreas Falkenhahn                             **
** Version:     2.0                                            **
** Date:        12.02.22                                       **
** Interpreter: Hollywood 2.0                                  **
** Licence:     Sample program for Hollywood                   **
** Function:    Hollywood port of an old intro written by me   **
**              back in April 1996                             **
**                                                             **
** Contact:     Andreas Falkenhahn                             **
**              An der Trift 2                                 **
**              36132 Eiterfeld-Arzell                         **
**              GERMANY                                        **
** E-Mail:      andreas@airsoftsoftwair.de                     **
** Internet:    http://www.airsoftsoftwair.com/                **
**                                                             **
** Notes:       -                                              **
**                                                             **
** History:                                                    **
**                                                             **
** 2.0: (12.02.22)                                             **
**                                                             **
** - uses Plananarama in palette mode now                      **
**                                                             **
** 1.3: (19.04.05)                                             **
**                                                             **
** - does not use layers any more but DisplaySprite()          **
** - adapted for Hollywood 2.0                                 **
**                                                             **
** 1.2: (08.04.04)                                             **
**                                                             **
** - VWait inserted                                            **
**                                                             **
** 1.1: (04.04.03)                                             **
**                                                             **
** - Hollywood version                                         **
**                                                             **
** 1.0: (xx.04.96)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Hollywood 9.0 is required
*/
@VERSION 9,0      


/*
** Put Plananarama in palette mode and set the sprite resolution to lores
*/
@REQUIRE "plananarama", {PaletteMode = True, SpriteResolution = 1}


/*
** Create a 320x256x5 display
*/
@DISPLAY {Width = 320, Height = 256, Palette = #PALETTE_OCS}


/*
** External data
*/
@BGPIC  2, "HappyEaster.iff", {LoadPalette = True}
@BRUSH  1, "End.iff", {LoadPalette = True}
@BRUSH  2, "Hase.iff", {LoadPalette = True}
@MUSIC 1, "Patzga.mod"

Function p_MoveSprite()

        sx = sx + xv
        If sx < 0
                xv = -xv
                sx = 0
        ElseIf sx > 256
                xv = -xv
                sx = 256
        EndIf

        yv = yv + 1
        sy = sy + yv

        If sy > 194
                yv = -10 - Rnd(10)
                sy = 194
        EndIf

	planar.MoveSprite(1, sx, sy)
	planar.MoveSprite(2, sx + 64, sy)
	
EndFunction

Function p_MainLoop()

        x = IIf(k > 5, 15, 27)

        y = k * 16
        x = x * 8

	SetDrawPen(c)
        TextOut(x, y, text$[k])

        c = c + 1
        If c > 15 Then c = 1

        k = k + 1
        If k > 13 Then k = 1

        p_MoveSprite()

        quit = IsLeftMouse()

EndFunction

SetPaletteMode(#PALETTEMODE_PEN)
HidePointer()

; duplicate palette entries 0-15 in color registers 16-31 in our BGPic
; this is necessary for our 16 color hardware sprite
SelectPalette(#BGPIC, 2)
SetDepth(5)
For Local k = 0 To 15 Do SetPen(16 + k, GetPen(k))
SelectPalette(#DISPLAY, 1)
	
DisplayBGPic(2)
	
; since our rabbit sprite is wider than 64 pixels, we must break it down
; to two separate hardware sprites which will then be displayed next to
; each other --> those two sprites are created here!
CreateBrush(3, 64, 72, {Palette = #PALETTE_GRAY16})
CreateBrush(4, 64, 72, {Palette = #PALETTE_GRAY16})

; split rabbit into two brushes
SelectBrush(3)
DisplayBrush(2, 0, 0)
EndSelect

SelectBrush(4)
DisplayBrushPart(2, 64, 0, 0, 0, 54, 72)
EndSelect

; convert the two rabbit brushes into two hardware sprites and map
; them to DMA channels
planar.CreateSprite(1, 3)
planar.CreateSprite(2, 4)
planar.MapSprite(1)
planar.MapSprite(2)

text$ = {"",
" Welcome",
"    to",
" Airsofts",
"  Easter",
"  Intro!",
"This is a quick port",
"of a small intro",
"released originally by",
"Airsoft Softwair back in",
"April 1996. First",
"written in Assembler",
"now written in",
"Hollywood!"}

; sprite position and velocity
sx = 50 + Rnd(100)
sy = -50 - Rnd(50)
xv = 1 + Rnd(4)
yv = 1

SetFont("ClearVT.font", 8)

PlayMusic(1)

c = 1
k = 1

EscapeQuit(True)

Repeat
	p_MainLoop()
	planar.VWait()
Until quit = True

planar.UnmapSprite(1)
planar.UnmapSprite(2)

SetDrawPen(0)

For y = 0 To 256 Step 2
        Line(0, y, 319, y, $000000)
        Wait(2)
Next

For y = 255 To 1 Step -2
        Line(0, y, 319, y, $000000)
        Wait(2)
Next

SetPen(1, #WHITE)

MoveBrush(1, 0, #BOTTOMOUT, 0, 0, 5)

Wait(50)

MoveBrush(1, 0, 0, 0, -256, 5)

Wait(50)

End


