/****************************************************************
**                                                             **
** Name:        Vectorballs (using sprites)                    **
** Author:      Andreas Falkenhahn                             **
** Version:     1.2                                            **
** Date:        13.02.22                                       **
** Interpreter: Hollywood 9.0                                  **
** Licence:     Sample program for Hollywood                   **
** Function:    Small demo with animated graphics; shows how   **
**              to use the different Hollywood animation       **
**              possibilities                                  **
**                                                             **
** History:                                                    **
**                                                             **
** 1.2: (13.02.22)                                             **
**                                                             **
** - uses Plananarama in palette mode now with real hardware   **
**   sprites now                                               **
**                                                             **
** 1.1: (29.03.13)                                             **
**                                                             **
** - added BeginRefresh()/EndRefresh() section for optimized   **
**   drawing on supported systems                              **
**                                                             **
** 1.0: (16.09.05)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Hollywood 9.0 is required
*/
@VERSION 9,0 


/*
** Put Plananarama in palette mode and set the sprite resolution to lores
*/
@REQUIRE "plananarama", {PaletteMode = True, SpriteResolution = 1}


/*
** External data
*/
@BRUSH 1, "ball1.iff", {LoadPalette = True}
@BRUSH 2, "ball2.iff", {LoadPalette = True}
@BRUSH 3, "ball4.iff", {LoadPalette = True}
@BRUSH 4, "ball3.iff", {LoadPalette = True}


/*
** Create a 320x256x5 display
*/
@DISPLAY {Width = 320, Height = 256, Palette = #PALETTE_OCS}

Function p_MainLoop()

        ;
        ; in this version, we use sprites as the animation technique!
        ; Sprites are always at the front of the display and can be moved
        ; around freely!
        ;

        For Local I = 0 To 2

                ; Setup gravity & x speed
                VY[I] = VY[I] + 1
                X[I] = X[I] + VX[I]
                Y[I] = Y[I] + VY[I]

                ; ybounce!
                If Y[I] >= 180
                        Y[I] = 180
                        VY[I] = -10 - Rnd(9)
                EndIf

                ; xbounce!
                If X[I] > 290 Or X[I] < 5 Then VX[I] = -VX[I]

                ; Display sprite
                planar.MoveSprite(I + 1, X[I], Y[I])
        Next
	
EndFunction

; disable remapping
SetPaletteMode(#PALETTEMODE_PEN)
HidePointer

; convert brushes to 16 colors
For Local k = 1 To 4 Do SetBrushDepth(k, 4)
	
; set palette (color registers 16-23 are used by the sprites)
For Local k = 0 To 31 Do SetPen(k, GetPen(IIf(k >= 16, k - 16, k), {Type = #BRUSH, ID = 1}))

LeftMouseQuit(True)
EscapeQuit(True)

X = {}
Y = {}
VX = {}
VY = {}

ExtractPalette(1, #BRUSH, 1)
SetPaletteDepth(1, 4)

; balls, we need balls!
; unfortunately, we can only use 3 balls because DMA channel is
; reserved by Intuition for the mouse pointer
For I = 0 To 2
	
        X[I] = 17 + Rnd(200)
        Y[I] = 17 + Rnd(100)
        VX[I] = 1 + Rnd(2)
        VY[I] = 1 + Rnd(2)
        
        ; make sure all sprites have the same size (40x40)
        CreateBrush(I + 5, 40, 40, {Palette = #PALETTE_GRAY16})
        SelectBrush(I + 5)
        DisplayBrush(I + 1, 0, 0)
        EndSelect
        
        ; convert brushes to hardware sprites
        SetPalette(1, {Type = #BRUSH, ID = I + 1})
        planar.CreateSprite(I + 1, I + 5)
        planar.MapSprite(I + 1)
        
        FreeBrush(I + 1)
        FreeBrush(I + 5)
Next

; main loop
Repeat
	p_MainLoop()
	planar.VWait()
Forever
