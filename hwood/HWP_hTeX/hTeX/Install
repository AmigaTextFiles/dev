;
; $VER: hTeX Setup 1.0 (01.06.24)
;
; Done by Andreas Falkenhahn
;

(message (cat "\nWelcome to the Setup utility for\n\n"
	"hTeX 1.0\n\n"
	"-  The Powerful LaTeX Engine for Hollywood -\n\n"
	"Copyright (C) 2024 by Andreas Falkenhahn\n\n"
	"http://www.hollywood-mal.com/"
))

(welcome)

(set basepath
	(askdir
		(prompt  "Where do you want to have hTeX installed? (a drawer will be created)")
		(help    @askdir-help)
		(default "SYS:")
	)
)

(set sstr (substr basepath (- (strlen basepath) 1) 1))
(if (or (= sstr "/") (= sstr ":"))
	(set destpath (cat basepath "hTeX"))
	(set destpath (cat basepath "/hTeX"))
)

(set @default-dest destpath)

(set defaultplat 0)

(if (exists "SYS:MorphOS/C/Assign") (
	(set defaultplat 3)
) (
	(if (exists "SYS:System/GrimReaper") (
		(set defaultplat 2)
	))
))

(set whichver
	(askchoice
		(prompt  "Please select your platform")
		(help    @askoptions-help)
		(choices "AmigaOS 3 (m68k)" "AmigaOS 3 (m68k/m881)" "AmigaOS 4 (ppc)" "MorphOS (ppc)")		
		(default defaultplat)
	)
)

(set #platform "m68k-amigaos")

(if (= whichver 1) (
	(set #platform "m881-amigaos")
))

(if (= whichver 2) (
	(set #platform "ppc-amigaos")
))

(if (= whichver 3) (
	(set #platform "ppc-morphos")
))

(makedir destpath)
(makedir "LIBS:Hollywood")  

(copylib
	(source (cat "Platforms/" #platform "/htex.hwp"))
	(dest   "LIBS:Hollywood")
	(prompt "htex.hwp")
	(help @copylib-help)
	(optional "nofail" "force" "askuser")
	(confirm)
)

(copyfiles
	(source  "Data.icon")
	(dest    basepath)
	(newname "hTeX.info")
	(optional force oknodelete)
)

(copyfiles
	(source "Data")
	(dest   destpath)
	(all)
	(optional "nofail" "force" "askuser")
)

(if (exists "Hollywood:GUI" (noreq)) (

	(makedir "Hollywood:Examples/hTeX")

	(copyfiles
		(source "Data/Examples")
		(dest   "Hollywood:Examples/hTeX")
		(all)
		(optional "nofail" "force" "askuser")
	)
))

(if (exists "Hollywood:LinkerPlugins" (noreq)) (

	(copyfiles
		(source "Platforms")
		(dest   "Hollywood:LinkerPlugins")
		(all)
		(optional "nofail" "force" "askuser")
	)
))

(set #cmdline (cat "Assign hTeX: \"" destpath "\""))

(startup
	(prompt  "\nAn assign has to be inserted in your user-startup. Please click Continue.")
	(help    @startup-help)
	"hTeX"
	(command #cmdline)
	(confirm)
)

(run #cmdline)

(message (cat "\nhTeX comes with several ready-to-run examples.\n\n"
	"The examples have been integrated into your Hollywood GUI (only "
	"if Hollywood is installed on this system). Now that you have installed "
	"hTeX, there will be a new hTeX tab in the Hollywood GUI which "
	"allows you to conveniently launch the example scripts that come with hTeX.\n\n"
	"Click to see a screenshot that shows how to launch the example scripts "
	"that come with hTeX from the Hollywood GUI."))
	
(run ("Sys:Utilities/MultiView GUI.png"))

(exit)
