/****************************************************************
**                                                             **
** Name:        Skid Row Cracktro "Lemmings" - a classic       **
** Author:      Mr.X of Barracuda                              **
** Version:     1.2                                            **
** Date:        13.05.17                                       **
** Interpreter: Hollywood 6.0                                  **
** Licence:     Sample program for Hollywood                   **
** Function:    Hollywood port of this nice cracktro           **
**                                                             **
** Notes:       Rotating starfield not yet implemented         **
**                                                             **
** History:                                                    **
**                                                             **
** 1.2: (13.05.17)                                             **
**                                                             **
** - adapted for RebelSDL                                      **
**                                                             **
** 1.1: (02.01.15)                                             **
**                                                             **
** - uses Disable/EnableLineHook() now for better performance  **
**                                                             **
** 1.0: (14.05.05)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Important! Check if the used Hollywood version is at least
** version 6.0!
*/
@VERSION 6,0

/*
** This script requires the RebelSDL plugin
*/
@REQUIRE "rebelsdl"

@BRUSH 4, "font_big.png", {Transparency = #BLACK}
@BRUSH 5, "font_small.png", {Transparency = #BLACK}
@BRUSH 8, "dan.png"

@MUSIC 1, "4mat.mod"

@DISPLAY {Width = 360, Height = 284}

/* prepare the background gradient and the color bars */
Function p_CreateBG()

        ; this is the gradient effect for the sine scroller
        CreateBrush(6, 360, 132)
        SelectBrush(6)
        SetFillStyle(#FILLGRADIENT, #LINEAR, #RED, #YELLOW)
        Box(0, 0, 360, 50)
        SetFillStyle(#FILLGRADIENT, #LINEAR, #YELLOW, #WHITE)
        Box(0, 50, 360, 16)
        SetFillStyle(#FILLGRADIENT, #LINEAR, #WHITE, #YELLOW)
        Box(0, 66, 360, 16)
        SetFillStyle(#FILLGRADIENT, #LINEAR, #YELLOW, #RED)
        Box(0, 82, 360, 50)
        EndSelect

        ; the color bars will cycle through these colors
        cols = {$F70000, $F72000, $F74100, $F76100, $F78200, $F7A200, $F7C300, $F7E300, $F7F300, $E7F300,
        $C6F300, $A5F300, $84F300, $63F300, $42F300, $21F300, $00F300, $00F321, $00F342, $00F363,
        $00F384, $00F3A5, $00F3A5, $00F3C6, $00F3E7, $00F3F7, $00E3F7, $00C3F7, $00A2F7, $0082F7,
        $0061F7, $0041F7, $0020F7, $0000F7, $2100F7, $4200F7, $6300F7, $8400F7, $A500F7, $C600F7,
        $E700F7, $F700F7, $F700E7, $F700C6}

        collength = ListItems(cols)
        start2 = collength - 1

EndFunction

/* prepare the text writer */
Function p_InitWriter()

        Local i, c
        Local textcols = {$840000, $844100, $848200, 0, $008200, 0, $004184, $000084, $420084, $840084}   ; base colors for gradient
        Local f$ = " !\"§$%&'()*+,-./0123456789:;<=>?#ABCDEFGHIJKLMNOPQRSTUVWXYZ"  ; format of font_small.iff
        Local x, y

        chars_s = {}

        ; create our lookup table chars_s{}
        For y = 0 To 1
                For x = 0 To 39
                        chars_s[Asc(MidStr(f$, y * 40 + x, 1))] = {x = x * 8, y = y * 8}
                Next
        Next

        Local t$ =
        "                * SKID ROW *                " ..
        "                  PRESENTS                  " ..
        "         ANOTHER LIGHTSPEED RELEASE         " ..
        "                                            " ..
        "               LEMMINGS FINAL               " ..
        "                                            " ..
        "    DONE BY SKID ROW PRODUCTIONS IN 1991    " ..
        "    CALL OUR BOARDS WORLDWIDE TO GET THE    " ..
        "  NEWEST STUFF AND OUR LATEST PRODUCTIONS.  " ..
        "     SEE YOU LATER IN ANOTHER INTRO !!!     "

        writertextlen = StrLen(t$)
        writertext = StrToArray(t$)

        ; this brush contains the copper effect for the writer text
        CreateBrush(7, 360, 80)
        SelectBrush(7)

        For k = 0 To 79
                If k % 8 = 1
                        c = textcols[i]
                        i = i + 1
                EndIf

                Line(0, k, 359, k, c)

                If k % 8 < 4
                        c = c + $212021
                Else
                        c = c - $212021
                EndIf
        Next

        EndSelect

        ; clear its mask
        SelectMask(7)
        SetMaskMode(#MASKINVISIBLE)
        Cls
        EndSelect

        ; all future renderings are now in "visible" mode
        SetMaskMode(#MASKVISIBLE)

        cx = 0
        cy = 29

EndFunction

/* init the sine scroller */
Function p_InitScroll()

        Local f$ = " !\"§$%&'()*+,-./0123456789:;<=>?#ABCDEFGHIJKLMNOPQRSTUVWXYZ"   ; format of font_big.iff
        Local x, y

        chars = {}

        ; create lookup table chars{}
        For y = 0 To 2
                For x = 0 To 19
                        chars[Asc(MidStr(f$, y * 20 + x, 1))] = {x = x * 16, y = y * 16}
                Next
        Next

        Local t$ = "                  * SKID ROW *                   " ..
        "- THE LEADING FORCE -                   IS BACK ON YOUR SCREEN WITH:" ..
        "                   LEMMINGS FINAL FROM PSYGNOSIS                   " ..
        "CRACKED BY SKID ROW IN THE YEAR OF 1991 !!!                   " ..
        "IF YOU WANT TO BUY THE LATEST STUFF FOR AMIGA AND PC WRITE TO PLK: " ..
        "101025 C  4000 DUESSELDORF 1WEST GERMANY !!!                      " ..
        "WELCOME OUR NEW MEMBER IRON MEN IN OUR GROUP                    " ..
        "GREETINGS TO ALL OUR FRIENDS AROUND THE GLOBE !!!                   " ..
        "CALL OUR BOARDS WORLDWIDE:                   " ..
        "ALCATRAZ (WHQ) : 703 323 5997                   " ..
        "INQUISITION : 805 967 8833                   " ..
        "H.M.S BOUNTY : 215 873 7287                   " ..
        "JUSTICE LEAGUE ++39 498 021067                   " ..
        "IF YOU USE THIS PIECE OF SOFTWARE, PLEASE CONSIDER BUYING THE ORIGINAL. "..
        "                                     ... SCROLL RESTARTS ..."

        scrolltextlen = StrLen(t$)
        scrolltext = StrToArray(t$)

EndFunction

/* create our sine lookup table */
Function p_MakeSine()

        Local i, s = 0, 2 * #PI / 500

        sine = {}
        sinelen = 500

        For Local k = 0 to sinelen - 1
                sine[k] = Sin(i) * 55 + 55
                i = i + s
        Next

EndFunction

/* prepare starfield */
Function p_InitStars()

    stars = {}

    For Local i = 0 To 199

            stars[i] = {
                x = Rnd(360),
                y = Rnd(238) + 24,
                speed = Rnd(4) + 1,
                color = (Rnd(11) + 4) * 0x111111}

    Next

EndFunction

/* this function is called 50 times a second */
Function p_MainLoop()

	DisableLineHook()
	
        Local charpos_tmp, scrollpos_tmp, sinepos_tmp = charpos, scrollpos, sinepos
        Local char = scrolltext[scrollpos_tmp]
        Local i, k

        ; advance to next positions
        charpos = charpos + 2

        If charpos >= 16
            charpos = 0
            scrollpos = scrollpos + 1
            If scrollpos >= scrolltextlen Then scrollpos = 0
        EndIf

        sinepos = sinepos + 4
        If sinepos >= sinelen Then sinepos = sinepos - sinelen

        ; flip the back buffer into view and make the front buffer the back buffer,
        ; so we can render the next frame!
        Flip

        ; now draw the next frame to the back buffer!
        Cls
        DisplayBrush(8, 284, 265)        ; display "DAN OF ANARCHY"

        ; draw starfield
        For i = 0 To 199

                If stars[i].x >= 360 Then stars[i].x = stars[i].x - 360

                Plot(stars[i].x, stars[i].y, stars[i].color)

                stars[i].x = stars[i].x + stars[i].speed
        Next

        ; display upper color bar
        i = start1

        For k = 0 To collength - 2
                Line(k * 8, 22, k * 8 + 7, 22, cols[i])
                i = i + 1
                If i = collength Then i = 0
        Next

        Line(k * 8, 22, 359, 22, cols[i])

        start1 = start1 + 1
        If start1 = collength Then start1 = 0

        ; display lower color bar (reverse direction!)
        i = start2

        For k = collength To 1 Step -1
                Line(k * 8 + 7, 262, k * 8, 262, cols[i])
                i = i - 1
                If i = -1 Then i = collength - 1
        Next

        Line(9, 262, 0, 262, cols[i])

        start2 = start2 - 1
        If start2 = -1 Then start2 = collength - 1

        ; now render the sine scroller intro the gradient's mask
        SelectMask(6)
        SetMaskMode(#MASKINVISIBLE)
        Cls
        SetMaskMode(#MASKVISIBLE)

        For Local x = 0 To 360 Step 16

            For Local k = 0 To 15

                    charpos_tmp = charpos_tmp + 1

                    if charpos_tmp >= 16
                        charpos_tmp = 0
                        scrollpos_tmp = scrollpos_tmp + 1
                        If scrollpos_tmp >= scrolltextlen Then scrollpos_tmp = 0
                        char = scrolltext[scrollpos_tmp]
                    EndIf

                    DisplayBrushPart(4, chars[char].x + charpos_tmp, chars[char].y, x + k, sine[sinepos_tmp], 1, 16)

                    sinepos_tmp = sinepos_tmp + 1
                    if sinepos_tmp >= sinelen Then sinepos_tmp = 0
            Next

        Next

        EndSelect

        DisplayBrush(6, 0, 132)    ; render the masked gradient to it

        ; writer not finished yet?
        If wc < writertextlen

                ; render its characters to our prepare copper brush's mask

                char = writertext[wc]

                If char <> ' ' And high = 1

                        SelectMask(7)
                        DisplayBrushPart(5, chars_s[char].x, chars_s[char].y, cx, cy - 29, 8, 8)
                        EndSelect

                EndIf

                DisplayBrush(7, 0, 29)

                ; highlight it first!
                If char <> ' ' And high = 0
                        DisplayBrushPart(5, chars_s[char].x, chars_s[char].y, cx, cy, 8, 8)
                EndIf

                ; advance to next character
                If high = 1
                        cx = cx + 8
                        wc = wc + 1

                        If cx = 352
                                cx = 0
                                cy = cy + 8
                        EndIf
                EndIf

                high = 1 - high

        Else

                ; simply display the brush now because the writer is done
                DisplayBrush(7, 0, 29)

        EndIf

	EnableLineHook()
	
EndFunction

p_CreateBG()
p_InitScroll()
p_InitWriter()
p_InitStars()
p_MakeSine()

EscapeQuit(True)

PlayMusic(1)

; begin double buffered output
BeginDoubleBuffer

Repeat
	p_MainLoop
	CheckEvent
Forever

