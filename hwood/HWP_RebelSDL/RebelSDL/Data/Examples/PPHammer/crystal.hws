/****************************************************************
**                                                             **
** Name:        Crystal Cracktro "PP Hammer" - by N.O.M.A.D.   **
** Author:      Mr.X of Barracuda                              **
** Version:     1.1                                            **
** Date:        13.05.17                                       **
** Interpreter: Hollywood 6.0                                  **
** Licence:     Sample program for Hollywood                   **
** Function:    Hollywood port of this nice cracktro           **
**                                                             **
** Notes:       This is my favourite cracktro of all time!     **
**              Coded by the legendary N.O.M.A.D. using the    **
**              pumping tune by Dezecrator - enjoy this rockin'**
**              cracktro!                                      **
**                                                             **
** History:                                                    **
**                                                             **
** 1.1: (13.05.17)                                             **
**                                                             **
** - adapted for RebelSDL                                      **
**                                                             **
** 1.0: (01.02.09)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Important! Check if the used Hollywood version is at least
** version 6.0!
*/
@VERSION 6,0

/*
** This script requires the RebelSDL plugin
*/
@REQUIRE "rebelsdl"

@DISPLAY {Width = 360, Height = 275}

@BRUSH 10, "font.png", {Transparency = $0000CC}

@MUSIC 1, "introfronty.mod"

/* precalculate a plasma table */
Function p_MakePlasmaSin()

	PlasmaSin = {}
	
	For Local k = 0 To 16383
		
		Local s = BitClear(k * 4, 15)
		
		s = (($7fff - s) * s) >> 13
		
		If BitTest(k * 4, 15) = True Then s = (-s) - 1
		PlasmaSin[k] = (s + 32768) \ 380			
	Next
	
EndFunction

/* compute plasma gradient (black-blue-black) */	
Function p_MakeColor()

	Local col = 0
	Local c = 8

	PlasmaColor = {}
	
	For Local i = 0 To 99 Do PlasmaColor[i] = 0
	
	For Local i = 100 To 221
		PlasmaColor[i] = col
		c = c - 1
		If c = 0
			col = col + 17   ; more blue!
			c = 8
		EndIf
	Next		

	c = 8
	
	For Local i = 222 To 341
		PlasmaColor[i] = col
		c = c - 1
		If c = 0
			col = col - 17   ; less blue!
			c = 8
		EndIf
	Next
	
	For Local i = 342 To 447 Do PlasmaColor[i] = 0
	
	; create base brush for plasma (448 rows of gradient)
	CreateBrush(4, 8, 448)
	SelectBrush(4)
	For Local y = 0 To 447 Do Box(0, y, 8, 1, PlasmaColor[y])
	EndSelect
	
EndFunction

/* prepare 8 gradient text brushes */	
Function p_MakeGradientText()

        ; note: these colors are directly from the original intro and use only
        ; 5 bits per gun --> need to convert them 8 bits per gun before use!
	Local GradientText = {
		$77, $88, $99, $BB, $DD,$FF,$FF,
		$DD, $BB, $99, $88, $77,$FFF,$FFF,
		$227, $338, $449, $66B, $88D,$AAF,$AAF,
		$88D, $66B, $449, $338, $227,$FFF,$FFF,
		$720, $830, $940, $B60, $D80,$FA0,$FA0,
		$D80, $B60, $940, $830, $720,$FFF,$FFF,
		$123, $245, $367, $489, $5AB,$6CD,$6CD,
		$5AB, $489, $367, $245, $123,$FFF,$FFF,
		$724, $835, $946, $B68, $D8A,$FAC,$FAC,
		$D8A, $B68, $946, $835, $724,$FFF,$FFF,
		$131, $242, $373, $494, $5B5,$6D6,$6D6,
		$5B5, $494, $373, $252, $131,$FFF,$FFF,
		$742, $853, $964, $B86, $DA8,$FCA,$FCA,
		$DA8, $B86, $964, $853, $742,$FFF,$FFF,
		$777, $888, $999, $BBB, $DDD,$FFF,$FFF,
		$DDD, $BBB, $999, $888, $777,$FFF,$FFF,
		-1}
	
	; font can appear in 8 different gradients!		
	For Local k = 0 To 7
	
		CreateBrush(11 + k, 320, 22)
		SelectBrush(11 + k)
		
		; draw gradient to brush
		For Local j = 0 To 1
			For Local y = 0 To 10
				Local c = GradientText[k * 14 + y]	
				Line(0, j * 11 + y, 360, j * 11 + y, RGB(((c & $f00) >> 8) * 17, ((c & $f0) >> 4) * 17, (c & $f) * 17))
			Next
		Next	
		
		EndSelect
		
		; combine gradient & text into a new brush
		SelectMask(11 + k)
		SetMaskMode(#MASKINVISIBLE)
		Cls
		SetMaskMode(#MASKVISIBLE)
		DisplayBrush(10, 0, 0)
		EndSelect
		
	Next		

EndFunction

/* initialize data */	
Function p_InitAll()

	valsPlasma = {300, 400, 300, 200, 
		200, 200, 200, 600,
		200, 200, 0, 200,
		300, 400, -200, 300,
		1}
		
	p_MakeGradientText()
	p_MakePlasmaSin()
	p_MakeColor()
	
EndFunction

/* calculate next plasma data */
Function p_DoPlasma()

	For Local k = 0 To 3
		plasmaIncmovA[k] = plasmadatas[k] + ((valsPlasma[vpc+k] - plasmadatas[k]) * plasmaIncmov) \ 300
	Next
			
	If plasmaIncmov < 300
		plasmaIncmov = plasmaIncmov + 1
	Else
		plasmadatas[0] = plasmaIncmovA[0]
		plasmadatas[1] = plasmaIncmovA[1]		
		plasmadatas[2] = plasmaIncmovA[2]		
		plasmadatas[3] = plasmaIncmovA[3]
		
		vpc = vpc + 4
		
		plasmaIncmov = 0
			
		If valsPlasma[vpc] = 1 Then vpc = 0
	EndIf	
			
EndFunction

/* vertical plasma move */
Function p_BlitPlasma()

	plasmamovV = plasmamovV + plasmaIncmovA[0]
	plasmaV = plasmamovV
	
	SelectBrush(5)
	
	For Local c = 0 To 56
	
	        ; we only have data for 52 blocks with 8 pixels each
		If c < 52
			plasmaV = plasmaV + plasmaIncmovA[1]
			y = PlasmaSin[(plasmaV & $7ffe) \ 2]  
		EndIf
		
		DisplayBrushPart(4, 0, y, c * 8, 0, 8, 275)
	Next
	
	EndSelect
	
EndFunction

/* horizontal plasma move */
Function p_MoveHPlasma()

	plasmamovH = plasmamovH + plasmaIncmovA[2]
	plasmaH = plasmamovH
	
	; 275 lines of plasma	
	For Local y = 0 To 274
		
		plasmaH = plasmaH + plasmaIncmovA[3]
		
		Local x = ((PlasmaSin[(plasmaH & $7ffe) \ 2]) \ 8) * 2 + 7
		Local pos = (x >> 1) - 27
		
		DisplayBrushPart(5, 0, y, pos * 4, y, 480, 1)	
	Next
	
EndFunction

/* handle the text writer */
Function p_DoText(bx, by, cols)

	Local k, x, y = 0, 0, 0

	While k <= tp	

		Local c = Asc(MidStr(t$, pp + k, 1))
		
		If c <> ' '
		
			Local ix, iy
			
			If (k = tp) And (cursor = True)
				ix = 6
				iy = 11
			ElseIf c <= 'T'
				ix = c - 'A'
				iy = 0
			Else
				ix = c - 'U'
				iy = 11
			EndIf
					
			DisplayBrushPart(cols[y \ 14], ix * 16, iy, bx + x, by + y, 16, 11)	
			
		Else
		
			If (k = tp) And (k <> textlimit - 1) Then tp = tp + 1
						
		EndIf
		
		x = x + 16
		If x = 320
			x = 0
			y = y + 14
		EndIf	
		
		k = k + 1
	Wend
	
EndFunction

/* main loop - called 25 times per second */
Function p_MainLoop()

	; draw next plasma frame
	If Not db Then p_DoPlasma()
	p_BlitPlasma()
	If Not db Then p_MoveHPlasma()
	
	db = Not db
	
	; endmode means that we show the Crystal member list (16 lines)
	If endmode = True
	
		p_DoText(20, 15, {0, 0, 14, 0, 16, 17, 18, 11, 12, 13, 14, 15, 0, 17, 18, 11})
		
	Else
	
		If pp >= 0 Then p_DoText(20, 116, {18, 11, 12})	
	
	EndIf
	
	; show next page after 5 seconds have elapsed
	If nextpage = True
	
		If GetTimer(2) >= 5000
			pp = pp + 60
			If pp = 240 Then pp = 0
			nextpage = False
			cursor = True
			tp = 0
		EndIf
	
	Else
	
		If GetTimer(1) >= 100
			If (cursor = True) And (tp > 0)
				cursor = False
			Else		
				If tp < textlimit - 1
					tp = tp + 1
					cursor = True
				ElseIf endmode = False
					nextpage = True
					StartTimer(2)
				EndIf	
			EndIf
			StartTimer(1)	
		EndIf		
	EndIf	
	
	; show Crystal member list	
	If IsLeftMouse() = True
	
		If (endmode = True) And (pp + tp >= 282) Then End   ; sanity check
	
		endmode = True	
		textlimit = 320
		pp = 240
		nextpage = False
		tp = 0
		cursor = True
	EndIf
	
	; RMB always exits
	If IsRightMouse() = True Then End
	
	; flip double buffer	
	Flip
	
EndFunction

CreateBrush(5, 480, 275)

t$ = 
"       CRYSTAL      "..
"       PRESENT      "..
"      PP HAMMER     "..
"        INTRO       "..
"      CODED  BY     "..
"        NOMAD       "..
"       ORIGINAL     "..
"     SUPPLIED BY    "..
"        GIZAM       "..
"ACCEPT NO IMITATIONS"..
"     WE ARE THE     "..
"  WORLD NUMBER ONE  "..
"                    "..
"                    "..
"CRYSTAL MEMBERS LIST"..
"                    "..
"IBM    NOMAD  SCROTE"..
"  MEGADRIVE  SCOTT  "..
" MR E    ACTION MAN "..
"ZIPPY  ZELNIK  KENZO"..
"PHIL DOUGLAS  MISFIT"..
"DANYSOFT  TIGER NINE"..
"POW SWAYZAR PROTOCOL"..
"  BOB DUNCAN  FLUX  "..
"                    "..
" IF YOUR NOT ON THE "..
" LIST YOU ARE NOT A "..
"   CRYSTAL MEMBER   "

SetFillStyle(#FILLCOLOR)

plasmadatas = {0, 0, 0, 0}
plasmaIncmovA = {300, 400, -200, 300}

p_InitAll()

; prepare plasma
For k = 0 To 160
	p_DoPlasma()
Next

EscapeQuit(True)

BeginDoubleBuffer

StartTimer(1)
StartTimer(2)

cursor = True
nextpage = True
pp = -60
textlimit = 60

; kick up the Dezecrator tune!!!
PlayMusic(1)

; and go!
Repeat
	p_MainLoop()
	CheckEvent
Forever		



	
