/****************************************************************
**                                                             **
** Name:        Horizon Cracktro "Pang!"                       **
** Author:      Mr.X of Barracuda                              **
** Version:     1.3                                            **
** Date:        13.05.17                                       **
** Interpreter: Hollywood 6.0                                  **
** Licence:     Sample program for Hollywood                   **
** Function:    Hollywood port of this nice cracktro           **
**                                                             **
** Notes:       Andreas said that he really liked my ports of  **
**              the Lemmings & Zool cracktros and asked me if  **
**              I could do another one for the Hollywood 2.0   **
**              CD so here you go. Enjoy!                      **
**                                                             **
** History:                                                    **
**                                                             **
** 1.3: (13.05.17)                                             **
**                                                             **
** - adapted for RebelSDL                                      **
**                                                             **
** 1.2: (02.01.15)                                             **
**                                                             **
** - uses Disable/EnableLineHook() now for better performance  **
**                                                             **
** 1.1: (04.03.07)                                             **
**                                                             **
** - the sample is now streamed using PlayMusic(); it is no    **
**   longer loaded completely into RAM (this update done by    **
**   Andreas Falkenhahn, original script by Barracuda Team)    **
**                                                             **
** 1.0: (09.12.05)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Important! Check if the used Hollywood version is at least
** version 6.0!
*/
@VERSION 6,0

/*
** This script requires the RebelSDL plugin
*/
@REQUIRE "rebelsdl"

@BRUSH 1, "font.png", {Transparency = #BLACK}
@BRUSH 2, "horizon.png", {Transparency = #BLACK}
@BRUSH 3, "bottombar.png"
@BRUSH 6, "gaston.png", {Transparency = #BLACK}
@BRUSH 7, "info.png", {Transparency = #BLACK}

@MUSIC 1, "blaizer.mod"

@DISPLAY {Width = 320, Height = 256}

/* prepare the background gradient and the color bars */
Function p_CreateBG()

        ; this is the gradient effect for the sine scroller
        CreateBrush(10, 360, 108)
        SelectBrush(10)
        SetFillStyle(#FILLGRADIENT, #LINEAR, $ef65ce, $ef)
        Box(0, 0, 320, 18)
        SetFillStyle(#FILLGRADIENT, #LINEAR, $ef, $bdba42)
        Box(0, 18, 320, 18)
        SetFillStyle(#FILLGRADIENT, #LINEAR, $bdba42, $ef65ce)
        Box(0, 36, 320, 18)
        SetFillStyle(#FILLGRADIENT, #LINEAR, $ef65ce, $ef)
        Box(0, 54, 320, 18)
        SetFillStyle(#FILLGRADIENT, #LINEAR, $ef, $bdba42)
        Box(0, 72, 320, 18)
        SetFillStyle(#FILLGRADIENT, #LINEAR, $bdba42, $ef65ce)
        Box(0, 90, 320, 18)
        EndSelect

        CopyBrush(1, 4)
        CopyBrush(1, 5)

        ReplaceColors(4, {#WHITE, $8cefff})
        ReplaceColors(5, {#WHITE, $65ad})

EndFunction

/* init the sine scroller */
Function p_InitScroll()

        Local f$ = " !\"§$%&'()*+,-./0123456789:;<=>?#ABCDEFGHIJKLMNOPQRSTUVWXYZ"   ; format of font.iff
        Local x, y

        chars = {}

        ; create lookup table chars{}
        For y = 0 To 2
                For x = 0 To 19
                        chars[Asc(MidStr(f$, y * 20 + x, 1))] = {x = x * 16, y = y * 16}
                Next
        Next

        Local t$ = "                 a    - HORIZON -     PROUDLY PRESENTS:      a     - PANG -       " ..
        "FINAL VERSION...  FROM OCEAN!!   CRACKED BY GASTON AND SUPPLIED BY BUG...      " ..
        "MUSIC BY BLAIZER OF TSL...      MAKE SURE TO CALL OUR HEADQUARTERS TO GET THE " ..
        "LATEST WARES....   GREETINGS TO OUR ALLIES IN CRIME: STRIDER - FAIRLIGHT     " ..
        "SKID ROW     PARADOX     ANGELS - DEFJAM      OK, GOTTA GET BACK TO JAIL NOW, " ..
        "SEE YOU LATER!!"

        scrolltextlen = StrLen(t$)
        scrolltext = StrToArray(t$)

EndFunction

/* create our sine lookup table */
Function p_MakeSine()

        Local i, s = 0, 2 * #PI / 300

        sine = {}
        sinelen = 300

        For Local k = 0 to sinelen - 1
                sine[k] = Sin(i) * 45 + 45
                i = i + s
        Next

EndFunction

/* prepare starfield */
Function p_InitStars()

    stars = {}

    For Local i = 0 To 49

            stars[i] = {
                x = Rnd(360),
                y = Rnd(198) + 26,
                speed = Rnd(4) + 1,
                color = (Rnd(11) + 4) * 0x111111}

    Next

EndFunction

/* render sine scroller to brush "br" */
Function p_RenderSine(br, charpos_tmp, sinepos_tmp, scrollpos_tmp, finetune)

        Local char = scrolltext[scrollpos_tmp]

        If char = 97 Then char = 32

        For Local x = 0 To 360 Step 16

            For Local k = 0 To 15

                    charpos_tmp = charpos_tmp + 1

                    if charpos_tmp >= 16
                            charpos_tmp = 0
                            scrollpos_tmp = scrollpos_tmp + 1
                            If scrollpos_tmp >= scrolltextlen Then scrollpos_tmp = 0
                            char = scrolltext[scrollpos_tmp]
                            If char = 97 Then char = 32
                    EndIf

                    DisplayBrushPart(br, chars[char].x + charpos_tmp, chars[char].y, x + k, sine[sinepos_tmp] + finetune, 1, 16)

                    sinepos_tmp = sinepos_tmp + 1
                    if sinepos_tmp >= sinelen Then sinepos_tmp = 0
            Next

        Next

EndFunction

/* compute gradient between startcol and endcol */
Function p_MakeGradient(startcol, endcol, offset)

        Local rs, gs, bs = Red(endcol), Green(endcol), Blue(endcol)
        Local re, ge, be = Red(startcol), Green(startcol), Blue(startcol)
        Local h = 31

        For Local y = 0 To h
                cols[offset] = RGB(rs*y/h+re*(h-y)/h, gs*y/h+ge*(h-y)/h, bs*y/h+be*(h-y)/h)
                offset = offset + 1
        Next

EndFunction

/* this function is called 50 times a second */
Function p_MainLoop()

	DisableLineHook()
	
        Local i, k
        Local old_charpos, old_sinepos, old_scrollpos = charpos, sinepos, scrollpos

        If hold = False

                ; advance to next positions
                charpos = charpos + 2

                If charpos >= 16
                    charpos = 0
                    scrollpos = scrollpos + 1

                    ; put scroller on hold!
                    If scrolltext[scrollpos] = 'a'
                        hold = True
                        StartTimer(1)
                    EndIf

                    If scrollpos >= scrolltextlen Then scrollpos = 0
                EndIf

        Else
                If GetTimer(1) >= 2000

                        If logoin = False

                                ; fade in Horizon logo
                                CopyBrush(2, 8)
                                CopyBrush(3, 9)
                                TintBrush(8, #WHITE, ratio)
                                TintBrush(9, #WHITE, ratio)
                                DisplayBrush(8, 0, 21)        ; display "HORIZON"
                                DisplayBrush(9, 0, 224)       ; display bottom bar
                                If backwards = False
                                        ratio = ratio + 15
                                        If ratio = 270
                                                ratio = ratio - 15
                                                backwards = True
                                        EndIf
                                Else
                                        ratio = ratio - 15
                                        If ratio = 0 Then logoin = True
                                EndIf

                        Else
                                hold = False
                        EndIf
                EndIf
        EndIf

        sinepos = sinepos + 4
        If sinepos >= sinelen Then sinepos = sinepos - sinelen

        ; flip the back buffer into view and make the front buffer the back buffer,
        ; so we can render the next frame!
        Flip

        ; now draw the next frame to the back buffer!
        Cls

        ; draw starfield
        For i = 0 To 49
                If stars[i].x >= 360 Then stars[i].x = stars[i].x - 360
                Plot(stars[i].x, stars[i].y, stars[i].color)
                stars[i].x = stars[i].x + stars[i].speed
        Next

        ; now render the sine scroller intro the gradient's mask
        SelectMask(10)
        SetMaskMode(#MASKINVISIBLE)
        Cls
        SetMaskMode(#MASKVISIBLE)
        p_RenderSine(1, old_charpos, old_sinepos, old_scrollpos, 0)
        EndSelect

        ; render scroller's edges
        p_RenderSine(4, old_charpos, old_sinepos, old_scrollpos, 118)
        p_RenderSine(5, old_charpos, old_sinepos, old_scrollpos, 120)

        If logoin = True

                ; the info text is updated 25 times a second
                If GetTimer(2) >= 40

                        ; cycle colors!

                        SelectBrush(7)
                        Local bak_colpos = colpos + 1
                        For k = 0 To 7
                                SetFillStyle(#FILLCOLOR)
                                Box(0, k * 12, 295, 12, cols[colpos])
                                colpos = colpos + 1
                                If colpos = collength Then colpos = 0
                        Next
                        colpos = bak_colpos
                        If colpos = collength Then colpos = 0
                        EndSelect
                        StartTimer(2)
                EndIf

                DisplayBrush(7, #CENTER, 119)  ; display info text
        EndIf

        DisplayBrush(10, 0, 119)       ; render the masked gradient to it

        If logoin = True
                DisplayBrush(2, 0, 21)        ; display "HORIZON"
                DisplayBrush(3, 0, 224)       ; display bottom bar
        EndIf

        DisplayBrush(6, 290, 214)       ; display "GASTON"

	EnableLineHook()
	
EndFunction

cols = {}

; make gradients for info text
p_MakeGradient($a0, $a0a000, 0)
p_MakeGradient($a0a000, $a00000, 32)
p_MakeGradient($a00000, $a000a0, 64)
p_MakeGradient($a000a0, $a00000, 96)
p_MakeGradient($a00000, 0, 128)
p_MakeGradient($0, $a0, 160)
p_MakeGradient($a0, $a0a000, 192)
p_MakeGradient($a0a000, 0, 224)
p_MakeGradient($0, $a00000, 256)
p_MakeGradient($a00000, $a000a0, 288)
p_MakeGradient($a000a0, 0, 320)
p_MakeGradient($0, $a000, 352)
p_MakeGradient($a000, $a0, 384)
p_MakeGradient($a0, $a00000, 416)
p_MakeGradient($a00000, 0, 448)
p_MakeGradient(0, $a0, 480)

collength = ListItems(cols)

p_CreateBG()
p_InitScroll()
p_InitStars()
p_MakeSine()

EscapeQuit(True)

PlayMusic(1, 0)
StartTimer(2)

; begin double buffered output
BeginDoubleBuffer

Repeat
	p_MainLoop()
	CheckEvent
Forever

