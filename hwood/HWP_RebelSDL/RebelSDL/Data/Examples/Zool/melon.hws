/****************************************************************
**                                                             **
** Name:        Melon/Crystal Cracktro "Zool ECS"              **
** Author:      Mr.X of Barracuda                              **
** Version:     1.3                                            **
** Date:        13.05.17                                       **
** Interpreter: Hollywood 6.0                                  **
** Licence:     Sample program for Hollywood                   **
** Function:    Hollywood port of this nice cracktro           **
**                                                             **
** Notes:       Please note the alpha shadow of the scroller!  **
**                                                             **
** History:                                                    **
**                                                             **
** 1.3: (13.05.17)                                             **
**                                                             **
** - adapted for RebelSDL                                      **
**                                                             **
** 1.2: (02.01.15)                                             **
**                                                             **
** - uses Disable/EnableLineHook() now for better performance  **
**                                                             **
** 1.1: (04.03.07)                                             **
**                                                             **
** - the sample is now streamed using PlayMusic(); it is no    **
**   longer loaded completely into RAM (this update done by    **
**   Andreas Falkenhahn, original script by Barracuda Team)    **
**                                                             **
** 1.0: (14.05.05)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Important! Check if the used Hollywood version is at least
** version 6.0!
*/
@VERSION 6,0

/*
** This script requires the RebelSDL plugin
*/
@REQUIRE "rebelsdl"

@BRUSH 4, "font.png", {Transparency = #WHITE}
@BRUSH 6, "melon.png", {Transparency = #RED}

@DISPLAY {Width = 360, Height = 284}

/*
** the original music is hard-coded so I had to rip it as a
** looping sample; note that we load this sample as a music!
** this has the advantage that the sample will not be loaded
** completely into RAM but it will be streamed from disk instead
*/
@MUSIC 1, "melon.wav"

@SAMPLE 1, "quiek.wav"

/* prepare background; the original background is drawn randomly, I did not port this */
Function p_CreateBG()

        Local k
        Local lines = {0, 66, 29, 66, 29, 84, 29, 84, 56, 84, 56, 55, 76, 55, 76, 72, 101, 72, 101, 97, 118, 97, 118, 67, 142, 67, 142, 89,
        161, 89, 161, 41, 212, 41, 212, 75, 254, 75, 254, 110, 301, 110, 301, 62, 350, 62, 350, 108, 359, 108}
        Local lines2 = {0, 203, 22, 203, 22, 236, 63, 236, 63, 184, 105, 184, 105, 233, 160, 233, 160, 195, 205, 195, 205, 221,
        246, 221, 246, 169, 279, 169, 279, 209, 301, 209, 301, 241, 321, 241, 321, 220, 355, 220, 355, 172, 359, 172}

        CreateBrush(3, 360, 284, $D6A242)
        SelectBrush(3)

        For k = 0 To ListItems(lines) - 4 Step 2
                Line(lines[k], lines[k + 1], lines[k + 2], lines[k + 3], #BLACK)
        Next

        For k = 0 To ListItems(lines2) - 4 Step 2
                Line(lines2[k], lines2[k + 1], lines2[k + 2], lines2[k + 3], #BLACK)
        Next

        EndSelect

        FloodFill(3, 0, 0, #BLACK, $E71063)
        FloodFill(3, 0, 283, #BLACK, $427152)

EndFunction

/* init sine scroller */
Function p_InitScroll()

        Local f$ = "ABCDEFGHIJKLMNOPQRSTUVWXYZ.,'!?:=/#-1234567890()$% "  ; format of font.iff
        Local x, y

        chars = {}

        CreateBrush(5, 360, 54, $333333)
        SelectBrush(5)

        ; create a lookup table
        For y = 0 To 2
                For x = 0 To 19
                        DisplayBrushPart(4, x * 16, y * 16, x * 18, y * 18, 16, 16)
                        chars[Asc(MidStr(f$, y * 20 + x, 1))] = {x = x * 18, y = y * 18}
                Next
        Next

        EndSelect

        ; create an alpha shadow for every character
        SelectAlphaChannel(5)
        SetAlphaIntensity(0)
        Cls

        For y = 0 To 2
                For x = 0 To 19
                        SetAlphaIntensity(128)
                        DisplayBrushPart(4, x * 16, y * 16, (x * 18) + 2, (y * 18) + 2, 16, 16)
                        SetAlphaIntensity(255)
                        DisplayBrushPart(4, x * 16, y * 16, x * 18, y * 18, 16, 16)
                Next
        Next

        EndSelect

        Local t$ = "                     ACCEPT NO IMITATIONS..  WE ARE THE WORLD'S #1!      " ..
        "$CRYSTAL$ PROUDLY PRESENTS ANOTHER FINE CRACK: ZOOL, FROM GREMLIN!      " ..
        "NOTE TO A FEW IGNORANT WANNA-BE CRACKING GROUPS: IT'S NOT VERY HARD TO " ..
        "RELEASE GAMES WHEN EVERYONE ELSE IS ON HOLIDAY, IS IT?  LETS SEE HOW MANY " ..
        "PROPER TITLES YOU'LL BE ABLE TO RELEASE NOW THE MORE QUALIFIED GROUPS ARE BACK.  " ..
        "CLAIMING TO BE THE BEST AND FASTEST IS A BIT STUPID WHEN NOONE ELSE COULD BE " ..
        "BOTHERED TO CRACK CERTAIN LAME GAMES, DON'T YOU THINK?  WE HAVE TO ADMIT THOUGH " ..
        "THAT WE'RE HAPPY TO SEE SOME GARBAGE CRACKERS WANTING TO CRACK THE LEFTOVER TITLES.  " ..
        "BUT PLEASE SPARE US THE BOASTING UNTIL YOU HAVE PROVEN ANYTHING!      " ..
        "FOR ALL THE LATEST IN GAMES AND UTILITIES ON AMIGA AND IBM-PC WRITE (PLEASE INCLUDE " ..
        "A PHONE# IF POSSIBLE): PO BOX 2, 8330 BEDER, DENMARK.  ITALIANS WRITE: FERMO POSTA, " ..
        "CI 83429870, MILANO CORDUSIO, ITALY.  UK PEOPLE WRITE: PO BOX 116, UPPER POPPLETON, " ..
        "YORK, YO2 6YY, ENGLAND.        " ..
        "INTRO INCREDIBLE PART HQM BY MELON DEZIGN                                                "

        scrolltextlen = StrLen(t$)
        scrolltext = StrToArray(t$)

EndFunction

/* compute sine lookup table (0 - 2 Pi) */
Function p_MakeSine()

        Local i, s = 0, 2 * #PI / 800

        sine = {}
        sinelen = 800

        For Local k = 0 to sinelen - 1
                sine[k] = Sin(i) * 86 + 86
                i = i + s
        Next

EndFunction

/* this procedure is called 50 times a second */
Function p_MainLoop()

	DisableLineHook()
	
        Local charpos_tmp, scrollpos_tmp, sinepos_tmp = charpos, scrollpos, sinepos
        Local char = scrolltext[scrollpos_tmp]

        charpos = charpos + 2

        ; advance to next position
        If charpos >= 18
            charpos = 0
            scrollpos = scrollpos + 1
            If scrollpos >= scrolltextlen Then scrollpos = 0
        EndIf

        sinepos = sinepos + 4
        If sinepos >= sinelen Then sinepos = sinepos - sinelen

        ; flip the back buffer into view and make the front buffer the back buffer,
        ; so we can render the next frame!
        Flip

        ; now draw the next frame to the back buffer!
        DisplayBrush(3, 0, 0)            ; render bgpic
        DisplayBrush(6, 11, 17)          ; render melon

        ; render sine scroller
        For Local x = 0 To 360 Step 18

            For Local k = 0 To 17

                    charpos_tmp = charpos_tmp + 1

                    if charpos_tmp >= 18
                        charpos_tmp = 0
                        scrollpos_tmp = scrollpos_tmp + 1
                        If scrollpos_tmp >= scrolltextlen Then scrollpos_tmp = 0
                        char = scrolltext[scrollpos_tmp]
                    EndIf

                    DisplayBrushPart(5, chars[char].x + charpos_tmp, chars[char].y, x + k, sine[sinepos_tmp] + 40, 1, 18)

                    sinepos_tmp = sinepos_tmp + 1
                    if sinepos_tmp >= sinelen Then sinepos_tmp = 0
            Next

        Next
        
        EnableLineHook()

EndFunction

p_CreateBG()
p_InitScroll()
p_MakeSine()

PlaySample(1)
PlayMusic(1, 0)

EscapeQuit(True)

; begin double buffering!
BeginDoubleBuffer

Repeat
	p_MainLoop()
	CheckEvent
Forever
