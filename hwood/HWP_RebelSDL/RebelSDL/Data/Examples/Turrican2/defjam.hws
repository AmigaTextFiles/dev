/****************************************************************
**                                                             **
** Name:        Defjam Cracktro "Turrican 2" - a classic       **
** Author:      Mr.X of Barracuda                              **
** Version:     1.1                                            **
** Date:        13.05.17                                       **
** Interpreter: Hollywood 6.0                                  **
** Licence:     Sample program for Hollywood                   **
** Function:    Hollywood port of this nice cracktro           **
**                                                             **
** Notes:       Another classic cracktro brought to modern     **
**              Amigas by Barracuda Team!!!                    **
**                                                             **
** History:                                                    **
**                                                             **
** 1.1: (13.05.17)                                             **
**                                                             **
** - adapted for RebelSDL                                      **
**                                                             **
** 1.0: (05.01.08)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Important! Check if the used Hollywood version is at least
** version 6.0!
*/
@VERSION 6,0

/*
** This script requires the RebelSDL plugin
*/
@REQUIRE "rebelsdl"

@BRUSH 3, "corsair.png", {Transparency = #BLACK}
@BRUSH 4, "font_big.png", {Transparency = #BLACK}
@BRUSH 5, "font_small.png", {Transparency = #BLACK}

@DISPLAY {Width = 360, Height = 284}

/* prepare the background gradient and the color bars */
Function p_CreateBG()

        Local bgcols = { {$213042, $314552, $425563, $526573, $63758C, $738A9C, $8C9AAD, $9CAABD, $ADBACE, $BDCFDE, $CEDFEF, $DEEFFF},
                {$423021, $524531, $635542, $736552, $8C7563, $9C8A73, $AD9A8C, $BDAA9C, $CEBAAD, $DECFBD, $EFDFCE, $FFEFDE},
                {$422031, $523042, $634552, $735563, $8C6573, $9C758C, $AD8A9C, $BD9AAD, $CEAABD, $DEBACE, $EFCFDE, $FFDFEF},
                {$312042, $423052, $524563, $635573, $73658C, $8C759C, $9C8AAD, $AD9ABD, $BDAACE, $CEBADE, $DECFEF, $EFDFFF},
                {$214521, $315531, $426542, $527552, $638A63, $739A73, $8CAA8C, $9CBA9C, $ADCFAD, $BDDFBD, $CEEFCE, $DEFFDE} }

        ; prepare 5 different brushes which will be used later to mask out the sinus scroller
        For Local k = 0 To 4

                Local rev = False
                Local j = 0

                CreateBrush(6 + k, 360, 66)
                SelectBrush(6 + k)

                For Local i = 0 To 21

                        Box(0, i * 3, 360, 3, bgcols[k][j])
                        
                        If rev = False
                                j = j + 1
                        Else
                                j = j - 1
                        EndIf

                        If j = 12
                                j = 10
                                rev = True
                        EndIf
                Next

                EndSelect
        Next

        ; the color bars will cycle through these colors
        cols = {$F70000, $F72000, $F74100, $F76100, $F78200, $F7A200, $F7C300, $F7E300, $F7F300, $E7F300,
        $C6F300, $A5F300, $84F300, $63F300, $42F300, $21F300, $00F300, $00F321, $00F342, $00F363,
        $00F384, $00F3A5, $00F3A5, $00F3C6, $00F3E7, $00F3F7, $00E3F7, $00C3F7, $00A2F7, $0082F7,
        $0061F7, $0041F7, $0020F7, $0000F7, $2100F7, $4200F7, $6300F7, $8400F7, $A500F7, $C600F7,
        $E700F7, $F700F7, $F700E7, $F700C6}

        collength = ListItems(cols)
        start2 = collength - 1

        line1 = 142
        line2 = 143

EndFunction

/* init the sine scroller */
Function p_InitScroll()

        Local f$ = "tabcdefghijklmnopqrs,uvwxyz 0123456789:._!?'-()"   ; format of font_big.iff
        Local x, y

        chars = {}

        ; create lookup table chars{}
        For y = 0 To 2
                For x = 0 To 19
                        chars[Asc(MidStr(f$, y * 20 + x, 1))] = {x = x * 16, y = y * 16}
                Next
        Next

        Local t$ = "                        yoohoo hords of fans !!!                      defjam -  is back with a new release :" ..
        "                    turrican 2  note if you have only one drive remove disk one when screen is " ..
        "flashing and insert disk two and press  left mouse button to continue see ya soon in our next " ..
        "productions                                      "

        scrolltextlen = StrLen(t$)
        scrolltext = StrToArray(t$)

EndFunction

/* init the info text for the end*/
Function p_InitText()

        Local f$ = "abcdefghijklmnopqrstuvwxyz 0123456789()[]<>=+-/*!?&.,'\"A" ; format of font_small.iff
        Local x, y
        Local cols = { {$BD8A52, $633000},          ; two colors for each line: one for text, one for its shadow
                {$CE9A63, $734510},
                {$DEAA73, $8C5521},
                {$EFBA8C, $9C6531},
                {$FFCF9C, $AD7542},
                {$FFDFAD, $AD8A52},
                {$FFEFBD, $AD9A63},
                {$FFFFCE, $ADAA73},
                {$FFFFDE, $ADAA8C},
                {$FFFFEF, $ADAA9C},
                {$EFFFFF, $9CAAAD},
                {$DEFFFF, $8CAAAD},
                {$CEFFFF, $73AAAD},
                {$BDEFFF, $639AAD},
                {$ADDFFF, $528AAD},
                {$9CCFFF, $4275AD},
                {$8CBAEF, $31659C},
                {$73AADE, $21558C},
                {$639ACE, $104573},
                {$528ABD, $003063} }
        Local chars_s = {}

        ; create our lookup table chars_s{}
        For x = 0 To 55
                chars_s[MidStr(f$, x, 1)] = {x = x * 8, y = 0}
        Next

        Local t$ =
                "****************************************" ..
                "*                                      *" ..
                "*            d e f j a m               *" ..
                "*                                      *" ..
                "*        present a new release         *" ..
                "*                                      *" ..
                "*             turrican 2               *" ..
                "*                                      *" ..
                "*                                      *" ..
                "*                                      *" ..
                "*                                      *" ..
                "*                                      *" ..
                "*                                      *" ..
                "*                                      *" ..
                "*                                      *" ..
                "*                                      *" ..
                "*                                      *" ..
                "* press left mouse button to continue. *" ..
                "*                                      *" ..
                "****************************************" ..
                "****************************************" ..
                "*                                      *" ..
                "*                                      *" ..
                "*                                      *" ..
                "*          c a l l                     *" ..
                "*                                      *" ..
                "*                                      *" ..
                "*             o n e                    *" ..
                "*                                      *" ..
                "*                                      *" ..
                "*                 o f                  *" ..
                "*                                      *" ..
                "*                                      *" ..
                "*                    o u r             *" ..
                "*                                      *" ..
                "*                                      *" ..
                "*                        b b s         *" ..
                "*                                      *" ..
                "*                                      *" ..
                "****************************************"

        ; create a brush for each page of text
        CreateBrush(1, 40 * 8, 20 * 9)
        CreateBrush(2, 40 * 8, 20 * 9)

        ; used as a mask
        CreateBrush(11, 8, 7)

        x = 0
        y = 0

        Local dst = 1
        Local r = 0

        ; create the two text pages with nicely colored & shadowed text!
        For Local k = 0 To StrLen(t$) - 1

                If MidStr(t$, k, 1) <> " "

                        ; Hollywood is magic!

                        SelectBrush(11)
                        Box(0, 0, 8, 7, cols[r][1])
                        EndSelect

                        SelectMask(11)
                        SetMaskMode(#MASKINVISIBLE)
                        Cls
                        SetMaskMode(#MASKVISIBLE)
                        DisplayBrushPart(5, chars_s[MidStr(t$, k, 1)].x, 0, 0, 0, 8, 7)
                        EndSelect

                        SelectBrush(dst)
                        DisplayBrush(11, x + 1, y + 1)
                        EndSelect

                        SelectBrush(11)
                        Box(0, 0, 8, 7, cols[r][0])
                        EndSelect

                        SelectBrush(dst)
                        DisplayBrush(11, x, y)
                        EndSelect
                EndIf

                x = x + 8

                ; wrap line
                If x = 320
                        x = 0
                        y = y + 9
                        r = r + 1
                EndIf

                ; wrap page
                If r = 20
                        y = 0
                        r = 0
                        dst = 2
                EndIf
        Next

        ; you can go now
        FreeBrush(11)

EndFunction

/* create our sine lookup table */
Function p_MakeSine()

        Local i, s = 0, 2 * #PI / 350

        sine = {}
        sinelen = 350

        For Local k = 0 to sinelen - 1
                sine[k] = Sin(i) * 25 + 25
                i = i + s
        Next

EndFunction

/* this is called when the user hits LMB or attempts to close the window */
Function p_EndCracktro()

        ; pressing window's close button before sinus is there or after pressing it
        ; twice quits us
        If showtext = True Or line1 <> 12 Then End

        showtext = True
        showbr = 1
        showx = -316

EndFunction

/* this function is called 50 times a second */
Function p_MainLoop()

        Local i, k

        ; flip the back buffer into view and make the front buffer the back buffer,
        ; so we can render the next frame!
        Flip

        ; now draw the next frame to the back buffer!
        Cls

        ; this code will render the sine scroller
        If line1 = 12 And showtext = False

                Local charpos_tmp, scrollpos_tmp, sinepos_tmp = charpos, scrollpos, sinepos
                Local char = scrolltext[scrollpos_tmp]

                ; advance to next positions
                charpos = charpos + 2

                If charpos >= 16
                        charpos = 0
                        scrollpos = scrollpos + 1
                        If scrollpos >= scrolltextlen Then scrollpos = 0
                EndIf

                sinepos = sinepos + 4
                If sinepos >= sinelen Then sinepos = sinepos - sinelen

                SelectMask(6)
                SetMaskMode(#MASKINVISIBLE)
                Cls
                SetMaskMode(#MASKVISIBLE)

                For Local x = 0 To 360 Step 16

                        For Local k = 0 To 15

                                charpos_tmp = charpos_tmp + 1

                                If charpos_tmp >= 16
                                        charpos_tmp = 0
                                        scrollpos_tmp = scrollpos_tmp + 1
                                        If scrollpos_tmp >= scrolltextlen Then scrollpos_tmp = 0
                                        char = scrolltext[scrollpos_tmp]
                                EndIf

                                DisplayBrushPart(4, chars[char].x + charpos_tmp, chars[char].y, x + k, sine[sinepos_tmp], 1, 16)

                                sinepos_tmp = sinepos_tmp + 1
                                If sinepos_tmp >= sinelen Then sinepos_tmp = 0
                        Next
                Next

                EndSelect

                ; now copy the pre-fabricated sinus text onto our colored brushes' masks
                For Local k = 7 To 10
                        SelectMask(k)
                        SetMaskMode(#MASKINVISIBLE)
                        Cls
                        SetMaskMode(#MASKVISIBLE)
                        DisplayBrush(6, 0, 0)
                        EndSelect
                Next

                i = 6

                ; make lots of sinus waves!
                For Local k = 0 To 17
                        DisplayBrush(i, 0, -35 + k * 17)
                        i = Wrap(i + 1, 6, 11)
                Next

                ; check for LMB! Call p_EndCracktro() if pressed!
                If IsLeftMouse() = True Then p_EndCracktro()
        EndIf

        ; this is the code for scrolling the two info pages at the end of the intro in & out
        If showtext = True

                DisplayBrush(showbr, showx, #CENTER)
                
                If showx = 20

                        ; LMB pressed? show next page or quit
                        If IsLeftMouse() = True

                                showrev = True
                                showx = showx - 4
                                If showbr = 1 Then nextbr = True

                        EndIf

                ElseIf showx = -320

                        ; wrap pages
                        If showbr = 1
                                showbr = 2
                                showx = showx + 4
                                showrev = False
                        Else
                                showtext = False
                                revline = True
                        EndIf

                Else

                        ; scroll in or out
                        If showrev = True
                                showx = showx - 4
                        Else
                                showx = showx + 4
                        EndIf

                EndIf
        EndIf

        ; display upper color bar
        i = start1

        For k = 0 To collength - 2
                Line(k * 8, line1, k * 8 + 7, line1, cols[i])
                i = i + 1
                If i = collength Then i = 0
        Next

        Line(k * 8, line1, 359, line1, cols[i])

        start1 = start1 + 1
        If start1 = collength Then start1 = 0

        ; display lower color bar (reverse direction!)
        i = start2

        For k = collength To 1 Step -1
                Line(k * 8 + 7, line2, k * 8, line2, cols[i])
                i = i - 1
                If i = -1 Then i = collength - 1
        Next

        Line(9, line2, 0, line2, cols[i])

        start2 = start2 - 1
        If start2 = -1 Then start2 = collength - 1

        ; display "CORSAIR"
        DisplayBrush(3, 311, line2 - 6)

        ; move the color lines in and out
        If revline = False
                If line1 <> 12 Then line1 = line1 - 1
                If line2 <> 271 Then line2 = line2 + 1
        Else
                line1 = line1 + 1
                line2 = line2 - 1

                If line1 = 143 Then End
        EndIf

EndFunction

SetFillStyle(#FILLCOLOR)

p_CreateBG()
p_InitScroll()
p_MakeSine()
p_InitText()

EscapeQuit(True)

; create a clip region so that the scroller doesn't touch the top&bottom areas
CreateClipRegion(1, #BOX, 0, 12, 360, 260)

; we want to be notified when the user presses the close gadget
InstallEventHandler({CloseWindow = p_EndCracktro})

; begin double buffered output
BeginDoubleBuffer

; make the clip region active!
SetClipRegion(1)

Repeat
	p_MainLoop()
	CheckEvent
Forever

