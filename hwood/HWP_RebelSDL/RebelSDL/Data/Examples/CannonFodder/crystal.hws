/****************************************************************
**                                                             **
** Name:        Cannon Fodder Cracktro by Crystal              **
** Author:      Mr.X of Barracuda                              **
** Version:     1.1                                            **
** Date:        13.05.17                                       **
** Interpreter: Hollywood 6.0                                  **
** Licence:     Sample program for Hollywood                   **
** Function:    Hollywood port of this nice cracktro           **
**                                                             **
** Notes:       Original cracktro by Ted's Farm^Movement       **
**              Say hi to the birdie and bunny!                **
**                                                             **
** History:                                                    **
**                                                             **
** 1.1: (13.05.17)                                             **
**                                                             **
** - adapted for RebelSDL                                      **
**                                                             **
** 1.0: (14.01.10)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Check if Hollywood version is at least 6.0 
*/
@VERSION 6,0

/*
** This script requires the RebelSDL plugin
*/
@REQUIRE "rebelsdl"

/*
** setup a 360x288 display
*/
@DISPLAY {Width = 360, Height = 288, ScaleMode = #SCALEMODE_AUTO, ScaleWidth = 720, ScaleHeight = 576}

/*
** our image data
*/
@BRUSH 2, "tweety.iff", {Transparency = $444455}
@BRUSH 3, "tweety2.iff", {Transparency = $444455}
@BRUSH 4, "font.iff", {Transparency = $000000}
@SPRITE 1, "rabbit.iff", {Width = 16, Height = 25, Frames = 2}

/*
** unfortunately, the original sound is in a format not supported by Hollywood, so
** we have to use a sampled version instead
*/
@MUSIC 1, "crystal.wav"

/* setup graphics */
Function p_MakeGfx()

	; we'll draw the current frame to brush 1
	CreateBrush(1, 360, 288)
	
	; scale birdie by 1000% !!
	ScaleBrush(2, "1000%", "1000%")
	ScaleBrush(3, "1000%", "1000%")

	Local w = GetAttribute(#BRUSH, 2, #ATTRWIDTH)
	Local h = GetAttribute(#BRUSH, 2, #ATTRHEIGHT)
	
	bird = {}
	
	; create an alpha shadow for the birdie (two frames)
	For Local k = 0 To 1
	
		bird[k] = CreateBrush(Nil, w + 6, h + 6, 0, {AlphaChannel = True, Clear = True})
		
		SelectAlphaChannel(bird[k])
		SetAlphaIntensity(80)
		DisplayBrush(2 + k, 6, 6)
		
		SelectBrush(bird[k], #SELMODE_COMBO)
		DisplayBrush(2 + k, 0, 0)
		EndSelect
	Next	
	
	; can free these now
	FreeBrush(2)
	FreeBrush(3)		
	
	; accept no imitations...	
	Local t$ =
		"   ^ C R Y S T A L ^  "..
		"        cracked       "..
		"                      "..
		"     CANNON FODDER    "..
		"     ~~~~~~~~~~~~~    "..
		"                      "..
		"Accept no imitations.."..
		"We are the world's #1!"..

		"For all in software on"..
		"Amiga, PC and consoles"..
		"write (please include "..
		"   a phone-number):   "..
		"                      "..
		"      P.O. Box 2      "..
		"      8330 Beder      "..
		"       Denmark        "..

		"                      "..
		"    UK people write:  "..
		"                      "..
		"     P.O. Box 27DE    "..
		"    Dundee, DD5-3YE   "..
		"       Scotland       "..
		"          UK          "..
		"                      "..
		
		"                      "..
		".oO Intro Fuglebad Oo."..
		"                      "..
		"  by  $ Ted's Farm $  "..
		"     of Movement      "..
		"                      "..
		"Press RETURN to quit!!"..
		"                      "
	
	pagebr = {}
	
	; create the different text pages	
	For Local k = 0 To 3
		
		Local tmp = CreateBrush(Nil, 22 * 8, 8 * 16, 0, {Mask = True, Clear = True})
		
		; create a page template using a masked brush
		For Local y = 0 To 7
			
			For Local x = 0 To 21
				
				Local c = k * 22 * 8 + (y * 22) + x
	
				SelectBrush(tmp)
							
				DisplayBrushPart(4, (Asc(MidStr(t$, c, 1)) - 32) * 8, 0, x * 8, y * 16, 8, 8)
	
				SelectMask(tmp)
				SetMaskMode(#MASKVISIBLE)
				
				DisplayBrushPart(4, (Asc(MidStr(t$, c, 1)) - 32) * 8, 0, x * 8, y * 16, 8, 8)
			Next
		Next
		
		EndSelect
		
		; invert colors (white text becomes black now!)
		Local tmp2 = CopyBrush(tmp, Nil)
		InvertBrush(tmp2)
		
		; our text pages use an alpha shadow and have a border		
		pagebr[k] = CreateBrush(Nil, 22 * 8 + 6, 8 * 16 + 6, 0, {AlphaChannel = True, Clear = True})

		SelectAlphaChannel(pagebr[k])
		SetAlphaIntensity(100)

		; draw alpha shadow		
		For Local y = 3 To 4
			For Local x = 3 To 4
				DisplayBrush(tmp, x, y)
			Next
		Next

		; create a black border around our white text
		; here we're drawing the black text
		For Local y = 0 To 2
			
			For Local x = 0 To 2
				
				SelectBrush(pagebr[k], #SELMODE_COMBO)
				DisplayBrush(tmp2, x, y)
				EndSelect
			Next	
		Next	
	
		; and here we're drawing the white text
		SelectBrush(pagebr[k], #SELMODE_COMBO)
		DisplayBrush(tmp, 1, 1)
		EndSelect		
				
		FreeBrush(tmp)	
		FreeBrush(tmp2)
	Next
	
	; make a flipped copy of the bunny
	CopySprite(1, 2)
	FlipSprite(2, True)
						
EndFunction

Function p_MainLoop()
	
        ; are we currently in bunny boucing mode?
	If bounce > 0
	
		; simply cosine wave is used here
		Local s = Cos(bs)
		
		; swap frames on up and down bouncing	
		DisplaySprite(bounce, bx, 130 + s * -70 + acc, IIf((bs >= #PI) And (bs <= 2 * #PI), 2, 1))
		bx = bx + IIf(bounce = 1, 2, -2)
		bs = bs + 0.1

		; reset cosine wave
		If bs >= 2 * #PI Then bs = bs - 2 * #PI
			
		If (bx >= 360) And (bounce = 1)

                	; right border reached --> start main intro now!
			bounce = 0
			
			fadein = True
			fadelevel = 255
			
			RemoveSprite(1)
			
			StartTimer(1)
			StartTimer(2)

		ElseIf (bx <= -20) And (bounce = 2)	
			
                        ; left border reached --> quit intro!
			Wait(10)
                        End
		EndIf
	
	Else		
	
		SelectBrush(1)
		
		; clear background
		Cls(0)
		
		; offset to start
		p = scroll
		
		; plot sine wave
		For Local x = 0 To 359
		
			Local y = 150 + Sin(p) * scale
 					
			Plot(x, y, $444477)
	
			p = p + s
		Next

		; transform sine wave
		If inc = True
			scale = scale + 0.25
			If scale = 45 Then inc = False
		Else
			scale = scale - 0.25
			If scale = -45 Then inc = True
		EndIf
	
		EndSelect
	
		; fill sine wave
		FloodFill(1, 0, 0, $444477, $444455)
		FloodFill(1, 0, 287, $444477, $444477)
	
		; switch birdie frames every 1/2 second
		If GetTimer(1) >= 500
			frame = Not frame
			ResetTimer(1)
		EndIf	
	
		; flip text every 6 seconds
		If GetTimer(2) >= 6000
			text = text + 1
			If text = 4 Then text = 0
			ResetTimer(2)
		EndIf	
		
		; draw birdie & text
		SelectBrush(1)		
		DisplayBrush(bird[frame], 20, #CENTER)
		DisplayBrush(pagebr[text], 180, #CENTER)
		EndSelect
		
		; check for exit keys
		If (IsLeftMouse() = True) Or (IsKeyDown("RETURN") = True) Then fadeout = True
		
		; are we in fade mode?
		If (fadein = True) Or (fadeout = True)
			
			; fade!
			TintBrush(1, $000000, fadelevel)
			
			If fadein = True
				
				fadelevel = fadelevel - 5
								
				If fadelevel < 0
					fadelevel = 0
					fadein = False
				EndIf				
				
			Else
				fadelevel = fadelevel + 5
				
				If fadelevel = 255 Then bounce = 2
				If fadelevel > 255 Then fadelevel = 255
			EndIf
		EndIf
	
		; now display our work brush containing the current frame		
		DisplayBrush(1, 0, 0)
	
		; scroll sine wave
		scroll = scroll - 0.01
	EndIf
		
EndFunction

EscapeQuit(True)

; make graphics
p_MakeGfx()

; initialize effect
s = (2 * #PI) / 300
scroll = 0.01

bounce = 1
inc = True

; play music
PlayMusic(1, 0)

; and kick it off!
Repeat
	p_MainLoop()
	CheckEvent
Forever	

	