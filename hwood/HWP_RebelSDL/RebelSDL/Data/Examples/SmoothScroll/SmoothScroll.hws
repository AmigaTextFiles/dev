/****************************************************************
**                                                             **
** Name:        SmoothScroll                                   **
** Author:      Andreas Falkenhahn                             **
** Version:     1.1                                            **
** Date:        13.05.17                                       **
** Interpreter: Hollywood 6.0                                  **
** Licence:     Sample program                                 **
** Function:    Demonstrates hardware-accelerated 2D drawing   **
**              with RebelSDL                                  **
**                                                             **
** History:                                                    **
**                                                             **
** 1.1: (13.05.17)                                             **
**                                                             **
** - adapted for RebelSDL                                      **
**                                                             **
** 1.0: (24.05.15)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Make sure we have at least Hollywood 6.0!
*/
@VERSION 6,0


/*
** This script requires the RebelSDL plugin
*/
@REQUIRE "rebelsdl"


/*
** Load images as hardware brushes for fast drawing
*/
@BRUSH 1, "scroll1.png", {Hardware = True}
@BRUSH 2, "scroll2.png", {Hardware = True}
@BRUSH 3, "scroll3.png", {Hardware = True}
@BRUSH 4, "scroll4.png", {Hardware = True}
@BRUSH 5, "scroll5.png", {Hardware = True}


/*
** Display configuration
*/ 
@DISPLAY {Color = #WHITE, Title = "Smooth scroll"}

EscapeQuit(True)

scrollers = {}
speedtable = {4, 3, 2, 3, 4}

; initialize background scrollers
For Local k = 0 To 4
	scrollers[k] = {xpos = Int(RndF() * 640), xpos2 = 0, ypos = 0, speed = speedtable[k], width = GetAttribute(#BRUSH, k + 1, #ATTRWIDTH)}
	If k > 0 Then scrollers[k].ypos = scrollers[k-1].ypos + GetAttribute(#BRUSH, k, #ATTRHEIGHT)
Next		

; setup a hardware doublebuffer managed by GL Galore	
BeginDoubleBuffer(True)

Repeat	

	For Local k = 0 To 4
		scrollers[k].xpos = scrollers[k].xpos - scrollers[k].speed
		If scrollers[k].xpos <= -scrollers[k].width Then scrollers[k].xpos = scrollers[k].xpos2 - scrollers[k].speed
		
		If scrollers[k].xpos <= 0
			scrollers[k].xpos2 = scrollers[k].xpos + scrollers[k].width
		Else
			scrollers[k].xpos2 = scrollers[k].xpos - scrollers[k].width
		EndIf		
	Next

	Cls(#WHITE)

	For Local k = 0 To 4
		DisplayBrush(k + 1, scrollers[k].xpos, scrollers[k].ypos) 
		DisplayBrush(k + 1, scrollers[k].xpos2, scrollers[k].ypos)
	Next
	
	; swap buffers		
	Flip
	
	CheckEvent
			
Forever	

