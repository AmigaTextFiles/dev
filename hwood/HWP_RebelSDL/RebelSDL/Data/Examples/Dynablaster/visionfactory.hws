/****************************************************************
**                                                             **
** Name:        Vision Factory Cracktro "Dynablaster"          **
** Author:      Mr.X of Barracuda                              **
** Version:     1.2                                            **
** Date:        13.05.17                                       **
** Interpreter: Hollywood 6.0                                  **
** Licence:     Sample program for Hollywood                   **
** Function:    Hollywood port of this nice cracktro           **
**                                                             **
** Notes:       This one uses the famous chessboard zoomer     **
**              looks impressive but is just a simple trick!   **
**              This is a straight backport of a disassembled  **
**              version of the original cracktro so don't      **
**              wonder about variable names like d0 or d1 :-)  **
**                                                             **
** History:                                                    **
**                                                             **
** 1.2: (13.05.17)                                             **
**                                                             **
** - adapted for RebelSDL                                      **
**                                                             **
** 1.1: (02.01.15)                                             **
**                                                             **
** - uses Disable/EnableLineHook() now for better performance  **
**                                                             **
** 1.0: (05.01.08)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Important! Check if the used Hollywood version is at least
** version 6.0!
*/
@VERSION 6,0

/*
** This script requires the RebelSDL plugin
*/
@REQUIRE "rebelsdl"

@DISPLAY {Width = 320, Height = 200}

@BRUSH 3, "font.png", {Transparency = #BLACK}
@BRUSH 6, "logo.png", {Transparency = #BLACK}

@MUSIC 1, "freeman.mod"

/* initialize lists for the checker board */
Function p_MakeList()

        For Local k = 0 To ListItems(List) - 1 Do List[k] = List[k] * 12
        
        bufz = {}
        
        For Local k = 0 To ListItems(List2) - 1 Do bufz[k] = List2[k] * 56 * 2
        
EndFunction

/* make "ROG" brush */
Function p_MakeROG()

        Local rog$  = {
                "**** **** ****",
                "*  * *  * *   ",
                "**** *  * * **",
                "* *  *  * *  *",
                "*  * **** ****" }

        CreateBrush(7, 14, 5, $CECFCE)

        SelectMask(7)
        SetMaskMode(#MASKINVISIBLE)
        Cls
        SetMaskMode(#MASKVISIBLE)

        For Local y = 0 To 4
                For Local x = 0 To 14
                        If MidStr(rog$[y], x, 1) = "*" Then Plot(x, y)
                Next
        Next

        EndSelect

EndFunction

/* init the sine scroller */
Function p_InitScroll()

        Local f$ = " ABCDEFGHIJKLMNOPQRSTUVWXYZ1234567890.=:,'?-*_/()+" ; format of font.iff
        Local x

        chars = {}

        ; create lookup table chars{}
        For x = 0 To 49
                chars[Asc(MidStr(f$, x, 1))] = {x = x * 16, y = 0}
        Next
        
        Local t$ = "                         VISION FACTORY PROVIDES YOU WITH :  " ..
                "DYNABLASTERS ORIGIANL SUPPLIED BY G.O.D.            VISION REGARDS "..
                "TO :      TRISTAR AND RED SECTOR , CRYSTAL , FAIRLIGHT , NEMESIS , "..
                "BITSTOPPERS AND ALL WE FORGOT THIS TIME SPECIAL VISION REGARDS TO " ..
                "PHIL/MAIDEN HOOD                    TO CONTACT VISION FACTORY CALL " ..
                "ONE OF OUR BULLETIN BOARD SYSTEMS ARROUND THE GLOBE         OR WRITE " ..
                "TO :  s  VISION FACTORY     s   P.O.  BOX 23    s   1040 EVERE 3      " ..
                "s     BELGIUM          GUESS THATS IT      BYE        "
        
        scrolltextlen = StrLen(t$)
        scrolltext = StrToArray(t$)
        
        CopyBrush(3, 4)
        CopyBrush(3, 5)
                
        ReplaceColors(3, {#WHITE, $6345ad})
        ReplaceColors(4, {#WHITE, $8c55bd})
        ReplaceColors(5, {#WHITE, $ad65ce})

EndFunction

/* Hollywood emulation for the 68000's BSET opcode */
Function BSet(bit, val, index)

        bit = bit % 8
        
        Switch index
        Case 0
                bit = bit + 24
        Case 1
                bit = bit + 16
        Case 2
                bit = bit + 8
        EndSwitch
        
        Return(BitSet(val, bit))                        

EndFunction

/* Hollywood emulation for the 68000's BCLR opcode */
Function BClr(bit, val, index)

        bit = bit % 8
        
        Switch index
        Case 0
                bit = bit + 24
        Case 1
                bit = bit + 16
        Case 2
                bit = bit + 8
        EndSwitch
        
        Return(BitClear(val, bit))                      

EndFunction

/* finish line & move cursor to next line */
Function p_NextLine(c, d1)

        Local a3c = 0
        Local a2c = 14

        For Local n = 0 To 13
                d6 = BufChecker[c + a3c]
                a3c = a3c + 1
                d6 = d6 ~ -1
                BufChecker[c + a2c] = d6
                a2c = a2c + 1
        Next

        c = c + 28
        d1 = d1 + 1

        Return(c,d1)

EndFunction

/* init the chessboard effect */
Function p_InitChecker()

        Local k = 0
        Local d1, d2, d3, d4, d5, d7

        BufChecker = {}

        For Local i = 0 To 5600-1 Do BufChecker[i] = 0

        For Local i = 1 To 200  
                For Local j = 0 To 6 Do BufChecker[k+j] = -1
                k = k + 28
        Next

        d1 = 1
        
        Local c = 0
        
        For Local i = 1 To 200
                
                d5 = d1
                d2 = 56 * 2 * 2
                d7 = d2
                        
                Repeat
                
                        d3 = d2
                        d4 = d3
                        d3 = d3 \ 8
        
                        d4 = BitComplement(d4) & 0xff
        
                        BufChecker[c + (d3 \ 4)] = BSet(d4, BufChecker[c + (d3 \ 4)], d3 % 4)

                        d3 = d7
                        d4 = d7
                        d3 = d3 \ 8
        
                        d4 = BitComplement(d4) & 0xff
        
                        BufChecker[c + (d3 \ 4)] = BClr(d4, BufChecker[c + (d3 \ 4)], d3 % 4)                   

                        d2 = d2 + 1
                        d7 = d7 - 1
        
                        If d2 > 56 * 2 * 4
                                c, d1 = p_NextLine(c, d1)
                                Break
                        EndIf

                        d5 = d5 - 1
                        If d5 <> -1 Then Continue
                        
                        d2 = d2 + d1
                        d7 = d7 - d1
                        d5 = d1
                
                        If d2 > 56 * 2 * 4      
                                c, d1 = p_NextLine(c, d1)
                                Break   
                        EndIf           
                Forever         
        Next
                        
EndFunction

/* complete the chessboard */
Function p_Complete(br, ly, by)

        Local k = 0
        
        For i = ly To 204
                DisplayBrush(br, 0, i)
                k = k + 1
                If k = by
                        br = IIf(br = 2, 1, 2)
                        k = 0
                EndIf
        Next    
        
        l1 = l1 + 1
        If l1 = end1 Then l1 = 0                                

EndFunction

/* chessboard zoomer */
Function p_Zoom()
       
        Local d2 = List2[l2]
        Local d1 = List[l1] + 20 * 3 * 4
        Local o = d1 - 12
        Local o2 = d1
        Local ly = 0
        Local d3 = d2
        
        ; NB: the loop structure might seem a bit odd, but I wanted to keep as
        ; close to the original assembler code as possible

        Repeat
        
                If o <= 0
                        Break
                Else
                        DisplayBrush(1, 0, ly)                          
                        ly = ly + 1
                        o = o - 12
                        d3 = d3 - 1
                        If d3 <> -1 Then Continue
                EndIf
        
                d3 = d2
        
                Repeat
                        If o <= 0
                                Break(2)
                        Else
                                ; got the pattern, we can now complete it and exit
                                p_Complete(2, ly, ly)
                                Return
                        EndIf
                        
                        d3 = d2
                        Break
                Forever
        Forever 

        d3 = d2
                
        Local by = ly
                
        Repeat
        
                If o2 >= 2460
                        Break
                Else
                        DisplayBrush(2, 0, ly)  
                        ly = ly + 1
                        o2 = o2 + 12
                        d3 = d3 - 1
                        If d3 <> -1 Then Continue
                EndIf
                
                d3 = d2
                
                Repeat
                        If o2 >= 2460
                                Break(2)
                        Else
                                ; got the pattern, we can now complete it and exit
                                p_Complete(1, ly, ly - by)
                                Return
                        EndIf
                        
                        d3 = d2
                        Break
                Forever
        Forever                                                                                 
        
        l1 = l1 + 1
        If l1 = end1 Then l1 = 0
        
EndFunction

/* draw the two checker lines */
Function p_DoChecker()

        Local o = Bufz[l2] \ 4
        
        For Local j = 1 To 2

                SelectBrush(j)
        
                Local x = 0
        
                For Local k = 0 To 13
                        For Local i = 31 To 0 Step -1
                                If BitTest(BufChecker[o + k], i) = j - 1
                                        Plot(x, 0, $939393)
                                Else
                                        Plot(x, 0, #WHITE)
                                EndIf
                                x = x + 1
                        Next
                Next
        
                EndSelect
        Next

        l2 = l2 + 1
        lb = lb + 1
        
        If l2 = end2
                l2 = 0
                lb = 0
        EndIf
        
        l3 = l3 + 1
        
        If l3 = end3 Then l3 = 0
        
EndFunction

/* create our sine lookup table */
Function p_MakeSine()

        Local i, s = 0, 2 * #PI / 300

        sine = {}
        sinelen = 300

        For Local k = 0 to sinelen - 1
                sine[k] = Sin(i) * 80 + 90
                i = i + s
        Next

EndFunction

/* render sine scroller */
Function p_RenderSine(br, charpos_tmp, sinepos_tmp, scrollpos_tmp, finetune)

        Local char = scrolltext[scrollpos_tmp]

        If char = 's' Then char = ' '

        For Local x = 0 To 360 Step 16

            For Local k = 0 To 15

                    charpos_tmp = charpos_tmp + 1

                    if charpos_tmp >= 16
                            charpos_tmp = 0
                            scrollpos_tmp = scrollpos_tmp + 1
                            If scrollpos_tmp >= scrolltextlen Then scrollpos_tmp = 0
                            char = scrolltext[scrollpos_tmp]
                            If char = 's' Then char = ' '
                    EndIf

                    DisplayBrushPart(br, chars[char].x + charpos_tmp, chars[char].y, x + k, sine[sinepos_tmp] + finetune, 1, 16)

                    sinepos_tmp = sinepos_tmp + 1
                    if sinepos_tmp >= sinelen Then sinepos_tmp = 0
            Next

        Next

EndFunction
        
/* this loop is called 50 times a second */
Function p_MainLoop()

	DisableLineHook()
	
        Local i, k
        Local old_charpos, old_sinepos, old_scrollpos = charpos, sinepos, scrollpos

        ; flip the back buffer into view and make the front buffer the back buffer,
        ; so we can render the next frame!
        Flip

        ; render and calculate next checker frame
        p_DoChecker
        p_Zoom

        ; display "ROG" brush
        DisplayBrush(7, 302, 193)

        ; calculate sine scroller
        If hold = False

                ; advance to next positions
                charpos = charpos + 2

                If charpos >= 16
                    charpos = 0
                    scrollpos = scrollpos + 1

                    ; put scroller on hold!
                    If scrolltext[scrollpos] = 's'
                        hold = True
                        StartTimer(1)
                    EndIf

                    If scrollpos >= scrolltextlen Then scrollpos = 0
                EndIf

        Else
                If GetTimer(1) >= 2000 Then hold = False
        EndIf

        sinepos = sinepos + 4
        If sinepos >= sinelen Then sinepos = sinepos - sinelen  

        DisplayBrush(6, #CENTER, #CENTER)
        
        ; render scroller's edges
        p_RenderSine(5, old_charpos, old_sinepos, old_scrollpos, 0)
        p_RenderSine(4, old_charpos, old_sinepos, old_scrollpos, 1) 
        p_RenderSine(3, old_charpos, old_sinepos, old_scrollpos, 2)
        
        EnableLineHook()
        
EndFunction

; tables for the chessboard zoom (ripped directly from the original assembler code)
List = {$002c,$002e,$0030,$0032,$0035,$0037,$0039,$003b,$003d,$0040,
        $0042,$0044,$0045,$0047,$0049,$004b,$004c,$004e,$004f,$0050,
        $0052,$0053,$0054,$0055,$0055,$0056,$0057,$0057,$0057,$0057,
        $0057,$0057,$0057,$0057,$0056,$0056,$0055,$0054,$0054,$0053,
        $0051,$0050,$004f,$004e,$004c,$004a,$0049,$0047,$0045,$0043,
        $0041,$003f,$003d,$003b,$0039,$0037,$0034,$0032,$0030,$002d,
        $002b,$0029,$0027,$0024,$0022,$0020,$001e,$001b,$0019,$0017,
        $0015,$0013,$0011,$000f,$000e,$000c,$000a,$0009,$0008,$0006,
        $0005,$0004,$0003,$0002,$0001,$0001,$0000,$0000,$0000,$0000,
        $0000,$0000,$0000,$0000,$0001,$0001,$0002,$0003,$0004,$0005,
        $0006,$0007,$0008,$000a,$000b,$000d,$000f,$0010,$0012,$0014,
        $0016,$0018,$001a,$001c,$001f,$0021,$0023,$0025,$0028,$002a}

List2 = {$0000,$0001,$0002,$0003,$0004,$0005,$0006,$0007,$0008,$0009,
         $000a,$000b,$000c,$000d,$000e,$000f,$0010,$0011,$0012,$0013,
         $0014,$0015,$0016,$0017,$0018,$0019,$001a,$001b,$001c,$001d,
         $001e,$001f,$0020,$0021,$0022,$0023,$0024,$0025,$0026,$0027,
         $0028,$0029,$002a,$002b,$002c,$002d,$002e,$002f,$0030,$0031,
         $0032,$0033,$0034,$0035,$0036,$0037,$0038,$0039,$003a,$003b,
         $003c,$003d,$003e,$003f,$0040,$0041,$0042,$0043,$0044,$0045,
         $0046,$0047,$0048,$0049,$004a,$004b,$004c,$004d,$004e,$004f,
         $0050,$0051,$0052,$0053,$0054,$0055,$0056,$0057,$0058,$0059,
         $005a,$005b,$005c,$005d,$005e,$005f,$0060,$0061,$0062,$0062,
         $0062,$0063,$0064,$0065,$0066,$0067,$0068,$0069,$006a,$006b,
         $006c,$006d,$006e,$006f,$0070,$0071,$0072,$0072,$0073,$0074,
         $0075,$0076,$0077,$0078,$0079,$007a,$007b,$007c,$007d,$007e,
         $007f,$0080,$0081,$0082,$0083,$0084,$0085,$0086,$0087,$0088,
         $0089,$008a,$008b,$008c,$008d,$008e,$008f,$0090,$0091,$0092,
         $0093,$0094,$0095,$0096,$0097,$0098,$0099,$009a,$009b,$009c,
         $009d,$009e,$009f,$00a0,$00a1,$00a2,$00a3,$00a4,$00a5,$00a6,
         $00a7,$00a8,$00a9,$00aa,$00ab,$00ac,$00ad,$00ae,$00af,$00b0,
         $00b1,$00b2,$00b3,$00b4,$00b5,$00b6,$00b7,$00b8,$00b9,$00ba,
         $00bb,$00bc,$00bd,$00be,$00bf,$00c0,$00c1,$00c2,$00c3,$00c4,
         $00c5,$00c5,$00c6,$00c6,$00c6,$00c6,$00c6,$00c6,$00c6,$00c6,
         $00c7,$00c6,$00c5,$00c4,$00c3,$00c2,$00c1,$00c0,$00bf,$00be,
         $00bd,$00bc,$00bb,$00ba,$00b9,$00b8,$00b7,$00b6,$00b5,$00b4,
         $00b3,$00b2,$00b1,$00b0,$00af,$00ae,$00ad,$00ac,$00ab,$00aa,
         $00a9,$00a8,$00a7,$00a6,$00a5,$00a4,$00a3,$00a2,$00a1,$00a0,
         $009f,$009e,$009d,$009c,$009b,$009a,$0099,$0098,$0097,$0096,
         $0095,$0094,$0093,$0092,$0091,$0090,$008f,$008e,$008d,$008c,
         $008b,$008a,$0089,$0088,$0087,$0086,$0085,$0084,$0083,$0082,
         $0081,$0080,$007f,$007e,$007d,$007c,$007b,$007a,$0079,$0078,
         $0077,$0076,$0075,$0074,$0073,$0072,$0071,$0070,$006f,$006e,
         $006d,$006c,$006b,$006a,$0069,$0068,$0067,$0066,$0065,$0064,
         $0063,$0062,$0061,$0060,$005f,$005e,$005d,$005c,$005b,$005a,
         $0059,$0057,$0056,$0055,$0054,$0053,$0052,$0051,$0050,$004f,
         $004e,$004d,$004c,$004b,$004a,$0049,$0048,$0047,$0046,$0045,
         $0044,$0043,$0042,$0041,$0040,$003f,$003e,$003d,$003c,$003b,
         $003a,$0039,$0038,$0037,$0036,$0035,$0034,$0033,$0032,$0031,
         $0030,$002f,$002e,$002d,$002c,$002b,$002a,$0029,$0028,$0027,
         $0026,$0025,$0024,$0023,$0022,$0021,$0020,$001f,$001e,$001d,
         $001c,$001b,$001a,$0019,$0018,$0017,$0016,$0015,$0014,$0013,
         $0012,$0011,$0010,$000f,$000e,$000d,$000c,$000b,$000a,$0009,
         $0008,$0007,$0006,$0005,$0004,$0003,$0002,$0001,$0000}

; two lines for the chessboard effect
CreateBrush(1, 56 * 8 * 2 + 16 * 8, 1)
CreateBrush(2, 56 * 8 * 2 + 16 * 8, 1)
        
p_MakeSine()
p_MakeList()
p_MakeROG()
p_InitScroll()
p_InitChecker()

end1 = ListItems(List)
end2 = ListItems(List2)

EscapeQuit(True)

; start music
PlayMusic(1)

; begin double buffering
BeginDoubleBuffer

; and go!
Repeat
	p_MainLoop()
	CheckEvent
Forever
        
