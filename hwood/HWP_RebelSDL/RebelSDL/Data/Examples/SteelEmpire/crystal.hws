/****************************************************************
**                                                             **
** Name:        Crystal Cracktro "Steel Empire" - a deluxe one **
** Author:      Mr.X of Barracuda                              **
** Version:     1.2                                            **
** Date:        13.05.17                                       **
** Interpreter: Hollywood 6.0                                  **
** Licence:     Sample program for Hollywood                   **
** Function:    Hollywood port of this nice cracktro           **
**                                                             **
** Notes:       This port does not use the original music      **
**              because that is in an exotic format. Instead   **
**              of it we use a nice Protracker chippy :-)      **
**                                                             **
** History:                                                    **
**                                                             **
** 1.2: (13.05.17)                                             **
**                                                             **
** - adapted for RebelSDL                                      **
**                                                             **
** 1.1: (02.01.15)                                             **
**                                                             **
** - uses Disable/EnableLineHook() now for better performance  **
**                                                             **
** 1.0: (10.01.08)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Important! Check if the used Hollywood version is at least
** version 6.0!
*/
@VERSION 6,0

/*
** This script requires the RebelSDL plugin
*/
@REQUIRE "rebelsdl"

@BRUSH 1, "font.png"
@BRUSH 3, "crystal.png", {Transparency = #RED}
@MUSIC 1, "jump_and_run_2.mod"

@DISPLAY {Width = 360, Height = 284}
        
/* build our sine table */
Function p_MakeSine()

        tabsin = {}

        Local i = 0
        Local s = #PI / 180

        For Local k = 0 To 179
                tabsin[k] = Sin(i) * 256
                tabsin[180+k] = -tabsin[k]
                If k < 90 Then tabsin[360+k] = tabsin[k]
                i = i + s
        Next

EndFunction

/* render text to our brush */
Function p_RenderText(br, t$)

        Local lines = 1
        Local chars, linelen = 0, 0

        ; count lines
        For Local k = 0 To StrLen(t$) - 1
                If MidStr(t$, k, 1) = "\n"
                        linelen = Max(linelen, chars)
                        chars = 0
                        lines = lines + 1
                Else
                        chars = chars + 1
                EndIf
        Next

        ; determine maximum line length
        linelen = Max(linelen, chars)

        ; create a brush big enough for all characters
        CreateBrush(br, linelen * 8, lines * 8)
        SelectBrush(br)

        Local x, y = 0, 0

        ; draw individual characters to our brush
        For Local k = 0 To StrLen(t$) - 1

                Local c = Asc(MidStr(t$, k, 1))

                If c = '\n'
                        x = 0
                        y = y + 8
                Else
                        DisplayBrushPart(1, (c - 32) * 8, 0, x, y, 8, 8)
                        x = x + 8
                EndIf

        Next

        EndSelect

EndFunction

/* init the scroller text */
Function p_InitText()

        Local t$ =
                "\n" ..
                "\n" ..
                "        CRYSTAL cracked:\n" ..
                "\n" ..
                "          STEEL EMPIRE\n" ..
                "          ||||||||||||\n" ..
                "\n" ..
                "\n" ..
                "\n" ..
                " Personal regards to John Lennon:\n" ..
                " nice meeting you last Friday! At\n" ..
                " first I really didn't believe it\n" ..
                " was you, but when you started to\n" ..
                " sing a realtime improvised song,\n" ..
                " I got convinced. Guess it's only\n" ..
                " guys like you and Jesus that can\n" ..
                " rise from the dead! I am really\n" ..
                " looking forward to see your new\n" ..
                " forthcoming albums..\n" ..
                "\n" ..
                "\n" ..
                "\n" ..
                "\n" ..
                "        Always remember:\n" ..
                "\n" ..
                "           adults can\n" ..
                "\n" ..
                "            also be\n" ..
                "\n" ..
                "            afraid!\n" ..
                "\n" ..
                "\n" ..
                "\n" ..
                "\n" ..
                " This production carries the fine\n" ..
                " {CRYSTAL} seal of quality..\n" ..
                "\n" ..
                "\n" ..
                "\n" ..
                "\n" ..
                "  Make impact at these addresses:\n" ..
                "\n" ..
                "         a) P.O. Box 2\n" ..
                "            8330 Beder\n" ..
                "             Denmark\n" ..
                "\n" ..
                "       b)  Fermo Posta\n" ..
                "           C.I.83429870\n" ..
                "         Milano Cordusio\n" ..
                "              Italy\n" ..
                "\n" ..
                "a) For getting all the latest in\n" ..
                "   games and utilities on Amiga\n" ..
                "   and IBM-PC. Please include a\n" ..
                "   phone-number if possible!\n" ..
                "b) Replaces a) in Italy.\n" ..
                "\n" ..
                "\n" ..
                "  Also make sure you call this\n" ..
                "  BBS - Italy's finest and best,\n" ..
                "  and one of the absoulte best\n" ..
                "  boards in Europe too!\n" ..
                "\n" ..
                "   *InsomniA*: +39-2-27002619\n" ..
                "\n" ..
                "\n" ..
                "\n" ..
                "\n" ..
                "      Accept no imitations..\n"..
                "      We are the world's #1!\n"

        p_RenderText(2, t$)
        p_RenderText(4, "Guess")
        p_RenderText(5, "who")
        p_RenderText(6, "cracked")
        p_RenderText(7, "STEEL EMPIRE")

        For Local k = 4 To 7 Do ReplaceColors(k, {#WHITE, #BLUE})

        scrollwidth = GetAttribute(#BRUSH, 2, #ATTRWIDTH)
        scrollheight = GetAttribute(#BRUSH, 2, #ATTRHEIGHT)

        FreeBrush(1)

        SetBrushTransparency(2, #BLACK)

EndFunction

/* init rotating cube animation */
Function p_InitAnim()

        anim = {3,1,
                0,1,1,2,2,3,3,0,
                3,1,
                5,4,4,7,7,6,6,5,
                3,2,
                4,5,5,1,1,0,0,4,
                3,2,
                3,2,2,6,6,7,7,3,
                3,3,
                4,0,0,3,3,7,7,4,
                3,3,
                1,5,5,6,6,2,2,1}

        coord = {-80,-80,80,
                80,-80,80,
                80,80,80,
                -80,80,80,
                -80,-80,-80,
                80,-80,-80,
                80,80,-80,
                -80,80,-80}

        BufCoord = {}
        For Local k = 0 To 23 Do BufCoord[k] = 0

        Local c = 2
        
        For Local k = 0 To 5
        
                For Local i = 0 To 7            
                        anim[c] = anim[c] * 3
                        c = c + 1
                Next    
                
                c = c + 2
        Next

        ; speed and position settings
        IncX = 4
        IncY = 8
        IncZ = 6
        pX = 300
        pY = 400
        pZ = 100
                
EndFunction

/* rotate cubes */
Function p_DoRotation()

        Local d3, d4, d5, d6, d7
        Local c = 0
        Local bc = 0
        
        pX = pX + IncX
        If pX >= 4 * 180 Then pX = pX - 4 * 180
        
        pY = pY + IncY
        If pY >= 4 * 180 Then pY = pY - 4 * 180
        
        pZ = pZ + IncZ
        If pZ >= 4 * 180 Then pZ = pZ - 4 * 180         

        Local x = pX \ 2
        Local y = pY \ 2
        Local z = pZ \ 2
        
        For Local k = 0 To 7
        
                d3 = Coord[c]
                c = c + 1
                d4 = Coord[c]
                c = c + 1
                d5 = Coord[c]
                c = c + 1

                d6 = tabsin[90 + x] * d3
                d7 = tabsin[x] * d4
                
                d6 = (d6 - d7) \ 256

                d4 = tabsin[90 + x] * d4
                d3 = tabsin[x] * d3
                d4 = (d4 + d3) \ 256
                
                d3 = d4
                d7 = d5

                d3 = tabsin[90 + y] * d3
                d7 = tabsin[y] * d7
                d3 = (d3 - d7) \ 256
                
                BufCoord[bc + 1] = d3
        
                d5 = tabsin[90 + y] * d5
                d4 = tabsin[y] * d4
                d5 = (d5 + d4) \ 256
                
                d3 = d6
                d7 = d5
                                
                d7 = tabsin[90 + z] * d7
                d6 = tabsin[z] * d6
                d7 = (d7 - d6) \ 256
                
                BufCoord[bc + 2] = d7
                
                d3 = tabsin[90 + z] * d3
                d5 = tabsin[z] * d5
                d3 = (d5 + d3) \ 256
                
                BufCoord[bc] = d3
                
                bc = bc + 3
        Next
        
EndFunction

/* calculate coordinates */
Function p_DoCoords()

        Local c = 0
        Local d1, d2

        For Local k = 0 To 7
        
                d1 = BufCoord[c] \ 4
                d2 = BufCoord[c + 1] \ 4
               
                If d1 % 2 Then d1 = d1 + 1
                If d2 % 2 Then d2 = d2 + 1

                BufCoord[c] = d1 \ 2
                BufCoord[c+1] = d2 \ 2
                
                c = c + 3
        Next
                
EndFunction

/* and draw the cubes */
Function p_DrawCubes()

        Local x1, y1, x2, y2, x3, y3
        Local quitloop = False
        Local ac = 0
        
        While quitloop = False
        
                offset = anim[ac + 2]
                
                x1 = BufCoord[offset]
                y1 = BufCoord[offset + 1]
                
                offset = anim[ac + 4]
                
                x2 = BufCoord[offset]
                y2 = BufCoord[offset + 1]
                
                offset = anim[ac + 6]
                
                x3 = BufCoord[offset]
                y3 = BufCoord[offset + 1]
                
                d5 = (y3 - y1) * (x2 - x1)
                d4 = (y2 - y1) * (x3 - x1)

                ac = ac + 1
                        
                If d5 >= d4
                
                        ac = ac + 9
                        If ac >= ListItems(anim) Then quitloop = True
                                                
                Else
                
                        Local colsel = ac
                        Local p = {}
                        Local ip = 0
                                                                
                        ac = ac + 1
                       
                        ; create a polygon for each side
                        For Local ix = 0 To anim[ac-2]
                        
                                offset = anim[ac]
                                ac = ac + 1
                        
                                p[ip] = BufCoord[offset] + 32
                                p[ip+1] = BufCoord[offset + 1] + 32
                        
                                offset = anim[ac]
                                ac = ac + 1
                        
                                p[ip+2] = BufCoord[offset] + 32
                                p[ip+3] = BufCoord[offset + 1] + 32
                                
                                ip = ip + 4
                        Next
                        
                        If ip > 0

                                ; draw 8 rows of cubes with 10 cubes per row (total: 80 cubes)
                                For Local y = 0 To 7
                                        For Local x = 0 To 9
                                        
                                                Local cube = y * 10 + x
                                                
                                                ; choose a color
                                                Switch colsel
                                                Case  1: col = cols[cube][0]
                                                Case 11: col = cols[cube][0]
                                                Case 21: col = cols[cube][1]
                                                Case 31: col = cols[cube][1]
                                                Case 41: col = cols[cube][2]
                                                Case 51: col = cols[cube][2]
                                                EndSwitch                                       
                                                
                                                ; draw cube side to double buffer
                                                Polygon(x * 32, y * 32, p, ip \ 2, col)
                                        Next
                                Next
                        Endif
                                                                        
                        If ac >= ListItems(anim) Then quitloop = True                                                                                   
                EndIf
                                
        Wend
        
EndFunction

/* this function is called 50 times per second */
Function p_MainLoop()

	DisableLineHook()
	
        ; flip the back buffer into view and make the front buffer the back buffer,
        ; so we can render the next frame!
        Flip

        ; now draw the next frame to the back buffer!
        Cls

        ; calculate next frame
        p_DoRotation()
        p_DoCoords()

        ; and draw it
        p_DrawCubes()

        ; draw scroll text
        SetClipRegion(1)
        DisplayBrushPart(2, 0, sy, 77, dy, scrollwidth, dheight)
        SetClipRegion(#NONE)

        ; move scroll text
        If dy > 0
                dy = dy - 0.5
                dheight = dheight + 1
        Else
                sy = sy + 0.5
                If sy = scrollheight
                        sy = 0
                        dy = 283
                        dheight = 1
                EndIf
        EndIf

        ; draw "CRYSTAL" logo
        DisplayBrush(3, 22, 14)

	EnableLineHook()
	
EndFunction

; lots of colors for lots of cubes!
cols = {
        {$009a00, $00ba00, $00ff00},
        {$009a21, $00ba21, $00ff21},
        {$009a42, $00ba42, $00ff42},
        {$009a63, $00ba63, $00ff63},
        {$009a73, $00ba73, $00ff73},
        {$009a9c, $00ba9c, $00ff9c},
        {$009abd, $00babd, $00ffbd},
        {$009ace, $00bace, $00ffce},
        {$009aef, $00baef, $00ffef},
        {$009aff, $00baff, $00ffff},

        {$217500, $21aa00, $21df00},
        {$217510, $21aa21, $21df21},
        {$217521, $21aa00, $21df00},
        {$107531, $21aa31, $21df42},
        {$107541, $10aa52, $10df52},
        {$107552, $10aa63, $10df73},
        {$107563, $10aa8c, $10df9c},
        {$007573, $00aaad, $00dfce},
        {$00758c, $00aabd, $00dfef},
        {$00759c, $00aace, $00dfff},

        {$425500, $428a00, $42ba00},
        {$425510, $428a21, $42ba21},
        {$315521, $318a31, $31ba42},
        {$315531, $318a52, $31ba52},
        {$215542, $218a63, $21ba73},
        {$215552, $218a8c, $21ba9c},
        {$105563, $108a9c, $10baad},
        {$105573, $108aad, $10bace},
        {$00558c, $008a8d, $00baef},
        {$00559c, $008ace, $00baff},

        {$424500, $526500, $639a00},
        {$424510, $426521, $529a21},
        {$314521, $426531, $529a42},
        {$314531, $316552, $429a52},
        {$215542, $316563, $319a73},
        {$214552, $21658c, $319a9c},
        {$104563, $10659c, $219aad},
        {$104573, $1065ad, $109ace},
        {$00458c, $0065bd, $109aef},
        {$00459c, $0065ce, $009aff},

        {$423000, $635500, $8c7500},
        {$423010, $525521, $8c7521},
        {$313021, $525531, $737542},
        {$313031, $425552, $637552},
        {$213042, $315563, $527573},
        {$213052, $31558c, $42759c},
        {$103063, $21559c, $3175ad},
        {$103073, $1055ad, $2175ce},
        {$00308c, $0055bd, $1075ef},
        {$00309c, $0055ce, $0075ef},

        {$521000, $733000, $ad5500},
        {$521010, $733021, $9c5521},
        {$421021, $633031, $8c5542},
        {$421031, $523052, $635552},
        {$311042, $423063, $525573},
        {$211052, $42308c, $42559c},
        {$211063, $31309c, $3155ad},
        {$101073, $2130ad, $2155ce},
        {$10108c, $1030bd, $1055ef},
        {$00109c, $0030ce, $0055ff},

        {$730000, $9c1000, $ce3000},
        {$730010, $8c1021, $bd3021},
        {$630021, $731031, $9c3042},
        {$520031, $631052, $8c3052},
        {$420042, $521063, $733073},
        {$420052, $42108c, $52309c},
        {$310063, $31109c, $4230ad},
        {$210073, $2110ad, $2130ce},
        {$10008c, $1010bd, $1030ef},
        {$00009c, $0010ce, $0030ff},

        {$9c0000, $ce0000, $ff0000},
        {$8c0010, $ad0021, $de0021},
        {$730021, $9c0031, $bd0042},
        {$630031, $730052, $ad0052},
        {$520042, $630063, $9c0073},
        {$420052, $52008c, $73009c},
        {$310063, $31009c, $5200ad},
        {$210073, $2100ad, $4200ce},
        {$10008c, $1000bd, $2100ef},
        {$00009c, $0000ce, $0000ff} }

; init stuff
p_InitAnim()
p_InitText()
p_MakeSine()

; a clip region for the scroll text
CreateClipRegion(1, #BOX, 16, 16, 328, 252)

; draw the intro text
sy = 0
dy = 283
dheight = 1
        
EnableLayers

For k = 4 To 7
        DisplayBrush(k, #CENTER, #CENTER)
        Wait(25)
        UndoFX(#BRUSH, k, #CROSSFADE, 8)
        Wait(5)
Next

DisableLayers

; start music
PlayMusic(1)

; we want to draw filled polygons
SetFillStyle(#fillcolor)

EscapeQuit(True)

; start double buffer
BeginDoubleBuffer

; and go
Repeat
	p_MainLoop()
	CheckEvent
Forever

