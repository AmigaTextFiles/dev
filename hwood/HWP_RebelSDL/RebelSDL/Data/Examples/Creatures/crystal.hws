/****************************************************************
**                                                             **
** Name:        Crystal Cracktro "Creatures"                   **
** Author:      Mr.X of Barracuda                              **
** Version:     1.1                                            **
** Date:        13.05.17                                       **
** Interpreter: Hollywood 6.0                                  **
** Licence:     Sample program for Hollywood                   **
** Function:    Hollywood port of this nice cracktro           **
**                                                             **
** Notes:       The famous Danish wheels a.k.a. as the         **
**              unstoppable cracking machine :-)               **
**                                                             **
** History:                                                    **
**                                                             **
** 1.1: (13.05.17)                                             **
**                                                             **
** - adapted for RebelSDL                                      **
**                                                             **
** 1.0: (17.01.10)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Important! Check if the used Hollywood version is at least
** version 6.0!
*/
@VERSION 6,0

/*
** This script requires the RebelSDL plugin
*/
@REQUIRE "rebelsdl"

/*
** some image data
*/
@BRUSH 3, "melon.iff", {Transparency = #RED}
@BRUSH 4, "font.iff", {Transparency = $000000}

/*
** the music
*/
@MUSIC 1, "1patternloop1v3.mod"

/*
** give us a 360x288 display as usual
*/
@DISPLAY {Width = 360, Height = 288, Color = $000000}

/* make the text objects */
Function p_MakeText()

	Local t$ = 
		"                                           "..
		"     The unstoppable cracking machine!     "..
		"                                           "..
		"          * C  R  Y  S  T  A  L *          "..
		"                                           "..
		"                  cracked                  "..
		"                                           "..
		"                 CREATURES                 "..
		"                 |||||||||                 "..
		"                                           "..
		"        Ma Baker is the FBI's most         "..
		"        wanted woman! Her photo is         "..
		"       hanging on every post-office        "..	
		"          wall. If you have any            "..
		"       information about this woman,       "..
		"        please contact the nearest         "..
		"              police station!              "..
		"                                           "..
		"          Accept no imitations..           "..
		"          We are the world's #1!           "..
		
		"                                           "..		
		" Tired of all those people and groups that "..
		" come and go? Looking for quick and reli-  "..
		" able trading? No need to look further!    "..
		" Instead, do write to:                     "..
		"                                           "..
		"            a) P.O. Box 2                  "..
		"               8330 Beder                  "..
		"                Denmark                    "..
		"                                           "..
		"     b) Fermo Posta   c) P.O. Box 116      "..
		"       C.I.83429870     Upper Poppleton    "..
		"      Milano Cordusio    York, YO2 6YY     "..
		"           Italy            England        "..
		"                                           "..
		"  a) All in games and utilities on Amiga   "..
		"  and PC. Include phone# if possible!      "..
		"  b) and c) replace a) in Italy and the    "..
		"  United Kingdom respectively..            "..
		"                                           "..		
		
		"                                           "..
		"                                           "..			
		" Call us NOW for the very best deals in    "..
		" Video Game Console Disk back-up Systems   "..
		" for the SUPER NINTENDO/SUPER FAMICOM and  "..
		" MEGADRIVE/GENESIS. We stock Super Wild-   "..
		" card(Magicom 2) for SNES and Super Magic  "..
		" Drive for Megadrive/Genesis consoles.     "..
		" These systems allow the backing-up of     "..
		" all game cartridges to floppy disks and   "..
		" allow them to be reloaded independantly   "..
		" of the original cartridge. The system     "..
		" includes a high quality PC compatable,    "..
		" 3.5\"  HD disk drive. A MUST for all real  "..
		" Console videogame freaks!                 "..
		"                                           "..
		"      0382 739 192        (U.K.)           "..
		"      917-462-5071        (USA ONLY)       "..
		"      ++44 382 739192     (WorldWide)      "..
		"                                           "..		
		
		"                                           "..
		"                                           "..			
		"     Intro Incredible part Danish Wheels   "..
		"              by Melon Dezign !            "..
		"                 ||||||||||||              "..
		"                                           "..
		"  Hardware programming and calculations    "..
		"  by ................ Bannasoft & paleface "..
		"                                           "..
		"  Melon-sprite, font, colors and design    "..
		"  by................. Seen                 "..
		"                                           "..
		"  Music composed and performed             "..
		"  by................. T D K                "..
		"                                           "..
		"                                           "..
		"    This one was for the manager/Melon     "..
		"                                           "..
		"                                           "..
		"                                           "						
	
	pagebr = {}
	
	; create the different text pages	
	For Local k = 0 To 3
		
		Local tmp = CreateBrush(Nil, 43 * 8, 20 * 12, 0, {Mask = True, Clear = True})
		
		; create a page template using a masked brush
		For Local y = 0 To 19
			
			For Local x = 0 To 42
				
				Local c = k * 43 * 20 + (y * 43) + x
	
				SelectBrush(tmp)
						
				DisplayBrushPart(4, (Asc(MidStr(t$, c, 1)) - 32) * 8, 0, x * 8, y * 12, 8, 8)
	
				SelectMask(tmp)
				SetMaskMode(#MASKVISIBLE)
				
				DisplayBrushPart(4, (Asc(MidStr(t$, c, 1)) - 32) * 8, 0, x * 8, y * 12, 8, 8)
			Next
		Next
		
		EndSelect
		
		; invert colors (white text becomes black now!)
		Local tmp2 = CopyBrush(tmp, Nil)
		InvertBrush(tmp2)
		
		; our text pages use an alpha shadow and have a border		
		pagebr[k] = CreateBrush(Nil, 43 * 8 + 6, 20 * 12 + 6, 0, {AlphaChannel = True, Clear = True})

		SelectAlphaChannel(pagebr[k])
		SetAlphaIntensity(100)

		; draw alpha shadow		
		For Local y = 3 To 4
			For Local x = 3 To 4
				DisplayBrush(tmp, x, y)
			Next
		Next

		; create a black border around our white text
		; here we're drawing the black text
		For Local y = 0 To 2
			
			For Local x = 0 To 2
				
				SelectBrush(pagebr[k], #SELMODE_COMBO)
				DisplayBrush(tmp2, x, y)
				EndSelect
			Next	
		Next	
	
		; and here we're drawing the white text
		SelectBrush(pagebr[k], #SELMODE_COMBO)
		DisplayBrush(tmp, 1, 1)
		EndSelect		
				
		FreeBrush(tmp)	
		FreeBrush(tmp2)
	Next
							
EndFunction

/* compute sine tables */
Function p_MakeSin()
	
	Local SineCut = {$00c9,$025b,$03ed,$057e,$070e,$089d,$0a2b,$0bb7,
		$0d41,$0eca,$1050,$11d3,$1354,$14d2,$164c,$17c4,
		$1937,$1aa7,$1c12,$1d79,$1edc,$203a,$2193,$22e7,
		$2435,$257e,$26c1,$27fe,$2935,$2a65,$2b8f,$2cb2,
		$2dcf,$2ee4,$2ff2,$30f9,$31f8,$32ef,$33df,$34c6,
		$35a5,$367d,$374b,$3812,$38cf,$3984,$3a30,$3ad3,
		$3b6d,$3bfd,$3c85,$3d03,$3d78,$3de3,$3e45,$3e9d,
		$3eeb,$3f30,$3f6b,$3f9c,$3fc4,$3fe1,$3ff5,$3fff}
		
	SinSmlI = {}
	SinSmlO = {}
	SinBigI = {}
	SinBigO = {}
	
	Local d4 = 1
	Local d5 = 2
	Local d6 = 0
	Local c = 0
	
	; this code ported straight from assembler... I even still use the register
	; names instead of variables :-)
	For Local k = 0 To 255 	
		
		Local d1 = ((SineCut[d6 \ 2] * d4 * 400) & $ffff0000) >> 16
		d1 = Cast(d1, True, #SHORT)
		SinBigO[c + 256] = d1
		SinBigO[c] = d1
	
		d1 = Sar(d1, 1)
		SinSmlO[c + 256] = d1
		SinSmlO[c] = d1
		
		Local d0 = ((SineCut[d6 \ 2] * d4 * 332) & $ffff0000) >> 16
		d0 = Cast(d0, True, #SHORT)
		SinBigI[c + 256] = d0
		SinBigI[c] = d0
		
		d0 = Sar(d0, 1)
		SinSmlI[c + 256] = d0
		SinSmlI[c] = d0
				
		c = c + 1
		
		d6 = d6 + d5
		If d6 < 0
			d4 = -d4
			d5 = -d5
			d6 = d6 + d5
		ElseIf d6 >= 128	
			d5 = -d5
			d6 = d6 + d5
		EndIf
	Next	
EndFunction		
	
/* create the small cog at x, y */		
Function p_SmallCog(x, y, off)

	Local p = {}
	Local i = 0
	
	off = off \ 2
	
	For Local k = 1 To 8
		
		p[i] = SinSmlI[off]
		p[i+1] = SinSmlI[off + 64]
		i = i + 2
		
		p[i] = SinSmlO[off + 4]
		p[i+1] = SinSmlO[off + 68]
		i = i + 2
		
		p[i] = SinSmlO[off + 12]
		p[i+1] = SinSmlO[off + 76]
		i = i + 2
		
		p[i] = SinSmlI[off + 16]
		p[i+1] = SinSmlI[off + 80]
		i = i + 2
		
		p[i] = SinSmlI[off + 32]
		p[i+1] = SinSmlI[off + 96]
		i = i + 2
		
		off = off + 32
	Next
	
	; draw it as a filled polygon!
	Polygon(x, y, p, i / 2, $BBCCCC)
	
EndFunction

/* create a big cog at x, y */
Function p_BigCog(x, y, off)
	
	Local p = {}
	Local i = 0
	
	off = off \ 2
	
	For Local k = 1 To 16
		
		p[i] = SinBigI[off]
		p[i+1] = SinBigI[off + 64]
		i = i + 2
		
		p[i] = SinBigO[off + 2]
		p[i+1] = SinBigO[off + 66]
		i = i + 2
		
		p[i] = SinBigO[off + 6]
		p[i+1] = SinBigO[off + 70]
		i = i + 2
		
		p[i] = SinBigI[off + 8]
		p[i+1] = SinBigI[off + 72]
		i = i + 2
		
		p[i] = SinBigI[off + 16]
		p[i+1] = SinBigI[off + 80]
		i = i + 2
		
		off = off + 16
	Next
	
	; draw it as a filled polygon!
	Polygon(x, y, p, i / 2, $BBCCCC)	
	
EndFunction
	
/* our main loop which gets called 50 times per second */	
Function p_MainLoop()
	
	; flip back buffer into view	
	Flip
	
	If scrollout = False
		
		; clear background
		Cls($DDEEDD)

		; reset cogs
		If pos = 0 Then SinPnts = {34,00,20,20,24}	
	
		; draw cogs
		p_SmallCog(100, 70, SinPnts[0])
		SinPnts[0] = (SinPnts[0] + 4) & $3f

		p_SmallCog(10, 50, SinPnts[1])
		SinPnts[1] = (SinPnts[1] + 4) & $3f		
		
		p_SmallCog(314, 256, SinPnts[4])
		SinPnts[4] = (SinPnts[4] + 4) & $3f	
					
		p_SmallCog(304, 55, SinPnts[4])
				
		p_SmallCog(231, -3, SinPnts[1])
								
		p_BigCog(210, 161, SinPnts[2])
		SinPnts[2] = (SinPnts[2] - 2) & $1f
	
		p_BigCog(40, 239, SinPnts[3])
		SinPnts[3] = (SinPnts[3] + 2) & $1f

		pos = pos + 1
		If pos = 16 Then pos = 0
	
		; draw current page
		If p <> -1 Then DisplayBrush(pagebr[p], #CENTER, #CENTER)

		; switch to next page every 10 secs
		If GetTimer(1) >= 10000
			p = p + 1
			If p = 4 Then p = 0
			ResetTimer(1)
		EndIf
	
		; check for exit key
		If (IsLeftMouse() = True) Then fadeout = True
		
		; are we in fade mode?
		If (fadein = True) Or (fadeout = True)
			
			; fade!
			Box(0, 0, 360, 288, ARGB(255 - fadelevel, $000000))

			If fadein = True
				
				fadelevel = fadelevel - 5
								
				If fadelevel < 0
					fadelevel = 0
					fadein = False
					p = 0
				EndIf				
				
			Else
				fadelevel = fadelevel + 5
				
				SetMusicVolume(1, ((255 - fadelevel) / 255) * 64)				
				
				If fadelevel = 255 Then scrollout = True
				If fadelevel > 255 Then fadelevel = 255
			EndIf
		EndIf
	Else
		Cls(0)
	EndIf
	
	; draw "Melon" logo		
	DisplayBrush(3, logox, 5)
	
	; scroll out Melon logo
	If scrollout = True
		logox = logox + 10
		If logox >= 400 Then End
	EndIf
				
EndFunction

; initialization
p_MakeSin()
p_MakeText()

SetFillStyle(#FILLCOLOR)

; scroll in Melon logo
MoveBrush(3, #RIGHTOUT, 5, 10, 5, 10)
 
; start timer for switching pages
StartTimer(1)

; init variables
fadelevel = 255
fadein = True
logox = 10
p = -1

EscapeQuit(True)

BeginDoubleBuffer

; start music
PlayMusic(1)

; and let's go!
Repeat
	p_MainLoop
	CheckEvent
Forever
