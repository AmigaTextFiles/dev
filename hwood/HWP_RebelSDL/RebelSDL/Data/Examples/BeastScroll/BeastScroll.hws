/****************************************************************
**                                                             **
** Name:        BeastScroll                                    **
** Author:      Andreas Falkenhahn                             **
** Version:     1.1                                            **
** Date:        13.05.17                                       **
** Interpreter: Hollywood 6.0                                  **
** Licence:     Sample program for Hollywood                   **
** Function:    Demonstrates hardware accelerated drawing with **
**              the RebelSDL plugin                            **
**                                                             **
** Notes:       Based on code by Chris Handley and aNa|0Gue    **
**              Graphics & music (C) by Psygnosis 1989         **
**                                                             **
** History:                                                    **
**                                                             **
** 1.1: (13.05.17)                                             **
**                                                             **
** - completely new demo based on code by Chris Handley from   **
**   PortablE and aNa|0Gue (BlitzBasic)                        **
**                                                             **
** 1.0: (05.02.12)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Important! Check if the used Hollywood version is at least
** version 6.0!
*/
@VERSION 6,0


/*
** This script requires the RebelSDL plugin
*/
@REQUIRE "rebelsdl"


/*
** some external data
*/
@BRUSH 1, "nuages0.bmp", {Transparency = $ff00ff, Hardware = True}
@BRUSH 2, "nuages1.bmp", {Transparency = $ff00ff, Hardware = True}
@BRUSH 3, "nuages2.bmp", {Transparency = $ff00ff, Hardware = True}
@BRUSH 4, "nuages3.bmp", {Transparency = $ff00ff, Hardware = True}
@BRUSH 5, "nuages4.bmp", {Transparency = $ff00ff, Hardware = True}
@BRUSH 6, "montagnes.bmp", {Transparency = $ff00ff, Hardware = True}
@BRUSH 7, "herbe0.bmp", {Hardware = True}
@BRUSH 8, "herbe1.bmp", {Hardware = True}
@BRUSH 9, "herbe2.bmp", {Hardware = True}
@BRUSH 10, "herbe3.bmp", {Hardware = True}
@BRUSH 11, "herbe4.bmp", {Hardware = True}
@BRUSH 12, "barriere.bmp", {Transparency = $ff00ff, Hardware = True}
@BRUSH 13, "lune.bmp", {Transparency = $ff00ff}
@BRUSH 14, "baum2.bmp", {Transparency = 0, Hardware = True}
@BRUSH 15, "beast.png", {Transparency = 0, Hardware = True}


/*
** give us a 576x400 screen
*/
@DISPLAY {Width = 576, Height = 400}


/*
** music
*/
@MUSIC 1, "title.mod"


/*
** display brush as a tile
*/
Function DisplayBrush_Tiled(br, x, y)

	Local width = GetAttribute(#BRUSH, br, #ATTRWIDTH)
	Local height = GetAttribute(#BRUSH, br, #ATTRHEIGHT)

	x = Int(x) % width
		
	If x > 0 Then DisplayBrushPart(br, width - x, 0, 0, y, x, height)	
	DisplayBrush(br, x, y)
	
EndFunction


/*
** update the screen
*/
Function p_Update()

	iScroll = iScroll + 2
	tPos = tPos + 4

	; draw background
	DisplayBrush(16, 0, 0)
	
	; draw clouds
	DisplayBrush_Tiled(1, iScroll, 0)	
	DisplayBrush_Tiled(2, iScroll / 2, 44)
	DisplayBrush_Tiled(3, iScroll / 3, 126)
	DisplayBrush_Tiled(4, iScroll / 4, 164)
	DisplayBrush_Tiled(5, iScroll / 5, 182)

	; draw mountains
	DisplayBrush_Tiled(6, iScroll / 2, 194)				
	
	; draw grass
	DisplayBrush_Tiled(7, iScroll, 340)
	DisplayBrush_Tiled(8, iScroll * 2, 344)
	DisplayBrush_Tiled(9, iScroll * 3, 350)
	DisplayBrush_Tiled(10, iScroll * 4, 364)
	DisplayBrush_Tiled(11, iScroll * 5, 378)
		
	; draw gate
	DisplayBrush_Tiled(12, iScroll * 6, 358)
		
	; draw trees
	If tPos >= 576 Then tPos = -576 - GetAttribute(#BRUSH, 14, #ATTRWIDTH)
	DisplayBrush(14, tPos, 0)
	
	; draw player
	DisplayBrushPart(15, (frame + 5) * 64, 0, #CENTER, 244, 64, 104)
	If GetTimer(1) >= 100
		frame = frame + 1
		If frame = 7 Then frame = 1
		ResetTimer(1)
	EndIf
					
	; flip the back buffer into view
	Flip

EndFunction

; double all pixels
For Local k = 1 To 15
	ScaleBrush(k, "200%", "200%")
Next

SetFillStyle(#FILLCOLOR)

; draw copper background
CreateBrush(17, 576, 400, 0)
SelectBrush(17)
Box(0, 0, 576, 152, RGB(99,113,132))
DisplayBrush(13, 368, 32)
Box(0, 152, 576, 54, RGB(115,113,132))
Box(0, 206, 576, 28, RGB(132,113,132))
Box(0, 234, 576, 20, RGB(148,113,132))
Box(0, 254, 576, 16, RGB(165,113,132))
Box(0, 270, 576, 14, RGB(181,113,132))
Box(0, 284, 576, 12, RGB(198,113,132))
Box(0, 296, 576, 12, RGB(214,113,132))
Box(0, 308, 576, 8, RGB(231,113,132))
Box(0, 316, 576, 12, RGB(247,113,132))
EndSelect	

; turn background into a hardware brush
CopyBrush(17, 16, {Hardware = True})
FreeBrush(17)

tPos = -1200
frame = 1

StartTimer(1)

EscapeQuit(True)

; start music
PlayMusic(1)

; create a hardware double buffer
BeginDoubleBuffer(True)

; main loop
Repeat
	p_Update()
	CheckEvent
Forever

