/****************************************************************
**                                                             **
** Name:        Hoodlum Cracktro "Turrican 3"                  **
** Author:      Mr.X of Barracuda                              **
** Version:     1.1                                            **
** Date:        13.05.17                                       **
** Interpreter: Hollywood 6.0                                  **
** Licence:     Sample program for Hollywood                   **
** Function:    Hollywood port of this nice cracktro           **
**                                                             **
** Notes:       Now, who the f*** is "The Black Cat"? At least **
**              he knows how to code a cracktro :-)            **
**                                                             **
** History:                                                    **
**                                                             **
** 1.1: (13.05.17)                                             **
**                                                             **
** - adapted for RebelSDL                                      **
**                                                             **
** 1.0: (05.01.08)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Important! Check if the used Hollywood version is at least
** version 6.0!
*/
@VERSION 6,0

/*
** This script requires the RebelSDL plugin
*/
@REQUIRE "rebelsdl"

@BRUSH 1, "font.png", {Transparency = #BLACK}
@BRUSH 6, "hoodlum.png"

@MUSIC 1, "HeatBeat.mod"

@DISPLAY {Width = 360, Height = 270}

/* initialize color bars */
Function p_InitBG()

        ; this gradient is displayed above and below the info text
        linecols = {$000031, $000063, $00008C, $0000CE, $313063, $63659C, $8C8ACE, $ADAAFF}

        ; the color bars will cycle through these colors
        barcols = {$000010, $000021, $000031, $000042, $000052, $000063, $101073, $21208C,
                $31309C, $4245AD, $5255BD, $6365CE, $7375DE, $8C8AEF, $9C9AFF, $ADAAFF, $BDBAFF,
                $ADAAFF, $9C9AFF, $8C8AEF, $7375DE, $6365CE, $5255BD, $4245AD, $31309C,
                $21208C, $101073, $000063, $000052, $000042, $000031, $000021, $000010,
                $000000, $000000, $000000, $000000, $000000, $000000, $000000, $000000,
                $000000, $000000, $000000, $000000 }

        ; the scroll text will cycle through these colors
        Local tcols = {$0000BD, $0000EF, $1010FF, $2120FF, $4245FF, $5255FF, $6365FF, $7375FF, $8C8AFF, $9C9AFF, $ADAAFF, $CECFFF, $DEDFFF, $FFFFFF}

        collength = ListItems(barcols)
        tcollength = ListItems(tcols)

        ; this is the background over which the scroll text will be put
        CreateBrush(5, 360, tcollength * 2 - 1)
        SelectBrush(5)

        For Local k = 0 To tcollength * 2 - 1
                If i >= 0 Then Line(0, k, 360, k, tcols[i])
                If k >= tcollength - 1
                        i = i - 1
                Else
                        i = i + 1
                EndIf
        Next

        EndSelect

EndFunction

/* prepare info text brushes (8 different colors per char) */
Function p_PrepareBrush(n)

        Local icols = {$7375DE, $31309C, $4245AD, $5255BD, $6365CE, $7375DE, $8C8AEF, $9C9AFF}

        SelectMask(n)
        SetMaskMode(#MASKINVISIBLE)
        Cls
        EndSelect

        ; we have 10 lines per page
        SelectBrush(n)
        For Local k = 0 To 79 Do Line(0, k, 360, k, icols[k % 8])
        EndSelect

EndFunction

/* create different gradients for the "INTRO CODED BY THE BLACK CAT" text */
Function p_MakeGrBrush(nr, t$, len, lev)

        Local x = 0

        CreateBrush(nr, len * 8, 8)
        SelectMask(nr)
        SetMaskMode(#MASKINVISIBLE)
        Cls
        SetMaskMode(#MASKVISIBLE)

        For Local k = 0 To len - 1

                If MidStr(t$, k, 1) <> ""

                        DisplayBrushPart(1, chars[MidStr(t$, k, 1)].x, 0, x, 0, 8, 8)

                EndIf

                x = x + 8
        Next

        EndSelect

        ; tint in steps of 16
        TintBrush(nr, #WHITE, lev)

EndFunction

/* this cracktro uses quite a lot of text */
Function p_InitText()

        Local f$ = " !\"#$%&'()*+,-./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[|]^"   ; font format
        Local x, y

        chars = {}

        ; create our lookup table chars{}
        For x = 0 To StrLen(f$) - 1
                chars[MidStr(f$, x, 1)] = {x = x * 8, y = 0}
        Next

        ; this is the text for the two pages
        Local t$ =
                "------------------------------------" ..
                "   - - ->  H.O.O.D.L.U.M.  <- - -   " ..
                "------------------------------------" ..
                "       >> TURRICAN 3 - 100% <<      " ..
                "                                    " ..
                "ORIGINAL SUPPLIED BY THE BLACK CAT! " ..
                "                                    " ..
                "PROTECTION REMOVED BY THE BLACK CAT " ..
                "                                    " ..
                "   SWAP DISKS WHEN SCREEN FLASHES   " ..
                "  FOR THE LATEST ON AMIGA,C64,SNES  " ..
                "------------------------------------" ..
                "PLK 077625 C - 12043 BERLIN- GERMANY" ..
                "------------------------------------" ..
                "  CREDITS FOR THIS SMALL INTRO .. . " ..
                "                                    " ..
                " BORING CODE..........THE BLACK CAT " ..
                " FONT (LOGO).............ANGEL DAWN " ..
                " FAB TUNE..................HEATBEAT " ..
                "------------------------------------"

        CreateBrush(2, 36 * 8, 10 * 8)        ; page 1
        CreateBrush(3, 36 * 8, 10 * 8)        ; page 2
        CreateBrush(4, 36 * 8, 10 * 8)        ; empty page

        SelectBrush(4)
        Box(0, 0, 36 * 8, 10 * 8, $BDBAFF)
        EndSelect

        ; draw color effect onto the pages
        p_PrepareBrush(2)
        p_PrepareBrush(3)

        SetMaskMode(#MASKVISIBLE)

        dst = 2

        x = 0
        y = 0

        ; now draw the text into the pages' masks
        For Local k = 0 To StrLen(t$) - 1

                If MidStr(t$, k, 1) <> ""

                        SelectMask(dst)
                        DisplayBrushPart(1, chars[MidStr(t$, k, 1)].x, 0, x, y, 8, 8)
                        EndSelect

                EndIf

                x = x + 8

                If x = 288
                        x = 0
                        y = y + 8
                        r = r + 1
                EndIf

                If r = 10
                        y = 0
                        r = 0
                        dst = 3
                EndIf
        Next

        brushin = True
        brushiny = 4
        br = 2

        ; make credit text gradients
        Local l$ = "INTRO WAS CODED BY THE BLACK CAT"
        Local ll = StrLen(l$)

        For Local k = 1 To 16 Do p_MakeGrBrush(6 + k, l$, ll, k * 16)

EndFunction

/* this loop is executed 50 times per second */
Function p_MainLoop()

        ; flip the back buffer into view and make the front buffer the back buffer,
        ; so we can render the next frame!
        Flip

        ; now draw the next frame to the back buffer!
        Cls

        ; display "HOODLUM" logo
        DisplayBrush(6, #CENTER, 26)

        ; display gradients above and below the info page
        For Local k = 0 To 7
                Line(0, 80 + k, 360, 80 + k, linecols[k])
                Line(0, 172 + k, 360, 172 + k, linecols[7-k])
        Next

        ; clear info page
        Box(0, 88, 360, 84, $BDBAFF)

        If brushin = True

                ; display a pages with a shuffling effect as in the original cracktro

                If br = 4 Then DisplayBrush(oldbr, 36, 90)

                ; shuffle bits into view
                For Local k = 0 To 9
                        DisplayBrushPart(br, 0, k * 8 + brushiny, 36, 90 + k * 8 + brushiny, 36 * 8, 4 - brushiny + 1)
                        DisplayBrushPart(br, 0, k * 8 + 4, 36, 90 + k * 8 + 4, 36 * 8, Limit(4 - brushiny + 1, 0, 4))
                Next

                If brushinc = 10
                        brushiny = brushiny - 1
        
                        If brushiny = -1
                                If br = 4
                                        br = nextbr
                                        brushiny = 4
                                Else
                                        brushin = False
                                EndIf
                        EndIf

                        brushinc = 0
                Else
                        brushinc = brushinc + 1
                EndIf

        Else

                ; no effect, just paint the plain text brush
                DisplayBrush(br, 36, 90)

        EndIf

        ; next page?
        If GetTimer(1) >= 5000
                oldbr = br
                nextbr = IIf(br = 2, 3, 2)
                br = 4
                brushin = True
                brushinc = 0
                brushiny = 4
                StartTimer(1)
        EndIf

        ; render color bars (we have 4 bars)
        For Local n = 0 To 3

                i = bars[n].start

                If bars[n].dir = 0

                        For Local k = 0 To collength - 2
                                Line(k * 8, bars[n].ypos, k * 8 + 7, bars[n].ypos, barcols[i])
                                i = i + 1
                                If i = collength Then i = 0
                        Next

                        Line(k * 8, bars[n].ypos, 359, bars[n].ypos, barcols[i])

                        bars[n].start = bars[n].start + 1
                        If bars[n].start = collength Then bars[n].start = 0

                Else

                        For Local k = collength To 1 Step -1
                                Line(k * 8 + 7, bars[n].ypos, k * 8, bars[n].ypos, barcols[i])
                                i = i - 1
                                If i = -1 Then i = collength - 1
                        Next

                        Line(9, bars[n].ypos, 0, bars[n].ypos, barcols[i])

                        bars[n].start = bars[n].start - 1
                        If bars[n].start = -1 Then bars[n].start = collength - 1

                EndIf
        Next

        ; the scrollers will be clipped
        SetClipRegion(1)

        ; switch the scrolling speed of both scrollers
        If speedc = 30

                If scrollers[0].speed = 8
                        scrollers[1].speed = 8
                        scrollers[0].speed = 2
                Else
                        scrollers[0].speed = 8
                        scrollers[1].speed = 2
                EndIf

                speedc = 0
        EndIf

        ; and render them to our double buffer!
        For Local k = 0 To 1

                Local vischars = ((294 - scrollers[k].x) / 8) + 1       ; visible characters
                Local pos = scrollers[k].pos

                SelectMask(5)
                SetMaskMode(#MASKINVISIBLE)
                Cls
                SetMaskMode(#MASKVISIBLE)

                For Local i = 0 To vischars - 1
                        DisplayBrushPart(1, chars[MidStr(scrollers[k].text$, pos, 1)].x, 0, 28 + scrollers[k].x + i * 8, scrollers[k].cpos, 8, 8)
                        pos = Wrap(pos + 1, 0, scrollers[k].len)
                Next

                EndSelect

                DisplayBrushPart(5, 28 + scrollers[k].x, scrollers[k].cpos, 28 + scrollers[k].x, scrollers[k].y, 294, 8)

                If scrollers[k].cposc = 50
                        scrollers[k].cpos = Wrap(scrollers[k].cpos + 1, 0, 20)
                        scrollers[k].cposc = 0
                Else
                        scrollers[k].cposc = scrollers[k].cposc + 1
                EndIf

                scrollers[k].x = scrollers[k].x - scrollers[k].speed
                If scrollers[k].x <= -8
                        scrollers[k].x = 0
                        scrollers[k].pos = Wrap(scrollers[k].pos + 1, 0, scrollers[k].len)
                EndIf
        Next

        speedc = speedc + 1

        ; remove clip region
        SetClipRegion(#NONE)

        ; display "INTRO CODED BY THE BLACK CAT" every now and then
        If docredit <> 0
                
                DisplayBrush(docredit, #CENTER, 240)

                If revcredit = True
                        docredit = docredit - 1
                        If docredit = 6
                                docredit = 0
                                StartTimer(2)
                        EndIf
                Else
                        docredit = docredit + 1
                        If docredit = 23
                                revcredit = True
                                docredit = 22
                        EndIf
                EndIf

        Else

                If GetTimer(2) >= 6000
                        docredit = 7
                        revcredit = False
                EndIf

        EndIf

EndFunction

SetFillStyle(#FILLCOLOR)

p_InitBG()
p_InitText()

; create a clip region for the scrollers
CreateClipRegion(1, #BOX, 28, 189, 294, 34)

; use a table for the two scrollers so we can have them easily handled by the same code
scrollers = {
        {pos = 0, speed = 2, x = 294, y = 190, cpos = 7, cposc = 0,
        text$ = " A RUSH ... A PUSH ... AND THERE IS NO TIME TO FEAR CAUSE - HOODLUM - ARE HERE!" ..
                "               WE ARE SEARCHING FOR MORE BACKUP CRACKERS AND PEOPLE TO CODE US INTROS!" ..
                " CALL SANCTUARY AND LEAVE A MAIL TO THE HOODLUM LEADERS ...             AND   > ALWAYS <" ..
                "  REMEMBER PROTECT OUR ENVIROMENT! AND STOP DESTROYING IT!                              " ..
                "C YA IN OUR NEXT RELEASE!                    "},
        {pos = 0, speed = 6, x = 294, y = 214, cpos = 0, cposc = 25,
        text$ = " CONTACT  HOODLUM WRITE TO   PLK 077625 C - 12043 BERLIN - GERMANY ... HOODLUM  - P.O.BOX 907 " ..
                "- LONDON SW11 6 TL - UNITED KINGDOM  ... HOODLUM  P.O.BOX 106 - DOWNERS GROVE IL - UNITED STATES " ..
                "OF AMERICA                           "} }

; initialize scroller lengths
scrollers[0].len = StrLen(scrollers[0].text$)
scrollers[1].len = StrLen(scrollers[1].text$)

; use a table for the bars also
bars = {
        {start = 0, ypos = 188, dir = 0},
        {start = collength \ 2, ypos = 199, dir = 1},
        {start = collength \ 2, ypos = 212, dir = 0},
        {start = 0 , ypos = 223, dir = 1} }

StartTimer(1)
StartTimer(2)

; start music
PlayMusic(1)

EscapeQuit(True)

; and go
BeginDoubleBuffer

Repeat
	p_MainLoop()
	CheckEvent
Forever

 
 

