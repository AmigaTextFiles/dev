;
; $VER: RebelSDL Setup 1.1 (27.09.20)
;
; Done by Andreas Falkenhahn
;

(message (cat "\nWelcome to the Setup utility for\n\n"
	"RebelSDL 1.1\n\n"
	"Copyright (C) 2014-2020 by Andreas Falkenhahn"
))

(welcome)

(set basepath
	(askdir
		(prompt  "Where do you want to have RebelSDL installed? (a drawer will be created)")
		(help    @askdir-help)
		(default "SYS:")
	)
)

(set sstr (substr basepath (- (strlen basepath) 1) 1))
(if (or (= sstr "/") (= sstr ":"))
	(set destpath (cat basepath "RebelSDL"))
	(set destpath (cat basepath "/RebelSDL"))
)

(set defaultplat 0)

(if (exists "SYS:MorphOS/C/Assign") (
	(set defaultplat 1)
)) 

(set whichver
	(askchoice
		(prompt  "Please select your platform.")
		(help    @askoptions-help)
		(choices "AmigaOS 4" "MorphOS (PowerSDL)" "MorphOS (SDL2)")
		(default defaultplat)		
	)
)

(if (= whichver 0) (
	(set #platform "AmigaOS4")
))

(if (= whichver 1) (
	(set #platform "MorphOS_PowerSDL")
))

(if (= whichver 2) (
	(set #platform "MorphOS")
))

(set @default-dest destpath)

(makedir destpath)
(makedir "LIBS:Hollywood")

(copylib
	(source (cat "Platforms/" #platform "/rebelsdl.hwp"))
	(dest   "LIBS:Hollywood")
	(prompt "rebelsdl.hwp")
	(help @copylib-help)
	(optional "nofail" "force" "askuser")
	(confirm)
)

(copyfiles
	(source  "Data.icon")
	(dest    basepath)
	(newname "RebelSDL.info")
	(optional force oknodelete)
)

(copyfiles
	(source "Data")
	(dest   destpath)
	(all)
	(optional "nofail" "force" "askuser")
)

(if (exists "Hollywood:GUI" (noreq)) (

	(makedir "Hollywood:Examples/RebelSDL")

	(copyfiles
		(source "Data/Examples")
		(dest   "Hollywood:Examples/RebelSDL")
		(all)
		(optional "nofail" "force" "askuser")
	)
))

(set #cmdline (cat "Assign RebelSDL: \"" destpath "\""))

(startup
	(prompt  "\nAn assign has to be inserted in your user-startup. Please click Continue.")
	(help    @startup-help)
	"RebelSDL"
	(command #cmdline)
	(confirm)
)

(run #cmdline)

(message (cat "\nRebelSDL comes with many ready-to-run examples.\n\n"
	"The examples have been integrated into your Hollywood GUI (only "
	"if Hollywood is installed on this system). Now that you have installed "
	"RebelSDL, there will be a new RebelSDL tab in the Hollywood GUI which "
	"allows you to conveniently launch the example scripts that come with RebelSDL."))

(message (cat "\nTo view RebelSDL's documentation, use Workbench or Ambient to go to the drawer "
	"where you installed RebelSDL. Alternatively, you can also launch RebelSDL's "
	"documentation from the \"Plugins\" dialog of the Hollywood GUI.\n\n"
	"For any questions you might have, please join the official forums at\n\n"
	"http://www.hollywood-mal.com/\n\n"
	"Thank you for installing RebelSDL! Have fun with this plugin!"))

(exit)
