/****************************************************************
**                                                             **
** Name:        Slideshow                                      **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        29.07.17                                       **
** Interpreter: Hollywood 7.0                                  **
** Licence:     BSD license                                    **
** Function:    Demonstrates PDF's slideshow capabilities      **
**                                                             **
** Notes:       Based on a libharu example program written by  **
**              Takeshi Kanno <takeshi_kanno@est.hi-ho.ne.jp>  ** 
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (29.07.17)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

@REQUIRE "polybios"

@DISPLAY {Hidden = True}

Function p_Link(page, Lf, Rt, Str, PageRef)
     
	page:BeginText()
     	page:TextOut(Lf, 50, Str)
      	page:EndText()

      	Local rect = {left = Lf, right = Rt, top = 70, bottom = 50}
      	Local dst = PageRef:CreateDestination()
      	dst:SetFit()
      	Local annot = page:CreateLinkAnnot(rect, dst)
      	annot:SetLinkAnnotBorderStyle(0, 0, 0)
      	annot:SetLinkAnnotHighlightMode(#HPDF_ANNOT_INVERT_BOX)
 
EndFunction
    
Function p_PrintPage(page, style, font, page_prev, page_next)

	Local r = RndF()
    	Local g = RndF()
    	Local b = RndF()

	page:SetWidth(800)
    	page:SetHeight(600)

    	page:SetRGBFill(r, g, b)

    	page:Rectangle(0, 0, 800, 600)
    	page:Fill()

    	page:SetRGBFill(1 - r, 1 - g, 1 - b)

    	page:SetFontAndSize(font, 30)

    	page:BeginText()
    	page:SetTextMatrix(0.8, 0.0, 0.0, 1.0, 0.0, 0.0)
    	page:TextOut(50, 530, UnrightStr(style, 1))

    	page:SetTextMatrix(1.0, 0.0, 0.0, 1.0, 0.0, 0.0)
    	page:SetFontAndSize(font, 20)
    	page:TextOut(55, 300, "Type \"Ctrl+L\" in order to return from full screen mode.")
	page:EndText()

    	page:SetSlideShow(GetConstant(style), 5.0, 1.0)

    	page:SetFontAndSize(font, 20)

	If page_next Then p_Link(page, 680, 750, "Next =>", page_next)	
	If page_prev Then p_Link(page, 50, 110, "<= Prev", page_prev)
	
EndFunction

Function p_SaveAndView(doc, file$)
	
	file$ = FileRequest("Save PDF to...", "pdf", #REQ_SAVEMODE, "", file$)
	If file$ = "" Then End
		
	doc:SaveToFile(file$)	
	doc:Free()
	
	ExitOnError(False)
   	pdf.OpenDocument(1, file$)
   	Local err = GetLastError()
   	ExitOnError(True)
   	If err Then Return
   		
   	pdf.GetBrush(1, 1, 1)
	   	
   	CreateDisplay(2, {Width = GetAttribute(#BRUSH, 1, #ATTRWIDTH), Height = GetAttribute(#BRUSH, 1, #ATTRHEIGHT), Sizeable = True})
   	OpenDisplay(2)
   	ActivateDisplay(2)
   	
   	DisplayBrush(1, 0, 0)
   	
   	InstallEventHandler({SizeWindow = Function(msg) DisplayBrush(1, 0, 0, {Width = msg.width, Height = msg.height}) EndFunction})
   	
   	Repeat
   		WaitEvent
   	Forever
   		
EndFunction

Local doc = pdf.CreateDocument()

; set compression mode
doc:SetCompressionMode(#HPDF_COMP_ALL)

; create default-font
Local font = doc:GetFont("Courier")

Local StyleList = {
      "#HPDF_TS_WIPE_RIGHT",
      "#HPDF_TS_WIPE_UP",
      "#HPDF_TS_WIPE_LEFT",
      "#HPDF_TS_WIPE_DOWN",
      "#HPDF_TS_BARN_DOORS_HORIZONTAL_OUT",
      "#HPDF_TS_BARN_DOORS_HORIZONTAL_IN",
      "#HPDF_TS_BARN_DOORS_VERTICAL_OUT",
      "#HPDF_TS_BARN_DOORS_VERTICAL_IN",
      "#HPDF_TS_BOX_OUT",
      "#HPDF_TS_BOX_IN",
      "#HPDF_TS_BLINDS_HORIZONTAL",
      "#HPDF_TS_BLINDS_VERTICAL",
      "#HPDF_TS_DISSOLVE",
      "#HPDF_TS_GLITTER_RIGHT",
      "#HPDF_TS_GLITTER_DOWN",
      "#HPDF_TS_GLITTER_TOP_LEFT_TO_BOTTOM_RIGHT",
      "#HPDF_TS_REPLACE"
}

Local page = {}

For J In IPairs(StyleList)
	InsertItem(page, doc:AddPage())
Next
	
For J, Style In IPairs(StyleList)
	; Don't worry about out-of-bound references; p_PrintPage() is expecting them
      	p_PrintPage(page[J], Style, font, RawGet(page, J - 1), RawGet(page, J + 1))
Next

doc:SetPageMode(#HPDF_PAGE_MODE_FULL_SCREEN)

; save the document to a file
p_SaveAndView(doc, "Slideshow.pdf")

; clean up
doc:Free()

End
