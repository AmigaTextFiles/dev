/****************************************************************
**                                                             **
** Name:        JPFont                                         **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        30.07.17                                       **
** Interpreter: Hollywood 7.0                                  **
** Licence:     BSD license                                    **
** Function:    Demonstrate Japanese fonts and custom encoding **
**                                                             **
** Notes:       Based on a libharu example program written by  **
**              Takeshi Kanno <takeshi_kanno@est.hi-ho.ne.jp>  **
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (30.07.17)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

@REQUIRE "polybios"

@DISPLAY {Hidden = True}

Const #PAGE_HEIGHT = 210

Function p_SaveAndView(doc, file$)
	
	file$ = FileRequest("Save PDF to...", "pdf", #REQ_SAVEMODE, "", file$)
	If file$ = "" Then End
		
	doc:SaveToFile(file$)	
	doc:Free()
	
	ExitOnError(False)
   	pdf.OpenDocument(1, file$)
   	Local err = GetLastError()
   	ExitOnError(True)
   	If err Then Return
   		
   	pdf.GetBrush(1, 1, 1)
	   	
   	CreateDisplay(2, {Width = GetAttribute(#BRUSH, 1, #ATTRWIDTH), Height = GetAttribute(#BRUSH, 1, #ATTRHEIGHT), Sizeable = True})
   	OpenDisplay(2)
   	ActivateDisplay(2)
   	
   	DisplayBrush(1, 0, 0)
   	
   	InstallEventHandler({SizeWindow = Function(msg) DisplayBrush(1, 0, 0, {Width = msg.width, Height = msg.height}) EndFunction})
   	
   	Repeat
   		WaitEvent
   	Forever
   		
EndFunction

; this file is in Microsoft codepage 932 encoding
samp_text = FileToString("sjis.txt")

Local doc = pdf.CreateDocument()

; configure pdf-document to be compressed
doc:SetCompressionMode(#HPDF_COMP_ALL)

; declaration for using Japanese font, encoding
doc:UseJPEncodings()
doc:UseJPFonts()

doc:LoadFont("MS Mincho")
doc:LoadFont("MS PMincho")
doc:LoadFont("MS Gothic")
doc:LoadFont("MS PGothic")

detail_font = {}
detail_font[0] = doc:GetFont("MS-Mincho", "90ms-RKSJ-H")
detail_font[1] = doc:GetFont("MS-Mincho,Bold", "90ms-RKSJ-H")
detail_font[2] = doc:GetFont("MS-Mincho,Italic", "90ms-RKSJ-H")
detail_font[3] = doc:GetFont("MS-Mincho,BoldItalic", "90ms-RKSJ-H")
detail_font[4] = doc:GetFont("MS-PMincho", "90msp-RKSJ-H")
detail_font[5] = doc:GetFont("MS-PMincho,Bold", "90msp-RKSJ-H")
detail_font[6] = doc:GetFont("MS-PMincho,Italic", "90msp-RKSJ-H")
detail_font[7] = doc:GetFont("MS-PMincho,BoldItalic", "90msp-RKSJ-H")
detail_font[8] = doc:GetFont("MS-Gothic", "90ms-RKSJ-H")
detail_font[9] = doc:GetFont("MS-Gothic,Bold", "90ms-RKSJ-H")
detail_font[10] = doc:GetFont("MS-Gothic,Italic", "90ms-RKSJ-H")
detail_font[11] = doc:GetFont("MS-Gothic,BoldItalic", "90ms-RKSJ-H")
detail_font[12] = doc:GetFont("MS-PGothic", "90msp-RKSJ-H")
detail_font[13] = doc:GetFont("MS-PGothic,Bold", "90msp-RKSJ-H")
detail_font[14] = doc:GetFont("MS-PGothic,Italic", "90msp-RKSJ-H")
detail_font[15] = doc:GetFont("MS-PGothic,BoldItalic", "90msp-RKSJ-H")

; set page mode to use outlines
doc:SetPageMode(#HPDF_PAGE_MODE_USE_OUTLINE)

; create outline root
root = doc:CreateOutline(Nil, "JP font demo", Nil)
root:SetOpened(True)

For Local i = 0 To 15
	
        ; add a new page object
        Local page = doc:AddPage()

        ; create outline entry
        Local outline = doc:CreateOutline(root, detail_font[i]:GetFontName(), Nil)
        Local dst = page:CreateDestination()
        outline:SetDestination(dst)

        Local title_font = doc:GetFont("Helvetica")
        page:SetFontAndSize(title_font, 10)

        page:BeginText()

        ; move the position of the text to top of the page
        page:MoveTextPos(10, 190)
        page:ShowText(detail_font[i]:GetFontName())

        page:SetFontAndSize(detail_font[i], 15)
        page:MoveTextPos(10, -20)
        page:ShowText("abcdefghijklmnopqrstuvwxyz")
        page:MoveTextPos(0, -20)
        page:ShowText("ABCDEFGHIJKLMNOPQRSTUVWXYZ")
        page:MoveTextPos(0, -20)
        page:ShowText("1234567890")
        page:MoveTextPos(0, -20)

        page:SetFontAndSize(detail_font[i], 10)
        page:ShowText(samp_text)
        page:MoveTextPos(0, -18)

        page:SetFontAndSize(detail_font[i], 16)
        page:ShowText(samp_text)
        page:MoveTextPos(0, -27)

        page:SetFontAndSize(detail_font[i], 23)
        page:ShowText(samp_text)
        page:MoveTextPos(0, -36)

        page:SetFontAndSize(detail_font[i], 30)
        page:ShowText(samp_text)

        Local x,y = page:GetCurrentTextPos()

        ; finish to print text
        page:EndText()

        page:SetLineWidth(0.5)

        Local x_pos = 20
        For Local j = 0 To ByteLen(samp_text) - 1
        	page:MoveTo(x_pos, y - 10)
            	page:LineTo(x_pos, y - 12)
            	page:Stroke()
            	x_pos = x_pos + 30
        Next

        page:SetWidth(x + 20)
        page:SetHeight(#PAGE_HEIGHT)

        page:MoveTo(10, #PAGE_HEIGHT - 25)
        page:LineTo(x + 10, #PAGE_HEIGHT - 25)
        page:Stroke()

        page:MoveTo(10, #PAGE_HEIGHT - 85)
        page:LineTo(x + 10, #PAGE_HEIGHT - 85)
        page:Stroke()

        page:MoveTo(10, y - 12)
        page:LineTo(x + 10, y - 12)
        page:Stroke()
Next

; save the document to a file
p_SaveAndView(doc, "JPFont.pdf")

; clean up
doc:Free()

End

