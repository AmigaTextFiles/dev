/****************************************************************
**                                                             **
** Name:        Logo                                           **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        29.07.17                                       **
** Interpreter: Hollywood 7.0                                  **
** Licence:     MIT license                                    **
** Function:    Draws the Lua logo to a PDF                    **
**                                                             **
** Notes:       Based on a LuaHPDF example program written by  **
**              Kurt Jung                                      ** 
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (29.07.17)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

@REQUIRE "polybios"

@DISPLAY {Hidden = True}

Function p_LogoDraw(doc, page, width, left, bottom)
	
	Local scale = 1 - Sqrt(2) / 2
  	Local radius_lg = width / 3
  	Local diamter_lg = 2 * radius_lg
  	Local radius_sm = radius_lg * (1 - Sqrt(2) / 2)
  	Local diameter_sm = 2 * radius_sm
  	Local ctr_x = left + (width + radius_sm / 2) / 2
  	Local ctr_y = bottom + (width + radius_sm / 2) / 2

  	page:SetRGBFill(0.125, 0.125, 0.5)

	; Planet
  	page:Circle(ctr_x, ctr_y, radius_lg)

	; Moon
  	page:Circle(ctr_x + radius_lg, ctr_y + radius_lg, radius_sm)
  	page:Fill()

  	; Moonshadow
  	page:SetRGBFill(1, 1, 1) ; Text and areas
  	page:Circle(ctr_x + radius_lg - diameter_sm, ctr_y + radius_lg - diameter_sm, radius_sm)
	page:Fill()

	; Orbit
	page:SetLineWidth(3)
	page:SetDash({12}, 1, 0)	
	page:SetRGBStroke(0.75, 0.75, 0.75) ; Lines
	page:Arc(ctr_x, ctr_y, radius_lg + radius_sm, 0, 30)
	page:Stroke()
  	page:Arc(ctr_x, ctr_y, radius_lg + radius_sm, 60, 360)
  	page:Stroke()

	; Text
  	Local font = doc:GetFont("Helvetica")
  	Local str = "Lua"
  	page:SetFontAndSize(font, width / 4.6)
  	Local wd = page:TextWidth(str)
  	page:BeginText()
  	page:TextOut(ctr_x - wd / 2, ctr_y - diameter_sm, str)
  	page:EndText()

EndFunction

Function p_SaveAndView(doc, file$)
	
	file$ = FileRequest("Save PDF to...", "pdf", #REQ_SAVEMODE, "", file$)
	If file$ = "" Then End
		
	doc:SaveToFile(file$)	
	doc:Free()
	
	ExitOnError(False)
   	pdf.OpenDocument(1, file$)
   	Local err = GetLastError()
   	ExitOnError(True)
   	If err Then Return
   		
   	pdf.GetBrush(1, 1, 1)
	   	
   	CreateDisplay(2, {Width = GetAttribute(#BRUSH, 1, #ATTRWIDTH), Height = GetAttribute(#BRUSH, 1, #ATTRHEIGHT), Sizeable = True})
   	OpenDisplay(2)
   	ActivateDisplay(2)
   	
   	DisplayBrush(1, 0, 0)
   	
   	InstallEventHandler({SizeWindow = Function(msg) DisplayBrush(1, 0, 0, {Width = msg.width, Height = msg.height}) EndFunction})
   	
   	Repeat
   		WaitEvent
   	Forever
   		
EndFunction

Local doc = pdf.CreateDocument()

; set compression mode
doc:SetCompressionMode(#HPDF_COMP_ALL)

; Add a new page object.
Local page = doc:AddPage()
Local height = page:GetHeight()
Local width = page:GetWidth()
Local edge = width / 2

p_LogoDraw(doc, page, edge, (width - edge) / 2, (height - edge) / 2)

p_SaveAndView(doc, "Logo.pdf")

doc:Free()

End
