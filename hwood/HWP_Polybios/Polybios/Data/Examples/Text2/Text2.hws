/****************************************************************
**                                                             **
** Name:        Text2                                          **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        29.07.17                                       **
** Interpreter: Hollywood 7.0                                  **
** Licence:     BSD license                                    **
** Function:    Demonstrates various text features             **
**                                                             **
** Notes:       Based on a libharu example program written by  **
**              Takeshi Kanno <takeshi_kanno@est.hi-ho.ne.jp>  ** 
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (29.07.17)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

@REQUIRE "polybios"

@DISPLAY {Hidden = True}

Function p_PrintGrid(doc, page)

	Local height = page:GetHeight()
  	Local width = page:GetWidth()
  	Local font = doc:GetFont("Helvetica")

  	page:SetFontAndSize(font, 5)
  	page:SetGrayFill(0.5)
  	page:SetGrayStroke(0.8)

  	; Draw horizontal lines
  	Local y = 0
  	While y < height
    		If y % 10 = 0
      			page:SetLineWidth(0.5)
    		ElseIf page:GetLineWidth() <> 0.25
    			page:SetLineWidth(0.25)
    		EndIf
    		page:MoveTo(0, y)
    		page:LineTo(width, y)
    		page:Stroke()
    		If (y % 10 = 0) And (y > 0)
      			page:SetGrayStroke(0.5)
      			page:MoveTo(0, y)
      			page:LineTo(5, y)
      			page:Stroke()
      			page:SetGrayStroke(0.8)
    		EndIf
    		y = y + 5
  	Wend

  	; Draw vertical lines
  	Local x = 0
  	While x < width
    		If x % 10 = 0
      			page:SetLineWidth(0.5)
    		ElseIf page:GetLineWidth() <> 0.25
       			page:SetLineWidth(0.25)
		EndIf
    		page:MoveTo(x, 0)
    		page:LineTo(x, height)
    		page:Stroke()
    		If (x % 50 = 0) And (x > 0)
      			page:SetGrayStroke(0.5)
      			page:MoveTo(x, 0)
      			page:LineTo(x, 5)
      			page:Stroke()
      			page:MoveTo(x, height)
      			page:LineTo(x, height - 5)
      			page:Stroke()
      			page:SetGrayStroke(0.8)
    		EndIf
    		x = x + 5
  	Wend

	; Draw horizontal text
  	y = 0
  	While y < height
    		if (y % 10 = 0) And (y > 0)
      			page:BeginText()
      			page:MoveTextPos(5, y - 2)
      			page:ShowText(y)
      			page:EndText()
    		EndIf
    		y = y + 5
  	Wend

	; Draw vertical text
  	x = 0
  	While x < width
    		If (x % 50 = 0) And (x > 0)
      			page:BeginText()
      			page:MoveTextPos(x, 5)
      			page:ShowText(x)
      			page:EndText()
      			page:BeginText()
      			page:MoveTextPos(x, height - 10)
      			page:ShowText(x)
      			page:EndText()
    		EndIf
    		x = x + 5
  	Wend
  	
  	page:SetGrayFill(0)
  	page:SetGrayStroke(0)

EndFunction 

Function p_SaveAndView(doc, file$)
	
	file$ = FileRequest("Save PDF to...", "pdf", #REQ_SAVEMODE, "", file$)
	If file$ = "" Then End
		
	doc:SaveToFile(file$)	
	doc:Free()
	
	ExitOnError(False)
   	pdf.OpenDocument(1, file$)
   	Local err = GetLastError()
   	ExitOnError(True)
   	If err Then Return
   		
   	pdf.GetBrush(1, 1, 1)
	   	
   	CreateDisplay(2, {Width = GetAttribute(#BRUSH, 1, #ATTRWIDTH), Height = GetAttribute(#BRUSH, 1, #ATTRHEIGHT), Sizeable = True})
   	OpenDisplay(2)
   	ActivateDisplay(2)
   	
   	DisplayBrush(1, 0, 0)
   	
   	InstallEventHandler({SizeWindow = Function(msg) DisplayBrush(1, 0, 0, {Width = msg.width, Height = msg.height}) EndFunction})
   	
   	Repeat
   		WaitEvent
   	Forever
   		
EndFunction

samp_txt = "The quick brown fox jumps over the lazy dog. "

Local doc = pdf.CreateDocument()

; add a new page object
page = doc:AddPage()
page:SetSize(#HPDF_PAGE_SIZE_A5, #HPDF_PAGE_PORTRAIT)

; draw grid to the page
;p_PrintGrid(doc, page)

page_height = page:GetHeight()

Local font = doc:GetFont("Helvetica")
page:SetTextLeading(20)

; text_rect method 

; #HPDF_TALIGN_LEFT
rect = {}
rect.left = 25
rect.top = 545
rect.right = 200
rect.bottom = rect.top - 40

page:Rectangle(rect.left, rect.bottom, rect.right - rect.left, rect.top - rect.bottom)
page:Stroke()

page:BeginText()
page:SetFontAndSize(font, 10)
page:TextOut(rect.left, rect.top + 3, "HPDF_TALIGN_LEFT")
page:SetFontAndSize(font, 13)
page:TextRect(rect.left, rect.top, rect.right, rect.bottom, samp_txt, #HPDF_TALIGN_LEFT)
page:EndText()

; #HPDF_TALIGN_RIGHT
rect.left = 220
rect.right = 395

page:Rectangle(rect.left, rect.bottom, rect.right - rect.left, rect.top - rect.bottom)
page:Stroke()

page:BeginText()
page:SetFontAndSize(font, 10)
page:TextOut(rect.left, rect.top + 3, "HPDF_TALIGN_RIGHT")
page:SetFontAndSize(font, 13)
page:TextRect(rect.left, rect.top, rect.right, rect.bottom, samp_txt, #HPDF_TALIGN_RIGHT)
page:EndText()

; #HPDF_TALIGN_CENTER
rect.left = 25
rect.top = 475
rect.right = 200
rect.bottom = rect.top - 40

page:Rectangle(rect.left, rect.bottom, rect.right - rect.left, rect.top - rect.bottom)
page:Stroke()

page:BeginText()
page:SetFontAndSize(font, 10)
page:TextOut(rect.left, rect.top + 3, "HPDF_TALIGN_CENTER")
page:SetFontAndSize(font, 13)
page:TextRect(rect.left, rect.top, rect.right, rect.bottom, samp_txt, #HPDF_TALIGN_CENTER)
page:EndText()

; #HPDF_TALIGN_JUSTIFY
rect.left = 220
rect.right = 395

page:Rectangle(rect.left, rect.bottom, rect.right - rect.left, rect.top - rect.bottom)
page:Stroke()

page:BeginText()

page:SetFontAndSize(font, 10)
page:TextOut(rect.left, rect.top + 3, "HPDF_TALIGN_JUSTIFY")
page:SetFontAndSize(font, 13)
page:TextRect(rect.left, rect.top, rect.right, rect.bottom, samp_txt, #HPDF_TALIGN_JUSTIFY)
page:EndText()

; Skewed coordinate system
page:GSave()

angle1 = 5
angle2 = 10
rad1 = angle1 / 180 * #PI
rad2 = angle2 / 180 * #PI

page:Concat(1, Tan(rad1), Tan(rad2), 1, 25, 350)
rect.left = 0
rect.top = 40
rect.right = 175
rect.bottom = 0

page:Rectangle(rect.left, rect.bottom, rect.right - rect.left, rect.top - rect.bottom)
page:Stroke()

page:BeginText()
page:SetFontAndSize(font, 10)
page:TextOut(rect.left, rect.top + 3, "Skewed coordinate system")

page:SetFontAndSize(font, 13)
page:TextRect(rect.left, rect.top, rect.right, rect.bottom, samp_txt, #HPDF_TALIGN_LEFT)
page:EndText()

page:GRestore()

; Rotated coordinate system
page:GSave()

angle1 = 5
rad1 = angle1 / 180 * 3.141592

page:Concat(Cos(rad1), Sin(rad1), -Sin(rad1), Cos(rad1), 220, 350)
rect.left = 0
rect.top = 40
rect.right = 175
rect.bottom = 0

page:Rectangle(rect.left, rect.bottom, rect.right - rect.left, rect.top - rect.bottom)
page:Stroke()

page:BeginText()
page:SetFontAndSize(font, 10)
page:TextOut(rect.left, rect.top + 3, "Rotated coordinate system")
page:SetFontAndSize(font, 13)
page:TextRect(rect.left, rect.top, rect.right, rect.bottom, samp_txt, #HPDF_TALIGN_LEFT)
page:EndText()

page:GRestore()
    
; text along a circle
page:SetGrayStroke(0)
page:Circle(210, 190, 145)
page:Circle(210, 190, 113)
page:Stroke()

angle1 = 360 / StrLen(samp_txt)
angle2 = 180

page:BeginText()
font = doc:GetFont("Courier-Bold")
page:SetFontAndSize(font, 30)

For Local i = 0 To StrLen(samp_txt) - 1
	
	rad1 = (angle2 - 90) / 180 * #PI
        rad2 = angle2 / 180 * #PI

        x = 210 + Cos(rad2) * 122
        y = 190 + Sin(rad2) * 122

        page:SetTextMatrix(Cos(rad1), Sin(rad1), -Sin(rad1), Cos(rad1), x, y)
        page:ShowText(MidStr(samp_txt, i, 1))
        angle2 = angle2 - angle1
Next

page:EndText()

; save the document to a file
p_SaveAndView(doc, "Text2.pdf")

; clean up
doc:Free()

End
