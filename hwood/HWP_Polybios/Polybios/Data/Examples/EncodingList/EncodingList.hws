/****************************************************************
**                                                             **
** Name:        EncodingList                                   **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        30.07.17                                       **
** Interpreter: Hollywood 7.0                                  **
** Licence:     BSD license                                    **
** Function:    Shows different PDF encodings                  **
**                                                             **
** Notes:       Based on a libharu example program written by  **
**              Takeshi Kanno <takeshi_kanno@est.hi-ho.ne.jp>  **
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (30.07.17)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

@REQUIRE "polybios"

@DISPLAY {Hidden = True}

Const #PAGE_WIDTH = 420
Const #PAGE_HEIGHT = 400
Const #CELL_WIDTH = 20
Const #CELL_HEIGHT = 20
Const #CELL_HEADER = 10

Function p_DrawGraph(page)
	
	; Draw 16 X 15 cells

	; Draw vertical lines
	page:SetLineWidth(0.5)

    	For Local i = 0 To 17
    		
		Local x = i * #CELL_WIDTH + 40

		page:MoveTo(x, #PAGE_HEIGHT - 60)
		page:LineTo(x, 40)
        	page:Stroke()

        	If (i > 0) And (i <= 16)         		
            		page:BeginText()
            		page:MoveTextPos(x + 5, #PAGE_HEIGHT - 75)
            		page:ShowText(UnrightStr(HexStr(i - 1), 1))
			page:EndText()
        	EndIf
        Next

    	; Draw horizontal lines
    	For Local i = 0 To 15

		Local y = i * #CELL_HEIGHT + 40

        	page:MoveTo(40, y)
        	page:LineTo(#PAGE_WIDTH - 40, y)
        	page:Stroke()

		If i < 14			
			page:BeginText()
            		page:MoveTextPos(45, y + 5)
			page:ShowText(UnrightStr(HexStr(15 - i), 1))
            		page:EndText()
        	EndIf
        Next
       
EndFunction

Function p_DrawFonts(page)

	page:BeginText()

    	; Draw all character from 0x20 to 0xFF to the canvas
    	For Local i = 1 To 16
    		
    		For Local j = 1 To 16
    			
			Local y = #PAGE_HEIGHT - 55 -((i - 1) * #CELL_HEIGHT)
			Local x = j * #CELL_WIDTH + 50
			Local c = (i - 1) * 16 + (j - 1)
			
			If c >= 32
				Local a$ = Chr(c, #ENCODING_RAW)
				Local d  = x - page:TextWidth(a$) / 2
				page:TextOut(d, y, a$)
			EndIf
		Next
	Next
	
	page:EndText()

EndFunction

Function p_SaveAndView(doc, file$)
	
	file$ = FileRequest("Save PDF to...", "pdf", #REQ_SAVEMODE, "", file$)
	If file$ = "" Then End
		
	doc:SaveToFile(file$)	
	doc:Free()
	
	ExitOnError(False)
   	pdf.OpenDocument(1, file$)
   	Local err = GetLastError()
   	ExitOnError(True)
   	If err Then Return
   		
   	pdf.GetBrush(1, 1, 1)
	   	
   	CreateDisplay(2, {Width = GetAttribute(#BRUSH, 1, #ATTRWIDTH), Height = GetAttribute(#BRUSH, 1, #ATTRHEIGHT), Sizeable = True})
   	OpenDisplay(2)
   	ActivateDisplay(2)
   	
   	DisplayBrush(1, 0, 0)
   	
   	InstallEventHandler({SizeWindow = Function(msg) DisplayBrush(1, 0, 0, {Width = msg.width, Height = msg.height}) EndFunction})
   	
   	Repeat
   		WaitEvent
   	Forever
   		
EndFunction

Local encodings = {
	"StandardEncoding",
        "MacRomanEncoding",
        "WinAnsiEncoding",
        "ISO8859-2",
        "ISO8859-3",
        "ISO8859-4",
        "ISO8859-5",
        "ISO8859-9",
        "ISO8859-10",
        "ISO8859-13",
        "ISO8859-14",
        "ISO8859-15",
        "ISO8859-16",
        "CP1250",
        "CP1251",
        "CP1252",
        "CP1254",
        "CP1257",
        "KOI8-R",
        "Symbol-Set",
        "ZapfDingbats-Set"}
        
Local doc = pdf.CreateDocument()

; set compression mode
doc:SetCompressionMode(#HPDF_COMP_ALL)

; set page mode to use outlines
doc:SetPageMode(#HPDF_PAGE_MODE_USE_OUTLINE)

; get default font
font = doc:GetFont("Helvetica")

; load font object
font_name = doc:LoadType1Font("a010013l.afm", "a010013l.pfb")
  
; create outline root
root = doc:CreateOutline(Nil, "Encoding list", Nil)
root:SetOpened(True)

For Local i = 0 To ListItems(encodings) - 1
	
        Local page = doc:AddPage()
	Local font2
	
        page:SetWidth(#PAGE_WIDTH)
        page:SetHeight(#PAGE_HEIGHT)

        Local outline = doc:CreateOutline(root, encodings[i], Nil)
        Local dst = page:CreateDestination()
        dst:SetXYZ(0, page:GetHeight(), 1)
        
        outline:SetDestination(dst)

        page:SetFontAndSize(font, 15)
        p_DrawGraph(page)

        page:BeginText()
        page:SetFontAndSize(font, 20)
        page:MoveTextPos(40, #PAGE_HEIGHT - 50)
        page:ShowText(encodings[i])
        page:ShowText(" Encoding")
        page:EndText()

	If encodings[i] = "Symbol-Set"
        	font2 = doc:GetFont("Symbol")
        ElseIf encodings[i] = "ZapfDingbats-Set"
            	font2 = doc:GetFont("ZapfDingbats")
        Else
            	font2 = doc:GetFont(font_name, encodings[i])
	EndIf
	
        page:SetFontAndSize(font2, 14)
        p_DrawFonts(page)
Next

; save the document to a file
p_SaveAndView(doc, "EncodingList.pdf")

; clean up
doc:Free()

End
