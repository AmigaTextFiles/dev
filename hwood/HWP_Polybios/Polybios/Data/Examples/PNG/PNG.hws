/****************************************************************
**                                                             **
** Name:        Arc                                            **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        29.07.17                                       **
** Interpreter: Hollywood 7.0                                  **
** Licence:     BSD license                                    **
** Function:    Embeds PNG images inside a PDF                 **
**                                                             **
** Notes:       Based on a libharu example program written by  **
**              Takeshi Kanno <takeshi_kanno@est.hi-ho.ne.jp>  ** 
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (29.07.17)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

@REQUIRE "polybios"

@DISPLAY {Hidden = True}

Function p_DrawImage(doc, filename, x, y, text)
	
	Local page = doc:GetCurrentPage()
    	Local image = doc:LoadPNGImage(filename)

    	; Draw image to the canvas
	page:DrawImage(image, x, y, image:GetWidth(), image:GetHeight())

	; Print the text
    	page:BeginText()
    	page:SetTextLeading(16)
    	page:MoveTextPos(x, y)
    	page:ShowTextNextLine(filename)
    	page:ShowTextNextLine(text)
    	page:EndText()

EndFunction

Function p_SaveAndView(doc, file$)
	
	file$ = FileRequest("Save PDF to...", "pdf", #REQ_SAVEMODE, "", file$)
	If file$ = "" Then End
		
	doc:SaveToFile(file$)	
	doc:Free()
	
	ExitOnError(False)
   	pdf.OpenDocument(1, file$)
   	Local err = GetLastError()
   	ExitOnError(True)
   	If err Then Return
   		
   	pdf.GetBrush(1, 1, 1)
	   	
   	CreateDisplay(2, {Width = GetAttribute(#BRUSH, 1, #ATTRWIDTH), Height = GetAttribute(#BRUSH, 1, #ATTRHEIGHT), Sizeable = True})
   	OpenDisplay(2)
   	ActivateDisplay(2)
   	
   	DisplayBrush(1, 0, 0)
   	
   	InstallEventHandler({SizeWindow = Function(msg) DisplayBrush(1, 0, 0, {Width = msg.width, Height = msg.height}) EndFunction})
   	
   	Repeat
   		WaitEvent
   	Forever
   		
EndFunction

Local doc = pdf.CreateDocument()

; set compression mode
doc:SetCompressionMode(#HPDF_COMP_ALL)

; create default-font
font = doc:GetFont("Helvetica")

; add a new page object
page = doc:AddPage()

page:SetWidth(550)
page:SetHeight(650)

dst = page:CreateDestination()
dst:SetXYZ(0, page:GetHeight(), 1)
doc:SetOpenAction(dst)

page:BeginText()
page:SetFontAndSize(font, 20)
page:MoveTextPos(220, page:GetHeight() - 70)
page:ShowText("PNG Demo")
page:EndText()

page:SetFontAndSize(font, 12)

p_DrawImage(doc, "basn0g01.png", 100, page:GetHeight() - 150, "1bit grayscale")
p_DrawImage(doc, "basn0g02.png", 200, page:GetHeight() - 150, "2bit grayscale")
p_DrawImage(doc, "basn0g04.png", 300, page:GetHeight() - 150, "4bit grayscale")
p_DrawImage(doc, "basn0g08.png", 400, page:GetHeight() - 150, "8bit grayscale")

p_DrawImage(doc, "basn2c08.png", 100, page:GetHeight() - 250, "8bit color")
p_DrawImage(doc, "basn2c16.png", 200, page:GetHeight() - 250, "16bit color")

p_DrawImage(doc, "basn3p01.png", 100, page:GetHeight() - 350, "1bit palette")
p_DrawImage(doc, "basn3p02.png", 200, page:GetHeight() - 350, "2bit palette")
p_DrawImage(doc, "basn3p04.png", 300, page:GetHeight() - 350, "4bit palette")
p_DrawImage(doc, "basn3p08.png", 400, page:GetHeight() - 350, "8bit palette")

p_DrawImage(doc, "basn4a08.png", 100, page:GetHeight() - 450, "8bit alpha")
p_DrawImage(doc, "basn4a16.png", 200, page:GetHeight() - 450, "16bit alpha")

p_DrawImage(doc, "basn6a08.png", 100, page:GetHeight() - 550, "8bit alpha")
p_DrawImage(doc, "basn6a16.png", 200, page:GetHeight() - 550, "16bit alpha")

; save the document to a file
p_SaveAndView(doc, "PNG.pdf")

; clean up
doc:Free()

End
