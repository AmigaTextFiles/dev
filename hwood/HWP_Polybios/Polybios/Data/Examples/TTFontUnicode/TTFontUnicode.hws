/****************************************************************
**                                                             **
** Name:        TTFont_Unicode                                 **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        30.07.17                                       **
** Interpreter: Hollywood 7.0                                  **
** Licence:     BSD license                                    **
** Function:    Create PDF in UTF-8 encoding                   **
**                                                             **
** Notes:       Based on a libharu example program written by  **
**              Takeshi Kanno <takeshi_kanno@est.hi-ho.ne.jp>  **
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (30.07.17)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

@REQUIRE "polybios"

@DISPLAY {Hidden = True}

Function p_SaveAndView(doc, file$)
	
	file$ = FileRequest("Save PDF to...", "pdf", #REQ_SAVEMODE, "", file$)
	If file$ = "" Then End
		
	doc:SaveToFile(file$)	
	doc:Free()
	
	ExitOnError(False)
   	pdf.OpenDocument(1, file$)
   	Local err = GetLastError()
   	ExitOnError(True)
   	If err Then Return
   		
   	pdf.GetBrush(1, 1, 1)
	   	
   	CreateDisplay(2, {Width = GetAttribute(#BRUSH, 1, #ATTRWIDTH), Height = GetAttribute(#BRUSH, 1, #ATTRHEIGHT), Sizeable = True})
   	OpenDisplay(2)
   	ActivateDisplay(2)
   	
   	DisplayBrush(1, 0, 0)
   	
   	InstallEventHandler({SizeWindow = Function(msg) DisplayBrush(1, 0, 0, {Width = msg.width, Height = msg.height}) EndFunction})
   	
   	Repeat
   		WaitEvent
   	Forever
   		
EndFunction

; some pangrams from non-English languages
tests = {
	"Die heiße Zypernsonne quälte Max und Victoria ja böse auf dem Weg bis zur Küste.",
	"Le cœur déçu mais l'âme plutôt naïve, Louÿs rêva de crapaüter en canoë au delà des îles, près du mälström où brûlent les novæ.",
	"El pingüino Wenceslao hizo kilómetros bajo exhaustiva lluvia y frío, añoraba a su querido cachorro.",
	"Jó foxim és don Quijote húszwattos lámpánál ülve egy pár bűvös cipőt készít.",
	"Pójdźże, kiń tę chmurność w głąb flaszy!",
	"Съешь ещё этих мягких французских булок, да выпей чаю.",
	"Γαζέες καὶ μυρτιὲς δὲν θὰ βρῶ πιὰ στὸ χρυσαφὶ ξέφωτο."
}

; create new document
doc = pdf.CreateDocument()

; declaration for using Japanese encoding
doc:UseUTFEncodings()

; enable compression
doc:SetCompressionMode(#HPDF_COMP_ALL)

; load ttf file
detail_font_name = doc:LoadTTFont("DejaVuSans.ttf", True)

; add a new page object
page = doc:AddPage()

title_font = doc:GetFont("Helvetica")
detail_font = doc:GetFont(detail_font_name, "UTF-8")

page:SetFontAndSize(title_font, 10)

page:BeginText()

; move the position of the text to top of the page
page:MoveTextPos(10, 260)
page:ShowText(detail_font_name)
page:ShowText("(")
page:ShowText(detail_font:GetEncodingName())
page:ShowText(")")

page:SetFontAndSize(detail_font, 15)
page:MoveTextPos(10, -20)
page:ShowText("abcdefghijklmnopqrstuvwxyz")
page:MoveTextPos(0, -20)
page:ShowText("ABCDEFGHIJKLMNOPQRSTUVWXYZ")
page:MoveTextPos(0, -20)
page:ShowText("1234567890")
page:MoveTextPos(0, -20)

page:SetFontAndSize(detail_font, 16)
pw = 0

For Local k = 0 To ListItems(tests) - 1
	page:ShowText(tests[k])
	page:MoveTextPos(0, -27)
	pw = Max(pw, page:TextWidth(tests[k]))	
Next

page_height = 280
page_width = pw + 40

page:SetWidth(page_width)
page:SetHeight(page_height)

; finish to print text
page:EndText()

page:SetLineWidth(0.5)

page:MoveTo(10, page_height - 25)
page:LineTo(page_width - 10, page_height - 25)
page:Stroke()

page:MoveTo(10, page_height - 85)
page:LineTo(page_width - 10, page_height - 85)
page:Stroke()

; save the document to a file
p_SaveAndView(doc, "TTFontUnicode.pdf")

; clean up
doc:Free()

End
