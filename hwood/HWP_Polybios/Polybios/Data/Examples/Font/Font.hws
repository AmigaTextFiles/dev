/****************************************************************
**                                                             **
** Name:        Font                                           **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        29.07.17                                       **
** Interpreter: Hollywood 7.0                                  **
** Licence:     BSD license                                    **
** Function:    Shows how to use different fonts               **
**                                                             **
** Notes:       Based on a libharu example program written by  **
**              Takeshi Kanno <takeshi_kanno@est.hi-ho.ne.jp>  ** 
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (29.07.17)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

@REQUIRE "polybios"

@DISPLAY {Hidden = True}

Function p_SaveAndView(doc, file$)
	
	file$ = FileRequest("Save PDF to...", "pdf", #REQ_SAVEMODE, "", file$)
	If file$ = "" Then End
		
	doc:SaveToFile(file$)	
	doc:Free()
	
	ExitOnError(False)
   	pdf.OpenDocument(1, file$)
   	Local err = GetLastError()
   	ExitOnError(True)
   	If err Then Return
   		
   	pdf.GetBrush(1, 1, 1)
	   	
   	CreateDisplay(2, {Width = GetAttribute(#BRUSH, 1, #ATTRWIDTH), Height = GetAttribute(#BRUSH, 1, #ATTRHEIGHT), Sizeable = True})
   	OpenDisplay(2)
   	ActivateDisplay(2)
   	
   	DisplayBrush(1, 0, 0)
   	
   	InstallEventHandler({SizeWindow = Function(msg) DisplayBrush(1, 0, 0, {Width = msg.width, Height = msg.height}) EndFunction})
   	
   	Repeat
   		WaitEvent
   	Forever
   		
EndFunction

Local font_list = {
	"Courier",
  	"Courier-Bold",
  	"Courier-Oblique",
  	"Courier-BoldOblique",
  	"Helvetica",
  	"Helvetica-Bold",
  	"Helvetica-Oblique",
  	"Helvetica-BoldOblique",
  	"Times-Roman",
  	"Times-Bold",
  	"Times-Italic",
  	"Times-BoldItalic",
  	"Symbol",
  	"ZapfDingbats"
}

Local page_title = "Font demonstration"

Local doc = pdf.CreateDocument()

; set compression mode
doc:SetCompressionMode(#HPDF_COMP_ALL)

; Add a new page object.
Local page = doc:AddPage()
Local height = page:GetHeight()
Local width = page:GetWidth()

; Print the lines of the page.
page:SetLineWidth(1)
page:Rectangle(50, 50, width - 100, height - 110)
page:Stroke()

; Print the lines of the page.
page:SetLineWidth(1)
page:Rectangle(50, 50, width - 100, height - 110)
page:Stroke()

; Print the title of the page (with positioning center).
Local def_font = doc:GetFont("Helvetica")
page:SetFontAndSize(def_font, 24)

Local tw = page:TextWidth(page_title)
page:BeginText()
page:TextOut((width - tw) / 2, height - 50, page_title)
page:EndText()

; Output subtitle.
page:BeginText();
page:SetFontAndSize(def_font, 16)
page:TextOut(60, height - 80, "Standard Type1 fonts samples")
page:EndText()

page:BeginText()
page:MoveTextPos(60, height - 105)

For j, font_str In IPairs(font_list)

	Local samp_text = "abcdefgABCDEFG12345!#$%&+-@?"
    	Local font = doc:GetFont(font_str)

	; print a label of text
	page:SetFontAndSize(def_font, 9)
    	page:ShowText(font_str)
    	page:MoveTextPos(0, -18)

	; print a sample text.
    	page:SetFontAndSize(font, 20)
    	page:ShowText(samp_text)
    	page:MoveTextPos(0, -20)
Next

page:EndText()

p_SaveAndView(doc, "Font.pdf")
doc:Free()

End
