/****************************************************************
**                                                             **
** Name:        Arc                                            **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        29.07.17                                       **
** Interpreter: Hollywood 7.0                                  **
** Licence:     BSD license                                    **
** Function:    Shows how to add an outline to a PDF           **
**                                                             **
** Notes:       Based on a libharu example program written by  **
**              Takeshi Kanno <takeshi_kanno@est.hi-ho.ne.jp>  ** 
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (29.07.17)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

@REQUIRE "polybios"

@DISPLAY {Hidden = True}

Function p_PrintPage(page, page_num)
	page:SetWidth(800)
  	page:SetHeight(800)
  	page:BeginText()
  	page:MoveTextPos(30, 740)
  	page:ShowText("Page " .. page_num)
  	page:EndText()
EndFunction

Function p_CreateDst(page, outline)
	Local dst = page:CreateDestination()
  	dst:SetXYZ(0, page:GetHeight(), 1)
  	outline:SetDestination(dst)
EndFunction

Function p_SaveAndView(doc, file$)
	
	file$ = FileRequest("Save PDF to...", "pdf", #REQ_SAVEMODE, "", file$)
	If file$ = "" Then End
		
	doc:SaveToFile(file$)	
	doc:Free()
	
	ExitOnError(False)
   	pdf.OpenDocument(1, file$)
   	Local err = GetLastError()
   	ExitOnError(True)
   	If err Then Return
   		
   	pdf.GetBrush(1, 1, 1)
	   	
   	CreateDisplay(2, {Width = GetAttribute(#BRUSH, 1, #ATTRWIDTH), Height = GetAttribute(#BRUSH, 1, #ATTRHEIGHT), Sizeable = True})
   	OpenDisplay(2)
   	ActivateDisplay(2)
   	
   	DisplayBrush(1, 0, 0)
   	
   	InstallEventHandler({SizeWindow = Function(msg) DisplayBrush(1, 0, 0, {Width = msg.width, Height = msg.height}) EndFunction})
   	
   	Repeat
   		WaitEvent
   	Forever
   		
EndFunction

Local doc = pdf.CreateDocument()

; create default-font
Local font = doc:GetFont("Helvetica")

; set page mode to use outlines
doc:SetPageMode(#HPDF_PAGE_MODE_USE_OUTLINE)

; add 3 pages to the document
Local page = {}
For j = 0 To 2
	page[j] = doc:AddPage()
    	page[j]:SetFontAndSize(font, 30)
    	p_PrintPage(page[j], j + 1)
Next

; create outline root
Local root = doc:CreateOutline(Nil, "OutlineRoot")
root:SetOpened(True)

Local outline = {}
outline[0] = doc:CreateOutline(root, "Page 1")
outline[1] = doc:CreateOutline(root, "Page 2")

; create outline with test which is ISO8859-2 encoding
a$ = "ISO8859-2 text "
For Local k = $D3 To $D9 Do a$ = a$ .. Chr(k, #ENCODING_RAW)
	
outline[2] = doc:CreateOutline(root, a$, doc:GetEncoder("ISO8859-2"))

; create destination objects on each pages and link it to outline items
For j = 0 To 2 Do p_CreateDst(page[j], outline[j])

; save the document to a file
p_SaveAndView(doc, "Outline.pdf")

; clean up
doc:Free()

End
