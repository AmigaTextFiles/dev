/****************************************************************
**                                                             **
** Name:        Permission                                     **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        29.07.17                                       **
** Interpreter: Hollywood 7.0                                  **
** Licence:     BSD license                                    **
** Function:    Shows how to modify PDF document permissions   **
**                                                             **
** Notes:       Based on a libharu example program written by  **
**              Takeshi Kanno <takeshi_kanno@est.hi-ho.ne.jp>  ** 
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (29.07.17)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

@REQUIRE "polybios"

@DISPLAY {Hidden = True}

Function p_SaveAndView(doc, file$)
	
	file$ = FileRequest("Save PDF to...", "pdf", #REQ_SAVEMODE, "", file$)
	If file$ = "" Then End
		
	doc:SaveToFile(file$)	
	doc:Free()
	
	ExitOnError(False)
   	pdf.OpenDocument(1, file$)
   	Local err = GetLastError()
   	ExitOnError(True)
   	If err Then Return
   		
   	pdf.GetBrush(1, 1, 1)
	   	
   	CreateDisplay(2, {Width = GetAttribute(#BRUSH, 1, #ATTRWIDTH), Height = GetAttribute(#BRUSH, 1, #ATTRHEIGHT), Sizeable = True})
   	OpenDisplay(2)
   	ActivateDisplay(2)
   	
   	DisplayBrush(1, 0, 0)
   	
   	InstallEventHandler({SizeWindow = Function(msg) DisplayBrush(1, 0, 0, {Width = msg.width, Height = msg.height}) EndFunction})
   	
   	Repeat
   		WaitEvent
   	Forever
   		
EndFunction

Local text = "User cannot print and copy this document."
Local owner_passwd = "owner"
Local user_passwd = ""

Local doc = pdf.CreateDocument()

; create default-font
Local font = doc:GetFont("Helvetica")

; add a new page object
Local page = doc:AddPage()

page:SetSize(#HPDF_PAGE_SIZE_B5, #HPDF_PAGE_LANDSCAPE)

page:BeginText()
page:SetFontAndSize(font, 20)
Local tw = page:TextWidth(text)
page:MoveTextPos((page:GetWidth() - tw) / 2, (page:GetHeight() - 20) / 2)
page:ShowText(text)
page:EndText()

doc:SetPassword(owner_passwd, user_passwd)
doc:SetPermission(#HPDF_ENABLE_READ)
doc:SetEncryptionMode(#HPDF_ENCRYPT_R3, 16)

; save the document to a file
p_SaveAndView(doc, "Permission.pdf")

; clean up
doc:Free()

End
