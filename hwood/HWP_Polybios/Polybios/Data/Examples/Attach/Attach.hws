/****************************************************************
**                                                             **
** Name:        Attach                                         **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        29.07.17                                       **
** Interpreter: Hollywood 7.0                                  **
** Licence:     BSD license                                    **
** Function:    Demonstrates how to attach files to a PDF      **
**                                                             **
** Notes:       Based on a libharu example program written by  **
**              Takeshi Kanno <takeshi_kanno@est.hi-ho.ne.jp>  ** 
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (29.07.17)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

@REQUIRE "polybios"

@DISPLAY {Hidden = True}

Function p_SaveAndView(doc, file$)
	
	file$ = FileRequest("Save PDF to...", "pdf", #REQ_SAVEMODE, "", file$)
	If file$ = "" Then End
		
	doc:SaveToFile(file$)	
	doc:Free()
	
	ExitOnError(False)
   	pdf.OpenDocument(1, file$)
   	Local err = GetLastError()
   	ExitOnError(True)
   	If err Then Return
   		
   	pdf.GetBrush(1, 1, 1)
	   	
   	CreateDisplay(2, {Width = GetAttribute(#BRUSH, 1, #ATTRWIDTH), Height = GetAttribute(#BRUSH, 1, #ATTRHEIGHT), Sizeable = True})
   	OpenDisplay(2)
   	ActivateDisplay(2)
   	
   	DisplayBrush(1, 0, 0)
   	
   	InstallEventHandler({SizeWindow = Function(msg) DisplayBrush(1, 0, 0, {Width = msg.width, Height = msg.height}) EndFunction})
   	
   	Repeat
   		WaitEvent
   	Forever
   		
EndFunction	

text = "This PDF should have attachments named Attach.hws and Logo.png"

Local doc = pdf.CreateDocument()

; create default-font
font = doc:GetFont("Helvetica")

; add a new page object
page = doc:AddPage()

page:SetSize(#HPDF_PAGE_SIZE_LETTER, #HPDF_PAGE_PORTRAIT)

page:BeginText()
page:SetFontAndSize(font, 16)
tw = page:TextWidth(text)
page:MoveTextPos((page:GetWidth() - tw) / 2, (page:GetHeight() - 20) / 2)
page:ShowText(text)
page:EndText()

; attach a file to the document
doc:AttachFile("Attach.hws")
doc:AttachFile("Logo.png")

; save the document to a file
p_SaveAndView(doc, "Attach.pdf")

; clean up
doc:Free()

End
