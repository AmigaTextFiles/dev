/****************************************************************
**                                                             **
** Name:        Line                                           **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        29.07.17                                       **
** Interpreter: Hollywood 7.0                                  **
** Licence:     BSD license                                    **
** Function:    Demonstrates different line styles             **
**                                                             **
** Notes:       Based on a libharu example program written by  **
**              Takeshi Kanno <takeshi_kanno@est.hi-ho.ne.jp>  ** 
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (29.07.17)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

@REQUIRE "polybios"

@DISPLAY {Hidden = True}

Function p_DrawLine(page, x, y, llabel)

	page:BeginText()
  	page:MoveTextPos(x, y - 10)
  	page:ShowText(llabel)
  	page:EndText()

  	page:MoveTo(x, y - 15)
  	page:LineTo(x + 220, y - 15)
  	page:Stroke()

EndFunction

Function p_DrawLine2(page, x, y, llabel)

	page:BeginText()
	page:MoveTextPos(x, y)
  	page:ShowText(llabel)
  	page:EndText()

	page:MoveTo(x + 30, y - 25)
	page:LineTo(x + 160, y - 25)
  	page:Stroke()

EndFunction

Function p_DrawRect(page, x, y, llabel)

	page:BeginText()
    	page:MoveTextPos(x, y - 10)
    	page:ShowText(llabel)
    	page:EndText()

	page:Rectangle(x, y - 40, 220, 25)

EndFunction

Function p_SaveAndView(doc, file$)
	
	file$ = FileRequest("Save PDF to...", "pdf", #REQ_SAVEMODE, "", file$)
	If file$ = "" Then End
		
	doc:SaveToFile(file$)	
	doc:Free()
	
	ExitOnError(False)
   	pdf.OpenDocument(1, file$)
   	Local err = GetLastError()
   	ExitOnError(True)
   	If err Then Return
   		
   	pdf.GetBrush(1, 1, 1)
	   	
   	CreateDisplay(2, {Width = GetAttribute(#BRUSH, 1, #ATTRWIDTH), Height = GetAttribute(#BRUSH, 1, #ATTRHEIGHT), Sizeable = True})
   	OpenDisplay(2)
   	ActivateDisplay(2)
   	
   	DisplayBrush(1, 0, 0)
   	
   	InstallEventHandler({SizeWindow = Function(msg) DisplayBrush(1, 0, 0, {Width = msg.width, Height = msg.height}) EndFunction})
   	
   	Repeat
   		WaitEvent
   	Forever
   		
EndFunction

Local doc = pdf.CreateDocument()

; set compression mode
doc:SetCompressionMode(#HPDF_COMP_ALL)

Local page_title = "Line Example"
Local DASH_MODE1 = {3}
Local DASH_MODE2 = {3, 7}
Local DASH_MODE3 = {8, 7, 2, 7}

; create default-font
Local font = doc:GetFont("Helvetica")

; add a new page object
Local page = doc:AddPage()

; print the lines of the page
page:SetLineWidth(1)
page:Rectangle(50, 50, page:GetWidth() - 100, page:GetHeight() - 110)
page:Stroke()

; print the title of the page(with positioning center)
page:SetFontAndSize(font, 24)
Local tw = page:TextWidth(page_title)
page:BeginText()
page:MoveTextPos((page:GetWidth() - tw) / 2, page:GetHeight() - 50)
page:ShowText(page_title)
page:EndText()

page:SetFontAndSize(font, 10)

; Draw verious widths of lines
page:SetLineWidth(0)
p_DrawLine(page, 60, 770, "line width = 0")

page:SetLineWidth(1.0)
p_DrawLine(page, 60, 740, "line width = 1.0")

page:SetLineWidth(2.0)
p_DrawLine(page, 60, 710, "line width = 2.0")

; Line dash pattern
page:SetLineWidth(1.0)

page:SetDash(DASH_MODE1, ListItems(DASH_MODE1), 1)
p_DrawLine(page, 60, 680, "dash_ptn=[3], phase=1 ; 2 on, 3 off, 3 on...")

page:SetDash(DASH_MODE2, ListItems(DASH_MODE2), 2)
p_DrawLine(page, 60, 650, "dash_ptn=[7, 3], phase=2 ; 5 on 3 off, 7 on,...")

page:SetDash(DASH_MODE3, ListItems(DASH_MODE3), 0)
p_DrawLine(page, 60, 620, "dash_ptn=[8, 7, 2, 7], phase=0")

page:SetDash(nil, 0, 0)

page:SetLineWidth(30)
page:SetRGBStroke(0.0, 0.5, 0.0)

; Line Cap Style
page:SetLineCap(#HPDF_BUTT_END)
p_DrawLine2(page, 60, 570, "HPDF_BUTT_END")

page:SetLineCap(#HPDF_ROUND_END)
p_DrawLine2(page, 60, 505, "HPDF_ROUND_END")

page:SetLineCap(#HPDF_PROJECTING_SQUARE_END)
p_DrawLine2(page, 60, 440, "HPDF_PROJECTING_SQUARE_END")

; Line Join Style
page:SetLineWidth(30)
page:SetRGBStroke(0.0, 0.0, 0.5)

page:SetLineJoin(#HPDF_MITER_JOIN)
page:MoveTo(120, 300)
page:LineTo(160, 340)
page:LineTo(200, 300)
page:Stroke()

page:BeginText()
page:MoveTextPos(60, 360)
page:ShowText("HPDF_MITER_JOIN")
page:EndText()

page:SetLineJoin(#HPDF_ROUND_JOIN)
page:MoveTo(120, 195)
page:LineTo(160, 235)
page:LineTo(200, 195)
page:Stroke()

page:BeginText()
page:MoveTextPos(60, 255)
page:ShowText("HPDF_ROUND_JOIN")
page:EndText()

page:SetLineJoin(#HPDF_BEVEL_JOIN)
page:MoveTo(120, 90)
page:LineTo(160, 130)
page:LineTo(200, 90)
page:Stroke()

page:BeginText()
page:MoveTextPos(60, 150)
page:ShowText("HPDF_BEVEL_JOIN")
page:EndText()

; Draw Rectangle
page:SetLineWidth(2)
page:SetRGBStroke(0, 0, 0)
page:SetRGBFill(0.75, 0.0, 0.0)

p_DrawRect(page, 300, 770, "Stroke")
page:Stroke()

p_DrawRect(page, 300, 720, "Fill")
page:Fill()

p_DrawRect(page, 300, 670, "Fill then Stroke")
page:FillStroke()

; Clip Rect
page:GSave()  ; Save the current graphic state
p_DrawRect(page, 300, 620, "Clip Rectangle")
page:Clip()
page:Stroke()
page:SetFontAndSize(font, 13)

page:BeginText()
page:MoveTextPos(290, 600)
page:SetTextLeading(12)
page:ShowText("Clip Clip Clip Clip Clip Clipi Clip Clip Clip")
page:ShowTextNextLine("Clip Clip Clip Clip Clip Clip Clip Clip Clip")
page:ShowTextNextLine("Clip Clip Clip Clip Clip Clip Clip Clip Clip")
page:EndText()
page:GRestore()

; Curve Example(CurveTo2)
Local x = 330
Local y = 440
Local x1 = 430
Local y1 = 530
Local x2 = 480
Local y2 = 470
Local x3 = 480
Local y3 = 90

page:SetRGBFill(0, 0, 0)

page:BeginText()
page:MoveTextPos(300, 540)
page:ShowText("CurveTo2(x1, y1, x2. y2)")
page:EndText()

page:BeginText()
page:MoveTextPos(x + 5, y - 5)
page:ShowText("Current point")
page:MoveTextPos(x1 - x, y1 - y)
page:ShowText("(x1, y1)")
page:MoveTextPos(x2 - x1, y2 - y1)
page:ShowText("(x2, y2)")
page:EndText()

page:SetDash(DASH_MODE1, ListItems(DASH_MODE1), 0)

page:SetLineWidth(0.5)
page:MoveTo(x1, y1)
page:LineTo(x2, y2)
page:Stroke()

page:SetDash()

page:SetLineWidth(1.5)

page:MoveTo(x, y)
page:CurveTo2(x1, y1, x2, y2)
page:Stroke()

; Curve Example(CurveTo3)
y = y - 150
y1 = y1 - 150
y2 = y2 - 150

page:BeginText()
page:MoveTextPos(300, 390)
page:ShowText("CurveTo3(x1, y1, x2. y2)")
page:EndText()

page:BeginText()
page:MoveTextPos(x + 5, y - 5)
page:ShowText("Current point")
page:MoveTextPos(x1 - x, y1 - y)
page:ShowText("(x1, y1)")
page:MoveTextPos(x2 - x1, y2 - y1)
page:ShowText("(x2, y2)")
page:EndText()

page:SetDash(DASH_MODE1, ListItems(DASH_MODE1), 0)

page:SetLineWidth(0.5)
page:MoveTo(x, y)
page:LineTo(x1, y1)
page:Stroke()

page:SetDash()

page:SetLineWidth(1.5)
page:MoveTo(x, y)
page:CurveTo3(x1, y1, x2, y2)
page:Stroke()

; Curve Example(CurveTo)
y = y - 150
y1 = y1 - 160
y2 = y2 - 130
x2 = x2 + 10

page:BeginText()
page:MoveTextPos(300, 240)
page:ShowText("CurveTo(x1, y1, x2. y2, x3, y3)")
page:EndText()

page:BeginText()
page:MoveTextPos(x + 5, y - 5)
page:ShowText("Current point")
page:MoveTextPos(x1 - x, y1 - y)
page:ShowText("(x1, y1)")
page:MoveTextPos(x2 - x1, y2 - y1)
page:ShowText("(x2, y2)")
page:MoveTextPos(x3 - x2, y3 - y2)
page:ShowText("(x3, y3)")
page:EndText()

page:SetDash(DASH_MODE1, ListItems(DASH_MODE1), 0)

page:SetLineWidth(0.5)
page:MoveTo(x, y)
page:LineTo(x1, y1)
page:Stroke()
page:MoveTo(x2, y2)
page:LineTo(x3, y3)
page:Stroke()

page:SetDash()

page:SetLineWidth(1.5)
page:MoveTo(x, y)
page:CurveTo(x1, y1, x2, y2, x3, y3)
page:Stroke()

; save the document to a file
p_SaveAndView(doc, "Line.pdf")

; clean up
doc:Free()

End
