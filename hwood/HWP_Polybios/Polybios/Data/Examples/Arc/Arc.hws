/****************************************************************
**                                                             **
** Name:        Arc                                            **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        29.07.17                                       **
** Interpreter: Hollywood 7.0                                  **
** Licence:     BSD license                                    **
** Function:    Creates a pie chart PDF                        **
**                                                             **
** Notes:       Based on a libharu example program written by  **
**              Takeshi Kanno <takeshi_kanno@est.hi-ho.ne.jp>  ** 
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (29.07.17)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

@REQUIRE "polybios"

@DISPLAY {Hidden = True}

Function p_PrintGrid(doc, page)

	Local height = page:GetHeight()
  	Local width = page:GetWidth()
  	Local font = doc:GetFont("Helvetica")

  	page:SetFontAndSize(font, 5)
  	page:SetGrayFill(0.5)
  	page:SetGrayStroke(0.8)

  	; Draw horizontal lines
  	Local y = 0
  	While y < height
    		If y % 10 = 0
      			page:SetLineWidth(0.5)
    		ElseIf page:GetLineWidth() <> 0.25
    			page:SetLineWidth(0.25)
    		EndIf
    		page:MoveTo(0, y)
    		page:LineTo(width, y)
    		page:Stroke()
    		If (y % 10 = 0) And (y > 0)
      			page:SetGrayStroke(0.5)
      			page:MoveTo(0, y)
      			page:LineTo(5, y)
      			page:Stroke()
      			page:SetGrayStroke(0.8)
    		EndIf
    		y = y + 5
  	Wend

  	; Draw vertical lines
  	Local x = 0
  	While x < width
    		If x % 10 = 0
      			page:SetLineWidth(0.5)
    		ElseIf page:GetLineWidth() <> 0.25
       			page:SetLineWidth(0.25)
		EndIf
    		page:MoveTo(x, 0)
    		page:LineTo(x, height)
    		page:Stroke()
    		If (x % 50 = 0) And (x > 0)
      			page:SetGrayStroke(0.5)
      			page:MoveTo(x, 0)
      			page:LineTo(x, 5)
      			page:Stroke()
      			page:MoveTo(x, height)
      			page:LineTo(x, height - 5)
      			page:Stroke()
      			page:SetGrayStroke(0.8)
    		EndIf
    		x = x + 5
  	Wend

	; Draw horizontal text
  	y = 0
  	While y < height
    		if (y % 10 = 0) And (y > 0)
      			page:BeginText()
      			page:MoveTextPos(5, y - 2)
      			page:ShowText(y)
      			page:EndText()
    		EndIf
    		y = y + 5
  	Wend

	; Draw vertical text
  	x = 0
  	While x < width
    		If (x % 50 = 0) And (x > 0)
      			page:BeginText()
      			page:MoveTextPos(x, 5)
      			page:ShowText(x)
      			page:EndText()
      			page:BeginText()
      			page:MoveTextPos(x, height - 10)
      			page:ShowText(x)
      			page:EndText()
    		EndIf
    		x = x + 5
  	Wend
  	
  	page:SetGrayFill(0)
  	page:SetGrayStroke(0)

EndFunction 

Function p_SaveAndView(doc, file$)
	
	file$ = FileRequest("Save PDF to...", "pdf", #REQ_SAVEMODE, "", file$)
	If file$ = "" Then End
		
	doc:SaveToFile(file$)	
	doc:Free()
	
	ExitOnError(False)
   	pdf.OpenDocument(1, file$)
   	Local err = GetLastError()
   	ExitOnError(True)
   	If err Then Return
   		
   	pdf.GetBrush(1, 1, 1)
	   	
   	CreateDisplay(2, {Width = GetAttribute(#BRUSH, 1, #ATTRWIDTH), Height = GetAttribute(#BRUSH, 1, #ATTRHEIGHT), Sizeable = True})
   	OpenDisplay(2)
   	ActivateDisplay(2)
   	
   	DisplayBrush(1, 0, 0)
   	
   	InstallEventHandler({SizeWindow = Function(msg) DisplayBrush(1, 0, 0, {Width = msg.width, Height = msg.height}) EndFunction})
   	
   	Repeat
   		WaitEvent
   	Forever
   		
EndFunction	

Local doc = pdf.CreateDocument()

; enable compression
doc:SetCompressionMode(#HPDF_COMP_ALL)

; add a new page object
Local page = doc:AddPage()

page:SetHeight(220)
page:SetWidth(200)

; draw grid to the page
p_PrintGrid(doc, page)

; draw pie chart
;
;   A: 45% Red
;   B: 25% Blue
;   C: 15% green
;   D: other yellow

; A
page:SetRGBFill(1.0, 0, 0)
page:MoveTo(100, 100)
page:LineTo(100, 180)
page:Arc(100, 100, 80, 0, 360 * 0.45)
Local x, y = page:GetCurrentPos()
page:LineTo(100, 100)
page:Fill()

; B
page:SetRGBFill(0, 0, 1.0)
page:MoveTo(100, 100)
page:LineTo(x, y)
page:Arc(100, 100, 80, 360 * 0.45, 360 * 0.7)
x, y = page:GetCurrentPos()
page:LineTo(100, 100)
page:Fill()

; C
page:SetRGBFill(0, 1.0, 0)
page:MoveTo(100, 100)
page:LineTo(x, y)
page:Arc(100, 100, 80, 360 * 0.7, 360 * 0.85)
x, y = page:GetCurrentPos()
page:LineTo(100, 100)
page:Fill()

; D
page:SetRGBFill(1.0, 1.0, 0)
page:MoveTo(100, 100)
page:LineTo(x, y)
page:Arc(100, 100, 80, 360 * 0.85, 360)
x, y = page:GetCurrentPos()
page:LineTo(100, 100)
page:Fill()

; draw center circle
page:SetGrayStroke(0)
page:SetGrayFill(1)
page:Circle(100, 100, 30)
page:Fill()

; save the document to a file
p_SaveAndView(doc, "Arc.pdf")

; clean up
doc:Free()

End
