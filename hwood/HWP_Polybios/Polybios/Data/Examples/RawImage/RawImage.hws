/****************************************************************
**                                                             **
** Name:        RawImage                                       **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        30.07.17                                       **
** Interpreter: Hollywood 7.0                                  **
** Licence:     BSD license                                    **
** Function:    Demonstrates how to embed raw images in PDF    **
**                                                             **
** Notes:       Based on a libharu example program written by  **
**              Takeshi Kanno <takeshi_kanno@est.hi-ho.ne.jp>  **
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (30.07.17)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

@REQUIRE "polybios"

@DISPLAY {Hidden = True}

Function p_SaveAndView(doc, file$)
	
	file$ = FileRequest("Save PDF to...", "pdf", #REQ_SAVEMODE, "", file$)
	If file$ = "" Then End
		
	doc:SaveToFile(file$)	
	doc:Free()
	
	ExitOnError(False)
   	pdf.OpenDocument(1, file$)
   	Local err = GetLastError()
   	ExitOnError(True)
   	If err Then Return
   		
   	pdf.GetBrush(1, 1, 1)
	   	
   	CreateDisplay(2, {Width = GetAttribute(#BRUSH, 1, #ATTRWIDTH), Height = GetAttribute(#BRUSH, 1, #ATTRHEIGHT), Sizeable = True})
   	OpenDisplay(2)
   	ActivateDisplay(2)
   	
   	DisplayBrush(1, 0, 0)
   	
   	InstallEventHandler({SizeWindow = Function(msg) DisplayBrush(1, 0, 0, {Width = msg.width, Height = msg.height}) EndFunction})
   	
   	Repeat
   		WaitEvent
   	Forever
   		
EndFunction

doc = pdf.CreateDocument()

doc:SetCompressionMode(#HPDF_COMP_ALL)

; create default-font
font = doc:GetFont("Helvetica")

; add a new page object
page = doc:AddPage()

page:SetWidth(172)
page:SetHeight(80)

page:BeginText()
page:SetFontAndSize(font, 20)
page:MoveTextPos(220, page:GetHeight() - 70)
page:ShowText("RawImageDemo")
page:EndText()

; load RGB raw-image file
image = doc:LoadRawImage("32_32_rgb.dat", 32, 32, #HPDF_CS_DEVICE_RGB)

x = 20
y = 20

; draw image to the canvas (normal-mode with actual size)
page:DrawImage(image, x, y, 32, 32)

; load GrayScale raw-image file
image = doc:LoadRawImage("32_32_gray.dat", 32, 32, #HPDF_CS_DEVICE_GRAY)

x = 70
y = 20

; draw image to the canvas (normal-mode with actual size)
page:DrawImage(image, x, y, 32, 32)

; load GrayScale raw-image(1bit) file from memory
AllocMem(1, 128)
OpenFile(1, "32_32_2color.dat")
ReadMem(1, 1, 128)
CloseFile(1)

image = doc:CreateImageFromMem(GetMemPointer(1), 32, 32, #HPDF_CS_DEVICE_GRAY, 1)

x = 120
y = 20

; draw image to the canvas (normal-mode with actual size)
page:DrawImage(image, x, y, 32, 32)

; save the document to a file
p_SaveAndView(doc, "RawImage.pdf")

; clean up
doc:Free()

End
