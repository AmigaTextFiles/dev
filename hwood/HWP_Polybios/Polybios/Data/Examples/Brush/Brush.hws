/****************************************************************
**                                                             **
** Name:        Brush                                          **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        29.07.17                                       **
** Interpreter: Hollywood 7.0                                  **
** Licence:     BSD license                                    **
** Function:    Shows how to embed Hollywood brushes inside    **
**              PDF documents                                  **
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (29.07.17)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

@REQUIRE "polybios"

@BRUSH 1, "hw70.png"

@DISPLAY {Hidden = True}

Function p_DrawImage(doc, br, x, y, text, usejpeg, q)
	
	Local page = doc:GetCurrentPage()
	Local image = doc:CreateImageFromBrush(br, {UseJPEG = usejpeg, Quality = q})
		
    	; Draw image to the canvas
	page:DrawImage(image, x, y, image:GetWidth(), image:GetHeight())

	; Print the text.
	page:BeginText()
    	page:SetTextLeading(16)
    	page:MoveTextPos(x, y)
    	page:ShowTextNextLine(filename)
    	page:ShowTextNextLine(text)
    	page:EndText()

EndFunction

Function p_SaveAndView(doc, file$)
	
	file$ = FileRequest("Save PDF to...", "pdf", #REQ_SAVEMODE, "", file$)
	If file$ = "" Then End
		
	doc:SaveToFile(file$)	
	doc:Free()
	
	ExitOnError(False)
   	pdf.OpenDocument(1, file$)
   	Local err = GetLastError()
   	ExitOnError(True)
   	If err Then Return
   		
   	pdf.GetBrush(1, 1, 1)
	   	
   	CreateDisplay(2, {Width = GetAttribute(#BRUSH, 1, #ATTRWIDTH), Height = GetAttribute(#BRUSH, 1, #ATTRHEIGHT), Sizeable = True})
   	OpenDisplay(2)
   	ActivateDisplay(2)
   	
   	DisplayBrush(1, 0, 0)
   	
   	InstallEventHandler({SizeWindow = Function(msg) DisplayBrush(1, 0, 0, {Width = msg.width, Height = msg.height}) EndFunction})
   	
   	Repeat
   		WaitEvent
   	Forever
   		
EndFunction	

Local doc = pdf.CreateDocument()

; set compression mode
doc:SetCompressionMode(#HPDF_COMP_ALL)

; create default-font
font = doc:GetFont("Helvetica")

; add a new page object.
page = doc:AddPage()

page:SetWidth(800)
page:SetHeight(600)

dst = page:CreateDestination()
dst:SetXYZ(0, page:GetHeight(), 1)
doc:SetOpenAction(dst)

page:BeginText()
page:SetFontAndSize(font, 20)
page:MoveTextPos(300, page:GetHeight() - 70)
page:ShowText("Hollywood brush demo")
page:EndText()

page:SetFontAndSize(font, 12)

p_DrawImage(doc, 1, 20, 100, "Brush embedded as JPEG at 75% quality", True, 75)
p_DrawImage(doc, 1, 410, 100, "Brush embedded as raw pixels", False, 0)

; save the document to a file
p_SaveAndView(doc, "Brush.pdf")

; clean up
doc:Free()

End
