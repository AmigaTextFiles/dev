/****************************************************************
**                                                             **
** Name:        Arc                                            **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        29.07.17                                       **
** Interpreter: Hollywood 7.0                                  **
** Licence:     BSD license                                    **
** Function:    Demonstrates various text features             **
**                                                             **
** Notes:       Based on a libharu example program written by  **
**              Takeshi Kanno <takeshi_kanno@est.hi-ho.ne.jp>  ** 
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (29.07.17)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

@REQUIRE "polybios"

@DISPLAY {Hidden = True}

Function p_PrintGrid(doc, page)

	Local height = page:GetHeight()
  	Local width = page:GetWidth()
  	Local font = doc:GetFont("Helvetica")

  	page:SetFontAndSize(font, 5)
  	page:SetGrayFill(0.5)
  	page:SetGrayStroke(0.8)

  	; Draw horizontal lines
  	Local y = 0
  	While y < height
    		If y % 10 = 0
      			page:SetLineWidth(0.5)
    		ElseIf page:GetLineWidth() <> 0.25
    			page:SetLineWidth(0.25)
    		EndIf
    		page:MoveTo(0, y)
    		page:LineTo(width, y)
    		page:Stroke()
    		If (y % 10 = 0) And (y > 0)
      			page:SetGrayStroke(0.5)
      			page:MoveTo(0, y)
      			page:LineTo(5, y)
      			page:Stroke()
      			page:SetGrayStroke(0.8)
    		EndIf
    		y = y + 5
  	Wend

  	; Draw vertical lines
  	Local x = 0
  	While x < width
    		If x % 10 = 0
      			page:SetLineWidth(0.5)
    		ElseIf page:GetLineWidth() <> 0.25
       			page:SetLineWidth(0.25)
		EndIf
    		page:MoveTo(x, 0)
    		page:LineTo(x, height)
    		page:Stroke()
    		If (x % 50 = 0) And (x > 0)
      			page:SetGrayStroke(0.5)
      			page:MoveTo(x, 0)
      			page:LineTo(x, 5)
      			page:Stroke()
      			page:MoveTo(x, height)
      			page:LineTo(x, height - 5)
      			page:Stroke()
      			page:SetGrayStroke(0.8)
    		EndIf
    		x = x + 5
  	Wend

	; Draw horizontal text
  	y = 0
  	While y < height
    		if (y % 10 = 0) And (y > 0)
      			page:BeginText()
      			page:MoveTextPos(5, y - 2)
      			page:ShowText(y)
      			page:EndText()
    		EndIf
    		y = y + 5
  	Wend

	; Draw vertical text
  	x = 0
  	While x < width
    		If (x % 50 = 0) And (x > 0)
      			page:BeginText()
      			page:MoveTextPos(x, 5)
      			page:ShowText(x)
      			page:EndText()
      			page:BeginText()
      			page:MoveTextPos(x, height - 10)
      			page:ShowText(x)
      			page:EndText()
    		EndIf
    		x = x + 5
  	Wend
  	
  	page:SetGrayFill(0)
  	page:SetGrayStroke(0)

EndFunction 

Function p_ShowStripePattern(page, x, y)

	Local iy = 0
  
  	While iy < 50
    		page:SetRGBStroke(0.0, 0.0, 0.5)
    		page:SetLineWidth(1)
    		page:MoveTo(x, y + iy)
    		page:LineTo(x + page:TextWidth("ABCabc123"), y + iy)
    		page:Stroke()
    		iy = iy + 3
  	Wend
  	
  	page:SetLineWidth(2.5)

EndFunction

Function p_ShowDescription(page, x, y, text)

	Local fsize = page:GetCurrentFontSize()
 	Local font = page:GetCurrentFont()
  	Local c = page:GetRGBFill()
  
  	page:BeginText()
  	page:SetRGBFill(0, 0, 0)
  	page:SetTextRenderingMode(#HPDF_FILL)
  	page:SetFontAndSize(font, 10)
  	page:TextOut(x, y - 12, text)
  	page:EndText()
  	page:SetFontAndSize(font, fsize)
  	page:SetRGBFill(c.r, c.g, c.b)

EndFunction

Function p_SaveAndView(doc, file$)
	
	file$ = FileRequest("Save PDF to...", "pdf", #REQ_SAVEMODE, "", file$)
	If file$ = "" Then End
		
	doc:SaveToFile(file$)	
	doc:Free()
	
	ExitOnError(False)
   	pdf.OpenDocument(1, file$)
   	Local err = GetLastError()
   	ExitOnError(True)
   	If err Then Return
   		
   	pdf.GetBrush(1, 1, 1)
	   	
   	CreateDisplay(2, {Width = GetAttribute(#BRUSH, 1, #ATTRWIDTH), Height = GetAttribute(#BRUSH, 1, #ATTRHEIGHT), Sizeable = True})
   	OpenDisplay(2)
   	ActivateDisplay(2)
   	
   	DisplayBrush(1, 0, 0)
   	
   	InstallEventHandler({SizeWindow = Function(msg) DisplayBrush(1, 0, 0, {Width = msg.width, Height = msg.height}) EndFunction})
   	
   	Repeat
   		WaitEvent
   	Forever
   		
EndFunction

Local page_title = "Text Demonstration"
Local samp_text = "abcdefgABCDEFG123!#$%&+-@?"
Local samp_text2 = "The quick brown fox jumps over the lazy dog."

Local doc = pdf.CreateDocument()

; set compression mode
doc:SetCompressionMode(#HPDF_COMP_ALL)

; create default-font
Local font = doc:GetFont("Helvetica")

; add a new page object
Local page = doc:AddPage()

; draw grid to the page
;p_PrintGrid(doc, page)

; print the title of the page(with positioning center)
page:SetFontAndSize(font, 24)
Local tw = page:TextWidth(page_title)

page:BeginText()
page:TextOut((page:GetWidth() - tw) / 2, page:GetHeight() - 50, page_title)
page:EndText()

page:BeginText()
Local margin = 60
page:MoveTextPos(margin, page:GetHeight() - 60)

; font size
Local fsize = 8
Local bound = page:GetWidth() - 2 * margin

While fsize < 60
	
	; set style and size of font.
    	page:SetFontAndSize(font, fsize)
    
    	; set the position of the text.
    	page:MoveTextPos(0, -5 - fsize)
    
    	; measure the number of characters which are included in the page.
    	Local len = page:MeasureText(samp_text, bound, False)
    
    	; truncate the text
    	page:ShowText(LeftStr(samp_text, len))
    
    	; print the description.
    	page:MoveTextPos(0, -10)
    	page:SetFontAndSize(font, 8)
    	page:ShowText("Font size = " .. fsize)
    
    	fsize = fsize * 1.5
Wend

; font color
page:SetFontAndSize(font, 8)
page:MoveTextPos(0, -30)
page:ShowText("Font color")

page:SetFontAndSize(font, 18)
page:MoveTextPos(0, -20)

Local len = StrLen(samp_text)

For Local J = 1 To len
	Local r = J / len
    	Local g = 1 - J / len
    	Local buf = MidStr(samp_text, J - 1, 1)
    	page:SetRGBFill(r, g, 0)
    	page:ShowText(buf)
Next

page:MoveTextPos(0, -25)

For Local J = 1 To len
	Local r = J / len
	Local b = 1 - J / len
    	Local buf = MidStr(samp_text, J - 1, 1)
    	page:SetRGBFill(r, 0, b)
    	page:ShowText(buf)
Next

page:MoveTextPos(0, -25)

For Local J = 1 To len
	Local g = J / len
    	Local b = 1 - J / len
    	Local buf = MidStr(samp_text, J - 1, 1)
    	page:SetRGBFill(0, g, b)
    	page:ShowText(buf)
Next

page:EndText()

Local ypos = 450

; Font rendering mode
page:SetFontAndSize(font, 32)
page:SetRGBFill(0.5, 0.5, 0.0)
page:SetLineWidth(1.5)

; PDF_FILL
p_ShowDescription(page, 60, ypos, "RenderingMode=PDF_FILL")
page:SetTextRenderingMode(#HPDF_FILL)
page:BeginText()
page:TextOut(60, ypos, "ABCabc123")
page:EndText()

; PDF_STROKE
p_ShowDescription(page, 60, ypos - 50, "RenderingMode=PDF_STROKE")
page:SetTextRenderingMode(#HPDF_STROKE)
page:BeginText()
page:TextOut(60, ypos - 50, "ABCabc123")
page:EndText()

; PDF_FILL_THEN_STROKE
p_ShowDescription(page, 60, ypos - 100, "RenderingMode=PDF_FILL_THEN_STROKE")
page:SetTextRenderingMode(#HPDF_FILL_THEN_STROKE)
page:BeginText()
page:TextOut(60, ypos - 100, "ABCabc123")
page:EndText()

; PDF_FILL_CLIPPING
p_ShowDescription(page, 60, ypos - 150, "RenderingMode=PDF_FILL_CLIPPING")
page:GSave()
page:SetTextRenderingMode(#HPDF_FILL_CLIPPING)
page:BeginText()
page:TextOut(60, ypos - 150, "ABCabc123")
page:EndText()
p_ShowStripePattern(page, 60, ypos - 150)
page:GRestore()

; PDF_STROKE_CLIPPING
p_ShowDescription(page, 60, ypos - 200, "RenderingMode=PDF_STROKE_CLIPPING")
page:GSave()
page:SetTextRenderingMode(#HPDF_STROKE_CLIPPING)
page:BeginText()
page:TextOut(60, ypos - 200, "ABCabc123")
page:EndText()
p_ShowStripePattern(page, 60, ypos - 200)
page:GRestore()

; PDF_FILL_STROKE_CLIPPING
p_ShowDescription(page, 60, ypos - 250, "RenderingMode=PDF_FILL_STROKE_CLIPPING")
page:GSave()
page:SetTextRenderingMode(#HPDF_FILL_STROKE_CLIPPING)
page:BeginText()
page:TextOut(60, ypos - 250, "ABCabc123")
page:EndText()
p_ShowStripePattern(page, 60, ypos - 250)
page:GRestore()

; Reset text attributes
page:SetTextRenderingMode(#HPDF_FILL)
page:SetRGBFill(0, 0, 0)
page:SetFontAndSize(font, 30)

; Rotating text
Local angle1 = 30 ; A rotation of 30 degrees
Local rad1 = Rad(angle1)
p_ShowDescription(page, 320, ypos - 60, "Rotating text")
page:BeginText()
page:SetTextMatrix(Cos(rad1), Sin(rad1), -Sin(rad1), Cos(rad1), 330, ypos - 60)
page:ShowText("ABCabc123")
page:EndText()

; Skewing text
p_ShowDescription(page, 320, ypos - 120, "Skewing text")
page:BeginText()
angle1 = 10
Local angle2 = 20
rad1 = Rad(angle1)
Local rad2 = Rad(angle2)
page:SetTextMatrix(1, Tan(rad1), Tan(rad2), 1, 320, ypos - 120)
page:ShowText("ABCabc123")
page:EndText()

; scaling text(X direction)
p_ShowDescription(page, 320, ypos - 175, "Scaling text (X direction)")
page:BeginText()
page:SetTextMatrix(1.5, 0, 0, 1, 320, ypos - 175)
page:ShowText("ABCabc12")
page:EndText()

; scaling text(Y direction)
p_ShowDescription(page, 320, ypos - 250, "Scaling text (Y direction)")
page:BeginText()
page:SetTextMatrix(1, 0, 0, 2, 320, ypos - 250)
page:ShowText("ABCabc123")
page:EndText()

; char spacing, word spacing
p_ShowDescription(page, 60, 140, "char-spacing 0")
p_ShowDescription(page, 60, 100, "char-spacing 1.5")
p_ShowDescription(page, 60, 60, "char-spacing 1.5, word-spacing 2.5")

page:SetFontAndSize(font, 20)
page:SetRGBFill(0.1, 0.3, 0.1)

; char-spacing 0
page:BeginText()
page:TextOut(60, 140, samp_text2)
page:EndText()

; char-spacing 1.5
page:SetCharSpace(1.5)

page:BeginText()
page:TextOut(60, 100, samp_text2)
page:EndText()

; char-spacing 1.5, word-spacing 3.5
page:SetWordSpace(2.5)

page:BeginText()
page:TextOut(60, 60, samp_text2)
page:EndText()

; save the document to a file
p_SaveAndView(doc, "Text.pdf")

; clean up
doc:Free()

End
