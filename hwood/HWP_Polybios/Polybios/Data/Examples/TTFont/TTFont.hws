/****************************************************************
**                                                             **
** Name:        TTFont                                         **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        30.07.17                                       **
** Interpreter: Hollywood 7.0                                  **
** Licence:     BSD license                                    **
** Function:    Demonstrates how to embed TrueType font in PDF **
**                                                             **
** Notes:       Based on a libharu example program written by  **
**              Takeshi Kanno <takeshi_kanno@est.hi-ho.ne.jp>  **
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (30.07.17)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

@REQUIRE "polybios"

@DISPLAY {Hidden = True}

Function p_SaveAndView(doc, file$)
	
	file$ = FileRequest("Save PDF to...", "pdf", #REQ_SAVEMODE, "", file$)
	If file$ = "" Then End
		
	doc:SaveToFile(file$)	
	doc:Free()
	
	ExitOnError(False)
   	pdf.OpenDocument(1, file$)
   	Local err = GetLastError()
   	ExitOnError(True)
   	If err Then Return
   		
   	pdf.GetBrush(1, 1, 1)
	   	
   	CreateDisplay(2, {Width = GetAttribute(#BRUSH, 1, #ATTRWIDTH), Height = GetAttribute(#BRUSH, 1, #ATTRHEIGHT), Sizeable = True})
   	OpenDisplay(2)
   	ActivateDisplay(2)
   	
   	DisplayBrush(1, 0, 0)
   	
   	InstallEventHandler({SizeWindow = Function(msg) DisplayBrush(1, 0, 0, {Width = msg.width, Height = msg.height}) EndFunction})
   	
   	Repeat
   		WaitEvent
   	Forever
   		
EndFunction

Local SAMP_TXT = "The quick brown fox jumps over the lazy dog."

Local doc = pdf.CreateDocument()

; Add a new page object
page = doc:AddPage()

title_font = doc:GetFont("Helvetica")
detail_font_name = doc:LoadTTFont("PenguinAttack.ttf", True)
detail_font = doc:GetFont(detail_font_name)

page:SetFontAndSize(title_font, 10)

page:BeginText()

; Move the position of the text to top of the page.
page:MoveTextPos(10, 190)
page:ShowText(detail_font_name)
page:ShowText("(Embedded Subset)")

page:SetFontAndSize(detail_font, 15)
page:MoveTextPos(10, -20)
page:ShowText("abcdefghijklmnopqrstuvwxyz")
page:MoveTextPos(0, -20)
page:ShowText("ABCDEFGHIJKLMNOPQRSTUVWXYZ")
page:MoveTextPos(0, -20)
page:ShowText("1234567890")
page:MoveTextPos(0, -20)

page:SetFontAndSize(detail_font, 10)
page:ShowText(SAMP_TXT)
page:MoveTextPos(0, -18)

page:SetFontAndSize(detail_font, 16)
page:ShowText(SAMP_TXT)
page:MoveTextPos(0, -27)

page:SetFontAndSize(detail_font, 23)
page:ShowText(SAMP_TXT)
page:MoveTextPos(0, -36)

page:SetFontAndSize(detail_font, 30)
page:ShowText(SAMP_TXT)
page:MoveTextPos(0, -36)

pw = page:TextWidth(SAMP_TXT)
page_height = 210
page_width = pw + 40

page:SetWidth(page_width)
page:SetHeight(page_height)

; Finish to print text.
page:EndText()

page:SetLineWidth(0.5)

page:MoveTo(10, page_height - 25)
page:LineTo(page_width - 10, page_height - 25)
page:Stroke()

page:MoveTo(10, page_height - 85)
page:LineTo(page_width - 10, page_height - 85)
page:Stroke()

; save the document to a file
p_SaveAndView(doc, "TTFont.pdf")

; clean up
doc:Free()

End
