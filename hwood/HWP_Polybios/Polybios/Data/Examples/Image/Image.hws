/****************************************************************
**                                                             **
** Name:        Image                                          **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        29.07.17                                       **
** Interpreter: Hollywood 7.0                                  **
** Licence:     BSD license                                    **
** Function:    Shows how to use image transformation in PDF   **
**                                                             **
** Notes:       Based on a libharu example program written by  **
**              Takeshi Kanno <takeshi_kanno@est.hi-ho.ne.jp>  ** 
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (29.07.17)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

@REQUIRE "polybios"

@DISPLAY {Hidden = True}

Function p_ShowDescription(page, x, y, text)
	
	page:MoveTo(x, y - 10)
    	page:LineTo(x, y + 10)
    	page:MoveTo(x - 10, y)
    	page:LineTo(x + 10, y)
    	page:Stroke()

    	page:SetFontAndSize(page:GetCurrentFont(), 8)
    	page:SetRGBFill(0, 0, 0)

	page:BeginText()
	Local buf = FormatStr("(x=%d,y=%d)", x, y)
	page:MoveTextPos(x - page:TextWidth(buf) - 5, y - 10)
	page:ShowText(buf)
    	page:EndText()

	page:BeginText()
    	page:MoveTextPos(x - 20, y - 25)
    	page:ShowText(text)
    	page:EndText()

EndFunction

Function p_SaveAndView(doc, file$)
	
	file$ = FileRequest("Save PDF to...", "pdf", #REQ_SAVEMODE, "", file$)
	If file$ = "" Then End
		
	doc:SaveToFile(file$)	
	doc:Free()
	
	ExitOnError(False)
   	pdf.OpenDocument(1, file$)
   	Local err = GetLastError()
   	ExitOnError(True)
   	If err Then Return
   		
   	pdf.GetBrush(1, 1, 1)
	   	
   	CreateDisplay(2, {Width = GetAttribute(#BRUSH, 1, #ATTRWIDTH), Height = GetAttribute(#BRUSH, 1, #ATTRHEIGHT), Sizeable = True})
   	OpenDisplay(2)
   	ActivateDisplay(2)
   	
   	DisplayBrush(1, 0, 0)
   	
   	InstallEventHandler({SizeWindow = Function(msg) DisplayBrush(1, 0, 0, {Width = msg.width, Height = msg.height}) EndFunction})
   	
   	Repeat
   		WaitEvent
   	Forever
   		
EndFunction

Local doc = pdf.CreateDocument()

; enable compression
doc:SetCompressionMode(#HPDF_COMP_ALL)

; create default-font
font = doc:GetFont("Helvetica")

; add a new page object
page = doc:AddPage()

page:SetWidth(550)
page:SetHeight(500)

dst = page:CreateDestination()
dst:SetXYZ(0, page:GetHeight(), 1)
doc:SetOpenAction(dst)

page:BeginText()
page:SetFontAndSize(font, 20)
page:MoveTextPos(220, page:GetHeight() - 70)
page:ShowText("Image Demo")
page:EndText()

; load image file
image = doc:LoadPNGImage("basn3p02.png")

; image1 is masked by image2
image1 = doc:LoadPNGImage("basn3p02.png")

; image2 is a mask image
image2 = doc:LoadPNGImage("basn0g01.png")

; image3 is a RGB-color image. we use this image for color-mask demo
image3 = doc:LoadPNGImage("maskimage.png")

iw = image:GetWidth()
ih = image:GetHeight()

page:SetLineWidth(0.5)

x = 100
y = page:GetHeight() - 150

; Draw image to the canvas (normal-mode with actual size)
page:DrawImage(image, x, y, iw, ih)

p_ShowDescription(page, x, y, "Actual Size")

x = x + 150

; Scaling image (X direction)
page:DrawImage(image, x, y, iw * 1.5, ih)
p_ShowDescription(page, x, y, "Scaling image (X direction)")

x = x + 150

; Scaling image (Y direction)
page:DrawImage(image, x, y, iw, ih * 1.5)
p_ShowDescription(page, x, y, "Scaling image (Y direction)")

x = 100
y = y - 120

; Skewing image
angle1 = 10
angle2 = 20
rad1 = angle1 / 180 * #PI
rad2 = angle2 / 180 * #PI

page:GSave()
page:Concat(iw, Tan(rad1) * iw, Tan(rad2) * ih, ih, x, y)
page:ExecuteXObject(image)
page:GRestore()

p_ShowDescription(page, x, y, "Skewing image")

x = x + 150

; Rotating image
angle = 30                ; rotation of 30 degrees.
Rad = angle / 180 * #PI   ; calcurate the radian value

page:GSave()
page:Concat(iw * Cos(rad), iw * Sin(rad), ih * -Sin(rad), ih * Cos(rad), x, y)
page:ExecuteXObject(image)
page:GRestore()

p_ShowDescription(page, x, y, "Rotating image")

x = x + 150

; Draw masked image

; Set image2 to the mask image of image1
image1:SetMaskImage(image2)

page:SetRGBFill(0, 0, 0)
page:BeginText()
page:MoveTextPos(x - 6, y + 14)
page:ShowText("MASKMASK")
page:EndText()

page:DrawImage(image1, x - 3, y - 3, iw + 6, ih + 6)

p_ShowDescription(page, x, y, "masked image")

x = 100
y = y - 120

; Color mask
page:SetRGBFill(0, 0, 0)
page:BeginText()
page:MoveTextPos(x - 6, y + 14)
page:ShowText("MASKMASK")
page:EndText()

image3:SetColorMask(0, 255, 0, 0, 0, 255)
page:DrawImage(image3, x, y, iw, ih)

p_ShowDescription(page, x, y, "Color Mask")

; save the document to a file
p_SaveAndView(doc, "Image.pdf")

; clean up
doc:Free()

End
     