/****************************************************************
**                                                             **
** Name:        ExtGState                                      **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        29.07.17                                       **
** Interpreter: Hollywood 7.0                                  **
** Licence:     BSD license                                    **
** Function:    Shows how to use extended graphics states      **
**                                                             **
** Notes:       Based on a libharu example program written by  **
**              Takeshi Kanno <takeshi_kanno@est.hi-ho.ne.jp>  ** 
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (29.07.17)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

@REQUIRE "polybios"

@DISPLAY {Hidden = True}

Const #PAGE_WIDTH = 600
Const #PAGE_HEIGHT = 900

Function p_DrawCircles(page, description, x, y)
	
	page:SetLineWidth(1.0)
  	page:SetRGBStroke(0.0, 0.0, 0.0)
  	page:SetRGBFill(1.0, 0.0, 0.0)
  	page:Circle(x + 40, y + 40, 40)
  	page:ClosePathFillStroke()
  	page:SetRGBFill(0.0, 1.0, 0.0)
  	page:Circle(x + 100, y + 40, 40)
  	page:ClosePathFillStroke()
  	page:SetRGBFill(0.0, 0.0, 1.0)
  	page:Circle(x + 70, y + 74.64, 40)
  	page:ClosePathFillStroke()
  	page:SetRGBFill(0.0, 0.0, 0.0)
  	page:BeginText()
  	page:TextOut(x + 0.0, y + 130.0, description)
  	page:EndText()

EndFunction

Function p_SaveAndView(doc, file$)
	
	file$ = FileRequest("Save PDF to...", "pdf", #REQ_SAVEMODE, "", file$)
	If file$ = "" Then End
		
	doc:SaveToFile(file$)	
	doc:Free()
	
	ExitOnError(False)
   	pdf.OpenDocument(1, file$)
   	Local err = GetLastError()
   	ExitOnError(True)
   	If err Then Return
   		
   	pdf.GetBrush(1, 1, 1)
	   	
   	CreateDisplay(2, {Width = GetAttribute(#BRUSH, 1, #ATTRWIDTH), Height = GetAttribute(#BRUSH, 1, #ATTRHEIGHT), Sizeable = True})
   	OpenDisplay(2)
   	ActivateDisplay(2)
   	
   	DisplayBrush(1, 0, 0)
   	
   	InstallEventHandler({SizeWindow = Function(msg) DisplayBrush(1, 0, 0, {Width = msg.width, Height = msg.height}) EndFunction})
   	
   	Repeat
   		WaitEvent
   	Forever
   		
EndFunction

Local doc = pdf.CreateDocument()

; set compression mode
doc:SetCompressionMode(#HPDF_COMP_ALL)

Local hfont = doc:GetFont("Helvetica-Bold")

; add a new page object.
Local page = doc:AddPage()

page:SetFontAndSize(hfont, 10)

page:SetHeight(#PAGE_HEIGHT)
page:SetWidth(#PAGE_WIDTH)

; normal
page:GSave()
p_DrawCircles(page, "normal", 40.0, #PAGE_HEIGHT - 170)
page:GRestore()

; transparency(0.8)
page:GSave()
Local gstate = doc:CreateExtGState()
gstate:SetAlphaFill(0.8)
gstate:SetAlphaStroke(0.8)
page:SetExtGState(gstate)
p_DrawCircles(page, "alpha fill = 0.8", 230.0, #PAGE_HEIGHT - 170)
page:GRestore()

; transparency(0.4)
page:GSave()
gstate = doc:CreateExtGState()
gstate:SetAlphaFill(0.4)
page:SetExtGState(gstate)
p_DrawCircles(page, "alpha fill = 0.4", 420.0, #PAGE_HEIGHT - 170)
page:GRestore()

Local BlendList = {
	{"#HPDF_BM_MULTIPLY", 40, 340},
    	{"#HPDF_BM_SCREEN", 230, 340},
    	{"#HPDF_BM_OVERLAY", 420, 340},
    	{"#HPDF_BM_DARKEN", 40, 510},
    	{"#HPDF_BM_LIGHTEN", 230, 510},
    	{"#HPDF_BM_COLOR_DODGE", 420, 510},
    	{"#HPDF_BM_COLOR_BUM", 40, 680},
    	{"#HPDF_BM_HARD_LIGHT", 230, 680},
    	{"#HPDF_BM_SOFT_LIGHT", 420, 680},
    	{"#HPDF_BM_DIFFERENCE", 40, 850},
    	{"#HPDF_BM_EXCLUSION", 230, 850},
}

For J, Rec In IPairs(BlendList)
	page:GSave()
    	gstate = doc:CreateExtGState()
    	gstate:SetBlendMode(GetConstant(Rec[0]))
    	page:SetExtGState(gstate)
    	p_DrawCircles(page, UnrightStr(Rec[0], 1), Rec[1], #PAGE_HEIGHT - Rec[2])
    	page:GRestore()
Next

; save the document to a file
p_SaveAndView(doc, "ExtGState.pdf")

; clean up
doc:Free()

End
