;
; $VER: Polybios-Setup 1.3 (06.12.20)
;
; Done by Andreas Falkenhahn
;

(message (cat "\nWelcome to the Setup utility for\n\n"
	"Polybios 1.3\n\n"
	"- Making History in PDF -\n\n"
	"Copyright (C) 2013-2020 by Andreas Falkenhahn\n\n"
	"http://www.hollywood-mal.com/"
))

(welcome)

(set basepath
	(askdir
		(prompt  "Where do you want to have Polybios installed? (a drawer will be created)")
		(help    @askdir-help)
		(default "SYS:")
	)
)

(set sstr (substr basepath (- (strlen basepath) 1) 1))
(if (or (= sstr "/") (= sstr ":"))
	(set destpath (cat basepath "Polybios"))
	(set destpath (cat basepath "/Polybios"))
)

(set @default-dest destpath)

(set defaultplat 0)

(if (exists "SYS:MorphOS/C/Assign") (
	(set defaultplat 4)
) (
	(if (exists "SYS:System/GrimReaper") (
		(set defaultplat 2)
	))
))

(set whichver
	(askchoice
		(prompt  "Please select your platform")
		(help    @askoptions-help)
		(choices "AmigaOS 3 (m68k)" "AmigaOS 3 (m68k/m881)" "AmigaOS 4 (ppc)" "AROS (x86)" "MorphOS (ppc)")		
		(default defaultplat)
	)
)

(set #platform "m68k-amigaos")

(if (= whichver 1) (
	(set #platform "m881-amigaos")
))

(if (= whichver 2) (
	(set #platform "ppc-amigaos")
))

(if (= whichver 3) (
	(set #platform "x86-aros")
))

(if (= whichver 4) (
	(set #platform "ppc-morphos")
))

(makedir destpath)
(makedir "LIBS:Hollywood")

(copylib
	(source (cat "Platforms/" #platform "/polybios.hwp"))
	(dest   "LIBS:Hollywood")
	(prompt "polybios.hwp")
	(help @copylib-help)
	(optional "nofail" "force" "askuser")
	(confirm)
)

(if (exists (cat "Platforms/" #platform "/polybios.ext")) (
	(copylib
		(source (cat "Platforms/" #platform "/polybios.ext"))
		(dest    "LIBS:Hollywood")
		(prompt "polybios.ext")
		(help @copylib-help)
		(optional "nofail" "force" "askuser")
		(confirm)
	)
))

(copyfiles
	(source  "Data.icon")
	(dest    basepath)
	(newname "Polybios.info")
	(optional force oknodelete)
)

(copyfiles
	(source "Data")
	(dest   destpath)
	(all)
	(optional "nofail" "force" "askuser")
)

(if (exists "Hollywood:GUI" (noreq)) (

	(makedir "Hollywood:Examples/Polybios")

	(copyfiles
		(source "Data/Examples")
		(dest   "Hollywood:Examples/Polybios")
		(all)
		(optional "nofail" "force" "askuser")
	)
))

(if (exists "Hollywood:LinkerPlugins" (noreq)) (

	(copyfiles
		(source "Platforms")
		(dest   "Hollywood:LinkerPlugins")
		(all)
		(optional "nofail" "force" "askuser")
	)
))

(set #cmdline (cat "Assign Polybios: \"" destpath "\""))

(startup
	(prompt  "\nAn assign has to be inserted in your user-startup. Please click Continue.")
	(help    @startup-help)
	"Polybios"
	(command #cmdline)
	(confirm)
)

(run #cmdline)

(message (cat "\nPolybios comes with over 25 ready-to-run examples.\n\n"
	"The examples have been integrated into your Hollywood GUI (only "
	"if Hollywood is installed on this system). Now that you have installed "
	"Polybios, there will be a new Polybios tab in the Hollywood GUI which "
	"allows you to conveniently launch the example scripts that come with Polybios.\n\n"
	"Click to see a screenshot that shows how to launch the example scripts "
	"that come with Polybios from the Hollywood GUI."))

(run ("Sys:Utilities/MultiView GUI.png"))

(message (cat "\nIMPORTANT INFORMATION:\n\nIF YOU UPGRADED FROM A PREVIOUS "
	"VERSION OF POLYBIOS, YOU NEED TO REBOOT YOUR SYSTEM NOW TO COMPLETE "
	"THE INSTALLATION."))

(exit)
