;
; $VER: Pangomonium-Setup 2.1 (27.09.24)
;
; Done by Andreas Falkenhahn
;

(message (cat "\nWelcome to the Setup utility for\n\n"
	"Pangomonium 2.1\n\n"
	"- The Ultimate Text & Graphics Engine for Hollywood -\n\n"
	"Copyright (C) 2022-24 by Andreas Falkenhahn\n\n"
	"http://www.hollywood-mal.com/"
))

(welcome)

(set basepath
	(askdir
		(prompt  "Where do you want to have Pangomonium installed? (a drawer will be created)")
		(help    @askdir-help)
		(default "SYS:")
	)
)

(set sstr (substr basepath (- (strlen basepath) 1) 1))
(if (or (= sstr "/") (= sstr ":"))
	(set destpath (cat basepath "Pangomonium"))
	(set destpath (cat basepath "/Pangomonium"))
)

(set @default-dest destpath)

(set defaultplat 0)

(if (exists "SYS:MorphOS/C/Assign") (
	(set defaultplat 3)
) (
	(if (exists "SYS:System/GrimReaper") (
		(set defaultplat 2)
	))
))

(set whichver
	(askchoice
		(prompt  "Please select your platform")
		(help    @askoptions-help)
		(choices "AmigaOS 3 (m68k)" "AmigaOS 3 (m68k/m881)" "AmigaOS 4 (ppc)" "MorphOS (ppc)")		
		(default defaultplat)
	)
)

(set #platform "m68k-amigaos")

(if (= whichver 1) (
	(set #platform "m881-amigaos")
))

(if (= whichver 2) (
	(set #platform "ppc-amigaos")
))

(if (= whichver 3) (
	(set #platform "ppc-morphos")
))

(makedir destpath)
(makedir "LIBS:Hollywood")  

(copylib
	(source (cat "Platforms/" #platform "/pangomonium.hwp"))
	(dest   "LIBS:Hollywood")
	(prompt "pangomonium.hwp")
	(help @copylib-help)
	(optional "nofail" "force" "askuser")
	(confirm)
)

(copyfiles
	(source  "Data.icon")
	(dest    basepath)
	(newname "Pangomonium.info")
	(optional force oknodelete)
)

(copyfiles
	(source "Data")
	(dest   destpath)
	(all)
	(optional "nofail" "force" "askuser")
)

(if (exists "Hollywood:GUI" (noreq)) (

	(makedir "Hollywood:Examples/Pangomonium")

	(copyfiles
		(source "Data/Examples")
		(dest   "Hollywood:Examples/Pangomonium")
		(all)
		(optional "nofail" "force" "askuser")
	)
))

(if (exists "Hollywood:LinkerPlugins" (noreq)) (

	(copyfiles
		(source "Platforms")
		(dest   "Hollywood:LinkerPlugins")
		(all)
		(optional "nofail" "force" "askuser")
	)
))

(set #cmdline (cat "Assign Pangomonium: \"" destpath "\""))

(startup
	(prompt  "\nAn assign has to be inserted in your user-startup. Please click Continue.")
	(help    @startup-help)
	"Pangomonium"
	(command #cmdline)
	(confirm)
)

(run #cmdline)

(message (cat "\nPangomonium comes with over 30 ready-to-run examples.\n\n"
	"The examples have been integrated into your Hollywood GUI (only "
	"if Hollywood is installed on this system). Now that you have installed "
	"Pangomonium, there will be a new Pangomonium tab in the Hollywood GUI which "
	"allows you to conveniently launch the example scripts that come with Pangomonium.\n\n"
	"Click to see a screenshot that shows how to launch the example scripts "
	"that come with Pangomonium from the Hollywood GUI."))
	
(run ("Sys:Utilities/MultiView GUI.png"))

(exit)
