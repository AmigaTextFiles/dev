/****************************************************************
**                                                             **
** Name:        CircleText                                     **
** Author:      Andreas Falkenhahn, based on Pango example code**
** Version:     1.0                                            **
** Date:        10.03.24                                       **
** Interpreter: Hollywood 10.0                                 **
** Licence:     Sample program                                 **
** Function:    Draws a text circle                            **
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (10.03.24)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Make sure we have at least Hollywood 10.0!
*/
@VERSION 10,0


/*
** This script requires the Pangomonium plugin
*/
@REQUIRE "pangomonium"


/*
** Give us a white background display
*/
@DISPLAY {Color = #WHITE}

Const #RADIUS = 150
Const #N_WORDS = 10
Const #THEFONT = "Sans Bold 27"

;img = cairo.PDFSurface("test.pdf", 595.276, 841.89)
;img = cairo.SVGSurface("test.svg", 595.276, 841.89)

img = cairo.ImageSurface(#CAIRO_FORMAT_ARGB32, 640, 480)
cr = cairo.Context(img)

cr:SetSourceRGB(1.0, 1.0, 1.0)
cr:Paint()

; Center coordinates on the middle of the region we are drawing
cr:Translate(#RADIUS, #RADIUS)

; Create a PangoLayout, set the font and text
layout = cr:PangoLayout()

layout:SetText("Text")
desc = pango.FontDescription(#THEFONT)
layout:SetFontDescription(desc)

; Draw the layout #N_WORDS times in a circle 
For Local i = 0 To #N_WORDS - 1

      Local angle = (360 * i) / #N_WORDS

      cr:Save()

      ; Gradient from red at angle == 60 to blue at angle == 240
      Local red = (1 + Cos((angle - 60) * #PI / 180)) / 2
      cr:SetSourceRGB(Red, 0, 1.0 - red)

      cr:Rotate(angle * #PI / 180)

      ; Inform Pango to re-layout the text with the new transformation 
      cr:UpdateLayout(layout)

      Local width, height = layout:GetSize()
      cr:MoveTo(-(width / #PANGO_SCALE) / 2, - #RADIUS)
      cr:ShowLayout(layout)

      cr:Restore()
Next
    
img:ToBrush(1)
DisplayBrush(1, 0, 0)
WaitLeftMouse
End
