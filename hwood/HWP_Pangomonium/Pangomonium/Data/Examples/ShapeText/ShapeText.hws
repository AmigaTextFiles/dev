/****************************************************************
**                                                             **
** Name:        ShapeText                                      **
** Author:      Andreas Falkenhahn, based on original code by  **
**              Behdad Esfahbod                                **
** Version:     1.0                                            **
** Date:        10.03.24                                       **
** Interpreter: Hollywood 10.0                                 **
** Licence:     Sample program                                 **
** Function:    Draws custom shapes in a text layout           **
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (10.03.24)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Make sure we have at least Hollywood 10.0!
*/
@VERSION 10,0


/*
** This script requires the Pangomonium plugin
*/
@REQUIRE "pangomonium"


/*
** Give us a white background display
*/
@DISPLAY {Color = #WHITE}

Const #BULLET = "•"

text$ =
	"The GNOME project provides two things:\n" ..
	"\n" ..
	"  • The GNOME desktop environment\n" ..
	"  • The GNOME development platform\n" ..
	"  • Planet GNOME"

GnomeFootLogo = {
	width = 96.2152, height = 118.26,
  	path = "M 86.068,1 C 61.466,0 56.851,35.041 70.691,35.041 C 84.529,35.041 110.671,0 86.068,0 z " ..
  	"M 45.217,30.699 C 52.586,31.149 60.671,2.577 46.821,4.374 C 32.976,6.171 37.845,30.249 45.217,30.699 z " ..
  	"M 11.445,48.453 C 16.686,46.146 12.12,23.581 3.208,29.735 C -5.7,35.89 6.204,50.759 11.445,48.453 z " ..
  	"M 26.212,36.642 C 32.451,35.37 32.793,9.778 21.667,14.369 C 10.539,18.961 19.978,37.916 26.212,36.642 L 26.212,36.642 z " ..
  	"M 58.791,93.913 C 59.898,102.367 52.589,106.542 45.431,101.092 C 22.644,83.743 83.16,75.088 79.171,51.386 C 75.86,31.712 15.495,37.769 8.621,68.553 C 3.968,89.374 27.774,118.26 52.614,118.26 C 64.834,118.26 78.929,107.226 81.566,93.248 C 83.58,82.589 57.867,86.86 58.791,93.913 L 58.791,93.913 z " ..
  	"\0"
}

Function p_GetNum(p$, comma)
	
	Local v, chrs = Val(p$)
  	Return(v, UnrightStr(p$, chrs + comma))
  
EndFunction
 
Function p_MiniSVGRender(shape, cr, do_path)

	Local pos = 0
  	Local x, y = cr:GetCurrentPoint()
  	Local p$ = shape.path
  
  	cr:Translate(x, y)
  
	Repeat
  		Local op$ = LeftStr(p$, 1)
  		p$ = UnrightStr(p$, 2)
  		If op$ = "\0" Then Break
  		
  		Switch op$
	  	Case "M":
	  		x, p$ = p_GetNum(p$, True)
	  		y, p$ = p_GetNum(p$, True)
			cr:MoveTo(x, y)
		Case "L":
	  		x, p$ = p_GetNum(p$, True)
	  		y, p$ = p_GetNum(p$, True)
			cr:LineTo(x, y)		
		Case "C":
			Local x1, y1, x2, y2, x3, y3
	  		x1, p$ = p_GetNum(p$, True)
	  		y1, p$ = p_GetNum(p$, True)
	  		x2, p$ = p_GetNum(p$, True)
	  		y2, p$ = p_GetNum(p$, True)
	  		x3, p$ = p_GetNum(p$, True)
	  		y3, p$ = p_GetNum(p$, True)   	
			cr:CurveTo(x1, y1, x2, y2, x3, y3)
		Case "z":
			cr:ClosePath()
		EndSwitch
   	Forever

	If Not do_path Then cr:Fill()

EndFunction

Function p_MiniSVGShapeRenderer(cr, attr, do_path)

	Local ink_rect, log_rect, shape = attr:GetValue()

	Local scale_x = ink_rect.width  /(#PANGO_SCALE * shape.width )
  	Local scale_y = ink_rect.height /(#PANGO_SCALE * shape.height)

	cr:RelMoveTo(ink_rect.x / #PANGO_SCALE, ink_rect.y / #PANGO_SCALE)
  	cr:Scale(scale_x, scale_y)

	p_MiniSVGRender(shape, cr, do_path)

EndFunction

Function p_GetLayout(cr)
	
	Local ink_rect     = {x = 1 * #PANGO_SCALE, y = -11 * #PANGO_SCALE, width = 8 * #PANGO_SCALE, height = 10 * #PANGO_SCALE}
  	Local logical_rect = {x = 0 * #PANGO_SCALE, y = -12 * #PANGO_SCALE, width = 10 * #PANGO_SCALE, height = 12 * #PANGO_SCALE}

  	; Create a PangoLayout, set the font and text
  	Local layout = cr:PangoLayout()
  	Local ctx = layout:GetContext()
  
  	ctx:SetShapeRenderer(p_MiniSVGShapeRenderer)
  
  	layout:SetText(text$)

  	attrs = pango.AttrList()
  
  	Local pos = 0
  
  	refs = {}

  	; prevent ctx from being garbage collected (see manual to learn why this is necessary)
  	refs[rc] = ctx
  	rc = rc + 1
  
  	; Set gnome shape attributes for all bullets  
  	Repeat
     		pos = FindStr(text$, #BULLET, True, pos, #ENCODING_ISO8859_1)
     		If pos = -1 Then break
     	
     		Local attr = pango.Attribute("shape", ink_rect, logical_rect, GnomeFootLogo)
     
     		; must keep a reference to the attribute to prevent it from being garbage collected
     		; because the attribute uses user data(see manual for details)
     		refs[rc] = attr
     		rc = rc + 1
     
     		attr:SetRange(pos, pos + ByteLen(#BULLET))
     		attrs:Insert(attr)
     
		pos = pos + ByteLen(#BULLET)
	Forever
  
	layout:SetAttributes(attrs)
  
	Return(layout)

EndFunction

Function p_DrawText(cr)
	
  	Local layout = p_GetLayout(cr)

  	cr:MoveTo(10, 10)
  	cr:ShowLayout(layout)

EndFunction

surface = cairo.ImageSurface(#CAIRO_FORMAT_ARGB32, 640, 480)
cr = cairo.Context(surface)

cr:SetSourceRGB(1.0, 1.0, 1.0)
cr:Paint(cr)
cr:SetSourceRGB(0.0, 0.0, 0.5)
p_DrawText(cr)

surface:ToBrush(1)
DisplayBrush(1, 0, 0)
WaitLeftMouse
End 
