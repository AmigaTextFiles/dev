/****************************************************************
**                                                             **
** Name:        VectorTest                                     **
** Author:      Andreas Falkenhahn                             **
** Version:     1.1                                            **
** Date:        16.11.14                                       **
** Interpreter: Hollywood 5.0                                  **
** Licence:     Sample program for Hollywood                   **
** Function:    Demonstrates use of real vector graphics which **
**              are supported starting with Hollywood 5.0      **
**                                                             **
** History:                                                    **
**                                                             **
** 1.1: (16.11.14)                                             **
**                                                             **
** - forgot to close the path for shape 2 (simple circle)      **
**                                                             **
** 1.0: (05.03.10)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Important! Check if the used Hollywood version is at least
** version 5.0!
*/
@VERSION 5,0


/*
** some external data
*/
@ANIM 1, "buttons.png", {Width = 40, Height = 40, Frames = 6}
@ANIM 2, "buttons2.png", {Width = 40, Height = 40, Frames = 6}


/*
** set up our display
*/
@DISPLAY {Width = 640, Height = 480, FillStyle = #FILLGRADIENT, GradientStartColor = #WHITE, GradientEndColor = $0000CC}

; generic star drawing function
Function p_Star(cx, cy, radius, nodes, iradius)

	For Local i = 0 To nodes - 1

		Local angle = i*2*#PI/nodes
		Local r
		
		If i % 2 = 1
			r = radius
		Else
			r = iradius
		EndIf
				
		Local x = cx + Cos(angle) * r
		Local y = cy + Sin(angle) * r

		If i <> 0
			LineTo(1, x, y)
		Else
			MoveTo(1, x, y)
		EndIf
	Next
	
	ClosePath(1)
	
EndFunction

; draw specified shape
Function p_DrawShape(w)

	Local tx, ty = 0, 0

	; shape already there? --> remove it now!
	If LayerExists("shape") Then RemoveLayer("shape")

	; start new path		
	StartPath(1)
	
	SetFillRule(#FILLRULEEVENODD)
			
	line_width = 10
	
	Switch w
	Case 0:
		; two polygons combined into one path
		MoveTo(1, 428.0, 225.6)
		LineTo(1, 530.4, 430.4)
		RelLineTo(1, -102.4, 0.0)
		CurveTo(1, 351.2, 430.4, 351.2, 328.0, 428.0, 328.0)
		ClosePath(1)

		MoveTo(1, 364.0, 225.6)
		RelLineTo(1, 51.2, 51.2)
		RelLineTo(1, -51.2, 51.2)
		RelLineTo(1, -51.2, -51.2)
		ClosePath(1)
		
	Case 1:
		; vector text on a path
		MoveTo(1, 320, 300)
		AddTextToPath(1, "Hollywood")	
		line_width = 3	
		
	Case 2:	
		; simple circle
	      	AddCircleToPath(1, 470, 390, 150)
	      	ClosePath(1)
	      		
	Case 3:
		; arc style shape
		MoveTo(1, 420, 310)
		AddArcToPath(1, 420, 310, 100, 100, 45, 315)
		ClosePath(1) 
				
	Case 4:
		; star shape
		p_Star(200, 200, 150, 26, 100)	
		tx = 250
		ty = 150
		
	Case 5:
		; C shape
		MoveTo(1, 260.03474,686.53686)
		LineTo(1, 260.03474,711.77767)
		CurveTo(1, 255.55029,709.00125,250.55973,707.6306,245.06314,707.6306)
		CurveTo(1, 239.81958,707.6306,234.94858,708.94501,230.46412,711.5949)
		CurveTo(1, 225.97264,714.22371,222.44416,717.82254,219.86456,722.38426)
		CurveTo(1, 217.28495,726.91792,215.99159,731.81001,215.99159,737.00442)
		CurveTo(1, 215.99159,742.25499,217.28495,747.11193,219.86456,751.59639)
		CurveTo(1, 222.44416,756.06678,225.98669,759.61635,230.49927,762.23812)
		CurveTo(1, 235.00481,764.85287,239.85472,766.1743,245.06314,766.1743)
		CurveTo(1, 250.51053,766.1743,255.50811,764.84584,260.03474,762.21001)
		LineTo(1, 260.03474,787.56329)	
		CurveTo(1, 255.17072,789.03233,250.18021,789.76331,245.06314,789.76331)
		CurveTo(1, 235.67253,789.76331,226.90753,787.39458,218.78909,782.65713)
		CurveTo(1, 210.67775,777.91963,204.23929,771.47415,199.50179,763.3628)
		CurveTo(1, 194.76434,755.25139,192.39562,746.45827,192.39562,737.00442)
		CurveTo(1, 192.39562,730.00362,193.76625,723.25588,196.51455,716.7752)
		CurveTo(1, 199.26285,710.3016,203.07954,704.63629,207.97162,699.78637)
		CurveTo(1, 212.84972,694.9364,218.45878,691.17596,224.76367,688.49794)
		CurveTo(1, 231.08269,685.81292,237.84449,684.47039,245.06314,684.47039)
		CurveTo(1, 250.05369,684.47039,255.04418,685.17325,260.03474,686.53686)
		ClosePath(1)
		tx = 100
		ty = -450
		line_width = 2
	EndSwitch

	; draw path as filled blue shape with a black border of the specified size
	DrawPath(1, tx, ty, #BLUE, {AnchorX = 0.5, AnchorY = 0.5, Border = True, BorderSize = line_width, Name = "shape", InsertPos = 1})
	ClearPath(1)
	
	; reset transformation variables
	r = 0
	s = 1.0
	outline = False
	
EndFunction

; handles user-input
Function p_EventFunc(msg)

	Switch msg.action
	Case "OnMouseUp":
		; handle button highlighting
		For Local k = 1 To 6
			SetLayerStyle("button" .. k - 1, {ID = IIf(msg.id = k, 2, 1), Frame = k})
		Next
		
		; switch shape
		p_DrawShape(msg.id - 1)
			 
	Case "OnKeyDown":
		; handle shape transformations
	
		Switch msg.key
		Case "RIGHT"
			r = r + 1
		Case "LEFT"
			r = r - 1
		Case "UP"
			s = s + 0.1
		Case "DOWN"
			s = s - 0.1
		Case "o"
			outline = Not outline
			
			If outline = True
				SetLayerStyle("shape", {FillStyle = #FILLNONE, Color = #BLACK})
			Else
				SetLayerStyle("shape", {FillStyle = #FILLCOLOR, Color = #BLUE})
			EndIf
		Case "a"
			aa = Not aa	
		Case "+"
			line_width = line_width + 1
		Case "-"
			line_width = line_width - 1
			If line_width < 1 Then line_width = 1						
		EndSwitch

		Local fs_s = IIf(aa = True, #ANTIALIAS, 0)
		Local fs_c = IIf(aa = False, #ANTIALIAS, 0)
		
		; apply new layer style!		
		SetLayerStyle("shape", {Rotate = r, ScaleX = s, ScaleY = s, BorderSize = line_width, FormStyle = fs_s, FormStyleClear = fs_c})
	EndSwitch	
						
EndFunction

SetVectorEngine("pangomonium")

; variable initialization
r = 0
s = 1.0
line_width = 10.0
aa = True
outline = False

; listen to keyboard
InstallEventHandler({OnKeyDown = p_EventFunc})

EnableLayers

; print usage information
TextOut(#LEFT, #BOTTOM,
	"Keys:\n" ..
	"up: zoom in\n" ..
	"down: zoom out\n" ..
	"left: rotate clockwise\n" ..
	"right: rotate anticlockwise\n" ..
	"+ key: increase stroke width\n" ..
	"- key: decrease stroke width\n" ..
	"o key: toggle outline\n" ..
	"a key: toggle anti-alias\n") 

; prepare font for vector text
SetFont(#SANS, 100, {engine = #FONTENGINE_INBUILT})
SetFillStyle(#FILLCOLOR)
SetFormStyle(#ANTIALIAS)

; display buttons (note: we're storing buttons as anims with two frames [normal/selected])
For Local k = 0 To 5
	DisplayAnimFrame(1, 340 + k * 50, 425, k + 1, {Name = "button" .. k})
	MakeButton(k + 1, #LAYERBUTTON, "button" .. k, True, False, {OnMouseUp = p_EventFunc})
Next	

; wait for something to happen!
Repeat
	WaitEvent
Forever
	
