/****************************************************************
**                                                             **
** Name:        VectorTest3                                    **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        01.03.17                                       **
** Interpreter: Hollywood 7.0                                  **
** Licence:     Sample program for Hollywood                   **
** Function:    Demonstrates how to draw multi-colored vector  **
**              paths with Hollywood                           **
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (15.03.17)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Important! Check if the used Hollywood version is at least
** version 7.0!
*/
@VERSION 7,0


/*
** some external data
*/
@ANIM 1, "buttons.png", {Width = 40, Height = 40, Frames = 6}
@ANIM 2, "buttons2.png", {Width = 40, Height = 40, Frames = 6}


/*
** set up our display
*/
@DISPLAY {Width = 640, Height = 480, FillStyle = #FILLGRADIENT, GradientStartColor = #WHITE, GradientEndColor = $0000CC}

; generic star drawing function
Function p_Star(id, cx, cy, radius, nodes, iradius)

	For Local i = 0 To nodes - 1

		Local angle = i*2*#PI/nodes
		Local r
		
		If i % 2 = 1
			r = radius
		Else
			r = iradius
		EndIf
				
		Local x = cx + Cos(angle) * r
		Local y = cy + Sin(angle) * r

		If i <> 0
			LineTo(id, x, y)
		Else
			MoveTo(id, x, y)
		EndIf
	Next
	
	ClosePath(id)
	
EndFunction

; draw specified shape
Function p_DrawShape(w, doreset)

	; shape already there? --> remove it now!
	If LayerExists("shape") Then RemoveLayer("shape")

	SetFillStyle(IIf(outline, #FILLNONE, #FILLCOLOR))
	SetFormStyle(IIf(aa, #ANTIALIAS, #NORMAL))
	
	; start new path		
	StartPath(1)
			
	Switch w
	Case 0:
		; two polygons
		MoveTo(1, 428.0, 225.6)
		LineTo(1, 530.4, 430.4)
		RelLineTo(1, -102.4, 0.0)
		CurveTo(1, 351.2, 430.4, 351.2, 328.0, 428.0, 328.0)
		ClosePath(1)

		StartPath(2)
		MoveTo(2, 364.0, 225.6)
		RelLineTo(2, 51.2, 51.2)
		RelLineTo(2, -51.2, 51.2)
		RelLineTo(2, -51.2, -51.2)
		ClosePath(2)
		
		PathToBrush(1, {{ID = 1, Color = #BLUE, X = 50}, {ID = 2, Color = #RED}})
		
	Case 1:
		; vector text on a path
		MoveTo(1, 0, 0)
		AddTextToPath(1, "A")
		ClosePath(1)

		StartPath(2)
		MoveTo(2, 0, 0)
		AddTextToPath(2, "B")
		ClosePath(2)

		StartPath(3)
		MoveTo(3, 0, 0)
		AddTextToPath(3, "C")
		ClosePath(3)
		
		PathToBrush(1, {{Color = #RED, ID = 1}, {Color = #BLUE, ID = 2, X = 70}, {Color = #YELLOW, ID = 3, X = 125}})
		
	Case 2:	
		AddBoxToPath(1, 0, 0, 200, 50)
		
		; make German flag
		PathToBrush(1, {{Color = #BLACK, ID = 1}, {Color = #RED, ID = 1, Y = 50}, {Color = #YELLOW, ID = 1, Y = 100}})
	      		
	Case 3:
		; stop sign
		MoveTo(1, 0, 0)
		AddTextToPath(1, "STOP")
		
		StartPath(2)
		MoveTo(2, 0, 75)
		LineTo(2, 75, 0)
		LineTo(2, 225, 0)
		LineTo(2, 300, 75)
		LineTo(2, 300, 175)
		LineTo(2, 225, 250)
		LineTo(2, 75, 250)
		LineTo(2, 0, 175)
		ClosePath(2)
		
		PathToBrush(1, {{Color = #RED, ID = 2}, {Color = #WHITE, ID = 1, X = #CENTER, Y = #CENTER}})
				
	Case 4:
		; star shape
		p_Star(1, 200, 200, 150, 26, 100)	
		
		StartPath(2)
		p_Star(2, 200, 200, 120, 26, 50)
		
		StartPath(3)
		AddCircleToPath(3, 200, 200, 30)
		ClosePath(3)
		
		PathToBrush(1, {{Color = #BLUE, ID = 1}, {Color = #RED, ID = 2, X = #CENTER, Y = #CENTER}, {Color = #YELLOW, ID = 3, X = #CENTER, Y = #CENTER}})
		
	Case 5:
		; C= logo shape
		MoveTo(1, 260.94946,759.73807)
		LineTo(1, 304.83958,759.73807)
		LineTo(1, 285.62379,739.70229)
		LineTo(1, 260.94946,739.70229)
		LineTo(1, 260.94946,759.73807)	
		ClosePath(1)
				
		StartPath(2)
		MoveTo(2, 260.99228,714.49756)
		LineTo(2, 304.8824,714.49756)
		LineTo(2, 285.66661,734.53334)
		LineTo(2, 260.99228,734.53334)
		LineTo(2, 260.99228,714.49756)
		ClosePath(2)
		
		StartPath(3)
		MoveTo(3, 260.03474,686.53686)
		LineTo(3, 260.03474,711.77767)
		CurveTo(3, 255.55029,709.00125,250.55973,707.6306,245.06314,707.6306)
		CurveTo(3, 239.81958,707.6306,234.94858,708.94501,230.46412,711.5949)
		CurveTo(3, 225.97264,714.22371,222.44416,717.82254,219.86456,722.38426)
		CurveTo(3, 217.28495,726.91792,215.99159,731.81001,215.99159,737.00442)
		CurveTo(3, 215.99159,742.25499,217.28495,747.11193,219.86456,751.59639)
		CurveTo(3, 222.44416,756.06678,225.98669,759.61635,230.49927,762.23812)
		CurveTo(3, 235.00481,764.85287,239.85472,766.1743,245.06314,766.1743)
		CurveTo(3, 250.51053,766.1743,255.50811,764.84584,260.03474,762.21001)
		LineTo(3, 260.03474,787.56329)	
		CurveTo(3, 255.17072,789.03233,250.18021,789.76331,245.06314,789.76331)
		CurveTo(3, 235.67253,789.76331,226.90753,787.39458,218.78909,782.65713)
		CurveTo(3, 210.67775,777.91963,204.23929,771.47415,199.50179,763.3628)
		CurveTo(3, 194.76434,755.25139,192.39562,746.45827,192.39562,737.00442)
		CurveTo(3, 192.39562,730.00362,193.76625,723.25588,196.51455,716.7752)
		CurveTo(3, 199.26285,710.3016,203.07954,704.63629,207.97162,699.78637)
		CurveTo(3, 212.84972,694.9364,218.45878,691.17596,224.76367,688.49794)
		CurveTo(3, 231.08269,685.81292,237.84449,684.47039,245.06314,684.47039)
		CurveTo(3, 250.05369,684.47039,255.04418,685.17325,260.03474,686.53686)
		ClosePath(3)

		PathToBrush(1, {{ID = 1, Color = #RED, X = 68, Y = 54}, {ID = 2, Color = #BLUE, X = 68, Y = 30}, {ID = 3, Color = #BLUE}})		
	EndSwitch

	If doreset
		; reset transformation variables
		r = 0
		s = 1.0
	EndIf
	
	; draw vector paths as brush
	DisplayBrush(1, #CENTER, #CENTER, {AnchorX = 0.5, AnchorY = 0.5, Name = "shape", InsertPos = 1, Rotate = r, ScaleX = s, ScaleY = s})
		
	curshape = w
	
EndFunction

; handles user-input
Function p_EventFunc(msg)

	Switch msg.action
	Case "OnMouseUp":
		; handle button highlighting
		For Local k = 1 To 6
			SetLayerStyle("button" .. k - 1, {ID = IIf(msg.id = k, 2, 1), Frame = k})
		Next
		
		; switch shape
		p_DrawShape(msg.id - 1, True)
			 
	Case "OnKeyDown":
		; handle shape transformations
	
		Switch msg.key
		Case "RIGHT"
			r = r + 1
		Case "LEFT"
			r = r - 1
		Case "UP"
			s = s + 0.1
		Case "DOWN"
			s = s - 0.1	
		Case "o"
			outline = Not outline
			p_DrawShape(curshape, False)
		Case "a"
			aa = Not aa
			p_DrawShape(curshape, False)						
		EndSwitch

		; apply new layer style!		
		SetLayerStyle("shape", {Rotate = r, ScaleX = s, ScaleY = s})
	EndSwitch	
						
EndFunction

SetVectorEngine("pangomonium")

; variable initialization
r = 0
s = 1.0
aa = True
outline = False

; listen to keyboard
InstallEventHandler({OnKeyDown = p_EventFunc})

EnableLayers

; print usage information
TextOut(#LEFT, #BOTTOM,
	"Keys:\n" ..
	"up: zoom in\n" ..
	"down: zoom out\n" ..
	"left: rotate clockwise\n" ..
	"right: rotate anticlockwise\n" ..
	"o key: toggle outline\n" ..
	"a key: toggle anti-alias\n") 	

; prepare font for vector text
SetFont(#SANS, 100, {engine = #FONTENGINE_INBUILT})
SetFillStyle(#FILLCOLOR)
SetFormStyle(#ANTIALIAS)

; display buttons (note: we're storing buttons as anims with two frames [normal/selected])
For Local k = 0 To 5
	DisplayAnimFrame(1, 340 + k * 50, 425, k + 1, {Name = "button" .. k})
	MakeButton(k + 1, #LAYERBUTTON, "button" .. k, True, False, {OnMouseUp = p_EventFunc})
Next	

; wait for something to happen!
Repeat
	WaitEvent
Forever
	
