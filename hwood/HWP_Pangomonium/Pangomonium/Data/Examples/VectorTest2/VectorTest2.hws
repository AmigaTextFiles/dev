/****************************************************************
**                                                             **
** Name:        VectorTest2                                    **
** Author:      Andreas Falkenhahn                             **
** Version:     1.1                                            **
** Date:        16.11.14                                       **
** Interpreter: Hollywood 5.0                                  **
** Licence:     Sample program for Hollywood                   **
** Function:    Demonstrates use of real vector graphics which **
**              are supported starting with Hollywood 5.0      **
**                                                             **
** History:                                                    **
**                                                             **
** 1.1: (16.11.14)                                             **
**                                                             **
** - forgot to close the circle path                           **
**                                                             **
** 1.0: (05.03.10)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Important! Check if the used Hollywood version is at least
** version 5.0!
*/
@VERSION 5,0


/*
** set up our display
*/
;@DISPLAY {Width = 800, Height = 600, Color = #WHITE}
@DISPLAY {Width = 800, Height = 600, FillStyle = #FILLGRADIENT, GradientStyle = #CONICAL, GradientStartColor = #WHITE, GradientEndColor = $0096FF, GradientAngle = 45}

; overlay of filled and stroked round rectangle with transparency
Function p_Shape1(x, y)

	Local x0 = 0
       	Local y0 = 0
       	Local rect_width = 204.8
       	Local rect_height = 204.8
        Local radius = 102.4
	Local x1, y1

	x1 = x0 + rect_width
	y1 = y0 + rect_height

	StartPath(1)
	
	If rect_width/2 < radius
	
		If rect_height/2 < radius
	        	MoveTo(1, x0, (y0 + y1)/2)
        		CurveTo(1, x0 ,y0, x0, y0, (x0 + x1)/2, y0)
        		CurveTo(1, x1, y0, x1, y0, x1, (y0 + y1)/2)
        		CurveTo(1, x1, y1, x1, y1, (x1 + x0)/2, y1)
        		CurveTo(1, x0, y1, x0, y1, x0, (y0 + y1)/2)
        	Else
        		MoveTo(1, x0, y0 + radius)
		        CurveTo(1, x0 ,y0, x0, y0, (x0 + x1)/2, y0)
		        CurveTo(1, x1, y0, x1, y0, x1, y0 + radius)
		        LineTo(1, x1 , y1 - radius)
		        CurveTo(1, x1, y1, x1, y1, (x1 + x0)/2, y1)
		        CurveTo(1, x0, y1, x0, y1, x0, y1- radius)
		EndIf
		
	Else

    		If rect_height/2 < radius
        		MoveTo(1, x0, (y0 + y1)/2)
		        CurveTo(1, x0 , y0, x0 , y0, x0 + radius, y0)
		        LineTo(1, x1 - radius, y0)
		        CurveTo(1, x1, y0, x1, y0, x1, (y0 + y1)/2)
        		CurveTo(1, x1, y1, x1, y1, x1 - radius, y1)
        		LineTo(1, x0 + radius, y1)
        		CurveTo(1, x0, y1, x0, y1, x0, (y0 + y1)/2)
    		Else
		        MoveTo(1, x0, y0 + radius)
		        CurveTo(1, x0 , y0, x0 , y0, x0 + radius, y0)
        		LineTo(1, x1 - radius, y0)
       			CurveTo(1, x1, y0, x1, y0, x1, y0 + radius)
        		LineTo(1, x1 , y1 - radius)
        		CurveTo(1, x1, y1, x1, y1, x1 - radius, y1)
        		LineTo(1, x0 + radius, y1)
        		CurveTo(1, x0, y1, x0, y1, x0, y1- radius)
    		Endif
    	Endif	

	ClosePath(1)
	
	SetFillStyle(#FILLCOLOR)
	DrawPath(1, x, y, $8080ff)
	SetFillStyle(#FILLNONE, 10)
	DrawPath(1, x + 1, y + 1, $80800000)

EndFunction

; clock-like shape
Function p_Shape2(x, y, neg)

	Local xc = 128.0
	Local yc = 128.0
	Local radius = 100.0
	Local angle1 = 45.0
	Local angle2 = 180.0

	StartPath(1)
	
	SetFillStyle(#FILLNONE)
	
	SetLineWidth(10)
	AddArcToPath(1, xc, yc, radius, radius, angle1, angle2, neg)
	DrawPath(1, x, y, 0)
	ClearPath(1)

	SetFillStyle(#FILLCOLOR)
	AddArcToPath(1, xc, yc, 10.0, 10.0, 0, 360)
	ClosePath(1)
	DrawPath(1, x, y, $66ff3333)
	ClearPath(1)

	SetFillStyle(#FILLNONE)		
	SetLineWidth(6)
	AddArcToPath(1, xc, yc, radius, radius, angle1, angle1)
	LineTo(1, xc, yc)
	AddArcToPath(1, xc, yc, radius, radius, angle2, angle2)
	LineTo(1, xc, yc)
	DrawPath(1, x, y, $66ff3333)	
	
EndFunction

; demonstrates different line join styles (miter, bevel, round)
Function p_Shape3(x, y)

	StartPath(1)
	
	SetFillStyle(#FILLNONE)
	SetLineWidth(40.96)
	MoveTo(1, 76.8, 84.48)
	RelLineTo(1, 51.2, -51.2)
	RelLineTo(1, 51.2, 51.2)
	SetLineJoin(#JOINMITER)
	DrawPath(1, x, y, 0)
	ClearPath(1)

	MoveTo(1, 76.8, 161.28)
	RelLineTo(1, 51.2, -51.2)
	RelLineTo(1, 51.2, 51.2)
	SetLineJoin(#JOINBEVEL)
	DrawPath(1, x, y, 0)
	ClearPath(1)
	
	MoveTo(1, 76.8, 238.08)
	RelLineTo(1, 51.2, -51.2)
	RelLineTo(1, 51.2, 51.2)
	SetLineJoin(#JOINROUND)
	DrawPath(1, x, y, 0)
	ClearPath(1)
	
	SetLineJoin(#JOINMITER)
	
EndFunction

; demonstrates different line cap styles (but, round, square)
Function p_Shape4(x, y)

	StartPath(1)
	
	SetFillStyle(#FILLNONE)
	
	SetLineWidth(30.0)
	SetLineCap(#CAPBUTT) 
	MoveTo(1, 64.0, 50.0)
	LineTo(1, 64.0, 200.0)
	DrawPath(1, x, y, 0)
	ClearPath(1)
	
	SetLineCap(#CAPROUND)
	MoveTo(1, 128.0, 50.0)
	LineTo(1, 128.0, 200.0)
	DrawPath(1, x, y, 0)
	ClearPath(1)
	
	SetLineCap(#CAPSQUARE)
	MoveTo(1, 192.0, 50.0)
	LineTo(1, 192.0, 200.0)
	DrawPath(1, x, y, 0)
	ClearPath(1)
	
	SetLineWidth(2.56)
	MoveTo(1, 64.0, 50.0)
	LineTo(1, 64.0, 200.0)
	MoveTo(1, 128.0, 50.0)
	LineTo(1, 128.0, 200.0)
	MoveTo(1, 192.0, 50.0)
	LineTo(1, 192.0, 200.0)
	DrawPath(1, x, y, $ff3333)
	
	SetLineCap(#CAPBUTT)

EndFunction

; demonstrates dashed line use
Function p_Shape5(x, y)

	SetDash(-50, 50, 10, 10, 10)
	SetFillStyle(#FILLNONE)
	SetLineWidth(10.0)

	StartPath(1)
	
	MoveTo(1, 128.0, 25.6)
	LineTo(1, 230.4, 230.4)
	RelLineTo(1, -102.4, 0.0)
	CurveTo(1, 51.2, 230.4, 51.2, 128.0, 128.0, 128.0)
	DrawPath(1, x, y, 0)
	
	SetDash(0, -1)
	
EndFunction

; demonstrates two polygons in the same path
Function p_Shape6(x, y)

	StartPath(1)
	
	MoveTo(1, 278.0, 175.6)
	LineTo(1, 380.4, 380.4)
	RelLineTo(1, -102.4, 0.0)
	CurveTo(1, 201.2, 380.4, 201.2, 278.0, 278.0, 278.0)
	ClosePath(1)

	MoveTo(1, 214.0, 175.6)
	RelLineTo(1, 51.2, 51.2)
	RelLineTo(1, -51.2, 51.2)
	RelLineTo(1, -51.2, -51.2)
	ClosePath(1)

	SetFillStyle(#FILLCOLOR)
	DrawPath(1, x - 170, y - 150, #BLUE, {Border = True, BorderSize = 10})

EndFunction

Function p_GetBox(b)

	Local x = (b % 3) * 800 / 3
	Local y = b \ 3 * 600 / 2
	
	Return(x + 20, y + 20)
	
EndFunction

SetVectorEngine("pangomonium")

EnableLayers

; setup drawing styles
SetFillStyle(#FILLCOLOR)
SetFormStyle(#ANTIALIAS)

; draw shapes!
p_Shape1(p_GetBox(0))
p_Shape2(p_GetBox(1))
p_Shape3(p_GetBox(2))
p_Shape4(p_GetBox(3))
p_Shape5(p_GetBox(4))
p_Shape6(p_GetBox(5))

EscapeQuit(True)

; main loop
Repeat
	WaitEvent
Forever	
