/****************************************************************
**                                                             **
** Name:        CurveRectangle                                 **
** Author:      Andreas Falkenhahn, based on original code by  **
**              Øyvind Kolås from cairographics.org/samples/   **
** Version:     1.0                                            **
** Date:        10.03.24                                       **
** Interpreter: Hollywood 10.0                                 **
** Licence:     Sample program                                 **
** Function:    Draws a curved rectangle                       **
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (10.03.24)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Make sure we have at least Hollywood 10.0!
*/
@VERSION 10,0


/*
** This script requires the Pangomonium plugin
*/
@REQUIRE "pangomonium"


/*
** Give us a white background display
*/
@DISPLAY {Color = #WHITE}

img = cairo.ImageSurface(#CAIRO_FORMAT_ARGB32, 640, 480)
cr = cairo.Context(img) 

; a custom shape that could be wrapped in a function 
x0 = 25.6
y0 = 25.6
rect_width  = 204.8
rect_height = 204.8
radius = 102.4   ; and an approximate curvature radius 

x1=x0+rect_width
y1=y0+rect_height

If rect_width/2 < radius
	If rect_height/2<radius
        	cr:MoveTo(x0,(y0 + y1)/2)
	        cr:CurveTo(x0 ,y0, x0, y0,(x0 + x1)/2, y0)
	        cr:CurveTo(x1, y0, x1, y0, x1,(y0 + y1)/2)
	        cr:CurveTo(x1, y1, x1, y1,(x1 + x0)/2, y1)
	        cr:CurveTo(x0, y1, x0, y1, x0,(y0 + y1)/2)
	Else
        	cr:MoveTo(x0, y0 + radius)
	        cr:CurveTo(x0 ,y0, x0, y0,(x0 + x1)/2, y0)
	        cr:CurveTo(x1, y0, x1, y0, x1, y0 + radius)
	        cr:LineTo(x1 , y1 - radius)
	        cr:CurveTo(x1, y1, x1, y1,(x1 + x0)/2, y1)
	        cr:CurveTo(x0, y1, x0, y1, x0, y1- radius)
	EndIf
Else
	If rect_height/2<radius
	        cr:MoveTo(x0,(y0 + y1)/2)
	        cr:CurveTo(x0 , y0, x0 , y0, x0 + radius, y0)
	        cr:LineTo(x1 - radius, y0)
	        cr:CurveTo(x1, y0, x1, y0, x1,(y0 + y1)/2)
	        cr:CurveTo(x1, y1, x1, y1, x1 - radius, y1)
	        cr:LineTo(x0 + radius, y1)
	        cr:CurveTo(x0, y1, x0, y1, x0,(y0 + y1)/2)
	Else
	        cr:MoveTo(x0, y0 + radius)
	        cr:CurveTo(x0 , y0, x0 , y0, x0 + radius, y0)
	        cr:LineTo(x1 - radius, y0)
	        cr:CurveTo(x1, y0, x1, y0, x1, y0 + radius)
	        cr:LineTo(x1 , y1 - radius)
	        cr:CurveTo(x1, y1, x1, y1, x1 - radius, y1)
	        cr:LineTo(x0 + radius, y1)
	        cr:CurveTo(x0, y1, x0, y1, x0, y1- radius)
    	EndIf
EndIf
cr:ClosePath()

cr:SetSourceRGBA(0.5, 0.5, 1)
cr:FillPreserve()
cr:SetSourceRGBA(0.5, 0, 0, 0.5)
cr:SetLineWidth(10.0)
cr:Stroke()

img:ToBrush(1)
DisplayBrush(1, 0, 0)
WaitLeftMouse
End
