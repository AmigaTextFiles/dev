/****************************************************************
**                                                             **
** Name:        TextMarkup                                     **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        10.03.24                                       **
** Interpreter: Hollywood 10.0                                 **
** Licence:     Sample program                                 **
** Function:    Demonstrates formatted text using Pango markup **
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (10.03.24)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Make sure we have at least Hollywood 10.0!
*/
@VERSION 10,0


/*
** This script requires the Pangomonium plugin
*/
@REQUIRE "pangomonium"


/*
** Give us a white background display
*/
@DISPLAY {Color = #WHITE}

fontmap = pango.FontMap(#CAIRO_FONT_TYPE_FT)
context = fontmap:CreateContext()
layout = pango.Layout(context)
fontdesc = pango.FontDescription("DejaVu Sans 16")
layout:SetFontDescription(fontdesc)
fontmap:LoadFont(context, fontdesc)
layout:SetWidth(640 * #PANGO_SCALE)

t$ = "In Hollywood 2.5 the text engine has been <b>completely rewritten</b>. It is now possible to embed formatting tags within the "..
	"text very similar to the way the formatting tags work in HTML. You can use these tags to change the <span foreground=\"red\">color of the text,</span> "..
	"or you could change the font style to <b>bold</b>(currently only supported by the AmigaOS4 true type library), <i>italics</i> or <u>underlined</u>, "..
	"or just <b><u><i>all three styles together</i></u></b>. There are also some more sophisticated font styles available: You can have the text appear "..
	"with a nice border around it or you can also add a shadow effect to your text. "..
	"Whatever you choose, with the new formatting tags you can easily achieve the look you want! This whole text is printed using a single call "..
	"to Print() and it is automatically adapted to the borders of your window. Try to resize the window and see how the text adapts to the new "..
	"window size!"

layout:SetMarkup(t$)

img = cairo.ImageSurface(#CAIRO_FORMAT_ARGB32, 640, 480)
cr = cairo.Context(img)
cr:SetSourceRGB(0, 0, 0)
cr:ShowLayout(layout)

img:ToBrush(1)
DisplayBrush(1, 0, 0)
WaitLeftMouse
End
