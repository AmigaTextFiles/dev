/****************************************************************
**                                                             **
** Name:        TextAlignCenter                                **
** Author:      Andreas Falkenhahn, based on original code by  **
**              Øyvind Kolås from cairographics.org/samples/   **
** Version:     1.0                                            **
** Date:        10.03.24                                       **
** Interpreter: Hollywood 10.0                                 **
** Licence:     Sample program                                 **
** Function:    Draws aligned text                             **
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (10.03.24)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Make sure we have at least Hollywood 10.0!
*/
@VERSION 10,0


/*
** This script requires the Pangomonium plugin
*/
@REQUIRE "pangomonium"


/*
** Give us a white background display
*/
@DISPLAY {Color = #WHITE}

img = cairo.ImageSurface(#CAIRO_FORMAT_ARGB32, 640, 480)
cr = cairo.Context(img)

t$ = "cairo"

cr:SelectFontFace("Sans", #CAIRO_FONT_SLANT_NORMAL, #CAIRO_FONT_WEIGHT_NORMAL)

cr:SetFontSize(52.0)
extents = cr:TextExtents(t$)
x = 128.0 - (extents.width/2 + extents.xbearing)
y = 128.0 - (extents.height/2 + extents.ybearing)

cr:MoveTo(x, y)
cr:ShowText(t$)

; draw helping lines
cr:SetSourceRGBA(1, 0.2, 0.2, 0.6)
cr:SetLineWidth(6.0)
cr:Arc(x, y, 10.0, 0, 2*#PI)
cr:Fill()
cr:MoveTo(128.0, 0)
cr:RelLineTo(0, 256)
cr:MoveTo(0, 128.0)
cr:RelLineTo(256, 0)
cr:Stroke()

img:ToBrush(1)
DisplayBrush(1, 0, 0)
WaitLeftMouse
End
