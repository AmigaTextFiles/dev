;
; $VER: Plugin Setup 1.5 (09.10.12)
;
; Done by Andreas Falkenhahn
;

(set #hwPlugFile "xml.hwp")
(set #hwPlugName "XML Plugin")
(set #hwPlugVersion "2.0")
(set #hwPlugAuthor "Andreas Falkenhahn")
(set #hwPlugCopyrightYear "2012-2022")
(set #hwPlugDescription "Parse XML documents with Hollywood")

(message (cat "\nWelcome to the Setup utility for\n\n" #hwPlugName " " #hwPlugVersion "\n\nCopyright © " #hwPlugCopyrightYear " by " #hwPlugAuthor "\n\n" #hwPlugDescription))

(welcome)

(set defaultplat 0)

(if (exists "SYS:MorphOS/C/Assign") (
	(set defaultplat 3)
) (
	(if (exists "SYS:System/GrimReaper") (
		(set defaultplat 1)
	) (
		(if (exists "LIBS:powerpc.library") (
			(set defaultplat 4)
		))
	))
))				
	
(set whichver
	(askchoice
		(prompt  "Please select your platform")
		(help    @askoptions-help)
		(choices "AmigaOS 3 (m68k)" "AmigaOS 4 (ppc)" "AROS (x86)" "MorphOS (ppc)" "WarpOS (ppc)")		
		(default defaultplat)
	)
) 

(set #platform "m68k-amigaos")

(if (= whichver 1) (
	(set #platform "ppc-amigaos")
))

(if (= whichver 2) (
	(set #platform "x86-aros")
))

(if (= whichver 3) (
	(set #platform "ppc-morphos")
))

(if (= whichver 4) (
	(set #platform "ppc-warpos")
))

(makedir "LIBS:Hollywood")

(copylib
	(source (cat "bin/" #platform "/" #hwPlugFile))
	(dest   "LIBS:Hollywood")
	(prompt #hwPlugFile)
	(help @copylib-help)
	(optional "nofail" "force" "askuser")
	(confirm)
)

(if (exists "Hollywood:LinkerPlugins" (noreq)) (

	(copyfiles
		(source "bin")
		(dest   "Hollywood:LinkerPlugins")
		(all)
		(optional "nofail" "force" "askuser")
	)
)) 

(if (= (exists "Hollywood:GUI" (noreq)) 1) (

	(makedir "Hollywood:PluginHelp")
	
	(copyfiles
		(source "Docs/xml.guide")
	        (dest   "Hollywood:PluginHelp")
		(optional "nofail" "force" "askuser")
	)	
))

(if (= (exists "LIBS:Hollywood/xmlparser.hwp") 1) (

	(if (= (askbool
		(prompt "The installer has detected that you still have xmlparser.hwp installed in LIBS:Hollywood. The xmlparser.hwp plugin is now obsolete as it has been superseded by xml.hwp. Do you want me to delete LIBS:Hollywood/xmlparser.hwp now?")
		(choices "Yes" "No")
		(help @askbool_help)
	) 1) (
		(delete "LIBS:Hollywood/xmlparser.hwp" (optional "force"))		
	))
))
	
(if (= (exists "ReadMe") 1) (
	(run ("Sys:Utilities/MultiView ReadMe"))
))

(message (cat "\n" #hwPlugName " has been successfully installed on your system."))

(exit (quiet))

