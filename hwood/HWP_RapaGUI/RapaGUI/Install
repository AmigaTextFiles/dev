;
; $VER: RapaGUI-Setup 2.2 (24.08.24)
;
; Done by Andreas Falkenhahn
;

(message (cat "\nWelcome to the Setup utility for\n\n"
	"RapaGUI 2.2\n\n"
	"- Rapid Cross-Platform GUI Development On All Islands -\n\n"
	"Copyright (C) 2015-2024 by Andreas Falkenhahn\n\n"
	"http://www.hollywood-mal.com/"
))

(welcome)

(set basepath
	(askdir
		(prompt  "Where do you want to have RapaGUI installed? (a drawer will be created)")
		(help    @askdir-help)
		(default "SYS:")
	)
)

(set sstr (substr basepath (- (strlen basepath) 1) 1))
(if (or (= sstr "/") (= sstr ":"))
	(set destpath (cat basepath "RapaGUI"))
	(set destpath (cat basepath "/RapaGUI"))
)

(set @default-dest destpath)

(set defaultplat 0)

(if (exists "SYS:MorphOS/C/Assign") (
	(set defaultplat 3)
) (
	(if (exists "SYS:System/GrimReaper") (
		(set defaultplat 1)
	))
))

(set whichver
	(askchoice
		(prompt  "Please select your platform")
		(help    @askoptions-help)
		(choices "AmigaOS 3 (m68k)" "AmigaOS 4 (ppc)" "AROS (x86)" "MorphOS (ppc)")		
		(default defaultplat)
	)
)

(set #platform "m68k-amigaos")

(if (= whichver 1) (
	(set #platform "ppc-amigaos")
))

(if (= whichver 2) (
	(set #platform "x86-aros")
))

(if (= whichver 3) (
	(set #platform "ppc-morphos")
))

(makedir destpath)
(makedir "LIBS:Hollywood")  

(copylib
	(source (cat "Platforms/" #platform "/rapagui.hwp"))
	(dest   "LIBS:Hollywood")
	(prompt "rapagui.hwp")
	(help @copylib-help)
	(optional "nofail" "force" "askuser")
	(confirm)
)

(copyfiles
	(source  "Data.icon")
	(dest    basepath)
	(newname "RapaGUI.info")
	(optional force oknodelete)
)

(copyfiles
	(source "Data")
	(dest   destpath)
	(all)
	(optional "nofail" "force" "askuser")
)

(if (exists "Hollywood:GUI" (noreq)) (

	(makedir "Hollywood:Examples/RapaGUI")

	(copyfiles
		(source "Data/Examples")
		(dest   "Hollywood:Examples/RapaGUI")
		(all)
		(optional "nofail" "force" "askuser")
	)
))

(if (exists "Hollywood:LinkerPlugins" (noreq)) (

	(copyfiles
		(source "Platforms")
		(dest   "Hollywood:LinkerPlugins")
		(all)
		(optional "nofail" "force" "askuser")
	)
))

(set #cmdline (cat "Assign RapaGUI: \"" destpath "\""))

(startup
	(prompt  "\nAn assign has to be inserted in your user-startup. Please click Continue.")
	(help    @startup-help)
	"RapaGUI"
	(command #cmdline)
	(confirm)
)

(run #cmdline)

(message (cat "\nRapaGUI optionally requires the following components:\n\n"
	"- HTMLview.mcc 12.5 or better for <htmlview> class\n"
	"- TextEditor.mcc 15.55 or better for <texteditor> class\n"
	"- TheBar.mcc 26.17 or better for <toolbar> class\n"
	"- codesets.library for UTF-8 support\n\n"
	"None of these components is required by default. You only need them if you want to use the features described above."))

(message (cat "\nIMPORTANT INFORMATION:\n\n"
	"There are a few incompatibilities between RapaGUI 2.0 and 1.x "
	"so you might have to adapt your scripts. Please see the chapter "
	"\"Compatibility notes\" in the manual for all details on those "
	"incompatibilites."))
	
(message (cat "\nRapaGUI comes with over 25 ready-to-run examples.\n\n"
	"The examples have been integrated into your Hollywood GUI (only "
	"if Hollywood is installed on this system). Now that you have installed "
	"RapaGUI, there will be a new RapaGUI tab in the Hollywood GUI which "
	"allows you to conveniently launch the example scripts that come with RapaGUI.\n\n"
	"Click to see a screenshot that shows how to launch the example scripts "
	"that come with RapaGUI from the Hollywood GUI."))
	
(run ("Sys:Utilities/MultiView GUI.png"))

(exit)
