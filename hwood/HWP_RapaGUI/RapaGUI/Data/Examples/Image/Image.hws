/****************************************************************
**                                                             **
** Name:        Image                                          **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        24.04.16                                       **
** Interpreter: Hollywood 9.0                                  **
** Licence:     Sample program                                 **
** Function:    How to embed an image inside a GUI             **
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (24.04.16)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Make sure we have at least Hollywood 9.0!
*/
@VERSION 9,0


/*
** Enable DPI-awareness so that our GUI looks sharp even on high DPI monitors
*/
@OPTIONS {DPIAware = True}


/*
** This script requires the RapaGUI plugin
*/
@REQUIRE "RapaGUI"


/*
** Information about this app 
*/
@APPTITLE "Image"
@APPVERSION "$VER: Image 1.0 (24.04.16)"
@APPCOPYRIGHT "(C) Copyright 2012-2016 by Andreas Falkenhahn"
@APPAUTHOR "Andreas Falkenhahn"
@APPDESCRIPTION "Demonstrates how to use Hollywood brushes from inside a GUI"


/*
** External data
*/
@BRUSH 1, "Hollywood.png", {LoadAlpha = True}


/*
** Our XML GUI definition
*/
@FILE 1, "Image.xml"


/*
** Handles all incoming events
*/
Function p_EventFunc(msg)

	Switch msg.action
	Case "RapaGUI":
		Switch msg.attribute
		Case "Pressed":
			moai.Set("image", "brush", db + 1)
			db = Not db
 		EndSwitch
	EndSwitch

EndFunction

db = 1

CopyBrush(1, 2)
FlipBrush(2, False)

; dynamically create GUI from an external *.xml file definition
moai.CreateApp(ReadString(1))

; listen to these events!
InstallEventHandler({RapaGUI = p_EventFunc})

; main loop!
Repeat
	WaitEvent
Forever

