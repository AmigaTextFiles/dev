/****************************************************************
**                                                             **
** Name:        Dynamic3                                       **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        24.04.16                                       **
** Interpreter: Hollywood 9.0                                  **
** Licence:     Sample program                                 **
** Function:    Demonstrates dynamic creation of objects       **
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (24.04.16)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Make sure we have at least Hollywood 9.0!
*/
@VERSION 9,0


/*
** Enable DPI-awareness so that our GUI looks sharp even on high DPI monitors
*/
@OPTIONS {DPIAware = True}


/*
** This script requires the RapaGUI plugin
*/
@REQUIRE "RapaGUI"


/*
** Information about this app
*/
@APPTITLE "Dynamic3"
@APPVERSION "$VER: Dynamic3 1.0 (24.04.16)"
@APPCOPYRIGHT "(C) 2014-2016 by Andreas Falkenhahn"
@APPAUTHOR "Andreas Falkenhahn"
@APPDESCRIPTION "Demonstrates dynamic page groups"


/*
** Our XML GUI definition
*/
@FILE 1, "Dynamic3.xml"


/*
** Closes a tab
*/
Function p_CloseTab(id$)

	moai.DoMethod("register", "remove", id$)
	moai.FreeObject(id$)
	If moai.Get("register", "pages") = 0 Then count = 0

EndFunction


/*
** Handles all incoming events
*/
Function p_EventFunc(msg)

	Switch msg.action
	Case "RapaGUI":
		Switch msg.attribute
		Case "Pressed":
			If msg.id = "add"
				count = count + 1
				moai.CreateObject(ReplaceStr(groupdef$, "%COUNT", count), "register")
				moai.DoMethod("register", "append", "page" .. count, True)		
			ElseIf msg.id = "rem"
				If moai.Get("register", "pages") >= 1
					Local id$ = moai.DoMethod("register", "getpageid", "active")
					p_CloseTab(id$)
 				EndIf
			ElseIf LeftStr(msg.id, 6) = "button"
				moai.Request("Dynamic3", "You clicked " .. msg.id .. " on " .. moai.DoMethod("register", "getpageid", "active"), "OK")
			EndIf
 		EndSwitch
	EndSwitch

EndFunction

count = 1

groupdef$ = [[
<vgroup title="Page %COUNT" id="page%COUNT" padding="16">
<texteditor/>
<button id="button%COUNT">Click me %COUNT</button>
</vgroup>
]]

; dynamically create GUI from an external *.xml file definition
moai.CreateApp(ReplaceStr(ReadString(1), "%GROUP", ReplaceStr(groupdef$, "%COUNT", count)))
				
; listen to these events!
InstallEventHandler({RapaGUI = p_EventFunc})

; main loop!
Repeat
	WaitEvent
Forever

