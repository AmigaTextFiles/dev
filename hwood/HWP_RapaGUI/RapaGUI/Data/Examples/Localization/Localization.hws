/****************************************************************
**                                                             **
** Name:        Localization                                   **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        01.08.20                                       **
** Interpreter: Hollywood 9.0                                  **
** Licence:     Sample program                                 **
** Function:    Demonstrates GUI localization                  **
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (01.08.20)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Make sure we have at least Hollywood 9.0!
*/
@VERSION 9,0


/*
** Enable DPI-awareness so that our GUI looks sharp even on high DPI monitors
*/
@OPTIONS {DPIAware = True}


/*
** This script requires the RapaGUI plugin
*/
@REQUIRE "RapaGUI"


/*
** Information about this app
*/
@APPTITLE "Localization"
@APPVERSION "$VER: Localization 1.0 (01.08.20)"
@APPCOPYRIGHT "(C) 2020 Andreas Falkenhahn"
@APPAUTHOR "Andreas Falkenhahn"
@APPDESCRIPTION "Demonstrate GUI localization"


/*
** Our XML GUI definition
*/
@FILE 1, "Localization.xml"


/*
** Our catalog
*/
@CATALOG "Localization.catalog"


/*
** Catalog indices
*/
Const #CAT_PANGRAM  = 0
Const #CAT_ABOUT    = 1
Const #CAT_QUIT     = 2
Const #CAT_TITLE    = 3
Const #CAT_FILE     = 4
Const #CAT_ABOUTDLG = 5


/*
** Handles all incoming events
*/
Function p_EventFunc(msg)
	
	Switch msg.action
	Case "RapaGUI":
		If (msg.attribute = "Pressed") Or (msg.attribute = "Selected")

			Switch msg.id FallThrough
			Case "quit": 
			Case "mn_quit":	
				End			
			Case "about":
			Case "mn_about":	
				SystemRequest("", "Localization 1.0\n\n" .. GetCatalogString(#CAT_ABOUTDLG, "This program was written by Andreas Falkenhahn"), "OK")
			EndSwitch									
		EndIf
	EndSwitch

EndFunction

; dynamically create GUI from an external *.xml file definition
moai.CreateApp(ReadString(1))

; listen to these events!
InstallEventHandler({RapaGUI = p_EventFunc}) 

; main loop!
Repeat
	WaitEvent
Forever

