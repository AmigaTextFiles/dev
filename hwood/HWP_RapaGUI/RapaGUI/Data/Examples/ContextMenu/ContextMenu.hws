/****************************************************************
**                                                             **
** Name:        ContextMenu                                    **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        10.07.20                                       **
** Interpreter: Hollywood 9.0                                  **
** Licence:     Sample program                                 **
** Function:    Test context menus                             **
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (10.07.20)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Make sure we have at least Hollywood 9.0!
*/
@VERSION 9,0


/*
** Enable DPI-awareness so that our GUI looks sharp even on high DPI monitors
*/
@OPTIONS {DPIAware = True}


/*
** This script requires the RapaGUI plugin
*/
@REQUIRE "RapaGUI"


/*
** Information about this app
*/
@APPTITLE "ContextMenu"
@APPVERSION "$VER: Weight 1.0 (10.07.20)"
@APPCOPYRIGHT "(C) 2020 Andreas Falkenhahn"
@APPAUTHOR "Andreas Falkenhahn"
@APPDESCRIPTION "Test context menus"


/*
** Our XML GUI definition
*/
@FILE 1, "ContextMenu.xml"


/*
** Handles all incoming events
*/
Function p_EventFunc(msg)
	
	Switch msg.action
	Case "RapaGUI":
		Switch msg.attribute
		Case "Selected":
			SystemRequest("", "Item selected: " .. msg.id .. "\n" ..
				"Item state: " .. msg.triggervalue .. "\n" ..
				"Parent widget: " .. msg.parent, "OK")	
		Case "Pressed":
			Switch msg.id
			Case "but1": FallThrough
			Case "but2":
				Local x = moai.Get(msg.notifydata, "selected")
				moai.Set(msg.notifydata, "selected", Not x)
			Case "but3": FallThrough
			Case "but4":
				For Local k = 1 To 3
					If moai.Get(msg.notifydata .. k, "selected")
						selected = k
						Break
					EndIf
				Next
				selected = Wrap(selected + 1, 1, 4)
				moai.Set(msg.notifydata .. selected, "selected", True)	
			Case "but5": FallThrough
			Case "but6": 
				Local x = moai.Get(msg.notifydata, "disabled")
				moai.Set(msg.notifydata, "disabled", Not x)					
			Case "but7": FallThrough
			Case "but8": FallThrough
			Case "but9":	
				For Local k = 1 To 3 Do moai.Set("text" .. k, "contextmenu", msg.notifydata)				
			EndSwitch									
		EndSwitch
	EndSwitch

EndFunction

; dynamically create GUI from an external *.xml file definition
moai.CreateApp(ReadString(1))

; listen to these events!
InstallEventHandler({RapaGUI = p_EventFunc}) 

; main loop!
Repeat
	WaitEvent
Forever

