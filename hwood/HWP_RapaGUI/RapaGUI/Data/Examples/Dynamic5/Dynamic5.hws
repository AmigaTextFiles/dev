/****************************************************************
**                                                             **
** Name:        Dynamic5                                       **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        02.05.21                                       **
** Interpreter: Hollywood 9.0                                  **
** Licence:     Sample program                                 **
** Function:    Demonstrates dynamic creation of windows       **
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (02.05.21)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Make sure we have at least Hollywood 9.0!
*/
@VERSION 9,0


/*
** Enable DPI-awareness so that our GUI looks sharp even on high DPI monitors
*/
@OPTIONS {DPIAware = True}


/*
** This script requires the RapaGUI plugin
*/
@REQUIRE "RapaGUI"


/*
** Information about this app
*/
@APPTITLE "Dynamic5"
@APPVERSION "$VER: Dynamic 1.0 (02.05.21)"
@APPCOPYRIGHT "(C) 2021 Andreas Falkenhahn"
@APPAUTHOR "Andreas Falkenhahn"
@APPDESCRIPTION "Demonstrates dynamic adding/removal of listview columns"


/*
** List view images
*/
@ICON 1, {{Image = "data/add.png"}, {Image = "data/large_add.png"}}


/*
** Our XML GUI definition
*/
@FILE 1, "Dynamic5.xml"


/*
** Handles all incoming events
*/
Function p_EventFunc(msg)

	Switch msg.action
	Case "RapaGUI":
		Switch msg.attribute
		Case "Pressed":
			Switch msg.id
			Case "insert":
				Local t = {title = "New column " .. gc}
				Switch(moai.Get("coltype", "active"))
				Case 1:
					t.icon = True
				Case 2:
					t.checkbox = True
				EndSwitch		
				moai.DoMethod("lv", "insertcolumn", moai.Get("insertnum", "text"), "newcol" .. gc, t)
				gc = gc + 1
			Case "remove":
				If moai.Get("lv", "columns") > 0 Then moai.DoMethod("lv", "removecolumn", moai.Get("removenum", "text"))
			Case "insertentry":	
				Local t = {}
				Local i = 0
				For Local k = 0 To moai.Get("lv", "columns") - 1
					
					Local id$ = moai.DoMethod("lv", "getcolumnid", k)
					
					If moai.Get(id$, "checkbox")
						t[i] = True
					ElseIf moai.Get(id$, "icon")
						t[i] = 1
						i = i + 1						
					EndIf

					If moai.Get(id$, "checkbox") = False Then t[i] = "Column " .. (k + 1)					
					i = i + 1
				Next
						
				If ListItems(t) > 0 Then moai.DoMethod("lv", "insert", "bottom", Unpack(t))	
			Case "clear":
				moai.DoMethod("lv", "clear")							
			EndSwitch
 		EndSwitch
	EndSwitch

EndFunction

gc = 1

; dynamically create GUI from an external *.xml file definition
moai.CreateApp(ReadString(1))

; listen to these events!
InstallEventHandler({RapaGUI = p_EventFunc})

; main loop!
Repeat
	WaitEvent
Forever

