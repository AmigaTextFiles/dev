/****************************************************************
**                                                             **
** Name:        Dynamic4                                       **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        24.04.16                                       **
** Interpreter: Hollywood 9.0                                  **
** Licence:     Sample program                                 **
** Function:    Demonstrates dynamic creation of menu items    **
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (24.04.16)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Make sure we have at least Hollywood 9.0!
*/
@VERSION 9,0


/*
** Enable DPI-awareness so that our GUI looks sharp even on high DPI monitors
*/
@OPTIONS {DPIAware = True}


/*
** This script requires the RapaGUI plugin
*/
@REQUIRE "RapaGUI"


/*
** Information about this app
*/
@APPTITLE "Dynamic4"
@APPVERSION "$VER: Dynamic4 1.0 (24.04.16)"
@APPCOPYRIGHT "(C) 2014-2016 by Andreas Falkenhahn"
@APPAUTHOR "Andreas Falkenhahn"
@APPDESCRIPTION "Demonstrates dynamic creation of menu items"


/*
** Our XML GUI definition
*/
@FILE 1, "Dynamic4.xml"


/*
** Handles all incoming events
*/
Function p_EventFunc(msg)

	Switch msg.action
	Case "RapaGUI":
		Switch msg.attribute
		Case "Pressed":
			If msg.id = "addsingle"
				moai.CreateObject("<menuitem id=\"single" .. count .. "\">Item " .. count .. "</menuitem>")
				moai.DoMethod("projectmenu", "insert", "single" .. count, IIf(count = 1, "men_separator", "single" .. count - 1))
				If count = 1 Then moai.DoMethod("projectmenu", "insert", "separator", "single1")
				count = count + 1
			ElseIf (msg.id = "remsingle") And (count > 1)
				count = count - 1
				moai.DoMethod("projectmenu", "remove", "single" .. count)
				moai.FreeObject("single" .. count)
				If count = 1 Then moai.DoMethod("projectmenu", "remove", "separator")
			ElseIf msg.id = "addroot"
				moai.CreateObject(ReplaceStr(ReplaceStr(ReplaceStr(root$, "%COUNT", rcount), "%ID", "root"), "%TAG", "menu"))
				moai.DoMethod("strip", "append", "root" .. rcount)
				rcount = rcount + 1
 			ElseIf (msg.id = "remroot") And (rcount > 1)
				rcount = rcount - 1
				moai.DoMethod("strip", "remove", "root" .. rcount)
				moai.FreeObject("root" .. rcount)
			ElseIf msg.id = "addsub"				
				; we need to use <submenu> for compatibility with MUI 3.8 because 3.8 requires sub menus to be
				; derived from MUIC_Menuitem whereas 4.0 also accepts MUIC_Menu; it is *not* necessary to use
				; <submenu> when defining complete menu trees because in that case RapaGUI is clever enough to do
				; all arrangements on its own; for isolated objects, however, we need to specify whether they should
				; be submenus (i.e. <submenu>) or top-level menus (i.e. <menu>)				
				moai.CreateObject(ReplaceStr(ReplaceStr(ReplaceStr(root$, "%COUNT", scount), "%ID", "sub"), "%TAG", "submenu"))
				moai.DoMethod("projectmenu", "prepend", "sub" .. scount)
				If scount = 1 Then moai.DoMethod("projectmenu", "insert", "separator2", "sub1")
				scount = scount + 1
			ElseIf (msg.id = "remsub") And (scount > 1)
				scount = scount - 1
				moai.DoMethod("projectmenu", "remove", "sub" .. scount)
				moai.FreeObject("sub" .. scount)
				If scount = 1 Then moai.DoMethod("projectmenu", "remove", "separator2")
 			EndIf
		Case "Selected":
			If msg.id = "men_quit"
				End
			ElseIf msg.id = "men_about"
				moai.Request("Menus", "Some little about window.", "OK")
			Else
				moai.Request("Menus", "Your selection: " .. moai.Get(msg.id, "title"), "OK")
			EndIf
		EndSwitch
	EndSwitch

EndFunction

count = 1
scount = 1
rcount = 1

root$ = [[
<%TAG title="Menu %COUNT" id="%ID%COUNT">
	<item id="%IDa%COUNT">One</item>
	<item id="%IDb%COUNT">Two</item>
	<item id="%IDc%COUNT">Three</item>
	<item id="%IDd%COUNT">From New York</item>
	<item id="%IDe%COUNT">To Germany</item>
</%TAG>
]]

; dynamically create GUI from an external *.xml file definition
moai.CreateApp(ReadString(1))

; listen to these events!
InstallEventHandler({RapaGUI = p_EventFunc})

; main loop!
Repeat
	WaitEvent
Forever

