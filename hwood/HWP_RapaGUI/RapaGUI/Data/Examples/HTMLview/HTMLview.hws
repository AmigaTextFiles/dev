/****************************************************************
**                                                             **
** Name:        HTMLview                                       **
** Author:      Andreas Falkenhahn                             **
** Version:     1.1                                            **
** Date:        24.10.18                                       **
** Interpreter: Hollywood 9.0                                  **
** Licence:     Sample program                                 **
** Function:    Demonstrates RapaGUI's HTMLview class          **
**                                                             **
** History:                                                    **
**                                                             **
** 1.1: (24.10.18)                                             **
**                                                             **
** - added high dpi graphics                                   **
**                                                             **
** 1.0: (13.05.16)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Make sure we have at least Hollywood 9.0!
*/
@VERSION 9,0


/*
** Enable DPI-awareness so that our GUI looks sharp even on high DPI monitors
*/
@OPTIONS {DPIAware = True}


/*
** This script requires the RapaGUI plugin
*/
@REQUIRE "RapaGUI"


/*
** Information about this app
*/
@APPTITLE "HTMLview"
@APPVERSION "$VER: HTMLview 1.1 (24.10.18)"
@APPCOPYRIGHT "(C) 2016-2018 by Andreas Falkenhahn"
@APPAUTHOR "Andreas Falkenhahn"
@APPDESCRIPTION "Demonstrates how to use RapaGUI's HTMLview class"


/*
** Toolbar images
*/
@ICON 1, {{Image = "data/back.png"}, {Image = "data/large_back.png"}}
@ICON 2, {{Image = "data/forward.png"}, {Image = "data/large_forward.png"}}
@ICON 3, {{Image = "data/reload.png"}, {Image = "data/large_reload.png"}}
@ICON 4, {{Image = "data/home.png"}, {Image = "data/large_home.png"}}
@ICON 5, {{Image = "data/linkamiga.png"}, {Image = "data/large_linkamiga.png"}}
@ICON 6, {{Image = "data/linkgoogle.png"}, {Image = "data/large_linkgoogle.png"}}
@ICON 7, {{Image = "data/linkwikipedia.png"}, {Image = "data/large_linkwikipedia.png"}}
@ICON 8, {{Image = "data/linkyahoo.png"}, {Image = "data/large_linkyahoo.png"}}


/*
** Our XML GUI definition
*/
@FILE 1, "HTMLview.xml"


/*
** Shows a new URL
*/
Function p_ShowURL(t$)
	
	moai.Set("url", "text", t$)
	moai.Set("hv", "url", t$)
	
EndFunction

	
/*
** Handles all incoming events
*/
Function p_EventFunc(msg)

	Switch msg.action
	Case "RapaGUI"
		Switch msg.attribute
		Case "CanGoBack":
			moai.Set("tb_back", "disabled", Not msg.triggervalue)
		Case "CanGoForward":
			moai.Set("tb_forward", "disabled", Not msg.triggervalue)
		Case "Pressed":
			Switch msg.id
			Case "go":
				moai.Set("hv", "url", moai.Get("url", "text"))
			Case "tb_reload":
				moai.Set("hv", "url", moai.Get("hv", "url"))	
			Case "tb_back":
				moai.DoMethod("hv", "goback")
			Case "tb_forward"
				moai.DoMethod("hv", "goforward")		
			Case "tb_amiga"
				p_ShowURL("http://www.amigaworld.net/")
			Case "tb_google"
				p_ShowURL("http://www.google.com/")					
			Case "tb_wiki"
				p_ShowURL("http://www.wikipedia.org/")
			Case "tb_yahoo"
				p_ShowURL("http://www.yahoo.com/")
			Case "tb_home"
				p_ShowURL("http://www.hollywood-mal.com/")	
			EndSwitch
		Case "Selected":
			Switch msg.id
			Case "mn_about":
				moai.Request("", "HTMLview\n(C) 2016-2018 by Andreas Falkenhahn\n\nVisit the official Hollywood portal at:\nhttp://www.hollywood-mal.com", "OK")		
			Case "mn_aboutrapagui":
				moai.DoMethod("app", "aboutrapagui")	
			Case "mn_exit":
				End					
			EndSwitch	
		EndSwitch		
 	EndSwitch

EndFunction

; dynamically create app from an external *.xml file definition
moai.CreateApp(ReadString(1))

; listen to these events!
InstallEventHandler({RapaGUI = p_EventFunc})

moai.Set("hv", "file", "about.html")
moai.DoMethod("hv", "clearhistory")  ; so that "Back" button is disabled

; main loop!
Repeat
	WaitEvent
Forever

