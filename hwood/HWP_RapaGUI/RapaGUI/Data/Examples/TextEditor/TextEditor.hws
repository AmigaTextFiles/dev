/****************************************************************
**                                                             **
** Name:        TextEditor                                     **
** Author:      Andreas Falkenhahn                             **
** Version:     1.1                                            **
** Date:        26.08.18                                       **
** Interpreter: Hollywood 9.0                                  **
** Licence:     Sample program                                 **
** Function:    Demonstrates toolbar, status bar and editor    **
**              widgets                                        **
**                                                             **
** History:                                                    **
**                                                             **
** 1.1: (26.08.18)                                             **
**                                                             **
** - added high dpi graphics                                   **
**                                                             **
** 1.0: (27.04.16)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Make sure we have at least Hollywood 9.0!
*/
@VERSION 9,0


/*
** Enable DPI-awareness so that our GUI looks sharp even on high DPI monitors
*/
@OPTIONS {DPIAware = True}


/*
** This script requires the RapaGUI plugin
*/
@REQUIRE "RapaGUI"


/*
** Information about this app
*/
@APPTITLE "TextEditor"
@APPVERSION "$VER: TextEditor 1.1 (26.08.18)"
@APPCOPYRIGHT "(C) 2016-2018 by Andreas Falkenhahn"
@APPAUTHOR "Andreas Falkenhahn"
@APPDESCRIPTION "Demonstrates toolbar, status bar and editor widgets"


/*
** Toolbar images
*/
@ICON 1, {{Image = "data/new.png"}, {Image = "data/large_new.png"}}
@ICON 2, {{Image = "data/open.png"}, {Image = "data/large_open.png"}}
@ICON 3, {{Image = "data/close.png"}, {Image = "data/large_close.png"}}
@ICON 4, {{Image = "data/save.png"}, {Image = "data/large_save.png"}}
@ICON 5, {{Image = "data/saveas.png"}, {Image = "data/large_saveas.png"}}
@ICON 6, {{Image = "data/cut.png"}, {Image = "data/large_cut.png"}}
@ICON 7, {{Image = "data/copy.png"}, {Image = "data/large_copy.png"}}
@ICON 8, {{Image = "data/paste.png"}, {Image = "data/large_paste.png"}}
@ICON 9, {{Image = "data/undo.png"}, {Image = "data/large_undo.png"}}
@ICON 10, {{Image = "data/redo.png"}, {Image = "data/large_redo.png"}}
@ICON 11, {{Image = "data/font_bold.png"}, {Image = "data/large_font_bold.png"}}
@ICON 12, {{Image = "data/font_italic.png"}, {Image = "data/large_font_italic.png"}}
@ICON 13, {{Image = "data/font_uline.png"}, {Image = "data/large_font_uline.png"}}
@ICON 14, {{Image = "data/align_left.png"}, {Image = "data/large_align_left.png"}}
@ICON 15, {{Image = "data/align_center.png"}, {Image = "data/large_align_center.png"}}
@ICON 16, {{Image = "data/align_right.png"}, {Image = "data/large_align_right.png"}}


/*
** Our XML GUI definition
*/
@FILE 1, "TextEditor.xml"


/*
** Handle text style changes
*/
Function p_ChangeTextStyle(s$, state)
	
	If moai.Get("editor", "areamarked")	
		Local s, e = moai.DoMethod("editor", "getselection")
		moai.DoMethod("editor", "set" .. s$, s, e, state)
	Else
		moai.Set("editor", s$, state)
	EndIf

EndFunction
			

/*
** Handle alignment changes
*/	
Function p_ChangeAlignment(a$)
	
	moai.Set("editor", "align", a$)
	moai.Set("mn_" .. a$, "selected", True, "nonotify", True)
	moai.Set("tb_" .. a$, "selected", True, "nonotify", True)
	
EndFunction
	
	
/*
** Update window title
*/	
Function p_UpdateTitle()
	
	moai.Set("win", "title", "TextEditor - " .. FilePart(projectname$) .. IIf(changed, "*", ""))

EndFunction			


/*
** Enables/disables buttons which need changes to work
*/
Function p_UpdateButtonState(close, save, saveas)
	
	moai.Set("tb_close", "disabled", close)
	moai.Set("mn_close", "disabled", close)
	moai.Set("tb_save", "disabled", save)
	moai.Set("mn_save", "disabled", save)
	moai.Set("tb_saveas", "disabled", saveas)		
	moai.Set("mn_saveas", "disabled", saveas)
				
EndFunction
		
	
/*
** Save
*/
Function p_Save()
	
	StringToFile(moai.Get("editor", "text"), projectname$)
	
	changed = False
	p_UpdateTitle()
	p_UpdateButtonState(False, True, False)
	
EndFunction	


/*
** Save as
*/
Function p_SaveAs()
	
	Local f$ = FileRequest("Save as", "txt", #REQ_SAVEMODE)
	If f$ = "" Then Return(True)
		
	projectname$ = f$

	p_Save()
	
	Return(False)	
	
EndFunction


/*
** Save document
*/
Function p_SaveGate()
	
	If projectname$ = "Unnamed"
		If p_SaveAs() Then Return(True)
	Else			
		p_Save()
	EndIf		
	
	Return(False)
	
EndFunction

	
/*
** Warn user of losing his changes
*/
Function p_CheckSave()
	
	If changed
		
		If moai.Request("", "Text has been modified. Do you want to save your changes?", "Yes|No", "question")
			If p_SaveGate() Then Return(True)			
		EndIf
	EndIf				
	
	Return(False)
	
EndFunction


/*
** Open empty document
*/
Function p_New()
	
	If p_CheckSave() Then Return

	moai.DoMethod("editor", "clear")
	projectname$ = "Unnamed"
	changed = False
	
	p_UpdateTitle()
	p_UpdateButtonState(True, True, True)

EndFunction	

	
/*
** Handles toolbar and menu events
*/
Function p_DoAction(id$, msg)
	
	Switch id$
	Case "open":
		If p_CheckSave() Then Break
		
		Local f$ = FileRequest("Select", "txt")
		If f$ = "" Then Return
			
		moai.Set("editor", "text", (FileToString(f$)), "nonotify", True)
		projectname$ = f$
		changed = False
		p_UpdateTitle()	
		p_UpdateButtonState(False, True, False)
	Case "save":
		p_SaveGate()
	Case "saveas":
		p_SaveAs()	
	Case "close":
		p_New()
	Case "new":
		p_New()				
	Case "undo":
		moai.DoMethod("editor", "undo")
	Case "redo":
		moai.DoMethod("editor", "redo")
	Case "cut":
		moai.DoMethod("editor", "cut")
	Case "copy":
		moai.DoMethod("editor", "copy")
	Case "paste":
		moai.DoMethod("editor", "paste")		
	Case "bold":
		p_ChangeTextStyle("bold", msg.triggervalue)
	Case "italic":
		p_ChangeTextStyle("italic", msg.triggervalue)
	Case "underline":		
		p_ChangeTextStyle("underline", msg.triggervalue)
	Case "left":
		p_ChangeAlignment("left")
	Case "right":
		p_ChangeAlignment("right")
	Case "center":
		p_ChangeAlignment("center")	
	Case "about":
		moai.Request("TextEditor", "TextEditor\n(C) 2016-2018 by Andreas Falkenhahn\n\nVisit the official Hollywood portal at:\nhttp://www.hollywood-mal.com", "OK")		
	Case "aboutrapagui":
		moai.DoMethod("app", "aboutrapagui")	
	Case "exit":
		If p_CheckSave() Then Break
		End						
	EndSwitch		
EndFunction


/*
** Handles all incoming events
*/
Function p_EventFunc(msg)
	
	Switch msg.action
	Case "RapaGUI":
		Switch msg.attribute
		Case "CursorPos":
			Local x, y = moai.DoMethod("editor", "getxy", msg.triggervalue)
			moai.Set("cursorpos", "text", "X: " .. x + 1 .. " Y: " .. y + 1)
		Case "UndoAvailable":
			moai.Set("mn_undo", "disabled", Not msg.triggervalue)
			moai.Set("tb_undo", "disabled", Not msg.triggervalue)			
		Case "RedoAvailable":
			moai.Set("mn_redo", "disabled", Not msg.triggervalue)
			moai.Set("tb_redo", "disabled", Not msg.triggervalue)
		Case "Bold":
			moai.Set("mn_bold", "selected", msg.triggervalue, "nonotify", True)
			moai.Set("tb_bold", "selected", msg.triggervalue, "nonotify", True)
		Case "Italic":
			moai.Set("mn_italic", "selected", msg.triggervalue, "nonotify", True)
			moai.Set("tb_italic", "selected", msg.triggervalue, "nonotify", True)
		Case "Underline":
			moai.Set("mn_underline", "selected", msg.triggervalue, "nonotify", True)
			moai.Set("tb_underline", "selected", msg.triggervalue, "nonotify", True)	
		Case "AreaMarked":
			moai.Set("mn_cut", "disabled", Not msg.triggervalue)
			moai.Set("tb_cut", "disabled", Not msg.triggervalue)
			moai.Set("mn_copy", "disabled", Not msg.triggervalue)
			moai.Set("tb_copy", "disabled", Not msg.triggervalue)	
		Case "HasChanged":
			If msg.triggervalue And (Not changed)
				changed = True
				p_UpdateTitle()		
				p_UpdateButtonState(False, False, False)														
			EndIf	
		Case "Pressed":
			p_DoAction(UnrightStr(msg.id, 3), msg)						
		Case "Selected":
			p_DoAction(UnrightStr(msg.id, 3), msg)
		Case "CloseRequest":
			If p_CheckSave() Then Break
			End							
 		EndSwitch
	EndSwitch

EndFunction

; dynamically create GUI from an external *.xml file definition
moai.CreateApp(ReadString(1))

; listen to these events!
InstallEventHandler({RapaGUI = p_EventFunc})

projectname$ = "Unnamed"

; main loop!
Repeat
	WaitEvent
Forever

