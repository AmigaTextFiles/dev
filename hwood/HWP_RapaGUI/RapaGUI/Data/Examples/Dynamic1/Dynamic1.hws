/****************************************************************
**                                                             **
** Name:        Dynamic1                                       **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        24.04.16                                       **
** Interpreter: Hollywood 9.0                                  **
** Licence:     Sample program                                 **
** Function:    Demonstrates dynamic creation of windows       **
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (24.04.16)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Make sure we have at least Hollywood 9.0!
*/
@VERSION 9,0


/*
** Enable DPI-awareness so that our GUI looks sharp even on high DPI monitors
*/
@OPTIONS {DPIAware = True}


/*
** This script requires the RapaGUI plugin
*/
@REQUIRE "RapaGUI"


/*
** Information about this app
*/
@APPTITLE "Dynamic1"
@APPVERSION "$VER: Dynamic 1.0 (24.04.16)"
@APPCOPYRIGHT "(C) 2014-2016 Andreas Falkenhahn"
@APPAUTHOR "Andreas Falkenhahn"
@APPDESCRIPTION "Demonstrates dynamic creation of windows"


/*
** Our XML GUI definition
*/
@FILE 1, "Dynamic1.xml"


/*
** Close window and free its object if it has been created programmatically
*/
Function p_CloseWindow(id)

	moai.Set(id, "open", False)
	moai.DoMethod("app", "removewindow", id)
	moai.FreeObject(id)

	opencount = opencount - 1
	If opencount = 0 Then End

EndFunction


/*
** Handles all incoming events
*/
Function p_EventFunc(msg)

	Switch msg.action
	Case "RapaGUI":
		Switch msg.attribute
		Case "CloseRequest":
			p_CloseWindow(msg.id)
		Case "Pressed":
			If LeftStr(msg.id, 3) = "new"
				count = count + 1
				opencount = opencount + 1
				Seek(1, 0)
				moai.CreateObject(ReplaceStr(ReadString(1), "%COUNT", count))
				moai.DoMethod("app", "addwindow", "win" .. count)
				moai.Set("win" .. count, "open", True)
			ElseIf LeftStr(msg.id, 5) = "close"
				p_CloseWindow(ReplaceStr(msg.id, "close", "win"))
			EndIf
 		EndSwitch
	EndSwitch

EndFunction

count = 1
opencount = 1

; dynamically create GUI from an external *.xml file definition
moai.CreateApp([[
<?xml version="1.0" encoding="iso-8859-1"?>
<application id="app">
]] .. ReplaceStr(ReadString(1), "%COUNT", count) .. "</application>")

; listen to these events!
InstallEventHandler({RapaGUI = p_EventFunc})

; main loop!
Repeat
	WaitEvent
Forever

