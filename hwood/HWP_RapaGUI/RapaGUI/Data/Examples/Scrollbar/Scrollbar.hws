/****************************************************************
**                                                             **
** Name:        Scrollbar                                      **
** Author:      Andreas Falkenhahn                             **
** Version:     1.1                                            **
** Date:        10.01.19                                       **
** Interpreter: Hollywood 6.1                                  **
** Licence:     Sample program                                 **
** Function:    Demonstrates how to use RapaGUI scrollbars     **
**                                                             **
** History:                                                    **
**                                                             **
** 1.1: (10.01.19)                                             **
**                                                             **
** - only draw if something has changed; can speed up things   **
**   on slower systems                                         **
**                                                             **
** 1.0: (24.04.16)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Make sure we have at least Hollywood 6.1!
*/
@VERSION 6,1


/*
** This script requires the RapaGUI plugin
*/
@REQUIRE "RapaGUI"


/*
** Information about the app
*/
@APPTITLE "Scrollbar"
@APPVERSION "$VER: Scrollbar 1.0 (24.04.16)"
@APPCOPYRIGHT "(C) Copyright 2012-2016 by Andreas Falkenhahn"
@APPAUTHOR "Andreas Falkenhahn"
@APPDESCRIPTION "Demonstrates how to connect the scrollbar to a Hollywood widget"


/*
** Thumbnail images
*/
@BRUSH 1, "data/0.png"
@BRUSH 2, "data/1.png"
@BRUSH 3, "data/2.png"
@BRUSH 4, "data/3.png"
@BRUSH 5, "data/4.png"
@BRUSH 6, "data/5.png"
@BRUSH 7, "data/6.png"
@BRUSH 8, "data/7.png"
@BRUSH 9, "data/8.png"
@BRUSH 10, "data/9.png"
@BRUSH 11, "data/10.png"
@BRUSH 12, "data/11.png"
@BRUSH 13, "data/12.png"
@BRUSH 14, "data/13.png"
@BRUSH 15, "data/14.png"
@BRUSH 16, "data/15.png"
@BRUSH 17, "data/16.png"
@BRUSH 18, "data/17.png"
@BRUSH 19, "data/18.png"
@BRUSH 20, "data/19.png"
@BRUSH 21, "data/20.png"
@BRUSH 22, "data/21.png"
@BRUSH 23, "data/22.png"
@BRUSH 24, "data/23.png"
@BRUSH 25, "data/24.png"
@BRUSH 26, "data/25.png"


/*
** Our XML GUI definition
*/
@FILE 1, "Scrollbar.xml"


/*
** Display configuration
*/
@DISPLAY {Width = 10, Height = 10, Color = #WHITE}


/*
** Draw thumbnails
*/
Function p_DrawThumbs(scrollx, scrolly, vwidth, vheight)

	Local drwx, drwy, drwwidth, drwheight = (vwidth - 322) \ 2, 10, 322, 242
	Local srcx, srcy, width, height = scrollx, scrolly, vwidth, vheight

	If drwx < 10 Then drwx = 10
		
	For Local k = 0 To 25

		; get visible part of thumbnail
		Local ox, oy, owidth, oheight = Intersection(drwx, drwy, drwwidth, drwheight, srcx, srcy, width, height)

		If (owidth > 0) And (oheight > 0) Then DisplayBrushPart(k + 1, ox - drwx, oy - drwy, ox - scrollx, oy - scrolly, owidth, oheight)

		; leave 10 pixels space between thumbnails
		drwy = drwy + drwheight + 10
	Next

EndFunction

/*
** Redraw thumbnails relative to scrollbar position
*/
Function p_RefreshThumbs(msg, force)

	Local scrollx = moai.Get("hscroll", "level")
	Local scrolly = moai.Get("vscroll", "level")

	If (msg.width <> brwidth) Or (msg.height <> brheight)
		
		If Not IsNil(br) Then FreeBrush(br)
			
		br = CreateBrush(Nil, msg.width, msg.height, #WHITE)
		brwidth = msg.width
		brheight = msg.height
	EndIf
	
	; we can get lots of messages, make sure to only draw if something has changed!
	If (lastscrollx <> scrollx) Or (lastscrolly <> scrolly) Or force
	
		SelectBrush(br)
		Box(0, 0, msg.width, msg.height, #WHITE)
		p_DrawThumbs(scrollx, scrolly, msg.width, msg.height)	
		EndSelect
	
		DisplayBrush(br, 0, 0)
	
		lastscrollx = scrollx
		lastscrolly = scrolly
	EndIf
		
EndFunction
		
/*
** Handles all incoming events
*/
Function p_EventFunc(msg)

	Switch msg.action
	Case "RapaGUI":
		Switch msg.attribute
		Case "Pressed":
			End
		Case "Level":
			p_RefreshThumbs({Width = GetAttribute(#DISPLAY, 1, #ATTRWIDTH), Height = GetAttribute(#DISPLAY, 1, #ATTRHEIGHT)}, False)
		EndSwitch
		
	Case "SizeWindow":
		p_RefreshThumbs(msg, True)	
	EndSwitch

EndFunction
	
SetFillStyle(#FILLCOLOR)

lastscrollx = -1
lastscrolly = -1

; dynamically create GUI from an external *.xml file definition
moai.CreateApp(ReadString(1))

p_RefreshThumbs({Width = GetAttribute(#DISPLAY, 1, #ATTRWIDTH), Height = GetAttribute(#DISPLAY, 1, #ATTRHEIGHT)}, True)

InstallEventHandler({RapaGUI = p_EventFunc, SizeWindow = p_EventFunc})

; main loop!
Repeat
	WaitEvent
Forever

