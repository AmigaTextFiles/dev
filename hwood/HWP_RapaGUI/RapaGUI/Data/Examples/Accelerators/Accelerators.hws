/****************************************************************
**                                                             **
** Name:        Accelerators                                   **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        06.05.16                                       **
** Interpreter: Hollywood 9.0                                  **
** Licence:     Sample program                                 **
** Function:    Test accelerator keys                          **
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (06.05.16)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Make sure we have at least Hollywood 9.0!
*/
@VERSION 9,0


/*
** Enable DPI-awareness so that our GUI looks sharp even on high DPI monitors
*/
@OPTIONS {DPIAware = True}


/*
** This script requires the RapaGUI plugin
*/
@REQUIRE "RapaGUI"


/*
** Information about this app
*/
@APPTITLE "Accelerator"
@APPVERSION "$VER: Accelerator 1.0 (06.05.16)"
@APPCOPYRIGHT "(C) 2016 Andreas Falkenhahn"
@APPAUTHOR "Andreas Falkenhahn"
@APPDESCRIPTION "Test accelerator keys"


/*
** Our XML GUI definition
*/
@FILE 1, "Accelerators.xml"


/*
** Handles all incoming events
*/
Function p_EventFunc(msg)

	Switch msg.action
	Case "RapaGUI":
		Switch msg.attribute
		Case "Pressed":
			Switch msg.id
			Case "clear":
				moai.DoMethod("lv", "clear")
			Case "esc":
				End	
			Default:
				If HaveItem(msg, "notifydata")
					x$ = msg.notifydata
				Else
					x$ = UpperStr(msg.id)
				EndIf		
				
				moai.DoMethod("lv", "insert", "bottom", x$)
				moai.DoMethod("lv", "jump", "bottom")
			EndSwitch
 		EndSwitch
	EndSwitch

EndFunction

; dynamically create GUI from an external *.xml file definition
moai.CreateApp(ReadString(1))

; listen to these events!
InstallEventHandler({RapaGUI = p_EventFunc})

; main loop!
Repeat
	WaitEvent
Forever

