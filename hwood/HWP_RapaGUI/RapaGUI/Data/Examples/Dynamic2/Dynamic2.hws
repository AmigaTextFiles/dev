/****************************************************************
**                                                             **
** Name:        Dynamic2                                       **
** Author:      Andreas Falkenhahn                             **
** Version:     1.0                                            **
** Date:        24.04.16                                       **
** Interpreter: Hollywood 9.0                                  **
** Licence:     Sample program                                 **
** Function:    Demonstrates dynamic creation of objects       **
**                                                             **
** History:                                                    **
**                                                             **
** 1.0: (24.04.16)                                             **
**                                                             **
** - initial release                                           **
**                                                             **
****************************************************************/

/*
** Make sure we have at least Hollywood 9.0!
*/
@VERSION 9,0


/*
** Enable DPI-awareness so that our GUI looks sharp even on high DPI monitors
*/
@OPTIONS {DPIAware = True}


/*
** This script requires the RapaGUI plugin
*/
@REQUIRE "RapaGUI"


/*
** Information about this app
*/
@APPTITLE "Dynamic2"
@APPVERSION "$VER: Dynamic2 1.0 (24.04.16)"
@APPCOPYRIGHT "(C) 2014-2016 by Andreas Falkenhahn"
@APPAUTHOR "Andreas Falkenhahn"
@APPDESCRIPTION "Demonstrates dynamic creation of objects"


/*
** Our XML GUI definition
*/
@FILE 1, "Dynamic2.xml"


/*
** Handles all incoming events
*/
Function p_EventFunc(msg)

	Switch msg.action
	Case "RapaGUI":
		Switch msg.attribute
		Case "Pressed":
			If msg.id = "add"
				moai.CreateObject("<button id=\"button" .. count .. "\">Button " .. count .. "</button>", "container")
				moai.DoMethod("container", "initchange")
				If count = 1 Then moai.DoMethod("container", "remove", "blankspace")
				moai.DoMethod("container", "append", "button" .. count)
				moai.DoMethod("container", "exitchange", false)
				count = count + 1
			ElseIf (msg.id = "rem") And (count > 1)
				count = count - 1
				moai.DoMethod("container", "initchange")
				moai.DoMethod("container", "remove", "button" .. count)
				If count = 1 Then moai.DoMethod("container", "append", "blankspace")
				moai.DoMethod("container", "exitchange", false)
				moai.FreeObject("button" .. count)
			ElseIf LeftStr(msg.id, 6) = "button"
				moai.Request("Dynamic 2", "You've pressed button " .. UnrightStr(msg.id, 6) .. "!", "OK")
			EndIf			
 		EndSwitch
	EndSwitch

EndFunction

count = 1

; dynamically create GUI from an external *.xml file definition
moai.CreateApp(ReadString(1))
		
; listen to these events!
InstallEventHandler({RapaGUI = p_EventFunc})

; main loop!
Repeat
	WaitEvent
Forever

