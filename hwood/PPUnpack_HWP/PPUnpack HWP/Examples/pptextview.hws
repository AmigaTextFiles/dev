@REQUIRE "MUIRoyale"
@REQUIRE "ppunpack"
@APPTITLE "PPTextView"
@APPVERSION "$VER: PPTextView 1.0 (03.01.14)"
@APPCOPYRIGHT "(C) Copyright 2014 by Lázi"
@APPAUTHOR "Lázi"
@APPDESCRIPTION "Displays powerpacked text files."

@DISPLAY {hidden=true,width=640,height=400}

lfeed="\n"

Function p_EventFunc(msg)


	; handle MUI Royale events
   	Switch msg.class
  	case "Menuitem":
        switch msg.id
        case "men_about":
            mui.request("PPTextView","PPTextView V1.0 by Lázi\nContact: lazi@freemail.hu","Understood")
        case "men_quit":
            End
        case "mn_muisettings":
            mui.DoMethod("pptextview", "openconfigwindow")
        Case "mn_aboutmui":
		    mui.DoMethod("pptextview", "aboutmui")
		case "mn_aboutmuiroyale":
			mui.DoMethod("pptextview", "aboutmuiroyale")
        case "men_open":
            f$=FileRequest("View file","")
            DebugPrint(F$)
            if not Exists(f$) then break
            mui.set("window","Title","PPTextView-"..FilePart(f$))
            text$=pp.ReadText(f$)
            mui.set("text","text",text$)
            mui.set("men_save","disabled",0)
            mui.set("men_format","disabled",0)
        case "men_format":

            mui.set("progress","open",true)

            text2$=""
            i=0
            space=false
            enter=0
            mui.set("prog","Max",StrLen(text$))
            while i < StrLen(text$)-1
                l=MidStr(text$,i,1)

                switch l
                case " ":
                   space=true
                case "\n":
                   enter=enter+1
                   space=false
                default
                   if enter=1 or space=true then text2$=text2$.." "
                   if enter>1 then text2$=text2$..lfeed..lfeed
                   enter=0
                   space=false
                   text2$=text2$..l
                endswitch
                i=i+1
                mui.set("prog","current",i)
            wend

            text$=text2$
            text2$=""
            mui.set("text","text",text$)

            mui.set("progress","open",false)
            mui.set("prog","current",0)

        case "men_save":

            f$=FileRequest("Save as...","",#REQ_SAVEMODE)
            if Exists(f$)
                if SystemRequest("PPTextViewer","File exists. Do you want to overwrite it?","Overwrite|Cancel") = 0  then break
            endif

            OpenFile(1,f$,#MODE_WRITE)
            WriteString(1,text$)
            CloseFile(1)

        case "lf_amiga":
            lfeed="\n"
            mui.set("lf_pc","selected",false)
        case "lf_pc":
            lfeed="\r\n"
            mui.set("lf_amiga","selected",false)
        endswitch
    Case "Window":
      		Switch msg.Attribute
      		Case "CloseRequest":
	 		    switch msg.id
                case "objwin":
                  mui.set("objwin","open",false)
                default:
                  End
                endswitch
  		    EndSwitch
   	EndSwitch
EndFunction

mui.CreateGUI([[
<?xml version="1.0" encoding="iso-8859-1"?>
<application icon="pptextview.info" id="pptextview" base="pptextview" menustrip="strip">

<menustrip id="strip">
		<menu title="Project">
            <item id="men_open" shortcut="O" notify="selected">Open</item>
			<item id="men_save" shortcut="S" notify="selected" disabled="true">Save as...</item>
            <item/>
            <item id="men_about" shortcut="?" notify="selected">About</item>
            <item id="mn_aboutmui" notify="selected">About MUI...</item>
			<item id="mn_aboutmuiroyale" notify="selected">About MUI Royale...</item>
			<item/>
            <item id="men_quit" shortcut="Q" notify="selected">Quit</item>
		</menu>
        <menu title="Tools">
            <item id="men_format" notify="selected" shortcut="R" disabled="true">Reformat</item>
            <menu title="Line feed">
                <item id="lf_amiga" notify="selected" type="radio" selected="true">Amiga $0a</item>
                <item id="lf_pc" notify="selected" type="radio">DOS  $0d0a</item>
            </menu>
        </menu>
</menustrip>


<window title="PPTextView" id="window" muiid="MAIN" notify="closerequest">
 <hgroup>
  <floattext id="text"></floattext>
 </hgroup>
</window>

<window title="Please wait" id="progress" muiid="PROG" open="false">
        <vgroup>
           <gauge id="prog" horiz="true" infotext="...%ld chars completed..."/>
           <scale/>
        </vgroup>
</window>

</application>

]])

InstallEventHandler({MUIRoyale = p_EventFunc, SizeWindow = p_EventFunc,onmouseup=0,onmousemove=0})


Repeat
	WaitEvent
forever
