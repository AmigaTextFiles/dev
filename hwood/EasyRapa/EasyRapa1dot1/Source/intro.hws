Function p_newsprite(id,scl,posx,posy)
	posx=x0+posx
	posy=Y0+posy
	CopyBrush(901,1001)
	ScaleBrush(1001,scl,scl)
	sp1high=GetAttribute(#BRUSH, 1001, #ATTRHEIGHT)
	offX=GetAttribute(#BRUSH, 1001, #ATTRWIDTH)/2

	CreateBrush(id,2*sp1high,40*sp1high,#BLACK)

	For k=0 To 9 
		y=sp1high*4*k-1
		SelectBrush(id) 
			DisplayBrush(1001,sp1high,y,{AnchorX = .5, AnchorY = 1,rotate=-k*10+90})
			If k>1 And k<9 Then Line(sp1high*Cos(Rad(-k*10+90)),y-sp1high+sp1high*Sin(Rad(-k*10+90)),sp1high*2,y,#GRAY)
		EndSelect
	Next	
	
	SetBrushTransparency(id, #BLACK)
	CreateSprite(id, #BRUSH, id,2*sp1high, 4*sp1high, 10,1, 0, 0)
	FreeBrush(1001)
	FreeBrush(id)
	offX=GetAttribute(#SPRITE, id, #ATTRWIDTH)/2
	offY=GetAttribute(#SPRITE, id, #ATTRHEIGHT)
	DisplaySprite(id,posx-offx,posy-offy,0)
EndFunction

Function p_movesprite(id,posx,posy)
	posx=x0+posx
	posy=Y0+posy

	offX=GetAttribute(#SPRITE, id, #ATTRWIDTH)/2
	offY=GetAttribute(#SPRITE, id, #ATTRHEIGHT)
	For k=1 To 9 
		DisplaySprite(id,posx-offx,posy-offy,k)
		progr=progr+2.5
		moai.Set("prgbar", "level", progr)
		WaitTimer(1, t)		
	Next
EndFunction


Function p_infostop()
;	DebugPrint("aaaa")
	FreeBrush(900)
	FreeBrush(901)
	FreeBrush(910)
	FreeBrush(911)
		
	FreeSprite(100)
	FreeSprite(200)
	FreeSprite(300)
	FreeSprite(400)

	moai.Set("ER_pv","Active","0")
	moai.Set("LO_pages","Hide",False)
	moai.Set("prgbar", "level", 0)
	InstallEventHandler ({vanillakey = 0, OnMouseDown=0})
	intro=False
	rp1x,rp1y,rp2x,rp2y,rp3x,rp3y,rp4x,rp4y=Nil,Nil,Nil,Nil,Nil,Nil,Nil,Nil
	StopTimer(1)
EndFunction

Function p_intro()
	rp1x,rp1y=312,283
	rp2x,rp2y=360,279
	rp3x,rp3y=405,279
	rp4x,rp4y=445,280
	progr=0
	t=80
	StartTimer(1)
	@BRUSH 911, "data/rapagui000.png", {LoadAlpha = True}; backgroud picture
	@BRUSH 900, "data/rapagui000mask4.png"
	@BRUSH 901, "data/rp1.png", {LoadAlpha = True}
	@BRUSH 910, "data/smile.png"
	
	SetBrushTransparency(900, #BLACK)
	
	moai.Set("ER_pv","Active","1")

;	moai.Set("tab_xml","hide",True)

	SelectDisplay(1)
	InstallEventHandler ({vanillakey = p_infostop, OnMouseDown=p_infostop})

	X0=(GetAttribute(#DISPLAY, 1, #ATTRWIDTH)-GetAttribute(#BRUSH, 911, #ATTRWIDTH))/2
	Y0=(GetAttribute(#DISPLAY, 1, #ATTRHEIGHT)-GetAttribute(#BRUSH, 911, #ATTRHEIGHT))/2
	moai.Set("LO_pages","Hide",True)
		
	Cls(#WHITE)	
	DisplayBrush(911, X0, Y0)

	CreateClipRegion(1, #SHAPE, 900,X0,Y0)
	SetClipRegion(1)
	
	p_newsprite(100,"100%",rp1x,rp1y)
	p_newsprite(200,"90%",rp2x,rp2y)
	p_newsprite(300,"80%",rp3x,rp3y)
	p_newsprite(400,"70%",rp4x,rp4y)
	
	p_movesprite(400,rp4x,rp4y)
	p_movesprite(300,rp3x,rp3y)
	p_movesprite(200,rp2x,rp2y)
	p_movesprite(100,rp1x,rp1y)
	
	FreeClipRegion (1)
	
	DisplayBrush (910,x0+130,y0+130)
	
	SetFontColor(#RED)
	SetFont(#SERIF, 38)
	SetFontStyle(#BOLD|#ANTIALIAS)
	SetFontStyle(#EDGE, #YELLOW, 3)
	TextOut( #CENTER, 40,"EasyRapa 1.1")
	SetFont(#SERIF, 25)
	SetFontColor(#GREEN)
	SetFontStyle(#SHADOW, ARGB(128, $939393), 2, #SHDWSOUTHEAST)
	TextOut( #CENTER, 80,"a GIU builder for Hollywood RapaGui")
	
	moai.Set("prgbar", "level", progr+5)
	WaitTimer(1, t*20)
	p_infostop()
	
EndFunction