/****************************************************************
**                                                             
** Name:        EasyRapa Post Process code                                   
** Author:      Sandro Barbagelata    
** Version: 	1.0             
** Date:        August 2020                                    
** Interpreter: Hollywood               
**
**                                                         
****************************************************************/

Function p_Postprocess_hws()
	; function to autogenerate HWS code from ERP
	hwstext$=""
	hws_indent=0
	prf_space=Val(moai.Get("prf_tab_spc","text"))
	prg_eventh$=IIf(prg_eventh$<>"",prg_eventh$,prjname$.."_EventFunc") 
	
	add_func_list={}

	p_hws_addline([[/****************************************************************]])
	p_hws_addline("**")
	p_hws_addline("**  Project "..prjname$.." by "..prf_user_name$)
	p_hws_addline("**")
	p_hws_addline("**  Auto generated by EasyRapa program")
	p_hws_addline("**")
	p_hws_addline("**   "..GetDate())
	p_hws_addline("**")
	p_hws_addline("**   Interpreter: Hollywood")
	p_hws_addline("**")
	p_hws_addline([[****************************************************************/]])
	p_hws_addline("")
	p_hws_addline("@VERSION "..ReplaceStr(Hol_vers$,".",","))
	p_hws_addline("")
	p_hws_addline([[@REQUIRE "RapaGUI", {Version = ]]..LeftStr(RG_vers$,FindStr(RG_vers$,"."))..[[ , Revision = ]]..MidStr(RG_vers$,FindStr(RG_vers$,".")+1)..[[}]])
	p_hws_addline("")
	If moai.Get("ic_sel", "entries")>0  ; ==================list all BRUSHES
		For k=0 To moai.Get("ic_sel", "entries")-1
			aa=Pack(moai.DoMethod("ic_sel", "GetEntry", k))
			p_hws_addline([[@BRUSH ]]..aa[0]..[[, "]]..ReplaceStr(aa[1],Chr(92),Chr(47))..[[", {LoadAlpha = True}]])
		Next	
	EndIf	
	p_hws_addline("")
	
	For Local k=1 To TableItems(idsindexR)-1  ; ============ scan for Hollywood displays
		dtn0=SplitStr(idsindexR[k],"|")
		tx$,ln=p_get_linedata(dtn0[3])
		Local data,nd=SplitStr(tx$,",")
		If data[0]="hollywood" 
			p_hws_addline([[@DISPLAY ]]..data[5]..[[,{width=]]..data[6]..[[,height=]]..data[7].."}")
		EndIf
	Next
	p_hws_addline("")

	;Generate event handler function
	
	p_hws_addline("Function p_"..prg_eventh$.."(msg)",1)
	p_hws_addline([[Switch msg.action]])
	p_hws_addline([[Case "RapaGUI":]],1)

	p_pp_progbar(55, "generate event handler")
			
	p_hws_addline([[Switch msg.attribute]])
		
	If moai.Get("ic_sel", "entries")>0 Or anybutton
		p_hws_addline([[Case "Pressed":]],1);================== PRESSED
		p_hws_addline([[Switch msg.id]],1)
		For k=0 To moai.Get("ic_sel", "entries")-1 ; add all icons
			aa=Pack(moai.DoMethod("ic_sel", "GetEntry", k))			
			If aa[2]="On" ;add the id to the macro list only for toolbar icons
				p_hws_addline([[Case "]]..aa[3]..[[":]],1)
				InsertItem(add_func_list,"p_"..aa[3].."_press()|"..aa[5]) ;add the ID to the lit of the button to be handled with specific function and macro including if macro filled in  
				p_hws_addline("p_"..aa[3].."_press ()",-1)
			EndIf
		Next
		
		For Local k=0 To TableItems(idsindexR)-1  ; scan for other buttons
			dtn0=SplitStr(idsindexR[k],"|")
			tx$,ln=p_get_linedata(dtn0[3])
			Local data,nd=SplitStr(tx$,",")
			If data[0]="button" And data[8]="0" And FindStr(activepages,"|"..PadNum(data[4],2))>=0
				InsertItem(add_func_list,"p_"..data[1].."_press()|"..p_macro_find(data));add the ID to the lit of the button to be handled with specific function and macro including if macro filled in
				p_hws_addline([[Case "]]..data[1]..[[":]],1)
				p_hws_addline("p_"..data[1].."_press()")	
				hws_indent=hws_indent-1
			EndIf
;			If data[0]="htmlview" And FindStr(activepages,"|"..PadNum(data[4],2))>=0
;				DebugPrint(data[4], data[1])
;			EndIf
		Next
		p_notify_hws("pressed")
		hws_indent=hws_indent-1
		p_hws_addline([[EndSwitch]],-1)
	EndIf
	
	p_pp_progbar(60, "event handler SELECTED")

	If prg_menubar Or anytooglebutton ;==========================================  Menubar
	p_hws_addline([[Case "Selected":]],1);======================== SELECTED
	p_hws_addline([[Switch msg.id]],1)
		For Local t=0 To TableItems(mnuoptions)-1
		   p_hws_addline([[Case "]]..mnuoptions[t]..[[" :]],1)
		   If LowerStr(mnuoptions[t])="mn_about"
			p_hws_addline([[moai.Request("]]..prg_appname$..[[","]].. prg_appname$..[[ by ]]..prf_user_name$..[[ (]]..prf_user_mail$..[[) \n\n Autogenerated by EasyRapa\n\n 2020 by Sandro Barbagelata (sb5@libero.it)", "OK")]],-1)
		   ElseIf LowerStr(mnuoptions[t])="mn_aboutrapagui"
			p_hws_addline([[moai.DoMethod("]]..prg_appname$..[[", "aboutrapagui")]],-1)
		   Else	
			p_hws_addline([[p_msgbox (msg.attribute, msg.id)]],-1)
		   EndIf
		Next 
		For Local k=1 To TableItems(idsindexR)-1  ; scan for other toggle buttons
			dtn0=SplitStr(idsindexR[k],"|")
			tx$,ln=p_get_linedata(dtn0[3])
			Local data,nd=SplitStr(tx$,",")
			If data[0]="button" And data[8]="1" And FindStr(activepages,"|"..PadNum(data[4],2))>=0
				InsertItem(add_func_list,"p_"..data[1].."_tglpress()|"..p_macro_find(data)) ;add the ID to the lit of the button to be handled with specific function and macro including if macro filled in
				p_hws_addline([[Case "]]..data[1]..[[":]],1)
				p_hws_addline("p_"..data[1].."_tglpress()")	
				hws_indent=hws_indent-1
			EndIf
		Next
		p_notify_hws("selected")
		hws_indent=hws_indent-1
		p_hws_addline([[EndSwitch]],-1)
	EndIf	
	
	p_pp_progbar(80, "event add other notification")

	;add any other notification

	For Local u=0 To TableItems(objnotify)-1
		If FindStr(objprocessed, "|"..objnotify[u].notfun.."|", False)<0
			If LowerStr(objnotify[u].notfun)="closerequest"
				p_hws_addline([[Case "CloseRequest":]],1)
				p_hws_addline([[End]],-1)
				objprocessed=objprocessed.."closerequest|"
			Else
				p_hws_addline([[Case "]]..objnotify[u].notfun..[[":]],1)
					p_hws_addline([[Switch msg.id]],1)
					p_notify_hws(LowerStr(objnotify[u].notfun))
					hws_indent=hws_indent-1
					p_hws_addline([[EndSwitch]],-1)
;				p_hws_addline([[EndSwitch]],-1)
			EndIf
		EndIf
	Next

	  
	p_hws_addline([[EndSwitch]],-1)

	p_pp_progbar(90, "add HWS catalogs")

	p_hws_addline([[Case "OnMouseDown"]],1)
	p_hws_addline([[p_msgbox ( "click",msg.id)]],-1)	
	p_hws_addline([[EndSwitch]],-1)
	p_hws_addline([[EndFunction]])
	p_hws_addline("")
	
	If IsTableEmpty(add_func_list)= False Then p_macro_add();check if specific function need to be aded for button click with macro

	If has_cat Then p_pp_catalogadd()
	p_hws_addline("")
	
	p_pp_Userdata()
	p_hws_addline("")

	
;	p_hws_addline([[Function p_msgbox(fun, trig)]],1)
;	p_hws_addline([[moai.Request("]]..prg_appname$..[[", "You "..fun.." for ID "..trig, "OK")]],-1)				
;	p_hws_addline([[EndFunction]])
;	p_hws_addline("")

	p_hws_addline(";=====================================================================")
	p_hws_addline("")
	p_hws_addline([[moai.CreateApp(FileToString("]]..prjname$..[[.xml"))]])
	p_hws_addline("")
	If has_cat Then p_hws_addline("p_OpenCat()")
	If has_cat Then p_hws_addline("p_"..prjname$.."_CatalogData ()")
	p_hws_addline("")
	p_hws_addline("; listen to these events!")
	p_hws_addline("InstallEventHandler({RapaGUI = p_"..prg_eventh$..", OnMouseDown = p_"..prg_eventh$.."})")
	p_hws_addline("")
	p_hws_addline("Repeat",1)
	p_hws_addline("WaitEvent",-1)
	p_hws_addline("Forever")

	p_pp_progbar(100, "completed")

	StringToFile(hwstext$, UnleftStr(fullprojectname$, 3).."hws")
	moai.Set("ER_pv","Active","3") ; jump to HWS tab
;	moai.Set("hws", "text","wait ....")
	moai.Set("hws", "text",p_add_linen(hwstext$))

	p_pp_progbar(0, "")
	add_func_list=Nil
	
EndFunction

Function p_macro_find(dtlist)
	zz=0
;	DebugPrint(obj[dtlist[0]].param[z])
	For Local z=0 To TableItems(obj[dtlist[0]].param)-1
		If UpperStr(obj[dtlist[0]].param[z])="MACRO" Then zz=z
;		moai.Request("aaa", zz, "OK")	
	Next    
;		moai.Request("aaa", dtlist[zz+1], "OK")			
	If zz>0 Then Return(dtlist[zz+1]) Else Return("")
EndFunction
	
Function p_macro_add()
    ;this function adds from the list add_func_lis built before of the obj requring macro the macro file or a generic empty furnction in case no macro
	    a, b = NextItem(add_func_list)
	While GetType(a) <> #NIL
	    ;Adding function with name of the button _press
	    Local c=SplitStr (b,"|") 
	    Local funcname=c[0]
	    Local macroname=c[1]  
;            moai.Request("aaa", funcname.."!"..macroname, "OK")
	    
	    If macroname<>""
		Local s$=FileToString(macroname)
				funcname=ReplaceStr(funcname,"()","")
		s$=ReplaceStr(s$,"macroname",funcname)
		p_hws_addline(s$,1)

	    Else               
				p_hws_addline([[Function ]]..funcname,1)
				funcname=ReplaceStr(funcname,"p_","")
				funcname=ReplaceStr(funcname,"_tglpress()","")
				funcname=ReplaceStr(funcname,"_press()","")
			
				p_hws_addline([[moai.Request("]]..prg_appname$..[[", "You pressed button ]]..funcname..[[", "OK")]],-1)
				p_hws_addline([[EndFunction]])
			EndIf    
			p_hws_addline("")

	  a, b = NextItem(add_func_list, a)
	Wend
EndFunction
    
Function p_pp_catalogadd()
	
	p_crea_catalog_file()
		
	p_hws_addline("Function p_"..prjname$.."_CatalogData()",1)
	
	For k=0 To TableItems(cataloglist)-1
		catvar,n=SplitStr(cataloglist[k].usedin,"]")
;		DebugPrint(cataloglist[k].usedin,n)
		
		If FindStr(cataloglist[k].usedin, "menu")>0 And Not prg_menubar Then Continue ; case of menu are disabled then menu items will not be showed in the cat
;		If Not has_pages And cataloglist[k].pag<>"0" Then Continue  ;Case page 0 name in order to detectcase of page off on catalogs 
			
		For u=0 To n-2
			vr=LeftStr(catvar[u],FindStr(catvar[u],"[")) ; name of the item 
			pr=MidStr(catvar[u],FindStr(catvar[u],"#")+1) ; line description into the class definition
			cl=MidStr(catvar[u],FindStr(catvar[u],"[")+1) ;class type
			cl=LeftStr(cl,FindStr(cl,"#"))
			cnt=True
			vr1=vr
			If FindStr(vr1,"~")>0 Then vr1=LeftStr(vr1,FindStr(vr1,"~"))
;			DebugPrint(vr,vr1)
			If HaveItem (ids_n,vr1)
				;case of form has more pages and only page 0 is used but has a pageview on page 0 that must work
;				DebugPrint(has_pages,vr1,"<<<",Val(ids_n[vr1].pg),"<<<")
				If has_pages=False And ToNumber(ids_n[vr1].pg)>0 Then cnt=False
			EndIf
			If has_pages=False And vr1= page0name Then cnt=False

			If cnt
;			DebugPrint(cnt,vr)
				If UpperStr(pr)="TITLE_LO_PAGES" And UpperStr(cl)="PAGEVIEW" 
					p_hws_addline([[moai.Set("]]..vr..[[","Title",Cat_string$[]]..k.."])    ; "..cataloglist[k].text)
				ElseIf UpperStr(pr)="ITEM" And UpperStr(cl)="RADIO" 
					p_hws_addline(IIf(ToNumber(RG_vers$)>1.2,"",";")..[[moai.DoMethod("]]..LeftStr(vr,FindStr(vr,"~R"))..[[","SetItem",]]..MidStr(vr,FindStr(vr,"~R")+2)..[[, Cat_string$[]]..k.."])    ;"..cataloglist[k].text)			
				ElseIf UpperStr(pr)="ENTRIES" And UpperStr(cl)="CHOICE" 
					p_hws_addline([[moai.DoMethod("]]..vr..[[","Insert", "bottom" ,Cat_string$[]]..k.."])")			
				ElseIf UpperStr(pr)="ENTRIES" And UpperStr(cl)="COMBOBOX" 
					p_hws_addline([[moai.DoMethod("]]..vr..[[","Insert", "bottom" ,Cat_string$[]]..k.."])")			
				ElseIf UpperStr(pr)="TITLE" And UpperStr(cl)="RADIO" 
					p_hws_addline(IIf(ToNumber(RG_vers$)>1.2,"",";")..[[moai.Set("]]..vr..[[","Title",Cat_string$[]]..k.."])    ; "..cataloglist[k].text)	
				ElseIf UpperStr(pr)="COL_TITLES"	
					p_hws_addline([[moai.Set("]]..vr..[[","Title",Cat_string$[]]..k.."])    ; "..cataloglist[k].text)						
				ElseIf UpperStr(cl)="MENU" And prg_menubar
					p_hws_addline([[moai.Set("]]..vr..[[","Title",Cat_string$[]]..k.."])    ; "..cataloglist[k].text)	
				Else
					p_hws_addline([[moai.Set("]]..vr..[[","]]..pr..[[",Cat_string$[]]..k.."])    ; "..cataloglist[k].text)
				EndIf
			EndIf
		Next
	Next

	p_hws_addline("",-1)
	p_hws_addline([[EndFunction]])
	p_hws_addline("")
	p_hws_addline("")

	p_hws_addline("Function p_OpenCat()",1)
		p_hws_addline("Def_Cat_Str$ = {}")
		p_hws_addline("Cat_string$ = {}")
		
		For k=0 To TableItems(cataloglist)-1
			p_hws_addline([[Def_Cat_Str$[]]..k..[[]="]]..cataloglist[k].text..[["]])
		Next
		
		p_hws_addline("")
		p_hws_addline([[OpenCatalog("]]..FilePart(catfile$)..[[")]])
		p_hws_addline("For k = 0 To tableitems(Def_Cat_Str$)-1",1)
		p_hws_addline("Cat_string$[k] = GetCatalogString(k, Def_Cat_Str$[k])",-1)
		p_hws_addline("Next")
		p_hws_addline("CloseCatalog()",-1)
		hws_indent=0
		p_hws_addline("EndFunction")
EndFunction

Function p_pp_Userdata()
	p_hws_addline("Function p_"..prjname$.."_UserData()",1)
	p_hws_addline(";function ready to use for setting user data")

	For Local k=1 To TableItems(idsindexR)-1
		dtn0=SplitStr(idsindexR[k],"|")
		tx$,ln=p_get_linedata(dtn0[3])
		Local data,nd=SplitStr(tx$,",")
		
		If FindStr(activepages,"|"..PadNum(data[4],2))>=0		
			Switch data[0]	
				Case "text" 
					aa=IIf(data[6]<>"",[["]]..data[6]..[[")]],[["insert your data here")]])
					p_hws_addline( [[moai.Set("]] ..data[1]..[[","text",aa)]])
				Case "textentry"	
					p_hws_addline([[moai.Set("]]..data[1]..[[","text","insert your data here")]])
				Case "textview"	
					p_hws_addline([[moai.Set("]]..data[1]..[[","text","insert your data here")]])
				Case "htmlview"	
					p_hws_addline([[moai.Set("]]..data[1]..[[","contents","insert HTML data here")]])
				Case "label"	
					p_hws_addline([[moai.Set("]]..data[1]..[[","text","label comment")]])
				Case "checkbox"
					p_hws_addline([[moai.Set("]]..data[1]..[[","selected","]]..(data[6])..[[")]])
				Case "choice"
					p_hws_addline([[moai.Set("]]..data[1]..[[","active",0)]])
			EndSwitch
		EndIf
	Next
	p_hws_addline("",-1)
	p_hws_addline([[EndFunction]])

EndFunction


Function p_crea_catalog_file()
	MakeDirectory(PathPart(catfile$)) 
	erroncat=False
	OpenFile(1, catfile$,#MODE_WRITE )
		For k=0 To TableItems(cataloglist)-1
			WriteLine(1,cataloglist[k].transl)
			If cataloglist[k].transl="" Then erroncat=True
		Next
	CloseFile(1)
	OpenFile(1, PathPart(fullprojectname$).."catalogs/default4translation.txt",#MODE_WRITE )
		For k=0 To TableItems(cataloglist)-1
			WriteLine(1,cataloglist[k].text)
		Next
	CloseFile(1)
	If erroncat Then moai.Request("Error on catalog creation", "One catalog line is blank. All translations must be filled in", "OK", "Error")

EndFunction

	
Function p_Postprocess()
	; function to autogenerate xml code from ERP
	Local data=SplitStr(p_get_line_txt(0),",")
	If data[0]<>"window" 
		moai.Request("", "First line must be a window definition?", "OK", "error")
		Return			
	EndIf
	
	
	Global objnotify,objprocessed={},"|"
	
	; create indexes for rox and columns
	idsindexR={}
;	idsindexC={}
	anybutton=False
	anytooglebutton=False
	For Local k=1 To TableItems(ids)-1
		tx$,ln=p_get_linedata(ids[k].id)
		Local data,nd=SplitStr(tx$,",")
		idsindexR[k-1]=PadNum(ids[k].pg,2).."|"..PadNum(ids[k].r,5).."|"..PadNum(ids[k].c,5).."|"..ids[k].id
		If PadNum(ids[k].pg,2)="00" And LowerStr(ids[k].obj)="pageview" And ids[k].r<>"0" And ids[k].c<>"0" Then has_pagesonpage0=True
		If LowerStr(ids[k].obj)="button" Then anybutton=True
		If LowerStr(ids[k].obj)="button" And data[8]="1" Then anytooglebutton=True
		
;		idsindexC[k-1]=ids[k].c.."|"..ids[k].r.."|"..ids[k].id
	Next
	Sort(idsindexR)
;	Sort(idsindexC)
;	DebugPrint(TableItems(idsindexR))

;	For Local k=1 To TableItems(idsindexR)-1 Do DebugPrint(idsindexR[k])


	catfile$=PathPart(fullprojectname$).."catalogs/"..moai.Get("prf_Hol_cat","value").."/"..UnleftStr(projectname$, 3).."catalog"

	moai.Set("ER_pv","Active","2") ; jump to XML tab
	moai.Set("xml", "text","wait ....")

	Global prjname$=UnleftStr(FilePart(projectname$), 4)
	
	pp_error$=""
	activepages="|"
	p_Postprocess_xml()
	If StrLen(pp_error$)=0 Then p_Postprocess_hws()
	gotest=True
	If StrLen(pp_error$)>0 
		gotest=False
		SystemRequest("Post Processing ERROR",	pp_error$,"Ok",#REQICON_ERROR)
		p_pp_progbar(0, "")
		moai.Set("xml", "text","")
		moai.Set("hws", "text","")			
	EndIf
	p_UpdateButtonState()
EndFunction


Function p_Postprocess_xml()
	xml_indent=0
	xmltext$=""	
	page0name=""	
	
	Local data=SplitStr(p_get_line_txt(0),",")
	If data[0]<>"" Then page0name=data[0] ;determine if on window definition there is a page 0 name in order to detect case of page off on catalogs 
	
	Local data=SplitStr(p_get_line_txt(0),",")
	If data[0]<>"window"
		pp_error$="error line 1 in Project tab must be Window"
		Return
	EndIf

	p_pp_progbar(1,"Inizializing for post processing")

	prg_menubar=IIf(Val(data[6])=0,False,True)
	prg_menubar$=""
	has_pages=IIf(Val(data[4])=0,False,True)
	has_cat=IIf(Val(data[5])=0,False,True)
	prg_appname$=data[7] 
	prg_eventh$=data[8]
	If StripStr(prg_appname$)="" Then prg_appname$=prjname$
	p_xml_addline([[<?xml version="1.0" encoding="]].. p_get_choice(data[0],8,data[9]+1)..[["?>]])
	p_xml_addline([[<application id="]].. prg_appname$ ..[[">]])

	p_pp_progbar(10,"XML menu items")
	If prg_menubar Then p_xml_menu()
	p_pp_progbar(15,"XML Window definition")
	
	;window definition ===========================================================

	notL=p_notify_list("window", 13, data[14],prg_appname$)
;	moai.Request("debug",notL , "OK")
	If FindStr(UpperStr(notL), "CLOSEREQUEST")>=0 Then notL="CloseRequest" Else notL=""

	p_xml_addline([[<window id="]]..StripStr(data[1])..
	IIf(prg_menubar,[[" menubar="]]..prg_menubar$,"")..
	IIf(StripStr(data[7])<>"",[[" title="]]..data[7],"")..
	IIf(ToNumber(data[10])>0,[[" width="]]..StripStr(data[10]),"")..
	IIf(ToNumber(data[11])>0,[[" height="]]..StripStr(data[11]),"")..
	IIf(ToNumber(data[12])>0,[[" left="]]..StripStr(data[12]),"")..
	IIf(ToNumber(data[13])>0,[[" top="]]..StripStr(data[13]),"")..
	IIf(StrLen(notL)>0,[[" notify="]]..notL,"")..
	IIf(ToNumber(data[15])=0,[[" MaximizeGadget="false]],"")..
	IIf(ToNumber(data[16])=0,[[" MinimizeGadget="false]],"")..
	IIf(ToNumber(data[17])=0,[[" CloseGadget="false]],"")..[[">]])


	xml_indent=xml_indent+1
	p_xml_addline("<vgroup>",1)
	;end of window definition

	p_pp_progbar(10,"xml toolbar build")

	;toolbar section ==============================================================
	If moai.Get("ic_sel", "entries")>0
		tmp$=""
		For Local p=0 To moai.Get("ic_sel", "entries")-1			
			Local aa=Pack(moai.DoMethod("ic_sel", "GetEntry", p))
			If aa[2]="1" Or aa[2]="On"
			   tmp$=tmp$.."###"..([[<button id="]]..aa[3]..[[" icon="]]..aa[0]..[[" help="]]..aa[4]..[["/>]])..lf$
			EndIf
		Next
		aa=Nil
		If tmp$<>""
			p_xml_addline([[<toolbar>]],1)
			tmp$=ReplaceStr(tmp$,"###",p_indent(xml_indent))
			xmltext$=xmltext$..tmp$
			p_xml_addline([[</toolbar>]],-1,True)
		EndIf
	EndIf	
	;end of toolbar section
	

	;body items section ============================================ B O D Y ==========
	;idsindexR[k]="rown|column|id >>>> sorted by rown 
	
	p_pp_progbar(25, "Organize ID's")
	p_compact_IDs()
	p_pp_progbar(35, "XML build main block")

	Local data=SplitStr(p_get_line_txt(1),",")
	If has_pages=True And data[0]<>""
		If data[0]="pageview"
			p_xml_obj(p_get_linedata(data[1]))
			pagelist,pln=SplitStr(data[6],"|")
			pagetitles,tn=SplitStr(data[5],"|")
			If pln<>tn
				pp_error$="pageview "..data[1].." has different n. of pages and n. of titles" 
				Return 
			EndIf
			xml_indent=xml_indent+1
			For k=0 To pln-1
;				DebugPrint(pagetitles[k],PadNum(pagelist[k],2))	
				p_xml_addline([[<hgroup id="]]..data[1].."~P"..k..[[" Title="]]..pagetitles[k]..[[">]],1)	
				p_pp_pagedata(PadNum(pagelist[k],2))
				p_xml_addline("</hgroup>",-1,True)

			Next
			p_xml_addline([[</pageview>]],-1,True)
		
		Else
			pp_error$="error line 2 In Project tab must be Pageview or blank"
			Return
		EndIf
	Else
		p_pp_pagedata("00") ; if has pages is tagged off only page 0 is processed
	EndIf
		
	
	p_xml_addline([[</vgroup>]],-1,True)
	p_xml_addline([[</window>]],-1)
	p_xml_addline([[</application>]],-1)

	p_pp_progbar(50, "XML completed")
	

	StringToFile(xmltext$, UnleftStr(fullprojectname$, 3).."xml")
	
	moai.Set("xml", "text",p_add_linen(xmltext$))
	
EndFunction

Function p_xml_addline(txt$, Indent, before)
	If before
		If indent<>0 Then xml_indent=xml_indent+indent
		xmltext$=xmltext$..p_indent(xml_indent)..txt$..lf$
	Else	
		xmltext$=xmltext$..p_indent(xml_indent)..txt$..lf$
		If indent<>0 Then xml_indent=xml_indent+indent
	EndIf
EndFunction

Function p_hws_addline(txt$,Indent,tmp$)
	If tm$<>""
		tmp$=tmp$..p_indent(hws_indent)..txt$..lf$
	Else	
		hwstext$=hwstext$..p_indent(hws_indent)..txt$..lf$
		If indent<>0 Then hws_indent=hws_indent+indent
	EndIf
EndFunction

Function p_get_choice(obj$,functpos,choicepos)
	;DebugPrint(obj$,choicepos)
	Local aa= SplitStr((obj[obj$].funct[functpos]),"|")
	Return(aa[ToNumber(choicepos)])
EndFunction

Function p_get_bolean(msg)
	If LowerStr(msg)="true" Then Return("true")
	If LowerStr(msg)="false" Then Return("false")
	If msg="1"
		Return("true")
	Else
		Return("false")	
	EndIf	
EndFunction

Function p_indent(x)
	;return an idented line according indent set up and current indent position
	If x<=0 Then Return ("")
	If prf_tab
		Return(RepeatStr (Chr(9),x))
	Else
		Return(RepeatStr(RepeatStr (" ",prf_space),x))
	EndIf
EndFunction

	
Function p_xml_menu()

/*	mnuoptions={}
	mnuon=0	
	p_xml_addline([[<menubar id="]]..tvarray[0].id..[[">]],1)
	prg_menubar$=tvarray[0].id
	
	For k=1 To TableItems(tvarray)-1
		DebugPrint(tvarray[k].id,tvarray[k].parent)
		If tvarray[k].node
			If mnuon>0
				p_xml_addline([[</menu>]],-1,True)
				mnuon=mnuon-1
			EndIf
			p_xml_addline([[<menu title="]]..tvarray[k].items[0]..[[" id="]]..tvarray[k].id..[[">]],1)
			mnuon=mnuon+1

		Else
			p_menu_additemline(tvarray[k].items)
			InsertItem(mnuoptions,tvarray[k].items[1]);add item at the list of the menu options for event handler pressed
		EndIf
	Next
	xml_indent=xml_indent-1
	While mnuon>0 
		p_xml_addline("</menu>")
		mnuon=mnuon-1
	Wend
	p_xml_addline("</menubar>",-1,True)


	Return
	
*/	
;	    p_xml_addline("<menubar id=".. prg_menubar$..[[">]])
;	    xml_indent=xml_indent+1
	    mnuhome=True
	    mnuon=0	
	    mnuoptions={}
	    p_menu2xml("tv", "Root")	
	    xml_indent=xml_indent-1
	    While mnuon>0 
		p_xml_addline("</menu>")
		mnuon=mnuon-1
	    Wend
;	    xml_indent=xml_indent-1
	    p_xml_addline("</menubar>",-1,True)

EndFunction

Function p_menu2xml(id$, node)
;	DebugPrint("!xxxxx",node)
	   Local found, t = moai.DoMethod(id$, "GetEntry", node, "Head")
	   While found = True
		 If t.Node 
			If mnuhome
				p_xml_addline([[<menubar id="]]..t.items[0]..[[">]],1)
				prg_menubar$=t.items[0]
			Else
				p_xml_addline([[<menu title="]]..t.items[0]..[[" id="]]..t.id..[[">]],1)
				mnuon=mnuon+1
			EndIf
			mnuhome=False

		 Else
			p_menu_additemline(t.items)
			InsertItem(mnuoptions,t.items[1]);add item at the list of the menu options for event handler pressed
		 EndIf
		 If t.Node = True 
		    p_menu2xml(id$, t.uid)
				If mnuon>0
					p_xml_addline([[</menu>]],-1,True)
					mnuon=mnuon-1
				EndIf
		 EndIf
		 found, t = moai.DoMethod(id$, "GetEntry", t.uid, "Next")
	    Wend
EndFunction

Function p_menu_additemline(msg)
	Local bb=[[<item id="]]..msg[1]..
		IIf(msg[2]<>"",[[" shortcut="]]..msg[2],"")..
		IIf(msg[3]<>"",[[" help="]]..msg[3],"")..
		IIf(msg[4]<>"",[[" disabled="]]..msg[4],"")..
		[[">]]..msg[0]..[[</item>]]
	 p_xml_addline(bb)

EndFunction

Function p_xml_obj(linetxt$)
	;build the xml line for each obj listed with its parameters
;	DebugPrint((linetxt$))
	Local data,nd=SplitStr(linetxt$,",")
	If data[0]="window" Then Return
	Local aa=[[<]]..LowerStr(data[0])..[[ id="]]..data[1]..[[" ]]
	Local aab=""
	For Local k=5 To nd-1
		If data[k]<>"" 
		   Switch LowerStr(obj[data[0]].param[k-1])				
		   Case "text":
			aab=[[>]]..data[k]..[[</]]..LowerStr(data[0])..[[>]]
		   Case "align"
			aa=aa..obj[data[0]].param[k-1]..[[="]].. p_get_choice(data[0],k-1,data[k]+1)..[[" ]]
		   Case "mode"
			aa=aa..obj[data[0]].param[k-1]..[[="]].. p_get_choice(data[0],k-1,data[k]+1)..[[" ]]
		   Case "position"
			aa=aa..obj[data[0]].param[k-1]..[[="]].. p_get_choice(data[0],k-1,data[k]+1)..[[" ]]
		   Case "notify"
			notL=p_notify_list(data[0], k-1,data[k],data[1])
			aa=aa..IIf(StrLen(notL)>0,[[ notify="]]..NotL..[[" ]],"")
		   Case "iconpos"
			aa=aa..obj[data[0]].param[k-1]..[[="]].. p_get_choice(data[0],k-1,data[k]+1)..[[" ]]
		   Case "toggle"
			If  p_get_bolean(data[k])="true" Then aa=aa..obj[data[0]].param[k-1]..[[="]].. p_get_bolean(data[k])..[[" ]]
		   Case "horiz"
			If data[0]="progressbar"
				If  p_get_bolean(data[k])="false" Then aa=aa..obj[data[0]].param[k-1]..[[="]].. p_get_bolean(data[k])..[[" ]]
			Else
				If  p_get_bolean(data[k])="true" Then aa=aa..obj[data[0]].param[k-1]..[[="]].. p_get_bolean(data[k])..[[" ]]
			EndIf
		   Case "frame"
			If  p_get_bolean(data[k])="true" Then aa=aa..obj[data[0]].param[k-1]..[[="]].. p_get_bolean(data[k])..[[" ]]
		   Case "multiline"
			If  p_get_bolean(data[k])="true" Then aa=aa..obj[data[0]].param[k-1]..[[="]].. p_get_bolean(data[k])..[[" ]]
		   Case "selected"
			If  p_get_bolean(data[k])="true" Then aa=aa..obj[data[0]].param[k-1]..[[="]].. p_get_bolean(data[k])..[[" ]]			
		   Case "advanceoncr"
			If  p_get_bolean(data[k])="true" Then aa=aa..obj[data[0]].param[k-1]..[[="]].. p_get_bolean(data[k])..[[" ]]			
		   Case "styled"
			If  p_get_bolean(data[k])="true" Then aa=aa..obj[data[0]].param[k-1]..[[="]].. p_get_bolean(data[k])..[[" ]]			
		   Case "right"
			If  p_get_bolean(data[k])="true" Then aa=aa..obj[data[0]].param[k-1]..[[="]].. p_get_bolean(data[k])..[[" ]]			
		   Case "hrules"
			If  p_get_bolean(data[k])="true" Then aa=aa..obj[data[0]].param[k-1]..[[="]].. p_get_bolean(data[k])..[[" ]]			
		   Case "vrules"
			If  p_get_bolean(data[k])="true" Then aa=aa..obj[data[0]].param[k-1]..[[="]].. p_get_bolean(data[k])..[[" ]]			
		   Case "alternate"
			If  p_get_bolean(data[k])="true" Then aa=aa..obj[data[0]].param[k-1]..[[="]].. p_get_bolean(data[k])..[[" ]]			
		   Case "multiselect"
			If  p_get_bolean(data[k])="true" Then aa=aa..obj[data[0]].param[k-1]..[[="]].. p_get_bolean(data[k])..[[" ]]			
		   Case "nowrap"
			If  p_get_bolean(data[k])="true" Then aa=aa..obj[data[0]].param[k-1]..[[="]].. p_get_bolean(data[k])..[[" ]]			
		   Case "readonly"
			If  p_get_bolean(data[k])="true" Then aa=aa..obj[data[0]].param[k-1]..[[="]].. p_get_bolean(data[k])..[[" ]]	
		   Case "entries" 		
			If Not has_cat Then p_xml_add_item_list(data[k])			
		   Case "item"
			p_xml_add_item_list(data[k])
		   Case "title_lo_pages"
		   Case "title"
			If data[0]="pageview"	
			Else
				aa=aa..obj[data[0]].param[k-1]..[[="]]..data[k]..[[" ]]	
			EndIf	
			Case "macro"
				;do nothing
		   Default :
			If Not StartsWith(LowerStr(obj[data[0]].param[k-1]),"col_") Then aa=aa..obj[data[0]].param[k-1]..[[="]]..data[k]..[[" ]]	
		   EndSwitch
		EndIf
	Next
;	DebugPrint(">>>>>",data[0],aab)
	If StrLen(aab)>0
	   aa=aa..aab
	Else
	   If LowerStr(data[0])="combobox" Or LowerStr(data[0])="choice" Or LowerStr(data[0])="radio" 
		aa=aa..">"..lf$
		aa=aa..itn..p_indent(xml_indent)..[[</]]..LowerStr(data[0])..[[>]]..lf$
		itn=""
	   Else
		If LowerStr(data[0])="pageview" Or LowerStr(data[0])="listview"
			aa=aa..">"..lf$			
		ElseIf LowerStr(data[0])="busybar" Or LowerStr(data[0])="progressbar" Or LowerStr(data[0])="texteditor"
			aa=aa.."/>"..lf$
		ElseIf LowerStr(data[0])="htmlview"
			aa=aa.."/>"..lf$						
		Else	
			aa=aa..[[> </]]..LowerStr(data[0])..[[>]]..lf$
		EndIf
	   EndIf
	EndIf
	p_xml_addline(aa)
	If LeftStr(aa,9)="<pageview" And Val(data[3])<>0 And Val(data[2])<>0
;		DebugPrint(data[6],data[5])
;		p_xml_obj(p_get_linedata(data[1]))
		pagelist1,pln1=SplitStr(data[6],"|")
		pagetitles1,tn1=SplitStr(data[5],"|")
		If pln1<>tn1
			pp_error$="pageview "..data[1].." has different n. of pages and n. of titles" 
			Return 
		EndIf
		xml_indent=xml_indent+1
		For k1=0 To pln1-1
;			DebugPrint(pagetitles[k],PadNum(pagelist[k],2))	
			p_xml_addline([[<hgroup Title="]]..pagetitles1[k1]..[[" id="]]..data[1].."~P"..k1..[[">]],1)	
			p_pp_pagedata(PadNum(pagelist1[k1],2))
			p_xml_addline("</hgroup>",-1,True)
		Next
		p_xml_addline([[</pageview>]],-1,True)
	EndIf	
	If LeftStr(aa,9)="<listview" 
		Col_titles,lv1=SplitStr(data[10],"|")
		Col_Icons,lv2=SplitStr(data[11],"|")
		Col_Widths,lv3=SplitStr(data[12],"|")
		Col_Align,lv4=SplitStr(data[13],"|")
		Col_Checkbox,lv5=SplitStr(data[14],"|")
		Col_Editable,lv6=SplitStr(data[15],"|")
		Col_Hide,lv7=SplitStr(data[16],"|")
		Col_Sortable,lv8=SplitStr(data[17],"|")
		If lv1<>lv2 Or lv1<>lv3 Or lv1<>lv4 Or lv1<>lv5 Or lv1<>lv6 Or lv1<>lv7 Or lv1<>lv8
			pp_error$="listview "..data[1].." has incnsistent data on columns definition" 
			Return 
		EndIf
		xml_indent=xml_indent+1
		ii={"Left","Center","Right"}
		For k1=0 To lv1-1
			p_xml_addline([[<column ID="]]..data[1].."~col_"..k1..[[" title="]]..Col_titles[k1]..[["]]..
			IIf(Col_Icons[k1]<>"",[[ icon="]]..Col_Icons[k1]..[["]],"")..
			IIf(Col_Widths[k1]<>"",[[ Width="]]..Col_Widths[k1]..[["]],"")..
			IIf(Val(Col_Align[k1])>0,[[ Align="]]..ii[ToNumber(Col_Align[k1])]..[["]],"")..
			IIf(Val(Col_Checkbox[k1])>0,[[ Checkbox="]]..p_get_bolean(Col_Checkbox[k1])..[["]],"")..
			IIf(Val(Col_Editable[k1])>0,[[ Editable="]]..p_get_bolean(Col_Editable[k1])..[["]],"")..
			IIf(Val(Col_Hide[k1])>0,[[ Hide="]]..p_get_bolean(Col_Hide[k1])..[["]],"")..
			IIf(Val(Col_Sortable[k1])>0,[[ Sortable="]]..p_get_bolean(Col_Sortable[k1])..[["]],"")..[[/>]])
		Next
		ii=Nil
		p_xml_addline([[</listview>]],-1,True)		
	EndIf
EndFunction

Function p_xml_add_item_list(st$)
	bb,nl=SplitStr(st$,"|")
	itn=""
	xml_indent=xml_indent+1
	For z=0 To nl-1				
		itn=itn..p_indent(xml_indent)..[[<item>]]..bb[z]..[[</item>]]..lf$
	Next
	xml_indent=xml_indent-1

EndFunction

Function p_add_linen(txt$)
	;add the line number to the xml or hws pageview lines
	Local lines, L_n = SplitStr(txt$,lf$)	
	txt$=""
	For Local k=0 To L_n-1 Do txt$=txt$.. PadNum(k+1,4).." | "..lines[k]..lf$
	Return(txt$)
EndFunction


Function p_notify_list(objname, functpos, datastring, objid)
 ;   moai.Request(debug,objname.."   ".. functpos.."   "..  datastring.."   "..  objid , "OK")
	; add an obj ID to the list of the notifications to be handled in the HWS pp
	Local bb,np1=SplitStr(obj[objname].funct[functpos],"|")
	Local aa,np=SplitStr(datastring,"|")
	Local zz=""
	For Local u=0 To np-1
	    If ToNumber(aa[u])>0 
		zz=zz..bb[u+1]..";"	
		InsertItem(objnotify,{notfun=(bb[u+1]),Notobjid=objid})
	    EndIf
	Next
	If zz<>"" Then zz=UnleftStr(zz,1)
;	   DebugPrint(objname, zz,objid)
	Return(zz)	
EndFunction
	

Function p_notify_hws(func)
	;add the notification case block to hws pp
	    For Local zz=0 To TableItems(objnotify)-1
		If LowerStr(objnotify[zz].notfun)=func 
			p_hws_addline([[Case "]]..objnotify[zz].Notobjid..[[":]],1)
			p_hws_addline([[moai.Request("]]..prg_appname$..[[", "You selected notification for ]]..objnotify[zz].Notobjid..[[", "OK")]],-1)	
		EndIf
	    Next
	    objprocessed=objprocessed..func.."|"
EndFunction	


Function p_compact_IDs()
	;generate a table with ordered id's by page and row
	compactgrid={} ; table with n. of column per same row
	cgc,actrow,actpg = -1,"","00"
	;compact by row
	For kk=0 To TableItems(idsindexR)-1
		dtn0=SplitStr(idsindexR[kk],"|")   
;		DebugPrint( dtn0[0],dtn0[1],actpg,actrow  )
		   If  (dtn0[0])=actpg And (dtn0[1])=actrow ;compare row number
			compactgrid[cgc].c=compactgrid[cgc].c+1
			compactgrid[cgc].n=compactgrid[cgc].n.."|"..dtn0[3] ; add ID to the list of ID's on the same row and page
		   Else
			cgc=cgc+1
			compactgrid[cgc]={}
			compactgrid[cgc].r=(dtn0[1])
			compactgrid[cgc].c=1
			compactgrid[cgc].n=dtn0[3]
			compactgrid[cgc].pg=dtn0[0]
			actrow=compactgrid[cgc].r
			actpg=compactgrid[cgc].pg
		   EndIf		
	Next
	kk=Nil
	
	;compact by rows
	compactgrid1={}
	actcol,actpag,cgc=-1,"00",-1
	For kk=0 To TableItems(compactgrid)-1
;		DebugPrint(compactgrid[kk].pg,compactgrid[kk].r,compactgrid[kk].c,compactgrid[kk].n)
		   If  	compactgrid[kk].pg=actpag And compactgrid[kk].c=actcol ;compare col number
			compactgrid1[cgc].r=compactgrid1[cgc].r+compactgrid[kk].r
			compactgrid1[cgc].n=compactgrid1[cgc].n.."|"..compactgrid[kk].n
		   Else
			cgc=cgc+1
			compactgrid1[cgc]={}
			compactgrid1[cgc].p=compactgrid[kk].pg
			compactgrid1[cgc].r=compactgrid[kk].r
			compactgrid1[cgc].c=compactgrid[kk].c
			compactgrid1[cgc].n=compactgrid[kk].n
			actcol=compactgrid[kk].c
			actpag=compactgrid[kk].pg
;			DebugPrint("aa---->"..cgc,"--",compactgrid1[cgc].p,compactgrid1[cgc].r,compactgrid1[cgc].c,compactgrid1[cgc].n)
		   EndIf		
	Next
	kk=Nil	
EndFunction

Function p_pp_pagedata(pg)
	Local nline=1
	For Local uu=0 To TableItems(compactgrid1)-1
		If compactgrid1[uu].p=pg
			nline=nline+1
		EndIf
	Next
;	DebugPrint(pg,nline)
	If nline>1 Then p_xml_addline("<vgroup>",1)
	If nline=1
		pp_error$="error on page "..pg.." no items on page! Must have at least one"
		Return
	EndIf
	activepages=activepages..pg.."|"

	For Local uu=0 To TableItems(compactgrid1)-1
		Local txt$,l_n=SplitStr(compactgrid1[uu].n,"|")
;		DebugPrint("---->"..uu,"--",compactgrid1[uu].p,compactgrid1[uu].r,compactgrid1[uu].c,compactgrid1[uu].n,l_n)
;		DebugPrint("---->",compactgrid1[uu].p,pg)

		If compactgrid1[uu].p=pg
			If compactgrid1[uu].c>1 And compactgrid1[uu].c<>l_n	
				p_xml_addline([[<colgroup columns="]]..compactgrid1[uu].c..[[">]],1)
				For Local zz=0 To L_n-1 Do p_xml_obj(p_get_linedata(txt$[zz]))
				p_xml_addline("</colgroup>",-1,True)
			ElseIf compactgrid1[uu].c>1 And compactgrid1[uu].c=l_n
				p_xml_addline([[<hgroup>]],1)
				For Local zz=0 To L_n-1 Do p_xml_obj(p_get_linedata(txt$[zz]))
				p_xml_addline("</hgroup>",-1,True)
			Else
				For Local zz=0 To L_n-1 Do p_xml_obj(p_get_linedata(txt$[zz]))
			EndIf
		EndIf
	Next
	If nline>1 Then p_xml_addline("</vgroup>",-1,True)

	uu,zz=Nil,Nil
EndFunction

Function p_pp_progbar(progress, msg$)
	moai.Set("prgbar", "level", progress)
	moai.Set ("message","Text",msg$)	
EndFunction