;
; $VER: Malibu-Setup 1.5 (08.10.24)
;
; Done by Andreas Falkenhahn
;

(message (cat "\nWelcome to the Setup utility for\n\n"
	"Malibu 1.5\n\n"
	"- Scala Emulation For Your Workbench -\n\n"
	"Copyright (C) 2003-2024 by Andreas Falkenhahn\n\n"
	"http://www.hollywood-mal.com/"
))

(welcome)

(set basepath
	(askdir
		(prompt  "Where do you want to have Malibu installed? (a drawer will be created)")
		(help    @askdir-help)
		(default "SYS:")
	)
)

(set sstr (substr basepath (- (strlen basepath) 1) 1))
(if (or (= sstr "/") (= sstr ":"))
	(set destpath (cat basepath "Malibu"))
	(set destpath (cat basepath "/Malibu"))
)

(set defaultplat 0)

(if (exists "SYS:MorphOS/C/Assign") (
	(set defaultplat 3)
) (
	(if (exists "SYS:System/GrimReaper") (
		(set defaultplat 1)
	))
))					
	
(set whichver
	(askchoice
		(prompt  "Please select your platform")
		(help    @askoptions-help)
		(choices "AmigaOS 3 (m68k)" "AmigaOS 4 (ppc)" "AROS (x86)" "MorphOS (ppc)")		
		(default defaultplat)
	)
) 

(set #platform "m68k-amigaos")

(if (= whichver 1) (
	(set #platform "ppc-amigaos")
))

(if (= whichver 2) (
	(set #platform "x86-aros")
))

(if (= whichver 3) (
	(set #platform "ppc-morphos")
))

(set @default-dest destpath)

(makedir destpath)
(makedir "LIBS:Hollywood")

(copylib
	(source (cat "Platforms/" #platform "/malibu.hwp"))
	(dest   "LIBS:Hollywood")
	(prompt "malibu.hwp")
	(help @copylib-help)
	(optional "nofail" "force" "askuser")
	(confirm)
) 

(copyfiles
	(source (cat "Platforms/" #platform "/Prefs"))
	(dest   destpath)
	(optional force oknodelete)
)

(copyfiles
	(source  "Data.icon")
	(dest    basepath)
	(newname "Malibu.info")
	(optional force oknodelete)
)

(copyfiles
	(source "Data")
	(dest   destpath)
	(all)
	(optional "nofail" "force" "askuser")
)

(set #cmdline (cat "Assign Malibu: \"" destpath "\""))

(startup
	(prompt  "\nAn assign has to be inserted in your user-startup. Please click Continue.")
	(help    @startup-help)
	"Malibu"
	(command #cmdline)
	(confirm)
)

(run #cmdline)

(message (cat "\nMalibu has been successfully installed on your hard-disk. "
	"Hollywood is now able to display your Scala presentations. You can "
	"start them using the Hollywood GUI or also from the CLI. You can "
	"configure Malibu's Scala emulation by selecting the menu item \"Plugins\" "
	"in the Hollywood GUI and then \"Malibu settings\". For more information "
	"please see the documentation, that is also accessible through the "
	"Hollywood GUI."))
			
(exit (quiet))
