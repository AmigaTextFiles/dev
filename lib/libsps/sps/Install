; *** lipSPS++ Installation Script ***

(transcript "Installing the libsps++...")

(set #About
    (cat "libSPS++ installation\n\n"
         "An AmigaOS4 C++ Multi-Threading Library\n"
         "Version 1.0\n\n"
         "©2006 Jürgen Schober\n"
         "e-mail : juergen@cox.net")
)

(set #AskIncludeDirMsg
    (cat "Select the directory where the header files\n"
	     "should be stored (sdk:Local/common/include/)")
)

(set #AskLibDirMsg
    (cat "Select the directory where the link library\n"
	     "should be stored (sdk:Local/newlib/lib)")
)

(set #AskSourceDirMsg
    (cat "Select the directory to store the source and documentation.\n"
         "A directory 'sps' will be created.")
)

(set #EndMsg 
    (cat "\nBefore the Driver could be use the default setup must be set !\n"
         "\n Press Proceed to run the A2410Prefs !\n"
         "\nWhich version should I run ?\n")
)

(message #About)

; ---------------------------------------------
; Install link lib

(set @dest "sdk:local/newlib/lib")
(set @dest (expandpath @dest))
(set destination 
    (askdir
        (prompt #AskLibDirMsg)
        (help @askdir-help)
        (default @dest)
    )
)
(set @libdir destination)

(copyfiles
    (prompt "Installing link library...")
	(source "lib/libsps++.a")
    (dest destination)
	(infos)
    (help @copyfiles-help)
)

(complete 20)

; ---------------------------------------------
; Install includes

(set @dest "sdk:local/common/include")
(set @dest (expandpath @dest))
(set destination
    (askdir
        (prompt #AskIncludeDirMsg)
        (help @askdir-help)
        (default @dest)
    )
)
(set @incdir destination)

(copyfiles
    (prompt "Installing header files...")
	(source "include/")
    (dest destination)
    (all)
	(infos)
    (help @copyfiles-help)
)

(complete 40)

; ---------------------------------------------
; Install examples, source and documentation

(set @dest "sdk:examples")
(set @dest (expandpath @dest))
(set destination
    (cat
        (askdir
            (prompt #AskSourceDirMsg)
            (help @askdir-help)
            (default @dest)
        )
        "/sps"
    )
)
(set @srcdir destination)

(makedir destination (infos))
(makedir (cat destination "/obj"))
(makedir (cat destination "/lib"))

(copyfiles
    (prompt "Installing source files")
    (source "")
    (dest destination)
    (pattern "#?.cpp")
	(infos)
    (help @copyfiles-help)
)

(complete 55)

(copyfiles
    (prompt "Installing Makefile and Doxyfile")
    (source "")
    (dest destination)
    (choices "Makefile" "Doxyfile")
	(infos)
    (help @copyfiles-help)
)

(complete 65)

(copyfiles
    (prompt "Installing example code")
	(source "examples")
    (dest (cat destination "/examples"))
    (all)
	(infos)
    (help @copyfiles-help)
)

(complete 75)

(copyfiles
    (prompt "Installing documentation")
	(source "docs")
    (dest (cat destination "/docs"))
    (all)
	(infos)
    (help @copyfiles-help)
)

(complete 100)

(set @default-dest @srcdir)
(exit)

