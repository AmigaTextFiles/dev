/*
 * dictmerge
 * by megacz
 *
 * This ARexx script allows to merge all the dictionaries in
 * current directory. It also tracks alteration dates of the
 * input files so it will not attempt to recreate the output
 * over and over on each launch.
*/

library = "rexxsupport.library"
if show("l", library) = 0 then do
  addlib = addlib(library, 0, -30, 0)
end
signal on halt
signal on break_c
options failat 21
parse arg out
if out ~="" then do
  files = upper(showdir("", "files"))
  ofiles = ""
  gen = 0
  if files ~="" then do
    words = words(files)
    do cword=1 to words
      word = word(files, cword)
      if pos("DICT_", word) > 0 & pos(".TXT", word) > 0 then do
        ofiles = ofiles||" "||word
        fileinfo = statef(word)
        parse var fileinfo a b c d t1 t2 t3
        time = compress(t1||t2||t3)
        envvar = getclip(word)
        if envvar~="" then do
          if time ~= envvar then do
            setclip(word, time)
            gen = 1
          end
        end
        else do
          setclip(word, time)
          gen = 1
        end
      end
    end
    if open(ifd, out, "r") > 0 then do
      close(ifd)
    end
    else do
      gen = 1
    end
    if gen > 0 then do
      say " /// attempting to build one big dictionary file..."
      if open(ofd, out, "w") > 0 then do
        words = words(ofiles)
        do cword=1 to words
          word = word(ofiles, cword)
          if open(ifd, word, "r") > 0 then do
            do while ~eof(ifd)
              if writech(ofd, readch(ifd, 16384)) < 0 then do
                say " *** error, cannot write to the media!"
                exit 10
              end
            end
            close(ifd)
          end
          else do
            say " *** error, cannot open input file = '"|| word ||"'!"
            exit 10
          end
        end
        close(ofd)
      end
      else do
        say " *** error, cannot open output file = '"|| out ||"'!"
        exit 10
      end
    end
  end
end
else do
  say " *** template: dictmerge <output/a>"
end

halt:
break_c:
exit
