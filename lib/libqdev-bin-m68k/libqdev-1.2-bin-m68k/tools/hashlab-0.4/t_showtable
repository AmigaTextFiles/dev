/*
 * showtable
 * by megacz
 *
 * This ARexx script allows to generate comparison table to clarify
 * research results as done with 'research'.
 *
*/

library = "rexxsupport.library"
if show("l", library) = 0 then do
  addlib = addlib(library, 0, -30, 0)
end
signal on halt
signal on break_c
options failat 21
parse arg in
if in ~="" then do
  if open(ifd, in, "r") > 0 then do
    cnt = 0
    omod  = ""
    file  = ""
    lines = ""
    ltime = ""
    colls = ""
    wtime = ""
    say "--------------------------------------------------------------------------------"
    say "|  mod. name  |  dict. name  | dict. size | h. time |  colls  | detection time |"
    say "--------------------------------------------------------------------------------"
    do until eof(ifd)
      line = readln(ifd)
      parse var line " /// module '" module "'"
      if module ~= "" | eof(ifd) then do
        if omod ~= "" & file ~= "" then do
          cnt = cnt + 1
          say "| "|| RIGHT(omod, 11) ||" | "|| RIGHT(file, 12) ||" | "|| RIGHT(lines, 10) ||" | "|| RIGHT(ltime, 7) ||" | "|| RIGHT(colls, 7) ||" | "|| RIGHT(wtime, 14) ||" |"
        end
        omod  = module
        file  = ""
        lines = ""
        ltime = ""
        colls = ""
        wtime = ""
      end
      else do
        if file = "" then do
          parse var line "/// attempting to load '" file "'"
        end
        if lines = "" then do
          parse var line " /// file has " lines " "
        end
        if ltime = "" then do
          parse var line " /// data set was loaded and hashed in " ltime " "
        end
        if colls = "" then do
          parse var line " , cl: " colls " "
          if colls > 65536 then do
            colls = "> 65536"
          end
        end
        if wtime = "" then do
          parse var line " /// the CPU was tortured for " days " " word ", " time " "
          if days ~= "" & time ~= "" then do
            wtime = days||":"||time
          end
        end
      end
    end
    if cnt > 0 then do
      say "--------------------------------------------------------------------------------"
    end
    close(ifd)
  end
  else do
    say " *** error, cannot open input file = '"|| in ||"'!"
    exit 10
  end
end
else do
  say " *** template: showtable <input/a>"
end

halt:
break_c:
exit
