; libxmi install (c) Chris Young 2009

(procedure p_failedsobjs
	(if #failedsobjs
		(message "The following shared objects failed to copy.  These will be updated on next reboot.\n\n" #failedsobjs)
	)
)

(procedure p_schedulesobj #sobj
	(transcript "Scheduling update of " #sobj " for next reboot")

	(set #failedsobjs (cat #failedsobjs "\n" #sobj))

	(makedir "SObjs:so-installer")

	(copyfiles
		(source (tackon "SObjs" #sobj))
		(dest "SObjs:so-installer")
	)

	(startup "Shared object installer"
		(prompt "Adding commands to user-startup to schedule copy of " #sobj " on next reboot")
		(help @startup-help)
		(command "if EXISTS SObjs:so-installer\n")
		(command "  copy SObjs:so-installer SObjs: CLONE FORCE QUIET\n")
		(command "  delete SObjs:so-installer ALL FORCE QUIET\n")
		(command "endif\n")
	)
)

(procedure p_comparesobj #sobj
	(set #same 0)
	(set #sobj-version (getversion (tackon ".libs/" #sobj)))
	(if (= #sobj-version 0)
		(
			(if (exists (tackon "SObjs:" #sobj))
				(
					(set #file-newer (earlier (tackon "SObjs:" #sobj) (tackon ".libs/" #sobj)))
					(set #old-size (getsize (tackon "SObjs:" #sobj)))
					(set #new-size (getsize (tackon ".libs/" #sobj)))

					(if (AND (= #old-size #new-size) (= #file-newer 0)) (set #same 1))
				)
			)
		)
		; else if version info is available
		(
			(set #sobj-oldversion (getversion (tackon "SObjs:" #sobj)))

			(if (<= #sobj-version #sobj-oldversion) (set #same 1))
		)
	)
)

(procedure p_copysobj #sobj
	(set #sobj-version (getversion (tackon ".libs/" #sobj)))

	(if (= #sobj-version 0)
		(
			(transcript "Date compare " #sobj)

			(if (exists (tackon "SObjs:" #sobj))
				(
					(set #file-newer (earlier (tackon "SObjs:" #sobj) (tackon ".libs/" #sobj)))

					(if (= #file-newer 0)
						(
							(set #newer-text "has an older datestamp")
						)
						;else
						(
							(set #newer-text "has a newer datestamp")
						)
					)

					(set #old-size (getsize (tackon "SObjs:" #sobj)))
					(set #new-size (getsize (tackon ".libs/" #sobj)))

					(if (AND (= #old-size #new-size) (= #file-newer 0))
						(
							(set #copy 0)
						)
						;else
						(
							(if (OR (= @user-level 2) (AND (= @user-level 1) (<> #file-newer 0)))
; Expert users are always prompted
; Average users are prompted if the file trying to be copied is newer
; Novice users are never prompted
; This is roughly equivalent to (copylib (confirm))
; No prompting occurs if the destination does not exist (silent copy)
; or the files are the same size and the one being copied isn't newer (don't copy)
								(
									(set #copy
										(askbool
											(prompt "Copying " #sobj "...\n\n"
												"Version to install: " #new-size " bytes\n"
												"Version currently installed: " #old-size " bytes\n\n"
												"The file to copy " #newer-text)
											(help @askbool-help)
											(default #file-newer)
											(choices "Proceed with copy" "Skip this part")
										)
									)
								)
								;else
								(
									(set #copy #file-newer)
								)
							)
						)
					)
				)
				; else if dest file does not exist
				(
					(set #copy 1)
				)
			)

			(if (<> #copy 0)
				(
					(if (<> #AutoInstall 1)
						(
							(copyfiles
								(prompt "Copying " #sobj "...")
								(help @copyfiles-help)
								(source (tackon ".libs/" #sobj))
								(dest "SObjs:")
								(optional "nofail" "force")
							)
						)
						;else
						(
							(run "CopyStore .libs/" #sobj " SObjs:")
						)
					)

					(p_comparesobj #sobj)
					(if (= #same 0)
						(
							(p_schedulesobj #sobj)
						)
					)
				)
			)
		)
		; else if version info is available
		(
			(if (<> #AutoInstall 1)
				(
					(copylib
						(prompt "Copying " #sobj "...")
						(help @copylib-help)
						(source (tackon ".libs/" #sobj))
						(dest "SObjs:")
						(optional "nofail" "force")
						(confirm "expert")
					)
				)
				;else
				(
					(run "CopyStore .libs/" #sobj " SObjs:")
				)
			)

			(p_comparesobj #sobj)
			(if (= #same 0)
				(
					(p_schedulesobj #sobj)
				)
			)
		)
	)
)


(welcome)

(set @default-dest "SObjs:")

(p_copysobj "libxmi.so")

(if (exists "SDK:" (noreq))
	(
		(copyfiles
			(prompt "Copying libxmi.a...")
			(help @copyfiles-help)
			(source ".libs/libxmi.a")
			(dest "SDK:local/newlib/lib/")
			(optional "nofail" "force")
		)

		(copyfiles
			(prompt "Copying xmi.h...")
			(help @copyfiles-help)
			(source "xmi.h")
			(dest "SDK:local/newlib/include/")
			(optional "nofail" "force")
		)

		(copyfiles
			(prompt "Copying libxmi.guide...")
			(help @copyfiles-help)
			(source "libxmi.guide")
			(dest "SDK:local/documentation/")
			(infos)
			(optional "nofail" "force")
		)

		(run "makelink sdk:local/newlib/lib/libxmi.so sobjs:libxmi.so soft")
	)
)


