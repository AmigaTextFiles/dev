; Installer.script 2-3-95
; Mods by Yannick Koeller


;DATE Feb 5, 1995

FAILAT 21

SET NOMUI "false"
SET NEWDIRECTORY "false"          

;======= CHECK THE VERSION OF MUIMASTER.LIBRARY ========

VERSION >nil: libs:muimaster.library VERSION=8
IF WARN
  ECHO "=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-="" 
  ECHO "" 
  ECHO "                   ERROR:" 
  ECHO "" 
  ECHO "The muimaster.library must be Version 8 or greater."
  ECHO "MUI can be found on AmiNet as dev/gui/mui23usr.lha." 
  ECHO ""
  ECHO "You may continue the installation, even without MUI."
  ECHO "This will allow you to use the DOSReqTools package."
  ECHO ""
  SET NOMUI "true"
ENDIF

;============== BEGIN ==================================

`CD`/DOSReqTools/rtEZrequest "MUInt_Dev_v1" "This program will install DOSReqTools and MUInt." "Begin|Cancel" 
SET Result $RC

IF $Result eq 0
 SKIP ABORT
ENDIF

;=========WHERE DO WE PUT THE EXECUTABLES==============

DOSreqtools/rtGetString "Install the Executables" "Please enter the directory where the executables will be copied.\n\nWe suggest SYS:c but you can place them\nin any directory that is in the path." "OK|Make New Dir|Cancel" default "SYS:c"  TO ENV:target 
SET Result $RC

IF $Result eq 0
 SKIP ABORT
ENDIF

IF $Result eq 2
  DOSreqtools/rtGetString "Install the Executables" "Enter the path and name of the new directory.\nYou will have to ADD this directory to the PATH\nbefore usihng this package." "OK|Cancel" default "SYS:c"  TO ENV:target 
  SET Result $RC

  IF $Result eq 0
    SKIP ABORT
  ENDIF

  IF NOT EXISTS "$target"
    MAKEDIR $Target

    ECHO >ram:xabcd "PATH $Target ADD"
    EXECUTE ram:xabcd 
    DELETE ram:xabcd QUIET

    SET NEWDIRECTORY "true"
  ENDIF
ENDIF


;============ SET THE PATH TO THE GUIDE DOCS =============
;============ SO RTMAN & MAN.DEF CAN FIND THEM ===========

ECHO >ENV:DOSRTPATH `CD`/DOSReqTools
ECHO >ENV:MUINTPATH `CD`/MUInt

COPY ENV:DOSRTPATH TO SYS:Prefs/Env-Archive QUIET
COPY ENV:MUINTPATH TO SYS:Prefs/Env-Archive QUIET

;========== COPY THE DRT EXECUTABLES TO $TARGET ==========

COPY "$DOSRTPATH/rt#?" to "$Target"          CLONE QUIET
COPY "$DOSRTPATH/DOSExtensions" to "$Target" CLONE QUIET

;======== COPY THE MUINT EXECUTABLES TO $TARGET ==========

COPY "$MUINTPATH/MUInt" to "$Target"           CLONE QUIET
COPY "$MUINTPATH/TextWin/TextWin" to "$Target" CLONE QUIET
COPY "$MUINTPATH/Advice/Advice" to "$Target"   CLONE QUIET
COPY "$MUINTPATH/Extensions" to "$Target"      CLONE QUIET

IF EXISTS `CD`/C
  COPY `CD`/C to "$Target" CLONE QUIET
ENDIF    


PROTECT "$Target/rtman" +s

;================= MAKE THE UNINSTALL FILE ===============

LIST  >ram:BOTH.uninstall.list "$DOSRTPATH/rt#?" LFORMAT "%f%n"       
LIST >>ram:BOTH.uninstall.list "$DOSRTPATH/DOSExtensions" LFORMAT "%f%n"       
LIST >>ram:BOTH.uninstall.list "$MUINTPATH/MUInt" LFORMAT "%f%n"       
LIST >>ram:BOTH.uninstall.list "$MUINTPATH/TextWin/TextWin" LFORMAT "%f%n"       
LIST >>ram:BOTH.uninstall.list "$MUINTPATH/Advice/Advice" LFORMAT "%f%n"       
LIST >>ram:BOTH.uninstall.list "$MUINTPATH/Extensions" LFORMAT "%f%n"       

IF EXISTS `CD`/C
  LIST >>ram:BOTH.uninstall.list `CD`/C LFORMAT "%f%n"       
ENDIF    

COPY ram:BOTH.uninstall.list  TO `CD`

;===================== CLEANUP AND EXIT ===================


IF EXISTS ENV:TARGET
     DELETE ENV:TARGET QUIET
ENDIF


IF $NEWDIRECTORY EQ "true"
  $DOSRTPATH/rtEZrequest "Installation Status" "You have put the executables in a directory - $Target\nthat is not in the PATH.\n\nA temporary PATH ADD has been made so that you\ncan use the programs until you reboot." "_Continue" 
ENDIF


IF $NOMUI EQ "true"
  $DOSRTPATH/rtEZrequest "Installation Status" "Until you get MUI installed, you can't use any\nof the MUInt programs." "_Continue" 
ENDIF


$DOSRTPATH/rtEZrequest "Installation Status" "Installation Complete\nThe executables are in $target\nThe DOSrt.guide and Demo scripts are in $DOSRTPATH\nMUInt.guide is in $MUINTPATH" "_Continue" 

QUIT




LAB ABORT

`CD`/DOSReqTools/rtezrequest "Information" BODY "The installation has been aborted at your request.\nThank you and have a nice day, dude." BUTTONTEXT "_Quit" 
QUIT