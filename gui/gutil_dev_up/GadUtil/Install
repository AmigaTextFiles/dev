;--------------------------- Current version.revision ----------------------
(set vernum (getversion "libs:gadutil.library"))

(set thisvernum (getversion "libs/gadutil.library"))
(set thisver (/ thisvernum 65536))
(set thisrev (- thisvernum (* thisver 65536)))

;-------------------------------- Debugging... -----------------------------
;(set thisver 36)
;(set thisrev 54)
;(set thisvernum (+ thisrev (* thisver 65536)))
;(set @default-dest "ram:")

;---------------------- All strings in the script --------------------------

(set #install-msg		"Installing GadUtil.library...")

(set #info-msg 	(cat	"\n\nGadUtil installation script.\n"
						("This script installs GadUtil %ld.%ld on your system.\n\n" thisver thisrev)
						"Read the docs for more information on the "
						"distribution and for use in commercial or "
						"other non-free programs.\n\n"
						"GadUtil is Copyright © 1994-1996 by\n"
						"P-O Yliniemi and Staffan Hämälä.\n"
						"All rights reserved."))

(set #usertype-msg (cat "\n\nTo make it as easy as possible to install this "
						"product on your computer, you have now the chance "
						"to select what 'kind' of user you class yourself as. "
						"When you have done this selection, the installation "
						"process will go much smoother, since you may not have "
						"to change too much of the presets.\n\n"
						"Don't overuse this function, since you'll wear out "
						"your mouse buttons if you select that you are a pro, "
						"if you actually are a lamer!!!"))

(set #selusertype-msg	"Select the user type closest to yourself")
(set #usertype1-msg		"Minimal installation (library only)")
(set #usertype2-msg		'"Normal" user (library + some docs)')
(set #usertype3-msg		"Assembly language programmer")
(set #usertype4-msg		"'C' language programmer")
(set #usertype5-msg		"Assembly & 'C' language programmer")
(set #usertype6-msg		"GadUtil library author / ßeta tester")

(set #whatinstall-msg	"Which part(s) do you want to install?")
(set #userpart-msg		"User installation...")
(set #develpart-msg		"Developer installation...")

(set #useroptions-msg	"Select which user file(s) to install")
(set #useroption1-msg	"The library file (replaces old version)")
(set #useroption2-msg	"User documentation")
(set #useroption3-msg	"Aminet .readme file")
(set #useroption4-msg	"Snapshot pictures of some programs (GIF)")

(set #developtions-msg	"Select which development file(s) to install")
(set #develoption1-msg	"Developer documentation...")
(set #develoption2-msg	"Include files...")
(set #develoption3-msg	"Example programs...")
(set #develoption4-msg	"Catalog creation utilities...")
(set #develoption5-msg	"Extra commands...")
(set #develoption6-msg	"Other...")

(set #develdocs-msg		"Select which document(s) to install")
(set #develdocs1-msg	"Autodocs (text format)")
(set #develdocs2-msg	"Autodocs (AmigaGuide format)")
(set #develdocs3-msg	"Autodocs for private functions")
(set #develdocs4-msg	"Development history (English)")
(set #develdocs5-msg	"Development history (Swedish)")
(set #develdocs6-msg	"List with all GadTools tags")
(set #convertdocs-msg	"Converting Autodocs to AmigaGuide")

(set #develinc-msg		"Select which include file(s) to install")
(set #develinc1-msg		"C-language includes...")
(set #develinc2-msg		"Assembly language includes...")
(set #develinc3-msg		"LVOs.i (needed for Assembly examples)")
(set #develinc4-msg		"FD files (Basic...)")

(set #develcinc-msg		"Create pragma files for...")
(set #develcinc1-msg	"SAS-C")
(set #develcinc2-msg	"DICE-C")
(set #develcinc3-msg	"Aztec-C")
(set #develcinc4-msg	"Maxon-C")
(set #develcinc5-msg	"StormC")

(set #develasminc-msg	"Create Assembly function files for...")
(set #develasminc1-msg	"Misc Assemblers (LIBINIT, LIBDEF macros)")
(set #develasminc2-msg	"Aztec Asm (XDEF, EQU)")

(set #develex-msg		"Select which example(s) to install")
(set #develex1-msg		"Demonstration of a bug in GadTools (Asm)")
(set #develex2-msg		"Starter project for localized prog (Asm)")
(set #develex3-msg		"Test program for GadTools hotkeys, loc (Asm)")
(set #develex4-msg		"Bevelbox examples (Asm)")
(set #develex5-msg		"Code to change internal test to external (Asm)")
(set #develex6-msg		"Test program for listview functions (Asm)")
(set #develexC-msg		"AmigaGuide online help example (Asm)")
(set #develexF-msg		"Use of GU_FractWidth and GU_FractHeight (Asm)")

(set #develex7-msg		"The very first external test program (Asm)")
(set #develex8-msg		"The first internal test program, loc (Asm)")
(set #develex9-msg		"Three old test programs (Asm)")
(set #develexA-msg		"Three gadgets with border around them (Asm)")
(set #develexB-msg		"Test program for GadTools hotkeys, loc (C)")
(set #develexD-msg		"Demo programs by other programmers (C)")
(set #develexE-msg		"Demo programs by other programmers (Asm)")

(set #develcat-msg		"Select which catalog creator(s) to install")
(set #develcat1-msg		"FlexCat 1.4")
(set #develcat2-msg		"English documentation for FlexCat 1.4")
(set #develcat3-msg		"Quick usage instructions for FlexCat 1.4")
(set #develcat4-msg		"C source description file (Lattice/SAS-C)")
(set #develcat5-msg		"Asm source description file (Asm-One 1.25)")

(set #develcmd-msg		"Select which extra command(s) to install")
(set #develcmd1-msg		"AskEnv 2.5 (Create GUI's for DOS scripts)")
(set #develcmd2-msg		"FD2Pragma (Create C pragma files from .FD)")
(set #develcmd3-msg		"XIcon 2.5 (run DOS scripts from Workbench)")
(set #develcmd4-msg		"SCompare & SPatch")

(set #develoth-msg		"Other thing(s) you may want to install")
(set #develoth1-msg		"Debug version of library (gadutil.library_dbg)")
(set #develoth2-msg		"Include file editor/installer/remover")
(set #develoth3-msg		"Distribution archiver")
(set #develoth4-msg		"The Installer script")
(set #develoth5-msg		"The library's source code :-)")
(set #develoth6-msg		"Unfinished library routines")

(set #maindir-msg  (cat	"Where do you wish to install GadUtil related\n"
						"files? The Installer will NOT create a new\n"
						"drawer at the selected location."))

(set #sellocfor-msg		"Please select a location for")
(set #docdir-msg		("%s all documentation" #sellocfor-msg))
(set #cincdir-msg		("%s 'C' include files" #sellocfor-msg))
(set #asmincdir-msg		("%s Assembly include files" #sellocfor-msg))
(set #LVOSidir-msg		("%s the LVOs.i file" #sellocfor-msg))
(set #FDdir-msg			("%s the .FD file" #sellocfor-msg))
(set #exampledir-msg	("%s example sources" #sellocfor-msg))
(set #catutdir-msg		("%s catalog utilities" #sellocfor-msg))
(set #extcmddir-msg		("%s extra commands" #sellocfor-msg))

(set #StormCDict-msg (cat "Do you want to append GadUtil function names, "
						"structure offsets and types to your StormC "
						"dictionary files (names will be appended to "
						"Amiga Functions.dic and Amiga Types.dic) ?"))

(set #debuglib-msg (cat "\nYou have selected to install another version\n"
						"of GadUtil.library, that contains some extra\n"
						"code for debugging. This version can be used\n"
						"to trace problems while starting up applications\n"
						"that uses gadutil.library.\n\n"
						"To use the 'debug' version, rename the original\n"
						"library to something else, and rename the 'debug'\n"
						"version to 'gadutil.library'. Type 'avail flush'\n"
						"in Shell to remove the normal library from memory.\n"
						"You may have to reboot to remove the library from\n"
						"memory. Start the program and read...\n"))

(set #libinst-msg		"Installing gadutil.library.")
(set #debuglibinst-msg	"Installing gadutil.library (debug version).")

(set #obtainsrc-msg	(cat "\nIt seems like you're not a developer of this\n"
						"project. If you really need the source for this\n"
						"library, send both authors of the project at least\n"
						"$1.000.000 or anything equal in another currency.\n\n"
						"Don't forget to supply an empty diskette, and a SAE\n"
						"with stamps enough to cover postage expenses.\n\n"
						"Per-Olof Yliniemi            Staffan Hämälä\n"
						"Aspvägen 4E                  Vegagatan 2 3TR\n"
						"SE-957 32 Övertorneå         SE-172 34 Sundbyberg\n"
						"SWEDEN                       SWEDEN        \n"))

;----------- Copy source file to destination w/ version check --------------
;
; Inputs:	.file1 - The file to install (source file)
;			.file2 - The destination directory
;
(procedure safecopy
	(set .vernum1 (getversion .file1))
	(set .ver1 (/ .vernum1 65536))
	(set .rev1 (- .vernum1 (* .ver1 65536)))

	(set .vernum2 (getversion (tackon .file2 (fileonly .file1))))
	(set .ver2 (/ .vernum2 65536))
	(set .rev2 (- .vernum2 (* .ver2 65536)))

	(if (<> .vernum1 .vernum2)
	(if (> .vernum1 .vernum2)
		(copyfiles
			(source .file1) (dest .file2)
		)

		( (if (= @user-level 2)
			(if (askbool
				(prompt (cat ("Installing %s.\n\n" (fileonly .file1))
					("Version to install: %ld.%ld\n" .ver1 .rev1)
					("Version currently installed: %ld.%ld\n\n" .ver2 .rev2)
					 "Destination Drawer to Copy to:\n"
					 .file2))
				(choices "Proceed with Copy" "Skip This Part")
				(default 0)
				(help	(cat "The Installer has detected that a later version\n"
					"of the file that is going to be copied already\n"
					"exists at the selected destination.\n\n"
					"Select \"Proceed with Copy\" to replace the newer\n"
					"file with an older one. Select \"Skip This Part\"\n"
					"to keep the installed version.\n"))
				)
				(copyfiles
					(source .file1) (dest .file2)
				)
			)
		))
	))
)

;-------------------- Installation script begins here ----------------------
(if (exists "env:sys/def_doc.info")
	(set def_doc_icon "env:sys/def_doc.info")
	(set def_doc_icon "icons/def_doc.info")
)

(if (exists "env:sys/def_guide.info")
	(set def_guide_icon "env:sys/def_guide.info")
	(set def_guide_icon "icons/def_doc.info")
)

(set olduserlevel @user-level)
(user 2)
(transcript #install-msg)
(message #info-msg)
(message #usertype-msg)

;--------------------------------- Get user type ---------------------------
(if (= olduserlevel 0)
	(set usertype
		(askchoice
			(prompt #selusertype-msg)
			(help @askchoice-help)
			(choices	#usertype1-msg
						#usertype2-msg
			)
		)
	)

	(set usertype
		(askchoice
			(prompt #selusertype-msg)
			(help @askchoice-help)
			(choices	#usertype1-msg
						#usertype2-msg
						#usertype3-msg
						#usertype4-msg
						#usertype5-msg
						#usertype6-msg
			)
		)
	)
)
;------------------ Set default settings for user-types --------------------

(set def_installtype (select usertype  %01  %01  %11  %11  %11  %11))
(set def_userinstall (select usertype %0001 %1111 %1111 %1111 %1111 %1111))
(set def_develinstall (select usertype  %000000 %000000 %111111
										%111111 %111111 %111111))
(set def_develdocs (select usertype %000000 %000000 %001010
									%001010 %001010 %111111))
(set def_develinc (select usertype %0000 %0000 %0110 %1001 %1111 %1111))
(set def_develex1 (select usertype	%00000000 %00000000 %11101110
									%00000000 %11101110 %11111111))
(set def_develex2 (select usertype %0000000 %0000000 %1001000
									%0110000 %1111000 %1111111))

(set def_develcat (select usertype %00000 %00000 %10111 %01111 %11111 %11111))
(set def_develcmd (select usertype %0000 %0000 %1001 %1011 %1011 %1111))
(set def_develoth (select usertype	%000000 %000000 %000011
									%000011 %000011 %001111))

(user olduserlevel)
;--------------------------- Check type of installation --------------------
(set installtype
	(askoptions
		(prompt #whatinstall-msg)
		(help	@askoptions-help)
		(choices	#userpart-msg
					#develpart-msg
		)
		(default def_installtype)
;		(default (- 2 (> thisvernum vernum)))
	)
)

(if (= installtype 0)
	(message "So, you didn't want to install GadUtil yet...\nLater maybe???")
)

;-------------------------- Get options for user install -------------------
(if (IN installtype 0)
	(set userinstall
		(askoptions
			(prompt #useroptions-msg)
			(help	@askoptions-help)
			(choices	#useroption1-msg
						#useroption2-msg
						#useroption3-msg
						#useroption4-msg
			)
			(default def_userinstall)
		)
	)
)

;---------------------- Get options for developer install ------------------
(if (IN installtype 1)
	(set develinstall
		(askoptions
			(prompt #developtions-msg)
			(help	@askoptions-help)
			(choices	#develoption1-msg
						#develoption2-msg
						#develoption3-msg
						#develoption4-msg
						#develoption5-msg
						#develoption6-msg
			)
			(default def_develinstall)
		)
	)
)

;-------------------- Check which devel docs to install --------------------
(if (IN develinstall 0)
	(set develdocs
		(askoptions
			(prompt #develdocs-msg)
			(help	@askoptions-help)
			(choices	#develdocs1-msg
						#develdocs2-msg
						#develdocs3-msg
						#develdocs4-msg
						#develdocs5-msg
						#develdocs6-msg
			)
			(default def_develdocs)
		)
	)
)

;--------------------- Check which includes to install ---------------------
(if (IN develinstall 1)
	(set develinc
		(askoptions
			(prompt #develinc-msg)
			(help	@askoptions-help)
			(choices	#develinc1-msg
						#develinc2-msg
						#develinc3-msg
						#develinc4-msg
			)
			(default def_develinc)
		)
	)
)

;-------------------- Check which C includes to create ---------------------
(if (IN develinc 0)
	(set cinc
		(askchoice
			(prompt #develcinc-msg)
			(help	@askchoice-help)
			(choices	#develcinc1-msg
						#develcinc2-msg
						#develcinc3-msg
						#develcinc4-msg
						#develcinc5-msg
			)
		)
	)
)

;------------------- Check which Asm includes to create --------------------
(if (IN develinc 1)
	(set asminc
		(askchoice
			(prompt #develasminc-msg)
			(help	@askchoice-help)
			(choices	#develasminc1-msg
						#develasminc2-msg
			)
		)
	)
)

;-------------------- Check which examples to install ----------------------
(if (IN develinstall 2)(
	(set develex1
		(askoptions
			(prompt #develex-msg)
			(help	@askoptions-help)
			(choices	#develex1-msg
						#develex2-msg
						#develex3-msg
						#develex4-msg
						#develex5-msg
						#develex6-msg
						#develexC-msg
						#develexF-msg
			)
			(default def_develex1)
		)
	)

	(set develex2
		(askoptions
			(prompt #develex-msg)
			(help	@askoptions-help)
			(choices	#develex7-msg
						#develex8-msg
						#develex9-msg
						#develexA-msg
						#develexB-msg
						#develexD-msg
						#develexE-msg
			)
			(default def_develex2)
		)
	)
))

;---------------- Check which catalog utilities to install -----------------
(if (IN develinstall 3)
	(set develcat
		(askoptions
			(prompt #develcat-msg)
			(help	@askoptions-help)
			(choices	#develcat1-msg
						#develcat2-msg
						#develcat3-msg
						#develcat4-msg
						#develcat5-msg
			)
			(default def_develcat)
		)
	)
)

;------------------ Check which extra commands to install ------------------
(if (IN develinstall 4)
	(set develcmd
		(askoptions
			(prompt #develcmd-msg)
			(help	@askoptions-help)
			(choices	#develcmd1-msg
						#develcmd2-msg
						#develcmd3-msg
						#develcmd4-msg
			)
			(default def_develcmd)
		)
	)
)

;------------------- Check which other files to install --------------------
(if (IN develinstall 5)
	(set develoth
		(askoptions
			(prompt #develoth-msg)
			(help	@askoptions-help)
			(choices	#develoth1-msg
						#develoth2-msg
						#develoth3-msg
						#develoth4-msg
						#develoth5-msg
						#develoth6-msg
			)
			(default def_develoth)
		)
	)
)

;-------------------------- Create main directory --------------------------
(if (OR
		(AND (IN installtype 0) (IN userinstall 1 2 3))
		(AND (IN installtype 1) (OR	(<> develdocs 0)
									(OR (<> develex1 0) (<> develex2 0))
									(<> develcat 0)
									(> develoth 1)
								)
		)
	)

	(
		(set destdir
			(askdir
				(prompt #maindir-msg)
				(help @askdir-help)
				(default @default-dest)
			)
		)
		(if (= @user-level 0) (set destdir (tackon destdir "GadUtil")))

		(if (<> (substr destdir (- (strlen destdir) 1)) ":")
			(makedir destdir (infos))
		)

		(set @default-dest destdir)
	)
)

;-------------------------- Create docs directory --------------------------
(if (AND (<> installtype 0) (OR (> userinstall 1)(<> develdocs 0)))
	(
		(set docdir
			(askdir
				(prompt #docdir-msg)
				(help @askdir-help)
				(newpath)
				(default (tackon destdir "Docs"))
			)
		)
		(makedir docdir (infos))
	)
)

;-------------------------- Do user installation ---------------------------
(if (IN userinstall 0)
   	(copylib
		(prompt #libinst-msg)
       	(help @copylib-help)
       	(source "libs/gadutil.library")
       	(dest "libs:")
       	(confirm)
	)
)

(if (IN userinstall 1)
	(
		(copyfiles (source "docs/GadUtil.Manual")
			(dest docdir)
		)
		(copyfiles (source def_guide_icon)
			(dest docdir) (newname "GadUtil.Manual.info")
		)
	)
)

(if (IN userinstall 2)
	(
		(copyfiles (source "docs/GadUtil_dev.readme")
			(dest docdir)
		)
		(copyfiles (source def_doc_icon)
			(dest docdir) (newname "GadUtil_dev.readme.info")
		)

		(copyfiles (source "docs/GadUtil_usr.readme")
			(dest docdir)
		)
		(copyfiles (source def_doc_icon)
			(dest docdir) (newname "GadUtil_usr.readme.info")
		)

		(copyfiles (source "docs/GadUtil_html.readme")
			(dest docdir)
		)
		(copyfiles (source def_doc_icon)
			(dest docdir) (newname "GadUtil_html.readme.info")
		)
	)
)

(if (IN userinstall 3)
	(copyfiles
		(source "docs/pics")
		(dest (tackon docdir "Pics"))
		(all) (infos)
	)
)
	
;----------------- Install selected developer documents --------------------
(if (IN develdocs 0)
	(
		(copyfiles (source "Docs/GadUtil.doc") (dest docdir) )
		(copyfiles (source def_doc_icon) (dest docdir)
			(newname "GadUtil.doc.info")
		)
	)
)

(if (IN develdocs 1)
	(
		(message #convertdocs-msg)
		(run (cat "ExtraCmds/spatch -o" (tackon docdir "GadUtil.guide")
							" -pDocs/GadUtil_doc_to_guide.pch Docs/GadUtil.doc")
		)
		(copyfiles (source def_guide_icon) (dest docdir)
			(newname "GadUtil.guide.info")
		)

		(copyfiles (source "Docs/GadUtil.index") (dest docdir) )
		(copyfiles (source def_guide_icon) (dest docdir)
			(newname "GadUtil.index.info")
		)
	)
)

(if (IN develdocs 2)
	(
		(copyfiles (source "Docs/PrivateFunctions") (dest docdir) )
		(copyfiles (source def_doc_icon) (dest docdir)
			(newname "PrivateFunctions.info")
		)
	)
)

(if (IN develdocs 3)
	(
		(copyfiles (source "Docs/dev_history.guide") (dest docdir) )
		(copyfiles (source def_guide_icon) (dest docdir)
			(newname "dev_history.guide.info")
		)
	)
)

(if (IN develdocs 4)
	(
		(copyfiles (source "Docs/dev_historia.guide") (dest docdir) )
		(copyfiles (source def_guide_icon) (dest docdir)
			(newname "dev_historia.guide.info")
		)
	)
)

(if (IN develdocs 5)
	(
		(copyfiles (source "Docs/gadtools.tags") (dest docdir) )
		(copyfiles (source def_doc_icon) (dest docdir)
			(newname "gadtools.tags.info")
		)
	)
)

;-------------------- Install all selected include files -------------------
(if (AND (> installtype 1) (<> develinc 0))
	(	(	(	(
		(if (IN develinc 0)
			((set cincdir
				(askdir
					(prompt #cincdir-msg)
					(help @askdir-help)
					(newpath)
					(default "include:")
				)
			)

			(set ccomp (select cinc "SAS" "DICE" "Aztec" "Maxon" "Maxon"))

			(if (= ccomp "SAS")
				(set .file1 "include/clib/gadutil_protos_SAS.h")
				(set .file1 "include/clib/gadutil_protos.h")
			)

			((set .file2 (tackon cincdir "clib"))
			(safecopy))

			(if (= ccomp "SAS")
				(rename (tackon cincdir "clib/gadutil_protos_SAS.h")
								(tackon cincdir "clib/gadutil_protos.h"))
			)

			((set .file1 "include/libraries/gadutil.h")
			(set .file2	(tackon cincdir "libraries"))
			(safecopy))


			((set .file1 "include/gadutil_20to30comp.h")
			(set .file2 cincdir)
			(safecopy))

			(copyfiles
				(source "include/proto/gadutil.h")
				(dest (tackon cincdir "proto"))
			)

			(if (= ccomp "Aztec")
				(set pragmaname "pragmas/gadutil_lib.h")
				(set pragmaname "pragmas/gadutil_pragmas.h")
			)

			(makedir (tackon cincdir "pragmas"))

			(run (cat "ExtraCmds/fd2pragma include/FD/gadutil_lib.fd "
							ccomp ' "' (tackon cincdir pragmaname) '"'))

			(if (<> "" (getassign "StormC" "adv"))
				(
					(set StormCDict
						(askbool
							(prompt #StormCDict-msg)
							(help @askbool-help)
							(choices "Yes" "No, I'll do it later")
							(default 1)
						)
					)

					(if (= StormCDict 1)
						(
							(run "c:edit \"StormC:StormSys/Dictionary/Amiga Functions.dic\" WITH icons/EditorCommands1")
							(textfile (dest "StormC:StormSYS/Dictionary/Amiga Functions.dic")
									  (include "StormC:StormSYS/Dictionary/Amiga Functions.dic")
									  (include "include/StormC/GadUtil Functions.dic"))

							(run "c:edit \"StormC:StormSys/Dictionary/Amiga Types.dic\" WITH icons/EditorCommands2")
							(textfile (dest "StormC:StormSYS/Dictionary/Amiga Types.dic")
									  (include "StormC:StormSYS/Dictionary/Amiga Types.dic")
									  (include "include/StormC/GadUtil Types.dic"))
						)
					)
				)
			)
		))

		(if (IN develinc 1)
			(	(if (<> cincdir 0)
					(set defasminc cincdir)
					(set defasminc "include:")
				)
				(set asmincdir
					(askdir
						(prompt #asmincdir-msg)
						(help @askdir-help)
						(newpath)
						(default defasminc)
					)
				)

				((set .file1 "include/libraries/gadutil.i") 
				(set .file2 (tackon asmincdir "libraries"))
				(safecopy))

				((set .file1 "include/gadutil_20to30comp.i")
				(set .file2 asmincdir)
				(safecopy))

				(if (= asminc 0)
					((set .file1 "include/libraries/gadutil_lib.i")
					(set .file2 (tackon asmincdir "libraries"))
					(safecopy))
					(
						(run (cat "ExtraCmds/fd2pragma include/FD/gadutil_lib.fd "
							'AS "' (tackon asmincdir "libraries/gadutil_lib.asm") '"'))
					)
				)

			)
		))

		(if (IN develinc 2)
			(	(if (<> asmincdir 0)
					(set deflvodir asmincdir)
					(set deflvodir "include:")
				)
				(set LVOSidir
					(askdir
						(prompt #LVOSidir-msg)
						(help @askdir-help)
						(newpath)
						(default deflvodir)
					)
				)

				((set .file1 "include/LVOs.i")
				(set .file2 LVOSidir)
				(safecopy))
			)
		))

		(if (IN develinc 3)
			(	(set FDdir
					(askdir
						(prompt #FDdir-msg)
						(help @askdir-help)
						(newpath)
						(default "FD:")
					)
				)
				((set .file1 "include/FD/gadutil_lib.fd")
				(set .file2 FDdir)
				(safecopy))
			)
		))
	)
)

;----------------------- Create directory for examples ---------------------
(if (OR (<> develex1 0) (<> develex2 0))
	(
		(set exampledir
			(askdir
				(prompt #exampledir-msg)
				(help @askdir-help)
				(newpath)
				(default (tackon destdir "Examples"))
			)
		)
		(makedir exampledir (infos))
	)
)

;------------------------- Install selected examples -----------------------
(if (IN develex1 0)
	(	(copyfiles
			(source "Examples/GadToolsBug.s")
			(dest exampledir)
		)
		(copyfiles
			(source "Examples/GadToolsBug1")
			(dest exampledir)
		)
		(copyfiles
			(source "Examples/GadToolsBug2")
			(dest exampledir)
	)	)
)

(if (IN develex1 1)
	(copyfiles
		(source "Examples/AnyProgram")
		(dest (tackon exampledir "AnyProgram"))
		(all)(infos)
	)
)

(if (IN develex1 2)
	(copyfiles
		(source "Examples/BetterTest")
		(dest (tackon exampledir "BetterTest"))
		(all) (infos)
	)
)

(if (IN develex1 3)
	(	(copyfiles
			(source "Examples/BevelTest.s")
			(dest exampledir)
		)
		(copyfiles
			(source "Examples/BevelTest")
			(dest exampledir)
	)	)
)

(if (IN develex1 4)
	(copyfiles
		(source "Examples/ExternTestInit.s")
		(dest exampledir)
	)
)

(if (IN develex1 5)
	(	(copyfiles
			(source "Examples/LVTest.s")
			(dest exampledir)
		)

		(copyfiles
			(source "Examples/LVTest")
			(dest exampledir)
	)	)
)

(if (IN develex1 6)
	(	(copyfiles
			(source "Examples/AGuideTest.s")
			(dest exampledir)
		)
		(copyfiles
			(source "Examples/AGuideTest")
			(dest exampledir)
	)	)
)

(if (IN develex1 7)
	(	(copyfiles
			(source "Examples/FractWidHei.s")
			(dest exampledir)
		)
		(copyfiles
			(source "Examples/FractWidHei")
			(dest exampledir)
	)	)
)

(if (IN develex2 0)
	(	(copyfiles
			(source "Examples/mytestprog.s")
			(dest exampledir)
		)
		(copyfiles
			(source "Examples/mytestprog1")
			(dest exampledir)
		)
		(copyfiles
			(source "Examples/mytestprog2")
			(dest exampledir)
	)	)
)

(if (IN develex2 1)
	(copyfiles
		(source "Examples/OldInternal")
		(dest (tackon exampledir "OldInternal"))
		(all)(infos)
	)
)

(if (IN develex2 2)
	(copyfiles
		(source "Examples/OldTestProgs")
		(dest (tackon exampledir "OldTestProgs"))
		(all) (infos)
	)
)

(if (IN develex2 3)
	(	(copyfiles
			(source "Examples/ThreeGadgets.s")
			(dest exampledir)
		)
		(copyfiles
			(source "Examples/ThreeGadgets")
			(dest exampledir)
	)	)
)

(if (IN develex2 4)
	(copyfiles
		(source "Examples/BetterTest-C")
		(dest (tackon exampledir "BetterTest-C"))
		(all) (infos)
	)
)

(if (IN develex2 5)
	(copyfiles
		(source "Examples/MoreExamples-C")
		(dest (tackon exampledir "MoreExamples-C"))
		(all) (infos)
	)
)

(if (IN develex2 6)
	(copyfiles
		(source "Examples/MoreExamples")
		(dest (tackon exampledir "MoreExamples"))
		(all) (infos)
	)
)

;----------------- Create directory for catalog utilities ------------------
(if (<> develcat 0)
	(
		(set catutdir
			(askdir
				(prompt #catutdir-msg)
				(help @askdir-help)
				(newpath)
				(default (tackon destdir "CatalogUtils"))
			)
		)
		(makedir catutdir (infos))
	)
)

;-------------------- Install selected catalog utilities -------------------
(if (IN develcat 0)
	(copyfiles
		(source "CatalogUtils/FlexCat")
		(dest catutdir)
	)
)

(if (IN develcat 1)
	(	(copyfiles
			(source "CatalogUtils/FlexCat_Eng.guide")
			(dest catutdir)
		)
		(copyfiles
			(source def_guide_icon)
			(dest catutdir) (newname "FlexCat_Eng.guide.info")
	)	)
)

(if (IN develcat 2)
	(	(copyfiles
			(source "CatalogUtils/FlexCat_Quick.doc")
			(dest catutdir)
		)
		(copyfiles
			(source def_doc_icon)
			(dest catutdir) (newname "FlexCat_Quick.doc.info")
	)	)
)

(if (IN develcat 3)
	(copyfiles
		(source "CatalogUtils/Lattice_Data.sd")
		(dest catutdir)
	)
)

(if (IN develcat 4)
	(copyfiles
		(source "CatalogUtils/ASM-One_Data.sd")
		(dest catutdir)
	)
)

;------------------ Create directory for extra commands --------------------
(if (<> develcmd 0)
	(
		(set extcmddir
			(askdir
				(prompt #extcmddir-msg)
				(help @askdir-help)
				(newpath)
				(default "C:")
			)
		)
		(if (<> (exists extcmddir) 2)
			(makedir extcmddir (infos))
		)
	)
)

;---------------------- Install selected extra commands --------------------
(if (IN develcmd 0)
	(copyfiles
		(source "ExtraCmds/AskEnv")
		(dest extcmddir)
	)
)

(if (IN develcmd 1)
	(	(copyfiles
			(source "ExtraCmds/FD2Pragma")
			(dest extcmddir)
		)
		(copyfiles
			(prompt #sellocfor-msg)
			(source "ExtraCmds/FD2Pragma.readme")
			(dest extcmddir)
			(confirm) (help @copyfiles-help)
		)
	)
)

(if (IN develcmd 2)
	(copyfiles
		(source "ExtraCmds/XIcon")
		(dest extcmddir)
	)
)

(if (IN develcmd 3)
	(	(copyfiles
			(source "ExtraCmds/spatch")
			(dest extcmddir)
		)
		(copyfiles
			(source "ExtraCmds/scompare")
			(dest extcmddir)
		)
	)
)

;--------------------- Install debug version of library --------------------
(if (IN develoth 0)
	(
		(message #debuglib-msg)
		(copylib
			(prompt #debuglibinst-msg)
			(help @copylib-help)
			(source "libs/gadutil.library_dbg")
			(dest "libs:")
			(confirm)
		)
	)
)

(if (IN develoth 1)
	(	(copyfiles
			(source "InstallInc.info")
			(dest destdir)
		)
		(copyfiles
			(source "install.script")
			(dest destdir)
		)
		(copyfiles
			(source "InstallInc.gad")
			(dest destdir)
		)
	)
)

(if (IN develoth 2)
	(	(copyfiles
			(source "-MakeArchive-.info")
			(dest destdir)
		)
		(copyfiles
			(source "-MakeDevel-.info")
			(dest destdir)
		)
		(copyfiles
			(source "-MakeUser-.info")
			(dest destdir)
	)	)
)

(if (IN develoth 3)
	(	(copyfiles
			(source "Install") (infos)
			(dest destdir)
		)
		(copyfiles
			(source "Install_User") (infos)
			(dest destdir)
		)
		(copyfiles
			(source "icons")
			(dest (tackon destdir "icons"))
			(all)
		)
		(copyfiles
			(source "libs")
			(dest (tackon destdir "libs"))
			(all)(infos)
	)	)
)

(if (IN develoth 4)
	(	(if (exists "GadUtilLib.s")
			((set .file1 "GadUtilLib.s")
			(set .file2 destdir)
			(safecopy))
			(message #obtainsrc-msg)
		)
	)
)

(if (IN develoth 5)
	(
		(if (exists "GU_DrawBevelBoxA.s")
			(copyfiles
				(source "GU_DrawBevelBoxA.s")
				(dest destdir)
			)
			(message #obtainsrc-msg)
		)
	)
)

;--------------------------------- Done ------------------------------------
(exit)
