;
;    Convert a JPEG to GIF or Targa
;    SCRIPT FILENAME D.convertLR.scp 

;    REQUIRED EXECUTABLES:

;       DGPEGv5 (gfx/conv)
;       MUInt (beta)
;       FROM DOSReqTools (util/cli):
;         ADDPART
;         CHANGEEXT
;         INSTRING
;         RTEZREQUEST

;    VARIABLES USED IN THIS SCRIPT:

;       $LEFT     = SOURCE JPEG FILE
;       $RIGHT    = DESTINATION GIF FILE
;       $TEMPDEST = A TEMPORARY ASSIGNMENT FOR OUTFILENAME
;       $RADIO    = RADIO BUTTONS
;       $CHECKBOX = CHECKBOXES
;--------------------------------------------------

SET NOVIEW "FALSE"; BMP or TARGA PIC

;--------------------------------------------------
;    CHECK TO SEE IF THE SELECTED FILE IS A JPEG
;--------------------------------------------------


Instring >NIL: ".jpg" $left
IF WARN

;--------------------------------------------------
;     BUILD A PATH/FILENAME FOR THE OUTFILE
;     ADD THE SOURCE FILENAME TO DEST PATH
;--------------------------------------------------

  AddPart >env:right $rightpath $leftname

;--------------------------------------------------
;     CHANGE THE EXTENSION OF THE OUTFILE 
;     TO MATCH THE OUTFILE TYPE (GIF,TARGA,BMP,etc.)
;--------------------------------------------------

     TestSwitch -gif $radio

     IF WARN
        ChangeExt >env:tempdest $right ".gif"
     ENDIF


     TestSwitch -tar $radio

     IF WARN
        ChangeExt >env:tempdest $right ".tga"
        SET NOVIEW "TRUE"
     ENDIF
 
    
     TestSwitch -os2 $radio

     IF WARN
        ChangeExt >env:tempdest $right ".bmp"
        SET NOVIEW "TRUE"
     ENDIF

     TestSwitch -prm $radio

     IF WARN
        ChangeExt >env:tempdest $right ".PBM"
        SET NOVIEW "TRUE"
     ENDIF



     SETENV right $tempdest

;--------------------------------------------------
;     CALL DJPEG FOR THE CONVERSION
;--------------------------------------------------

IF EXISTS >NIL: DJPEG

     DJPEG  $RADIO $CHECKBOX $LEFT $RIGHT

ELSE

     rtEZrequest "System Warning:" BODY "You do not have DJPEG installed." BUTTONTEXT "Continue" POS CENTERSCR

ENDIF

;--------------------------------------------------
;     VIEW THE CONVERTED GIF.
;     CHANGE THE VIEWER IF YOU DON'T USE VIEWTEK
;--------------------------------------------------

     IF $NOVIEW EQ "TRUE"
       rtEZrequest "System Warning:" BODY "The Outfile, $RIGHT cannot be viewed\n(BMP, TARGA, PBM file)" BUTTONTEXT "Continue" POS CENTERSCR
       QUIT
     ENDIF


     IF EXISTS >NIL: ENV:XTVIEWER
          run >nil: $XTVIEWER $RIGHT
     ELSE
         run >nil: VT $RIGHT
     ENDIF

  QUIT

ENDIF
;--------------------------------------------------
;      NOT A JPEG
;--------------------------------------------------

  rtEZrequest "System Warning:" BODY "The selected file, $LEFT, is not a JPEG.\nPlease select another file" BUTTONTEXT "Continue" POS CENTERSCR

  QUIT 



