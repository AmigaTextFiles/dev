.key source/a,macro/a
.bra {
.ket }
;
;       $VER: EComp 1.0 (04.10.94)
;
;       Copyright 1994 Jaba Development.
;       Copyright 1994 Jan van den Baard.
;
;       This small script can be used to run a source through
;       Mac2E and EC resulting in an executable in one go.
;
;       For this to work you must do the following:
;
;       Setup an environment variable called MAC2EMACROS to
;       reflect the directory in which your pre-analized macro
;       files reside:
;
;               echo >ENVARC:MAC2EMACROS "E:PreAnalizeMacroFiles/"
;
;       Usage:  EComp SOURCE/A,MACRO/A
;
;       NAME:     Name of the source file without the .e!
;       MACRO:    Name of the macro file to use.
;
;       Currently this script can only handle one single macro file.
;-----------------------------------------------------------------------------

;       Pre-compile the source file and
;       put the resulting code in t:{source}.e
;
Mac2E {source}.e T:{source}.e $MAC2EMACROS$${macro}
;
;       Quietly optimize source output by
;       removing all TAG_IGNORE, 0 statements that
;       the macros introduced.
;
IF EXISTS E:Bin/OptiMUI2E
        OptiMUI2E >NIL: T:{source}.e T:{source}.e
ENDIF
;
;       Compile pre-compiled source.
;
Echo "Executing compiler..."
EC T:{source} QUIET
;
;       Put the current directory in a variable.
;
cd >ENV:dir{$$}
;
;       Copy the executable to the directory
;       from which the script was started.
;
copy > NIL: T:{source} $dir{$$}
;
;       Clean up.
;
delete > NIL: T:{source} T:{source}.e
;
;       Done...
;
Echo "Done..."
