.KEY rawline
.BRA {
.KET }


;         FILENAME LAB.print.scp
;         DATE 12-20-94
;         BY Colin Thompson
;
;         THIS SCRIPT USES THESE VARIABLES:
;
;         RAWLINE - The record to print, passed in by Label.def
;                   RAWLINE contains 5 fields, separated by commas:
;
;                   COMPANY,ADDRESS,CITY,STATE,CODE
;                   When printed, the record becomes this:
;
;                        COMPANY     
;                        ADDRESS
;                        CITY STATE CODE
;
;         $RADIO  - The RADIO button flags
;         $TIMES  - From RADIO. The number of copies to print.
;         $LARGE  - From RADIO. A large label
;         $INCR   - (Increment) This is the Loop Counter for $TIMES
;
;         NOTE: The commas in the record are translated as spaces with
;               the UNIX tool tr (translate)
;
;               The record is broken up into three fields (printed 
;               lines) with the UNIX tool cut
;
;               TestSwitch is a DOS extension supplied here
;
;               There is NO error checking to see if PAR: is available
;               Please fix this if you can. 
;
;=====================================================================
;           SEE IF A SELECTION WAS MADE
;=====================================================================

IF NOT EXISTS ENV:LV
     RTEZREQUEST "WARNING:" "You must make a selection"
     SKIP QUITIT
ENDIF

;=====================================================================
;           EXAMINE $RADIO TO SEE HOW MANY COPIES TO PRINT
;=====================================================================

TestSwitch "1" $radio
IF WARN
     SET times "1"
ENDIF

TestSwitch "4" $radio
IF WARN
     SET times "4"
ENDIF

TestSwitch "8" $radio
IF WARN
     SET times "8"
ENDIF

TestSwitch "12" $radio
IF WARN
     SET times "12"
ENDIF

TestSwitch "-v" $radio
IF WARN
     rtgetlong "Label" "How many labels to print?" to env:num MIN 1 MAX 100
     SET times $num
     DELETE ENV:NUM
ENDIF

;=====================================================================
;         EXAMINE $RADIO TO SEE WHICH SIZE LABEL IS BEING USED
;=====================================================================

TestSwitch "-lge" $radio
IF WARN
     SET large "true"
ENDIF


;=====================================================================
;         ESTABLISH AN ENVARIABLE TO HOLD THE RECORD
;=====================================================================


SETENV RAW {rawline}


;=====================================================================
;         CUT THE RECORD INTO THREE FIELDS FOR PRINTING
;=====================================================================



SETENV temp1 `cut -f1 -d, <env:raw`
SETENV temp2 `cut -f2 -d, <env:raw`
SETENV temp3 `cut -f3-5 -d, <env:raw`


;=====================================================================
;        REPLACE THE COMMAS SEPARATING CITY, STATE, AND CODE
;        WITH A SPACE
;=====================================================================


SETENV line3 `tr <env:temp3 "," " "`



;=====================================================================
;        SET THE LOOP COUNTER TO ZERO 
;=====================================================================


SET incr "0"


;=====================================================================
;      THIS IS THE LOOP THAT PRINTS ONE LABEL EVERY TIME
;      THROUGH THE LOOP. $TIMES IS THE VALUE FROM THE RADIO BUTTON.
;=====================================================================

;=====================================================================
;      IF A LARGE LABEL IS SELECTED, THREE MORE BLANK LINES ARE PRINTED
;=====================================================================

LAB LOOP

IF VAL $incr eq $times
  SKIP QUITFROMPRINTING
ENDIF

IF $large EQ "true"
     echo >par: ""
     echo >par: ""
ENDIF

echo >par: ""
echo >par: $temp1
echo >par: $temp2
echo >par: $line3
echo >par: ""
echo >par: ""

IF $large EQ "true"
     echo >par: ""
ENDIF

SET incr `EVAL $incr + 1`
SKIP BACK LOOP


;=====================================================================
;       WHEN THE LABEL HAS BEEN PRINTED <$TIMES> TIMES, IT FALLS
;       THROUGH TO THE QUIT STATEMENT 
;=====================================================================

LAB QUITFROMPRINTING

DELETE ENV:TEMP1 QUIET
DELETE ENV:TEMP2 QUIET
DELETE ENV:TEMP3 QUIET
DELETE ENV:LINE3 QUIET
DELETE ENV:RAW   QUIET

LAB QUITIT
QUIT 


