G4C

WINBIG 189 30 279 190 'Tutorials'
wintype 11110001
resinfo 8 640 256

xonload
	lvuse tutorials.gc 1
	lvmode SHOW                 ; change the lv mode on the fly
	tut = '$$lv.rec'            ; store current choice
	cutvar tut cut word 1 tut   ; because dir lvs have also the sizes etc
	file = 'guis:docs/tutorials/$tut'
	guiopen tutorials.gc

xonclose
	guiquit tutorials.gc

xonquit
	guiquit read.gc

xonRMB
	status

XLISTVIEW 3 17 180 172 '' tut guis:docs/tutorials 0 DIR ; start off with dir lv
	GadID 1
	attr resize 0022
	gadfont topaz.font 8 000
	cutvar tut cut word 1 tut ; because dir lvs have also the sizes etc
	file = 'guis:docs/tutorials/$tut'
	update tutorials.gc 2 'Tutorial : $tut'

TEXT 2 1 275 15 "Choose a tutorial.." 40 BOX
	attr resize 0020
	GadID 2

XBUTTON 186 18 90 15 'Read it'
	attr resize 2000
	if $file > ''
	   update tutorials.gc 2 'Reading $tut ..'
	   *FILENAME = $file
	   guiload guis:tools/read.gc
	   update tutorials.gc 2 'Tutorial : $tut'
	endif

XBUTTON 186 33 90 15 'Run it'
	attr resize 2000
	if $file > ''
	   update tutorials.gc 2 'Running $tut ..'
	   guiload $file
	   update tutorials.gc 2 'Tutorial : $tut'
	endif

XBUTTON 186 159 90 15 'Guide..'
	attr resize 2200
	run 'amigaguide guis:docs/gui4cli.guide'

XBUTTON 186 174 90 15 'Quit'
	attr resize 2200
	guiquit tutorials.gc

ICON 195 144 guis:gui4cli
	attr resize 2200

