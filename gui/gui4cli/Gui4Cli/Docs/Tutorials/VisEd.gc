G4C

WINBIG 37 10 585 206    "VisEd.gc"
winout 'CON:300/110/340/140/NOTES/AUTO'
WinType 11110001
usetopaz

;  Headings
CTEXT 28 3    'Visual Editing Practice' topaz.font 11 2 0 001
CTEXT 370 1   'Graded Suggestions' topaz.font 9 3 2 000

; Two squares to organize practice
SQUARE 287 30 285 71    1 nofill
SQUARE 9 19 271 79      2 nofill

; Graphics samples to play with
BOX 183 56 45 21        IN BUTTON
LINE 99 83 248 93       1
SQUARE 32 57 42 26      1 fill
CIRCLE 128 63 22 12     1 nofill

xonLoad 
	; Set up our path for the second window
	extract VisEd.gc guipath tem_path
	assign TEM: $tem_path
	delvar tem_path
	GuiLoad TEM:SecondWindow.g
	GuiOpen VisEd.gc

xonClose
	GuiQuit SecondWindow.g
	GuiQuit VisEd.gc

; More samples to play with

XBUTTON 24 36 63 14    'Button'

XCYCLER 63 103 92 15    'Cycler' cvar
	CSTR 'OFF' 0
	CSTR 'ON' 1

XCHECKBOX 250 105 26 12    'CheckBox' chvar 'ON' 'OFF' OFF

XHSLIDER 63 119 213 11    'Slider' hvar 0 100 50 ''

XTEXTIN 210 36 53 13    'TextIn' tvar Text 128

XLISTVIEW 43 147 205 57    ListView lv '' 0 NUM

; Five suggested practice activities

XBUTTON 397 14 26 13    '1'

	a = '                  Saving any changes\n'
	appvar a '                  ------------------\n'
	appvar a 'Any changes you make can be saved by pressing Ctrl/g.\n'
	appvar a 'Note that the effect will be to change permanently the\n'
	appvar a 'existing format of this tutorial. If you wish to\n'
	appvar a 'keep the original intact, make a copy now.'
	ezreq $a 'Okay' v

XBUTTON 424 14 26 13         '2'
	a = '               Practice in moving objects\n'
	appvar a '               --------------------------\n'
	appvar a 'Move the items from the white box into the black box.\n'
	appvar a 'Gui4Cli docs will tell you how to use Ctrl and the mouse\n'
	appvar a 'button (or "h") to move items.  Note the special needs of\n'
	appvar a 'graphics elements, especially circles.  Avoid the lower\n'
	appvar a 'right corner if you just want to move an object.'
	ezreq $a 'Okay' v

XBUTTON 450 14 26 13         '3'
	a = '               Practice in resizing objects\n'
	appvar a '               ----------------------------\n'
	appvar a 'Resize the items in one of the boxes so that, as far as possible,\n'
	appvar a 'they completely fill it. Remember, click on the lower right\n'
	appvar a 'corner to change the size or shape of an object. You can\n'
	appvar a 'change the gradient but not the thickness of the line.'
	ezreq $a 'Okay' v

XBUTTON 476 14 26 13         '4'
	a = '                Practice in cloning objects\n'
	appvar a '                ---------------------------\n'
	appvar a 'A new gui window will be opened for you in the lower right\n'
	appvar a 'section of the existing one. Clone a selection of objects\n'
	appvar a 'from the larger window into the smaller.'
	ezreq $a 'Okay' v
	ifexists WINDOW SecondWindow.g
	    ; nop
	else
	    GuiOpen SecondWindow.g
	endif

XBUTTON 502 14 26 13         '5'
	a =      '           Duplicating and deleting objects\n'
	appvar a '           --------------------------------\n'
	appvar a 'To duplicate an object in its own window, clone it into the\n'
	appvar a 'second window, and then clone that new object back into the\n'
	appvar a 'first window.\n'
	appvar a 'Check also how the delete key can be used with a selected object.'
	ezreq $a 'Okay' v
	ifexists WINDOW SecondWindow.g
	    ; nop
	else
	    GuiOpen SecondWindow.g
	endif

;------  Window for cloning practice

NEWFILE SecondWindow.g

WINBIG 320 124 300 100  "SecondWindow.g"
WinType 11110001
usetopaz

