G4C

WinBig -1 -1 500 130 "Menus.gc"
WinType 11110001
usetopaz

BOX 0 0 0 0 OUT RIDGE
SQUARE 14 18 100 52 2 NOFILL    ; Inset menu list
SQUARE 64 40 36 24 2 NOFILL

xonLoad 
	GuiOpen Menus.gc

xonClose
	GuiQuit Menus.gc

; ---- xMenu arguments are :   Menu Item SubItem ShortCut
;      Examples used here have been enclosed in quotes just
;      to lengthen the entries. They then appear less
;      cramped for space when used.
;      The effect of each selection is displayed in a Text gadget.
xMenu 'Project      ' 'Read   ' '' 'R'
	update Menus.gc 1 "You selected the 'Read' menu item."

xMenu 'Project      ' 'Edit   ' 'Ed       ' 'E'
	update Menus.gc 1 "You selected the 'Edit  ED' subitem."

xMenu 'Project      ' 'Edit   ' 'CED      ' 'C'
	update Menus.gc 1 "You selected the 'Edit  CED' subitem."

xMenu 'Project      ' 'Quit   ' '' 'Q'
	update Menus.gc 1 "Trying to Quit, eh?"

; ---- A poor reproduction of the menu layout, and explanatory notes.
Text 180 2 100 12 'Menus' 6 NOBOX 
Text 20 20 100 10 'Project' 7 NOBOX
Text 20 30 100 10 'Read' 4 NOBOX
Text 20 40 100 10 'Edit' 4 NOBOX
Text 70 42 100 10 'Ed' 10 NOBOX
Text 70 52 100 10 'CED' 3 NOBOX
Text 20 60 100 10  'Quit' 4 NOBOX

Text 130 20 100 10 'If you use the Right Mouse Button to check,' 44 NOBOX
Text 130 30 100 10 'you will see that a small set of menus, as' 44 NOBOX
Text 130 40 100 10 'shown in the box here, has been set up.' 44 NOBOX
Text 130 50 100 10 "You may of course 'Select' them.  Your" 44 NOBOX
Text 130 60 100 10 'selection will be confirmed, but nothing' 50 NOBOX
Text 130 70 100 10 "else will happen." 40 NOBOX

Text 30 85 100 10 "Note the shortcuts (RightAmiga/char) in the menus." 50 NOBOX
Text 30 95 100 10 "Check that they work." 24 NOBOX

; ---- This is the Text box to confirm your choices.
Text 100 110 100 10 '' 38 NOBOX
	gadid 1

