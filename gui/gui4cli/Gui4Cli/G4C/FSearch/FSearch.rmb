G4C

; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;	FSearch.rmb
;	popup on RMB in FSearch.gc (when in list results mode)
; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

WinBig 0 0 80 105 ""
WinType 00001000
resinfo 8 640 256
winonmouse 30 6
varpath fsearch.gc

xOnRMB
	guiclose #this

xOnInactive
	guiclose #this

xOnOpen
	if $fsearch.gc/details = INFO	; can't deal with file sizes etc
		setgad #this 2 off
	else
		setgad #this 2 on
	endif

xBefore
	guiclose #this

xOnHelp OFF
	gadid 905
	gosub fsearch.gc dohelp

;=============================== All/None/Rem

xbutton 0 0 20 15 "A"
	gadhelp 'Select all items'
	lvuse fsearch.gc 101
	lvmulti all

xbutton 20 0 20 15 "N"
	gadhelp 'Unselect all items'
	lvuse fsearch.gc 101
	lvmulti none

xbutton 40 0 40 15 "Rem"
	gadhelp 'Remove selected items from list'
	lvuse fsearch.gc 101
	lvmulti first
	while $$lv.line > ''
		lvdel -1
		lvmulti first
	endwhile

;=============================== Replace

xbutton 0 15 0 15 "Replace.." 
	gadhelp 'Replace in selected files (OFF if File Details are listed)'
	guiload :fsearch.rep $fsearch.gc/findtxt

;=============================== Copy

xbutton 0 30 0 15 'Copy..'
	gadhelp 'Copy selected files to destination (requester)'
	local dest
	dest = ''
	ReqFile	-1 -1 300 200 "Copy to:" DIR dest sys:
	if $dest > ' '
		guiwindow fsearch.gc wait
		lvuse fsearch.gc 101
		gosub fsearch.gc NextFile
		while $fsearch.gc/resfile > ''
			copy $fsearch.gc/resfile $dest
			gosub fsearch.gc NextFile
		endwhile
		guiwindow fsearch.gc resume
	endif

;=============================== Move

xbutton 0 45 0 15 'Move..'
	gadhelp 'Move selected files to destination (requester)'
	local dest
	dest = ''
	ReqFile	-1 -1 300 200 "Move to:" DIR dest sys:
	if $dest > ' '
		guiwindow fsearch.gc wait
		lvuse fsearch.gc 101
		gosub fsearch.gc NextFile
		while $fsearch.gc/resfile > ''
			action move $fsearch.gc/resfile $dest
			gosub fsearch.gc NextFile
		endwhile
		guiwindow fsearch.gc resume
	endif

;=============================== Delete

xbutton 0 60 0 15 'Delete..'
	gadhelp 'Delete selected files'
	ezreq 'Delete & destroy\nall selected files ?' "DELETE|CANCEL" choice
	if $choice = 1
		guiwindow fsearch.gc wait
		lvuse fsearch.gc 101
		gosub fsearch.gc NextFile
		while $fsearch.gc/resfile > ''
			delete $fsearch.gc/resfile
			gosub fsearch.gc NextFile
		endwhile
		guiwindow fsearch.gc resume
	endif

;=============================== 

xbutton 0 75 0 15 ''

;=============================== save file list

xbutton 0 90 0 15 Save..
	gadhelp 'Save file list as..'
	file = ''
	ReqFile	-1 -1 300 200 "Save output as:" SAVE file $fsearch.gc/searchpath
	if $file > ' '
		guiwindow fsearch.gc wait
		.temp = ''
		copy env:.temp $file
		delete env:.temp
		lvuse fsearch.gc 101
		lvmulti all
		gosub fsearch.gc NextFile
		while $fsearch.gc/resfile > ''
			append $file '$fsearch.gc/resfile\n'
			gosub fsearch.gc NextFile
		endwhile
		guiwindow fsearch.gc resume
	endif



