G4C

; Gui4Cli.gc
; =================================================================
; This is the gui which is opened when you click the "Prefs" button 
; on the Gui4Cli requester. 

; If you find yourself feeling queezy after having suffered the full
; effect of it's poping open at you unwarned, you may change it as 
; you see fit..   - set TABs to 3 -


WINBIG 100 -10 299 218 "Gui4Cli"
WinType 11110001
resinfo 8 640 256

BOX 0 0 299 218 out button
	gadid 523 ; arbitrary

; -----------------------------------------------------------------
;		System events
; -----------------------------------------------------------------
 
xOnLoad
	setgadvalues gui4cli.gc		; set default values
	mode = GUI

	path = $$G4C.editor			; update paths
	update #this 61 $path

	sampname = $*SNDGAD			; update sound fx
	update #this 64 $sampname

	imgname = $*IMDEV				; update lvdir imgs
	update #this 65 $imgname

	setgad #this 903 hide 		; palette
	palfile = ''
	palnum  = 0

	oldguidir = GUIs:
	update gui4cli.gc 50 'Port: $$g4c.port'
	gosub gui4cli.gc SetGuiStat ; starting state
	guiopen Gui4Cli.gc

xonreload
	guiopen Gui4Cli.gc
	guiscreen gui4cli.gc front

xOnOpen
	SetGad Gui4Cli.gc 1 OFF		; remove the appicon
	if $mode = GUI
	   lvuse Gui4Cli.gc 54
	   lvdir #env:$$g4c.port
	endif

xOnClose 
	SetGad Gui4Cli.gc 1 ON		; open the appicon

xOnFail
	ezreq "Failed.." OK ""

xAppIcon 0 0 GUIs:Gui4Cli "Gui4Cli" apivar OFF
	GadID 1
	GuiOpen Gui4Cli.gc
	GuiScreen Gui4Cli.gc FRONT
	if $apivar > ' '
		if $apivar H= 'G4C'
			guiload $apivar
		endif
	endif

; ****** ENABLE THIS HOT KEY IF YOU WANT IT ******
; xHotKey "rawkey shift f1" ON	; popup key for this gui
;	guiopen #this
;	guiscreen #this front
;	guiwindow #this front

; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
; -----------------------------------------------------------------
;		Mode CYCLER
;		- will rotate gui through all modes
; -----------------------------------------------------------------
; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

XBUTTON 9 5 80 14 "Guis"
	gadid 801
	gadhelp '- List of loaded Guis'
	gosub #this SetGuiStat	; because it's also called from xOnLoad

xRoutine SetguiStat
	mode = GUI
	setgad gui4cli.gc 50/59 SHOW	; other lv & Open/close/quit etc
	setgad gui4cli.gc 60/100 HIDE
	lvuse Gui4Cli.gc 54
	lvdir #env:$$g4c.port
	setwintitle gui4cli.gc 'Port $$g4c.port : Home'
	changearg #this 801 1 7
	changearg #this 802 1 5
	changearg #this 803 1 5
	gadredraw #this 523 523 523 523 1

XBUTTON 90 7 80 14 "Settings"
	gadhelp '- Set paths, sounds, images etc..'
	gadid 802
	mode = LOAD
	setgad gui4cli.gc 50/58 HIDE			; guis range
	setgad gui4cli.gc 60/79 SHOW			; settings range
	setgad gui4cli.gc 80/100 HIDE			; prefs range
	setwintitle gui4cli.gc '$oldguidir'
	changearg #this 801 1 5
	changearg #this 802 1 7
	changearg #this 803 1 5
	gadredraw #this 523 523 523 523 1

XBUTTON 171 5 80 14 "Prefs"
	gadhelp '- Set general preferences'
	gadid 803
	mode = SET
	setgad gui4cli.gc 50/79 HIDE
	setgad gui4cli.gc 80/100 SHOW
	; reset to current values...
	update #this 87 $$g4c.tab
	update #this 86 $$g4c.grid
	update #this 91 $$g4c.NOVOL
	update #this 92 $$g4c.LVFILL
	update #this 93 $$g4c.LVSORT
	margins = $$g4c.MARGINS
	gosub #this CheckSlash margins
	update #this 94 $margins
	tbmargins = $$g4c.TBMARGINS
	gosub #this CheckSlash tbmargins
	update #this 95 $tbmargins
	setwintitle gui4cli.gc 'Gui4Cli settings'
	changearg #this 801 1 5
	changearg #this 802 1 5
	changearg #this 803 1 7
	gadredraw #this 523 523 523 523 1


; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
; -----------------------------------------------------------------
;		GUIs mode
;		Open, Close, unload GUIs... gadid 50 - 59
; -----------------------------------------------------------------
; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

XTEXTBOX 6 20 286 39 '' ''
	gadid 50
	attr tbstyle 2/1/shadow/left
	attr tbox    2/1/3/button/in

; ------ The listview

XLISTVIEW 6 60 287 155 "" guivar '' 0 DIR
	gadid 54
	gadfont #mono 8 000
	gadhelp 'These are the guis currently loaded'
	extract guivar file fname
	guiopen $fname

; ------ Listview handling buttons

XBUTTON 15 39 20 14 "R"		; refresh list
	gadid 51
	gadhelp 'Refreshes the list of loaded guis'
	lvuse Gui4Cli.gc 54
	lvdir refresh

XBUTTON 48 39 20 14 "A"  ; also shown in gui mode
	gadhelp 'Select all items'
	gadid 51
	lvuse Gui4Cli.gc 54
	lvdir all

XBUTTON 69 39 20 14 "N"
	gadhelp 'Unselect all items'
	gadid 51
	lvuse Gui4Cli.gc 54
	lvdir none

XBUTTON 99 39 58 14 Open
	gadid 51
	gadhelp 'Will open the selected guis'
	lvuse Gui4Cli.gc 54
	lvmulti first
	while $guivar > ''
	    extract guivar file fname
	    guiopen $fname
	    lvmulti next
	endwhile
	lvdir none

XBUTTON 159 39 58 14 Close
	gadid 52
	gadhelp 'Will close the selected guis'
	lvuse Gui4Cli.gc 54
	lvmulti first
	while $guivar > ''
	    extract guivar file fname
	    guiclose $fname
	    lvmulti next
	endwhile
	lvdir none

XBUTTON 219 39 64 14 Unload
	gadid 53
	gadhelp 'Will quit the selected guis'
	lvuse Gui4Cli.gc 54
	lvmulti first
	while $guivar > ''
	    extract guivar file fname
	    guiquit $fname
	    lvmulti next
	endwhile
	; the guis won't actually quit until all commands have executed, so..
	reflag = 1

xAfter
	if $reflag = 1
	   reflag = 0
	   lvdir refresh
	endif

; ----------- Load guis

XBUTTON 219 24 64 14 "Load.."
	gadhelp 'Load a new Gui file - Requester will open..'
	gadid 51
	loadname = ''
	reqfile -1 -1 300 -40 'Load a GUI' LOAD loadname guis:
	if $loadname > ''
		if $loadname H= 'G4C'
			guiload $loadname
		   lvuse Gui4Cli.gc 54
		   lvdir refresh
		else
			ezreq 'File: $loadname\nis not a Gui4Cli script.' 'OK' ''
		endif
	endif

; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
; -----------------------------------------------------------------
;		Settings
;		Set paths, sounds, images etc.. gadid 60 - 79
; -----------------------------------------------------------------
; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

; =====================================>>>>> Paths

XTEXTBOX 9 20 280 38 "" "Paths:"
	gadid 60
	attr tbstyle 2/1/shadow/left
	attr tbox    2/1/3/button/in

XTEXTIN 18 39 259 15 "" path '' 140
	gadid 61
	gadhelp 'Enter the new path etc here..'
	set $setpath $path

XCYCLER 117 24 161 14 "" setpath
	gadid 60
	gadhelp 'Cycles through the various things you can set.'
	CSTR 'Editor'		'editor'
	CSTR 'File Pattern'	'filepattern'
	cstr 'GuiFile Pattern'	'guipattern'
	cstr 'Gui Load dir'	'guiloaddir'
	cstr 'Default Screen'	'defscreen'
	CSTR 'Output'		'output'
	temp = '\$\$G4C.$setpath'
	update gui4cli.gc 61 '$temp'

; =====================================>>>>> Images

XTEXTBOX 9 60 280 37 "" "Images:"
	gadid 60
	attr tbstyle 2/1/shadow/left
	attr tbox    2/1/3/button/in

XCYCLER 117 63 128 14 "" imgtype
	gadid 60
	gadhelp 'Shows the types of LVDir images you can set'
	cstr Devices 'IMDEV'
	cstr Volumes 'IMVOL'
	cstr Assigns 'IMASN'
	cstr Dirs    'IMDIR'
	cstr Files   'IMFILE'
	imgname = \$*$imgtype ; ie '$*IMDEV'
	update #this 65 $imgname

XBUTTON 249 78 29 15 "<"
	gadid 60
	gadhelp 'Opens requester so you can choose Image'
	reqfile -1 -1 300 -40 'Choose Image:' LOAD imgname guis:tools/images
	gosub #this loadimage

XTEXTIN 18 78 227 15 "" imgname "" 140
	gadid 65
	gadhelp 'Enter the Image to be used for this LVDir item'
	gosub #this loadimage

xROUTINE LoadImage
	FreeImage $imgtype					; free old, if any
	if $imgname > ''
		LoadImage $imgname $imgtype NOREMAP
		*$imgtype = $imgname				; store filename in var with type name
		Set $imgtype $imgtype			; use it
	endif
	update #this 65 $imgname

XBUTTON 249 63 29 14 "C"				; clear all
	gadhelp 'Clear all currently declared images'
	gadid 60
	ezreq 'Clear all images ?' Yes|CANCEL choice
	if $choice = 1
		freeimage IMDEV
		*IMDEV = ''
		freeimage IMVOL
		*IMVOL = ''
		freeimage IMASN
		*IMASN = ''
		freeimage IMDIR
		*IMDIR = ''
		freeimage IMFILE
		*IMFILE = ''
		update #this 65 ''
	endif


; =====================================>>>>> Sounds

XTEXTBOX 9 99 280 37 '' 'Sounds:'
	gadid 60
	attr tbstyle 2/1/shadow/left
	attr tbox    2/1/3/button/in

XCYCLER 117 102 128 14 "" samptype
	gadhelp 'Cycles through the events you can set sounds for.'
	gadid 60
	cstr Gadget		SNDGAD
	CSTR Open		SNDOPEN
	CSTR Close		SNDCLOSE
	cstr RMB			SNDRMB
	cstr Keyboard	SNDKEY
	sampname = \$*$samptype ; ie '$*SNDGAD'
	update #this 64 $sampname
 
XTEXTIN 18 117 227 15 "" sampname '' 140
	gadhelp 'Enter the sample you want for this effect'
	gadid 64
	gosub #this loadsound

XBUTTON 249 117 29 15 '<'
	gadhelp 'Opens requester so you can choose sample'
	gadid 60
	reqfile -1 -1 300 -40 'Choose Sample:' LOAD sampname guis:tools/samp

xROUTINE loadsound
	freesound $samptype
	if $sampname > ''
		loadsound $sampname $samptype
		*$samptype = $sampname
		set $samptype $samptype
	endif
	update #this 64 $sampname

XBUTTON 249 102 29 14 "C"
 	gadhelp 'Clear all loaded sound effects'
 	gadid 60
	ezreq 'Clear all sound effects ?' Yes|CANCEL choice
	if $choice = 1
		freesound SNDGAD
		*SNDGAD = ''
		freesound SNDOPEN
		*SNDOPEN = ''
		freesound SNDCLOSE
		*SNDCLOSE = ''
		freesound SNDRMB
		*SNDRMB = ''
		freesound SNDKEY
		*SNDKEY = ''
		update #this 64 ''
	endif

; ------------ External players

XTEXTBOX 9 138 280 26 '' 'External Players:'
	gadid 60
	attr tbstyle 2/1/shadow/left
	attr tbox    2/1/3/button/in

XBUTTON 156 144 124 14 "Configure.."
	gadid 60
	gadhelp 'Configure External players'
	ifexists variable *DEF.EDITOR
	   else
	   guiload guis:tools/prefs/players
	endif
	guiload guis:tools/config.gc

; =====================================>>>>> Palette

XTEXTBOX 9 168 279 26 "" "Palette:"
	gadid 60
	attr tbstyle 2/1/shadow/left
	attr tbox    2/1/3/button/in

XPALETTE 93 171 97 20 
	gadid 60
	gadhelp 'Loads the GUIs:tools/palette.gc gui'
	guiload guis:tools/palette.gc
	guiopen palette.gc

XBUTTON 225 174 22 15 "<"		; previous palette..
	gadhelp 'Load previous palette from Guis:Tools/Palette'
	gadid 60
	--palnum
	gosub #this setpalette

XBUTTON 249 174 22 15 ">"		; next palette..
	gadhelp 'Load next palette from Guis:Tools/Palette'
	gadid 60
	++palnum
	gosub #this setpalette

xRoutine setpalette
	lvuse #this 903
	if $palnum < 0
		palnum == $$lv.tot - 1
	elseif $palnum >= $$lv.tot
		palnum = 0
	endif
	lvmulti none		; cause the lv to select palnum file
	lvgo #$palnum
	lvmulti on
	lvmulti first
   if $palfile H= "G4C"						; support old type palettes..
      guiload $palfile Gui4Cli.gc
	elseif $palfile H= "FORM????ILBM"
		palette load $palfile tempimage	; load it into a image (created)
		palette set tempimage '*'			; set the current screen
		freeimage tempimage					; free palette
	else
		ezreq 'File: $palfile\nis neither a IFF or a Gui4Cli file' Naughty! ''
	endif

XLISTVIEW 2 0 210 92 "" palfile guis:tools/palette 10 DIR
	gadid 903 ; hide it at load


; ------------ DEBUG

XCHECKBOX 78 198 22 12 "" dbg ON OFF OFF
	gadid 60
	attr TITLE 2/1/shadow/lside/bottom/Debug
	gadhelp 'Sets DEBUG mode On/Off (state will not be saved)'
	if $dbg = ON
		if $dbgvar = 1
			set debugvars on
		endif
		if $dbgstep = 1
			set debugstep on
		endif
	endif
	set debug $dbg

XCHECKBOX 168 198 22 12 '' dbgvar ON OFF OFF
	gadid 60
	attr TITLE "2/0/plain/lside/bottom/Vars"
	gadhelp 'Also shows Variable settings in Debug output'
   set debugvars $dbgvar

XCHECKBOX 246 198 22 12 '' dbgstep ON OFF OFF
	gadid 60
	attr TITLE "2/0/plain/lside/bottom/Step"
	gadhelp 'Pops requester with every command - Very annoying!'
   set debugstep $dbgstep


; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
; -----------------------------------------------------------------
;		Prefs
;		Set general Gui4Cli preferences.. gadid 80 - 100
; -----------------------------------------------------------------
; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

XTEXTBOX 9 20 280 163 '' 'General:'
	gadid 80
	attr tbstyle 2/1/shadow/left
	attr tbox    2/1/3/button/in

; ------------ Tab & grid sizes

XHSLIDER 123 24 130 14 Grid grid 1 20 1 "%ld"
	gadid 86
	gadhelp 'Sets the size of the Edit Grid'
	set grid $grid

XHSLIDER 123 39 130 14 "Tabs" tab 0 20 8 "%ld"
	gadid 87
	gadhelp 'Sets the TAB size'
	set tab $tab


; =====================================>>>>> Various

; ** Hide Volumes
XCHECKBOX 20 58 22 12 "" volhide ON OFF OFF
	gadhelp 'Will show/hide the Volumes in LVDir device lists'
	gadid 91
	attr TITLE "2/0/plain/rside/bottom/Hide Volumes in Device lists"
	set NOVOL $volhide

;** LVFill
XCHECKBOX 20 72 22 12 "" lvfill "ON" "OFF" ON
	gadhelp 'Draw the litle box under listviews - Suggested'
	gadid 92
	attr TITLE "2/0/plain/rside/bottom/Fill in Listview height"
	set LVFILL $lvfill

;** LVDIR Sort mode
XCYCLER 20 86 80 12 "" sortmode
	CSTR Name NORMAL
	CSTR Date DATE
	cstr Size SIZE
	cstr Ext  EXTENSION
	gadhelp 'Globally set the way Dir Listviews sort the files'
	gadid 93
	attr TITLE "2/0/plain/rside/bottom/LVDir sort method"
	set LVSORT $sortmode

;** MARGINS
XTEXTIN 20 100 80 14 "" margins "" 100
	gadhelp 'Set the distance TITLEs will have from their gadgets'
	gadid 94
	attr TITLE "2/0/plain/rside/bottom/Titles (Side-Top-Offset)"
	gosub #this CheckSlash margins
	set MARGINS $margins
	update #this 94 $margins

;** TBMARGINS
XTEXTIN 20 116 80 14 "" tbmargins "TextIn" 100
	gadhelp 'Set the internal margins for xTextBox gadgets'
	gadid 95
	attr TITLE "2/0/plain/rside/bottom/xTextBox (Horz-Vert)"
	gosub #this CheckSlash tbmargins
	set TBMARGINS $tbmargins
	update #this 95 $tbmargins

xROUTINE checkSlash var ; convert to valid slash string
	local v/vlen
	repvar $var "-" "/" cs  ; convert to "/"..
	repvar $var " " "/" cs	; & try to cleanup..
	repvar $var "//" "/" cs
	v = \$$var
	if $v = "/"				; leading /
		$var = $v[1]
	endif
	v = \$$var
	if $v[-1][1] = "/"	; trailing /
		info variable v
		vlen == $$var.length - 1
		$var = $v[0][$vlen]
	endif

; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
; ================================================================
;	Save all settings
;	- construct a gui called "Gui4Cli.prefs", autoloaded at start.
; ================================================================

XTEXTBOX 8 186 281 27 "" "Save Prefs & Settings"
	gadid 80
	attr tbstyle 2/1/shadow/left
	attr tbox    2/1/3/icondrop/in

XBUTTON 192 192 85 14 "Save"
	gadhelp 'Save all Prefs & Settings in file Guis:Gui4Cli.prefs'
	gadid 80

	delete guis:gui4cli.prefs		; if it existed..

	.temp = 'G4C\n\n; Generated by Gui4Cli.gc on $$sys.date\n\nxONLOAD\n'

	; save starting palette 
	if $palfile > ''
		append env:.temp '\n; Set palette\nPalette load $palfile tempfile\n'
		append env:.temp "Palette set tempfile '*'\n"
		append env:.temp 'freeimage tempfile\n'
	endif

	; set paths
	append env:.temp '\n; Paths\n'
	append env:.temp 'Set TAB $tab\nSet GRID $grid\n'
	append env:.temp 'Set EDITOR $$G4C.EDITOR\n'
	append env:.temp 'Set FILEPATTERN $$G4C.FILEPATTERN\n'
	append env:.temp 'Set GUIPATTERN $$G4C.GUIPATTERN\n'
	append env:.temp 'Set GUILOADDIR $$G4C.GUILOADDIR\n'
	append env:.temp 'Set DEFSCREEN $$G4C.DEFSCREEN\n'
	append env:.temp 'Set OUTPUT $$G4C.OUTPUT\n'

	; save sound effects
	append env:.temp '\n; Sound fx\n'
	gosub #this StoreSound SNDGAD
	gosub #this StoreSound SNDOPEN
	gosub #this StoreSound SNDCLOSE
	gosub #this StoreSound SNDRMB
	gosub #this StoreSound SNDKEY

	; save LVDir image
	append env:.temp '\n; LVDir images\n'
	gosub #this StoreImg IMDEV
	gosub #this StoreImg IMVOL
	gosub #this StoreImg IMASN
	gosub #this StoreImg IMDIR
	gosub #this StoreImg IMFILE

	; various stuff..
	append env:.temp '\n; Various..\n'
	if $volhide = ON ; hide volumes - off is default
		append env:.temp 'set NOVOL ON\n'
	endif
	if $lvfill = OFF ; def=ON
		append env:.temp 'set LVFILL OFF\n'
	endif
	if $sortmode > ''
	and $sortmode != NORMAL
		append env:.temp 'set LVSORT $sortmode\n'
	endif
	if $margins > ''
		gosub #this CheckSlash margins
		append env:.temp 'set MARGINS $margins\n'
	endif
	if $tbmargins > ''
		gosub #this CheckSlash tbmargins
		append env:.temp 'set TBMARGINS $tbmargins\n'
	endif

	append env:.temp '\nGuiQuit #this\n'
	copy env:.temp GUIs:Gui4Cli.prefs
	delete env:.temp


; ----------- storage routines

xRoutine StoreSound name	; ie "SNDGAD"
	local t
	t = '\$*$name' ; ie '$*SNDGAD'
 	if $t > ''
 		append env:.temp 'loadsound $t $name\n'
 		append env:.temp 'set $name $name\n'
 		append env:.temp '*$name = $t\n'
 	endif

xRoutine StoreImg name  	; ie "IMDIR"
	local t
	t = '\$*$name' ; ie '$*IMDIR'
 	if $t > ''
 		append env:.temp 'loadimage $t $name NOREMAP\n'
 		append env:.temp 'set $name $name\n'
 		append env:.temp '*$name = $t\n'
 	endif

; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
; -----------------------------------------------------------------
;		========================== Menus
; -----------------------------------------------------------------
; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX

xMenu Project New.. '' N		; new gui
	guiclose gui4cli.gc
	guiload guis:tools/guiedit.gc
	gosub guiedit.gc newfile
	if $$ret.0 = 1				; a gui was chosen
	   guiopen guiedit.gc
	endif

	xmenu Project Gui.. '' ''		; use the status requester
	status

xMenu Project BARLABEL '' ''

xMenu Project About '' ''
	ezreq 'Gui4Cli\n(c)1995-99 D.Keletsekis\nEMail: dck@hol.gr\nWeb: http://users.hol.gr/~dck/gcmain.htm' 'OK' ''

xMenu Project 'Quit Gui' '' ''
	guiquit gui4cli.gc

xMenu Project 'Quit Gui4Cli' '' ''
	quit

; --------------- guis menu

xMenu Guis "File Manager   " '' ''
	guiload guis:dir/dir.gc
	guiopen dir.gc
	guiclose gui4cli.gc

xMenu Guis FSearch.gc '' ''
	guiload guis:g4c/fsearch/fsearch.gc Sys:
	guiclose gui4cli.gc

xMenu Guis Rep.gc '' ''
	guiload guis:g4c/rep.gc
	guiclose gui4cli.gc

xMenu Guis CedBar.gc '' ''
	guiload guis:g4c/ced/cedbar.gc
	guiclose gui4cli.gc

xMenu Guis "BARLABEL" '' ''

xMenu Guis Demo '' ''
	guiload guis:demo.gc
	guiopen demo.gc
	guiclose gui4cli.gc

xMenu Guis Tutorials '' ''
	guiload guis:docs/tutorials.gc
	guiopen tutorials.gc
	guiclose gui4cli.gc


; --------------- Help menu

xMenu Help "Gui4Cli Guide " '' ''
	run 'multiview guis:docs/gui4cli.guide'

xMenu Help "BARLABEL" '' ''

xMenu Help "Gui4Cli ReadMe" '' ''
	guiload guis:tools/read.gc guis:readme.now

xMenu Help "Command List" '' ''
	guiload guis:tools/read.gc guis:docs/PrintMe

xMenu Help "Version Changes" '' ''
	guiload guis:tools/read.gc guis:docs/Changes

xMenu Help "BARLABEL" '' ''

xMenu Help "CLI Commands Guide	" '' ''
	run 'multiview guis:docs/CLICommands.guide'

xMenu Help "Routines Guide" '' ''
	run 'multiview guis:docs/Routines.guide'

xMenu Help "BARLABEL" '' ''

xMenu Help Tutorials.. '' ''
	guiload guis:docs/tutorials.gc
	guiopen tutorials.gc


