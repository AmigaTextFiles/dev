G4C

WINBIG 20 20 279 127 "Clone Gadgets into your GUIs"
wintype 11110001
resinfo 8 640 256

xonload
	guiopen guiedit.gc

xonclose
	guiquit guiedit.gc

; ===================================================================
;			The menus
; ===================================================================


xMenu  Project "New GUI" "" N
	gosub guiedit.gc newfile

xroutine newfile		; also called from other guis
	ReqFile -1 -1 300 200 "Enter new GUI name:" SAVE guiname ''
	if $guiname < ' '
	   stop
	endif
	ifexists file $guiname
	   guiload $guiname
	else
	   extract guiname file name
	   .dummy = 'G4C\n\nWinBig -1 -1 200 100 $name\nWinType 11110001\n\nxOnLoad\n\tGuiOpen #This\n\nxOnClose\n\tGuiQuit #This\n\n'
	   copy env:.dummy $guiname
	   guiload $guiname
	   delete env:.dummy
	endif
	return 1

xMenu  Project "Load..." '' L
	ReqFile -1 -1 300 200 "Choose GUI:" LOAD guiname ''
	if $guiname > ' '
	   guiload $guiname
	   guiopen $guiname
	endif

xMenu  Project BARLABEL  '' ''

xMenu  Project About..  '' ''
	ezreq 'Gui4Cli Gui Editor Gui\n\nChoose Gadgets with CONTROL-Click\nand Click them into your window.' "You bet!.." ''

xMenu  Project Quit  '' Q
	guiquit guiedit.gc
	guiquit gridset.g


; ===================================================================
;		The gadgets for picking up & cloning
; ===================================================================

; ----------------------------- listviews

; some text for the lvs, so they can be clicked..
TextFile t:data
Empty
###

CTEXT 9 0 "Simple LV" #screen 8 2 0 0001
XLISTVIEW 6 15 80 22 "" variable t:data 10 TXT

CTEXT 9 36 "Multi LV" #screen 8 2 0 0001
XLISTVIEW 5 49 80 22 "" "variable" t:data 10 MULTI

CTEXT 9 69 "Dir lv" #screen 8 2 0 0001
XLISTVIEW 6 81 80 21 "" "variable" "" 10 DIR


; ----------------------------- other gadgets


XBUTTON 102 31 81 14 "Button"

XRADIO 165 4 18 9 variable 6
	RStr Radio1 1
	RStr Radio2 2

XCYCLER 102 46 80 16 "" variable
	CStr Cycler 1
	CStr Yes!   2

GAUGE 201 81 69 13 IN RIDGE 2 0 50

XHSLIDER 102 63 46 12 "" variable 0 100 50 "%ld%%"

XPALETTE 102 76 82 13 

XTEXTIN 102 90 83 15 "" variable "TextIn" 100

TEXT 102 106 82 15 "Text" 40 BOX

XCHECKBOX 57 109 26 11 ChkBx variable "On" "Off" ON


; ----------------------------- graphics

BOX 198 3 70 9 OUT ICONDROP

LINE 197 19 268 19 2

SQUARE 199 27 67 10 1 NOFILL

SQUARE 200 41 68 9 1 FILL

CIRCLE 231 59 12 6 1 NOFILL

CIRCLE 232 73 13 6 3 FILL

XTEXTBOX 198 96 74 26 "" "TextBox"

