G4C


WINBIG -1 -1 423 39 'Pack selected files/dirs to :'
WinType 11110010
resinfo 8 640 256
varpath dir.gc

BOX 0 0 0 0 out button

xOnLoad
gosub dir.lha startup

xOnReload
gosub dir.lha startup

xRoutine StartUp
old_dir  = $$G4C.DIR
cd $$lv.dir
lvmulti first
if $lv_file = ""
   ezreq "LHA:\nChoose some files first\nwhy don't you ?" OK ""
   guiquit dir.lha
   stop
endif
guiopen dir.lha

xOnOpen
setgad dir.lha 2 ON

xOnClose
guiquit dir.lha

xOnQuit
cd $old_dir

xOnFail
guiwindow dir.lha resume

;========================= Gadgets

XTEXTIN 10 5 400 14 "" lha_string "RAM:MyArc.lha" 130
gadid 2
gadhelp 'Enter name of Archive'

XBUTTON 10 20 99 14 "Pack"
gadhelp 'Click to start packing...'
setvar lha_pack 'c:lha -r -x a $lha_string'
while $lv_file > ""
    extract lv_file file lha_file
    appvar  lha_pack ' $lha_file'
    lvmulti next
endwhile
setwintitle dir.lha "Packing files..."
LAUNCH 1 '$lha_pack'
guiwindow dir.lha wait


xOnReturn 1
guiwindow dir.lha resume
guiquit dir.lha


XBUTTON 230 20 78 14 "Browse.."
gadhelp 'Choose another archive path'
setvar lha_req ""
ReqFile   -1 -1 300 -40 'Destination archive:' SAVE lha_req RAM:
if $lha_req > ""
   lha_string = $lha_req
   update dir.lha 2 $lha_req
endif


XBUTTON 310 20 100 14 CANCEL
GuiQuit dir.lha
