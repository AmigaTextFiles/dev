G4C

; This is a GUI script for the "Wrap" CLI command
; GUI script written by Graham Maddox & D.Keletsekis
; Wrap command by D.Keletsekis

; ----------------- The window

WinBig -1 -1 240 150 'TextWrap v2.0'
WinType 11110001  ; it's resizable & font sensitive with all gadgets
resinfo 8 640 256
varpath dir.gc

; ----------------- General events

xOnLoad		; Set the default values
tw_ext = 1	; Add extensions
tw_newdir = 0		; do not save to new dir
setgad dir.wrap 2 off	; and turn off the dir-select gadgets
setgad dir.wrap 3 off
tw_dest = RAM:	; default dir to save in
tw_para = 1   	; leave paragraphs alone
tw_skeol = 2    ; how many end of line characters to skip
tw_strhr = 0	; don't strip carriage returns (CRs)
tw_addcr = 0    ; don't add CRs
tw_tabflag = 0  ; delete all tabs
tw_tab = 4	; default tab -> spaces length
setgad dir.wrap 6 off
tw_colflag = 1  ; Yes, change line length.
tw_col = 75     ; to 75 characters per line
guiopen dir.wrap
run 'resident guis:c/wrap pure add'


xOnClose
delvar tw_#?
guiquit dir.wrap
run 'resident wrap remove'


; Some fancy boxes to separate the functions into sections.

box 0 0 240 51 out button
box 0 52 240 36 out button
box 0 89 240 35 out button
box 0 125 240 25 out button


; ---------------- Do we want to add extensions to the files ?

xcheckbox 200 5 26 11 'Add ".wrap" extension' tw_ext 1 0 ON


; ---------------- Do we want to save them in an other dir ?

xcheckbox 200 17 26 11 'Save to new directory' tw_newdir 1 0 OFF
if $tw_newdir = 1
   setgad dir.wrap 2 on
   setgad dir.wrap 3 on
else
   setgad dir.wrap 2 off
   setgad dir.wrap 3 off
endif

; Text in for new directory

xTextIn  10 31 180 15 "" tw_dest "RAM:" 512        ; destination dir
gadid 3

; add a browse button to save typing

xbutton 190 31 40 15 "Dir"
gadid 2
reqfile -1 -1 300 -40 "Destination Dir..." dir tw_dest SYS:
update dir.wrap 3 $tw_dest

; ---------------- paragraph options. default is off

xcheckbox 10 57 26 11 "Paragraph" tw_para 1 0 on
gadtitle right
if $tw_para = 1
    setgad dir.wrap 4 on
else
    setgad dir.wrap 4 off
endif

xhslider 157 57 50 11 "EOLs" tw_skeol 0 30 2 %1ld
gadtitle left
gadid 4

; ------------------- checkboxes for adding/stripping hard returns. 
; Default (unchecked) is to leave them alone.
; Check both of them if you want to re-warp PC files

xcheckbox 10 72 26 11 "StripCR" tw_strhr 1 0 off
gadtitle right

xcheckbox 120 72 26 11 "AddCR" tw_addcr 1 0 off
gadtitle right


; =====================================================================
; Now the sliders for tab settings and width. Default is tab=4 width=75
; If these are set to off, Tabs & line length will be left alone.
; NOTE : Max line length that wrap can deal with is 1000 characters.

xcheckbox 10 93  26 11 "Tabs" tw_tabflag 1 0 off
gadtitle right
if $tw_tabflag = 0
   setgad dir.wrap 6 off
else
   setgad dir.wrap 6 on
endif

xhslider 90 93  115 11 "" tw_tab 0 16 4 %1ld
gadid 6

; --- length slider

xcheckbox 10 108 26 11 "Length" tw_colflag 1 0 on
gadtitle right
if $tw_colflag = 0
   setgad dir.wrap 7 off
else
   setgad dir.wrap 7 on
endif

xhslider 90 108 115 11 "" tw_col 10 200 75 %1ld
gadid 7


; ====================================================================
; Do yes do, that thing you do, so weeeelllll... (refrain)

xbutton 70 130 80 15 START
tw_stopflag = 0			; reset abort flag
tw_lvid = $$LV.ID
lvuse dir.gc $tw_lvid
lvmulti first
if $lv_file = ""
   ezreq "I would.. if I had\nsome files.." OK ""
   stop
endif
if $tw_ext = 0			; check & warn if files will be overwriten
   if $tw_newdir = 0
      ezreq 'With the current settings\nselected files will be\nOverWriten !\n\nIf any file is not a pure\ntext file, it may be trashed!\n\nAre you sure ?\n' "Continue|CANCEL" tw_choice
      if $tw_choice = 0
         stop
      endif
   endif
endif
guiwindow dir.wrap wait
gosub dir.wrap setup			        ; construct the command line
launch 1 'wrap $lv_file $tw_options'		; and launch the first file


xOnReturn 1			; upon returning, launch the next file
lvuse dir.gc $tw_lvid
lvmulti off
if $tw_stopflag = 1		; check if user has aborted meanwhile
   guiwindow dir.wrap resume
   lvdir refresh
   stop
endif
lvmulti next
if $lv_file > ""
   gosub dir.wrap setup
   launch 1 'wrap $lv_file $tw_options'
else
   guiwindow dir.wrap resume
   if $tw_newdir = 1
      lvdir #$tw_dest
      setwintitle dir.wrap '$tw_dest                                    '
   else
      lvdir refresh
   endif
endif


xroutine setup			; construct the options command line
tw_options = ""
extract lv_file file tw_file
if $tw_ext = 1
   extract tw_file unquote tw_file2
   appvar  tw_file2 .wrap               ; add the file extension
   tw_file =  '\"$tw_file2\"'
endif
if $tw_newdir = 1
   JoinFile $tw_dest $tw_file tw_file   ; save to new dir
else
   extract lv_file path tw_path
   JoinFile $tw_path $tw_file tw_file   ; or to the same dir
endif
tw_options = '$tw_file '		; this is our correct file name
if $tw_para = 1
   appvar tw_options 'PARA=$tw_skeol '  ; paragraph wraping ?
endif
if $tw_strhr = 1
    appvar tw_options 'StripCR '	; strip CRs ?
endif
if $tw_addcr = 1
    appvar tw_options 'AddCR '		; add CRs ?
endif
if $tw_tabflag = 1
    appvar tw_options 'TAB=$tw_tab '	; convert tabs ?
endif
if $tw_colflag = 1
    appvar tw_options 'L=$tw_col'	; wrap file ?
endif


; ------------------------ Stop processing the rest of the files

xButton 150 130 80 15 _STOP
tw_stopflag = 1


; ------------------------ If the worst happens..

xOnFail
guiwindow dir.wrap resume
ezreq "Error wraping file :\n$tw-file" OK ""


; ------------------------- Must tell everyone how great I am..... 8)

xbutton 20 130 26 15  _?
EzReq "TextWrap 2.0\n\nBy :\nG.Maddox & D.Keletsekis\n\nFor use with the c:Wrap\ncommand, by D.Keletsekis\n" "Who cares.." opt

