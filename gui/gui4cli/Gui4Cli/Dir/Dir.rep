G4C   

; Dir.rep - GUI for the c:Rep replace command.


; ----------------- The window

WINBIG 129 75 400 88 'Replace in selected files'
WinType 11110001
resinfo 8 640 256
varpath dir.gc

BOX 0 0 0 0 OUT button

; ----------------- General events

xOnLoad
	rp_from  = ""
	rp_to    = ""
	rp_savemode = OVER
	rp_ci = ""
	guiopen Dir.rep
	RUN 'resident guis:c/Rep pure add'

xOnReload
	guiopen dir.rep

xOnClose
	delvar rp_#?
	guiquit Dir.rep
	run 'resident Rep remove'

; ---------------- text-in gadgets for entering..

XTEXTIN 81 6 310 15 'Replace:' rp_from "" 200	; string to Rep
	gadid 1
	attr resize 0120
	setgad dir.rep 2 on   ; activate next textin


XTEXTIN 81 21 310 15 'With:' rp_to "" 200		; Rep with this
	attr resize 0120
	gadid 2

; ---------------- Do we want to add extensions to the files ?

XCYCLER 9 39 190 14 "" rp_savemode
	gadid 3
	cstr 'OVERWRITE Files'    OVER
	cstr 'Add .rep extension' EXT
	attr resize 1110

; ---------------- case insensitive search ?

XCYCLER 201 39 190 14 "" rp_ci
	gadid 6
	cstr 'Case Sensitive'    ""
	cstr 'Case Insensitive'  I
	attr resize 1110

; ---------------- A text display, telling you what's going on.

xTEXTBOX 81 54 310 14 '' Waiting..
	gadid 4
	attr resize 0120
	attr tbox  1/0/0/button/out
	attr title 1/0/plain/lside/middle/Status:

; ---------------- Start converting..

XBUTTON 228 69 80 14 Start
	attr resize 2100
	gadid 5
	lvmulti first
	if $lv_file = ""
	   ezreq "No files selected!" OK ''
	   stop
	endif
	if $$LV_TYPE != FILE
	   ezreq 'Directories can not\nbe converted!\n\nTry again..\n' OK ""
	   stop 
	endif
	if $rp_from = ""
	   ezreq 'No "Replace:" defined!' OK ""
	   stop
	endif
	rp_flag = 0
	rp_destfile = ""
	if $rp_savemode = EXT
	   extract lv_file UNQUOTE rp_fname
	   appvar  rp_fname .rep
	   rp_destfile = \"$rp_fname\"
	endif
	gosub dir.rep disable
	update Dir.rep 4 'REP: $lv_file'
	set deeptrans off
	launch 5 'Rep $lv_file \"$rp_from\" \"$rp_to\" $rp_destfile $rp_ci'
	set deeptrans on

; Use of the launch command will allow the other GUIs running on
; this instance of Gui4Cli to go about their business normally

xOnReturn 5
	update Dir.rep 4 'Finished.'
	lvmulti OFF			; set selection off
	if $rp_flag != 0  		; check abort flag
	   rp_flag = 0    		; reset for next time
	   gosub dir.rep enable
	   stop
	endif
	lvmulti next			; get next file
	if $lv_file = ""		; no more files
	   lvdir refresh
	   gosub dir.rep enable
	   stop
	endif
	if $rp_savemode = EXT
	   extract lv_file UNQUOTE rp_fname
	   appvar  rp_fname .rep
	   rp_destfile = \"$rp_fname\"
	endif
	update Dir.rep 4 'REP: $lv_file'
	set deeptrans off
	launch 5 'Rep $lv_file \"$rp_from\" \"$rp_to\" $rp_destfile $rp_ci'
	set deeptrans on


; --------------- Cancel the rest of the files chosen.


XBUTTON 312 69 80 14 Stop
	attr resize 2100
	setvar rp_flag 1

; --------------- Routines to disables & enable guis

xRoutine disable
	guiwindow dir.gc wait
	setgad dir.rep 1 off
	setgad dir.rep 2 off
	setgad dir.rep 3 off
	setgad dir.rep 5 off
	setgad dir.rep 6 off

xRoutine enable
	guiwindow dir.gc resume
	setgad dir.rep 1 on
	setgad dir.rep 2 on
	setgad dir.rep 3 on
	setgad dir.rep 5 on
	setgad dir.rep 6 on

xOnFail
	gosub dir.rep enable








