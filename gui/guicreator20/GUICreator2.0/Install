;  *** GUICreator installation scipt ***


; *** Set some useful messages ***

   (set #install-msg (cat  "\n\nGUICreator installation script.\n"
                           "This script installs GUICreator on your Amiga.\n\n"
                           "Read the documentation for\n"
                           "more information on the distribution\n"
                           "and commercial usage of GUICreator.\n\n"
                           "GUICreator © 1995/96 Markus Hillenbrand\n"
                           "All rights reserved."))
   (set #welcome-msg       "Welcome to the GUICreator installation!")
   (set #bad-kick          "You must be using Kickstart 3.0 to use this program!")
   (set #bad-installer     "You must be using Installer 43.x to install GUICreator!")
   (set #ask-program-dir   "Where shall I install the program ?\n(A drawer will NOT be created)")
   (set #ask-doc-dir       "Where shall I install the docs ?")
   (set #comp              "Which C compiler(s) do you use ?")
   (set #comp-help         "\nIf you choose a compiler, I will update some includes for use with textfield gadget\n\n")
   (set #copyfiles         "Copying files to harddisk ...")
   (set #dice-msg          "\nAttention DICE users:\n\nYou will have to install textfield-gadget yourself!")
   (set #install-textfield "Installing Textfield.Gadget to your system")

; *** Welcome the user to the installation

   (message #install-msg)

; *** Check for Kickstart version ***

   (if (< (/ (getversion) 65536) 39)
      (
      (abort #bad-kick)
      )
   )

; *** Check for Installer-Version ***

   (if (< (/ @installer-version 65536) 43)
      (
      (abort #bad-installer)
      )
   )


; *** Ask the user for the default-destinations ***

  (set #prog-dest (askdir (prompt #ask-program-dir)
                           (help @askdir-help)
                           (default "Work:")
                           (disk)
                   )
   )
   (set #doc-dest  (askdir (prompt #ask-doc-dir)
                           (help @askdir-help)
                           (default #prog-dest)
                   )
   )
   (set #compilers (askoptions (prompt #comp)
                               (help #comp-help @askoptions-help)
                               (choices "Maxon C/C++ 3.x" "GNU C" "DICE C" "StormC")
                               (default 0)
                   )
   )
   (set #copy-tutor (askbool (prompt "Do you want to install the tutorial C sources ?")
                             (help @askbool-help)
                             (default 1)
                    )
   )


; *** Install the textfield gadget ***

   (copylib (prompt #install-textfield)
            (help @copylib-help)
            (source "textfield.gadget")
            (dest "SYS:Classes/Gadgets")
            (confirm)
   )


; *** Copy the program ***

   (copyfiles (prompt #copyfiles)
              (source "")
              (dest   #prog-dest)
              (choices "GUICreatorWB" "GUICreatorWB.info" "GUICreatorPrefs" "GUICreatorPrefs.info" ".prefs" ".windowpos" "About.IFF" "c" "Data" "Data.info")
   )


; *** Copy the docs ***

   (copyfiles (prompt #copyfiles)
              (source "")
              (dest   #doc-dest)
              (choices "GUICreatorWB.guide" "GUICreatorWB.guide.info" "DOC")
   )


; *** Copy the tutorial sources

   (if (= #copy-tutor 1)
       (
       (copyfiles (prompt #copyfiles)
                  (source "")
                  (dest   #prog-dest)
                  (choices "Demos" "Demos.info")
       )
       )
   )


; *** Update the Maxon C++ compiler ***

   (if (IN #compilers 0)
       (
       (copyfiles (prompt @copyfiles-help)
                  (source "Compilers/Maxon")
                  (dest "MCPP:")
                  (choices "lib")
       )
       (copyfiles (prompt @copyfiles-help)
                  (source "Compilers/Maxon")
                  (dest "MCPP:")
                  (choices "include")
       )
       )
   )


; *** Update the GNU compiler ***

   (if (IN #compilers 1)
       (
       (copyfiles (prompt @copyfiles-help)
                  (source "Compilers/GNU")
                  (dest "GNU:")
                  (choices "os-include")
       )
       )
   )

; *** Update DICE compiler ***

   (if (IN #compilers 2)
       (
       (message #dice-msg)
       )
   )


; *** Update STORM compiler ***

   (if (IN #compilers 3)
       (
       (copyfiles (prompt @copyfiles-help)
                  (source "Compilers/Storm")
                  (dest "STORMC:Include")
                  (choices "clib" "pragma" "pragmas" "gadgets" "proto")
       )
       )
   )


; *** Keyfile installlieren ***

   (if (exists "GUICreator_InstallDisk:Keyfiles/.keyfile" (NOREQ) )
       (
       (copyfiles (prompt @copyfiles-help)
                  (source "GUICreator_InstallDisk:Keyfiles")
                  (dest #prog-dest)
                  (choices ".keyfile")
       )
       )
   )


