; JS_TOOLS Library Installer Script
; by Jürgen Schmitz

;--------------------------------------------------------------------
;set text vars
;

(set #inst
  (cat "Installation script of\njs_tools.library\n\n"
       "for more details read the manual.\n\n"
       "The library is copyright (c) by J.Schmitz [SOFT]\n"
       "\nthe library may be copyed and used free - it's FREEWARE\n"
))

(set #copylib
  (cat "js_tools.library will be copyed to 'libs' as all other libraries."
))

(set #includes
  (cat "If you want to use 'js_tools' as programmer you may"
       "choose the includes and docs and where to copy them.\n\n"
       "(The C includes have some additional information, so you "
       "should read them anyway.)"
))

(set #inclAutodoc
  (cat "Autodocs"
))

(set #inclAutodoc-drawer
  (cat "Set drawer to copy autodocs into"
))

(set #inclC
  (cat "C & ASM includes (mostly SAS/C)"
))

(set #inclC-drawer
  (cat "Set drawer to copy includes into"
))

(set #inclKP
  (cat "Kick-Pascal (KP) includes"
))

(set #inclKP-drawer
  (cat "Set drawer to copy KP includes into\n"
       "(this are unit sources, so select you unit sources drawer)"
))
(set #inclKP-drawer_2
  (cat "Set a 2nd drawer to copy KP includes into\n"
       "(this is an object file, so select the drawer of your "
       "precompiled units)"
))

(set #inclFD
  (cat "FD file (e.g. for AmigaBasic.....)"
))

(set #inclFD-drawer
  (cat "Set drawer to copy FD file into"
))

(set #exit
  (cat "js_tools.library is installed."
))

;--------------------------------------------------------------------
;install
;

(message #inst)

(welcome)

(set @default-dest "libs:")

(copylib (prompt #copylib)
         (source "libs/js_tools.library")
         (help @copylib-help)
         (dest "libs:")
         (confirm)
)
           

(set res
 (askoptions (prompt #includes)
             (help @askchoice-help)
             (default 0)
             (choices #inclAutodoc #inclC #inclFD #inclKP)
 )
)

(if (in res 0)
 ( (set toDir
     (askdir (prompt #inclAutodoc-drawer)
             (default "Work:")
             (help @askdir-help))
   )

   (copyfiles (source "programmers/Autodocs/js_tools.doc")
              (dest toDir))

 )
)
(if (in res 1)
 ( (set toDir
     (askdir (prompt #inclC-drawer)
             (default "include:")
             (help @askdir-help))
   )

   (copyfiles (source "programmers/include")
              (dest toDir)
              (all))

 )
)
(if (in res 2)
 ( (set toDir
     (askdir (prompt #inclFD-drawer)
             (default "Work:")
             (help @askdir-help))
   )

   (copyfiles (source "programmers/fd")
              (dest toDir)
              (all))

 )
)
(if (in res 3)
 ( (set toDir
     (askdir (prompt #inclKP-drawer)
             (default "KP:Unit-Sources")
             (help @askdir-help))
   )
   (set toDir_2
     (askdir (prompt #inclKP-drawer_2)
             (default "KP:Unit")
             (help @askdir-help))
   )

   (copyfiles (source "programmers/Kick-Pascal/js_tools.p")
              (dest toDir))
   (copyfiles (source "programmers/Kick-Pascal/lvHookCode.o")
              (dest toDir_2))

 )
)

(exit #exit)
